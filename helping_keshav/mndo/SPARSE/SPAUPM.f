      SUBROUTINE SPAUPM (A,B,IIA,IIB,JJA,JJB,N,FACT1,FACT2,UPMIN1,
     +                   UPMAX1,UPMIN2,UPMAX2)
C     *
C     PERFORM UPDATE OF TWO SPARSE MATRICES: A + FACT*B, FOR DIAGONAL
C     ELEMENTS ONLY, AND RETURN MINIMUM AND MAXIMUM DIAGONAL ELEMENTS.
C     BOTH MATRICES ARE STORED IN CSR FORMAT.
C     THE ORDER OF COLUMN INDICES IS ARBITRARY.
C     *
C     NOTATION. I=INPUT, O=OUTPUT, S=SCRATCH.
C     A(*)      NONZERO VALUES OF FIRST MATRIX (I).
C     B(*)      NONZERO VALUES OF SECOND MATRIX (I).
C     IIA(N+1)  POINTER ARRAY FOR A IN CSR FORMAT (I).
C     IIB(N+1)  POINTER ARRAY FOR B IN CSR FORMAT (I).
C     JJA(*)    COLUMN INDICES FOR A IN CSR FORMAT (I).
C     JJB(*)    COLUMN INDICES FOR B IN CSR FORMAT (I).
C     N         DIMENSION OF ALL MATRICES (I).
C     FACT      UPDATE FACTOR (I).
C     UPMIN     MINIMUM DIAGONAL ELEMENT AFTER UPDATE (O).
C     UPMAX     MAXIMUM DIAGONAL ELEMENT AFTER UPDATE (O).
C     *
C     IMPLICIT DOUBLE PRECISION (A-H,O-Z)
      IMPLICIT NONE
      INTEGER :: N,I,K
      DOUBLE PRECISION :: UPMIN1,UPMAX1,AII,BII,FACT1,FACT2,CII,
     +                    DII,UPMIN2,UPMAX2
      INTEGER, DIMENSION (:), POINTER :: IIB,JJB,IIA,JJA
      DOUBLE PRECISION, DIMENSION (:), POINTER :: B,A
C
C *** INITIALIZATION FOR DENSITY MATRIX UPDATE.
      UPMIN1  = 2.0D0
      UPMAX1  = 0.0D0
      UPMIN2  = 2.0D0
      UPMAX2  = 0.0D0
C *** LOOP OVER DIAGONAL ELEMENTS.
      DO 50 I=1,N
C     GET DIAGONAL ELEMENT OF MATRIX A.
      DO 10 K=IIA(I),IIA(I+1)-1
      IF(JJA(K).EQ.I) THEN
         AII = A(K)
         GO TO 20
      ENDIF
   10 CONTINUE
      AII    = 0.0D0
   20 CONTINUE
C     GET DIAGONAL ELEMENT OF MATRIX B.
      DO 30 K=IIB(I),IIB(I+1)-1
      IF(JJB(K).EQ.I) THEN
         BII = B(K)
         GO TO 40
      ENDIF
   30 CONTINUE
      BII    = 0.0D0
   40 CONTINUE
C     CHECK UPDATE.
      CII    = AII + FACT1*BII
      DII    = AII + FACT2*BII
      UPMIN1  = MIN(UPMIN1,CII)
      UPMAX1  = MAX(UPMAX1,CII)
      UPMIN2  = MIN(UPMIN2,DII)
      UPMAX2  = MAX(UPMAX2,DII)
   50 CONTINUE
      RETURN
      END
