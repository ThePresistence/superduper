      SUBROUTINE SPARSP (PA,PPA,IIA,JJA,LM6,MPDIAG)
C     *
C     DEFINITION OF INITIAL SPARSE DENSITY MATRIX FOR CGDMS.
C     *
C     NOTATION. I=INPUT, O=OUTPUT, S=SCRATCH.
C     PA(*)     RHF DENSITY MATRIX IN CSR FORMAT (O).
C     IIA(N+1)  POINTERS FOR PA IN CSR FORMAT (O).
C     JJA(*)    INDICES FOR PA IN CSR FORMAT (O).
C     PPA(LM6)  ONE-CENTER RHF DENSITY MATRIX (O).
C     LM6       DIMENSION OF ONE-CENTER PAIR ARRAYS (I).
C     MPDIAG    FLAG TO INDICATE TYPE OF DENSITY MATRIX (O).
C               = 1  DIAGONAL, NONZERO ELEMENTS DIFFERENT.
C               = 2  DIAGONAL, NONZERO ELEMENTS ALL EQUAL.
C               = 3  UNIT MATRIX (MULTIPLIED BY 0.5).
C     *
      USE LIMIT, ONLY: LM1, LMI, LMZ
      USE module3
C     IMPLICIT DOUBLE PRECISION (A-H,O-Z)
      IMPLICIT NONE
      INTEGER :: NORBS,LM6,KHARGE,KTRIAL,NPRINT,I,NSPORB,NALPHA,
     +           NBETA,NUMAT,MPDIAG,NI,IORBS,NFIRST,NLAST,NAT,
     +           NB6,IN2,IS,NW,K,NUMB,NMOS,NBF,IP1,IP2,IP,III,IIID
      DOUBLE PRECISION :: PPA(LM6),ZERO,PT5,PT25,TEMP,FA,
     +                    YY,TWO,CORE,ONE,THREE,FOUR,W,
     +                    XEL,TINY,EISOL,EHEAT
      INTEGER, DIMENSION (:), POINTER :: IIA,JJA
      DOUBLE PRECISION, DIMENSION (:), POINTER :: PA
C
      PARAMETER (TINY=1.0D-10)
      COMMON
     ./ATOMS / NUMAT,NAT(LM1),NFIRST(LM1),NLAST(LM1)
     ./CONSTN/ ZERO,ONE,TWO,THREE,FOUR,PT5,PT25
     ./DNBND / III(LMZ),IIID(LMZ)
     ./FINDX1/ IP(LMI),IP1(LMI),IP2(LMI)
     ./INDEXW/ NW(LM1)
     ./INOPT2/ IN2(300)
     ./NBFILE/ NBF(20)
     ./ORBITS/ NUMB,NORBS,NMOS,NALPHA,NBETA
     ./PARDER/ CORE(LMZ),EHEAT(LMZ),EISOL(LMZ)
C
C *** CHECK WHETHER INITIAL DENSITY MATRIX IS ALREADY AVAILABLE.
C     IN THE CASE OF A JOB CONTINUATION RUN (IN2(37)=MIDDLE.GT.0)
C     THE PROGRAM DEFINES IN2(67)=KTRIAL=-1 (SEE SUBROUTINE INPUT).
C     IN THIS CASE THE INITIAL DENSITY MATRIX IS READ VIA DENSAV.
      IF(IN2(67).LT.0) RETURN
C *** FILE NUMBERS.
C     NB5    = NBF(5)
      NB6    = NBF(6)
C *** INPUT OPTIONS.
      KHARGE = IN2(65)
      KTRIAL = IN2(67)
      NPRINT = IN2(72)
C *** INITIALIZATION.
      DO 50 I=1,LM6
      PPA(I) = ZERO
   50 CONTINUE
C *** COMPUTE SIMPLIFIED DIAGONAL TRIAL DENSITY MATRIX (KTRIAL=1).
      IF(KTRIAL.EQ.1) THEN
         XEL    = (DBLE(NALPHA+NBETA)/DBLE(NORBS))*PT5
         DO 60 I=1,LM6
         IF(IP1(I).EQ.IP2(I)) THEN
            PPA(I) = XEL
         ENDIF
   60    CONTINUE
         MPDIAG = 2
         IF((NALPHA+NBETA).EQ.NORBS) MPDIAG=3
         GO TO 100
      ENDIF
C *** COMPUTE STANDARD DIAGONAL TRIAL DENSITY MATRIX (KTRIAL=0).
C     NSPORB IS THE NUMBER OF ATOMIC ORBITALS INITIALLY POPULATED.
C     D ORBITALS OF MAIN-GROUP ELEMENTS ARE NOT POPULATED.
C     P ORBITALS OF TRANSITION ELEMENTS ARE NOT POPULATED.
C     POLARIZATION FUNCTIONS IN AN EXTENDED BASIS ARE NOT POPULATED.
      NSPORB = NORBS
      DO 70 I=1,NUMAT
      NI     = NAT(I)
      IORBS  = NLAST(I)-NFIRST(I)+1
      IF(IORBS.EQ.9) THEN
         IF(III(NI).LE.IIID(NI)) NSPORB=NSPORB-5
         IF(III(NI).GT.IIID(NI)) NSPORB=NSPORB-3
      ENDIF
      IF(IORBS.EQ.4 .AND. NI.LE.2) NSPORB=NSPORB-3
   70 CONTINUE
      YY     = DBLE(KHARGE)/DBLE(NSPORB)
C     LOOP OVER ALL ATOMS.
      DO 80 I=1,NUMAT
      IORBS  = NLAST(I)-NFIRST(I)+1
      IS     = NW(I)
      NI     = NAT(I)
C     ATOMS WITH AN S BASIS (POSSIBLY WITH P POLARIZATION FUNCTIONS).
      IF(IORBS.EQ.1 .OR. (IORBS.EQ.4 .AND. NI.LE.2)) THEN
         PPA(IS) = (CORE(NI)-YY)*PT5
C     ATOMS WITH AN SP-BASIS.
      ELSE IF(IORBS.EQ.4) THEN
         W   = (CORE(NI)*PT25-YY)*PT5
         PPA(IS)   = W
         PPA(IS+2) = W
         PPA(IS+5) = W
         PPA(IS+9) = W
C     MAIN-GROUP ELEMENTS WITH AN SPD-BASIS.
      ELSE IF(IORBS.EQ.9 .AND. (III(NI).LE.IIID(NI))) THEN
         W   = (CORE(NI)*PT25-YY)*PT5
         IF (KTRIAL.EQ.2) THEN
            W=W*4/9
            PPA(IS)   =  W
            PPA(IS+2) =  W
            PPA(IS+5) =  W
            PPA(IS+9) =  W
            PPA(IS+14) = W
            PPA(IS+20) = W
            PPA(IS+27) = W
            PPA(IS+35) = W
            PPA(IS+44) = W
         ELSE
            PPA(IS)   = W
            PPA(IS+2) = W
            PPA(IS+5) = W
            PPA(IS+9) = W
         ENDIF
C     TRANSITION-METAL ELEMENTS WITH AN SPD-BASIS.
      ELSE IF(IORBS.EQ.9 .AND. (III(NI).GT.IIID(NI))) THEN
         TEMP = CORE(NI)-YY*6.0D0
C        UP TO 10 ELECTRONS, PUT INTO S AND D ORBITALS.
         IF(TEMP.LT.10.0D0) THEN
            W   = TEMP/12.0D0
            PPA(IS)    = W
            PPA(IS+14) = W
            PPA(IS+20) = W
            PPA(IS+27) = W
            PPA(IS+35) = W
            PPA(IS+44) = W
C        MORE THAN 10 ELECTRONS, FILL D ORBITALS, PUT REST IN S ORBITAL.
         ELSE
            W   = (TEMP-10.0D0)*PT5
            PPA(IS)    = W
            PPA(IS+14) = W
            PPA(IS+20) = W
            PPA(IS+27) = W
            PPA(IS+35) = W
            PPA(IS+44) = W
         ENDIF
      ENDIF
   80 CONTINUE
C *** ASSIGN TYPE OF DIAGONAL DENSITY MATRIX GENERATED.
      MPDIAG = 3
      DO 90 I=2,LM6
      IF(IP1(I).EQ.IP2(I)) THEN
         IF(ABS(PPA(I)-PPA(1)).GT.TINY) THEN
            MPDIAG = 1
            GO TO 100
         ENDIF
         IF(ABS(PPA(I)-PT5).GT.TINY) THEN
            MPDIAG = 2
         ENDIF
      ENDIF
   90 CONTINUE
C *** DEFINE INITIAL DENSITY MATRIX IN CSR FORMAT.
  100 CONTINUE
      K      = 0
      DO 130 I=1,LM6
      IF(IP1(I).EQ.IP2(I)) THEN
         K   = K+1
         IIA(K) = K
         JJA(K) = K
         PA(K)  = PPA(I)
      ENDIF
  130 CONTINUE
      IIA(K+1)  = K+1
C *** PRINTING SECTION.
      IF(NPRINT.GE.5) THEN
         WRITE(NB6,600)
         WRITE(NB6,610) (PPA(I)*TWO,I=1,LM6)
         WRITE(NB6,620) MPDIAG
      ENDIF
      IF(NPRINT.GE.9) THEN
         WRITE(NB6,500)
         CALL SPAPRT (PA,IIA,JJA,NORBS)
      ENDIF
C *** SET FLAG THAT INITIAL DENSITY MATRIX HAS BEEN GENERATED.
      IN2(67) = -1
      RETURN
  500 FORMAT (///1X,'INITIAL DENSITY MATRIX (MULTIPLIED BY 0.5).'/)
  600 FORMAT (///1X,'INITIAL ONE-CENTER DENSITY MATRIX ELEMENTS.'/)
  610 FORMAT (   1X,10F10.5)
  620 FORMAT (// 1X,'TYPE OF INITIAL DENSITY MATRIX: MPDIAG =',I2)
      END
