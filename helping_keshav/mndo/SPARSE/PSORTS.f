      SUBROUTINE PSORTS (I,J,PA2,PA,PPA,LM2,LM6,LMP)
C     *
C     EXTRACT DENSITY MATRIX FOR ATOM PAIR I-J (SPARSE VERSION).
C     OFFDIAGONAL ELEMENTS ARE MULTIPLIED BY 2.
C     *
C     NOTATION. I=INPUT, O=OUTPUT, S=SCRATCH.
C     I,J       NUMBERS OF THE TWO ATOMS (I).
C     PA2(LMP)  RHF DENSITY MATRIX FOR ATOM PAIR I-J (O).
C     PA(LM2,*) LOCAL RHF DENSITY MATRIX FOR ATOM I (I).
C     PPA(LM6)  GLOBAL ONE-CENTER RHF DENSITY MATRIX (I).
C     *
      USE LIMIT, ONLY: LM1
C     IMPLICIT DOUBLE PRECISION (A-H,O-Z)
      IMPLICIT NONE
      COMMON
     ./ATOMS / NUMAT,NAT(LM1),NFIRST(LM1),NLAST(LM1)
     ./INDEXW/ NW(LM1)
      INTEGER LMP,LM6,IA,IB,JA,JB,IJ,KK,I,KLOCAL,K,L,NFIRST,NLAST,
     +        NAT,LM2,NW,J,NUMAT
      DOUBLE PRECISION PA2(LMP),PA(LM2,*),PPA(LM6),TWO,PT5
      PARAMETER (PT5=0.5D0)
      PARAMETER (TWO=2.0D0)
C
C *** INITIALIZATION.
      IA     = NFIRST(I)
      IB     = NLAST(I)
      JA     = NFIRST(J)
      JB     = NLAST(J)
      IJ     = 0
C *** ONE-CENTER ELEMENTS FOR ATOM J.
      KK     = NW(J)-1
      DO 20 K=JA,JB
      DO 10 L=JA,K
      IJ     = IJ+1
      KK     = KK+1
      PA2(IJ)= PPA(KK)*TWO
   10 CONTINUE
      PA2(IJ)= PA2(IJ)*PT5
   20 CONTINUE
C *** LOOP OVER REMAINING ELEMENTS.
      KK     = NW(I)-1
      DO 50 K=IA,IB
      KLOCAL = K-IA+1
C     TWO-CENTER ELEMENTS.
      DO 30 L=JA,JB
      IJ     = IJ+1
      PA2(IJ)= PA(L,KLOCAL)*TWO
   30 CONTINUE
C     ONE-CENTER ELEMENTS FOR ATOM I.
      DO 40 L=IA,K
      IJ     = IJ+1
      KK     = KK+1
      PA2(IJ)= PPA(KK)+PPA(KK)
   40 CONTINUE
      PA2(IJ)= PA2(IJ)*PT5
   50 CONTINUE
      RETURN
      END
