      DOUBLE PRECISION FUNCTION DDOTS2 (A,B,IIA,IIB,JJA,JJB,N,
     +                                  MODE1,MODE2)
C     *
C     GENERAL CODE FOR SCALAR PRODUCT OF TWO SPARSE SYMMETRIC MATRICES.
C     BOTH MATRICES ARE STORED IN THE CSR FORMAT.
C     *
C     NOTATION. I=INPUT, O=OUTPUT, S=SCRATCH.
C     A(*)      NONZERO VALUES OF FIRST MATRIX (I).
C     B(*)      NONZERO VALUES OF SECOND MATRIX (I).
C     IIA(N+1)  POINTER ARRAY FOR A IN CSR FORMAT (I).
C     IIB(N+1)  POINTER ARRAY FOR B IN CSR FORMAT (I).
C     JJA(*)    COLUMN INDICES FOR A IN CSR FORMAT (I).
C     JJB(*)    COLUMN INDICES FOR B IN CSR FORMAT (I).
C     N         DIMENSION OF BOTH MATRICES (I).
C     MODE1     ORDER OF COLUMN INDICES (I).
C               = 0 ARBITRARY.
C               = 1 ASCENDING ORDER.
C     MODE2     RANGE OF SCALAR PRODUCT (I).
C               = 0 ALL ELEMENTS OF THE MATRICES.
C               = 1 ONLY UPPER TRIANGLE INCLUDING DIAGONAL.
C     DDOTS2    SCALAR PRODUCT (O).
C     *
C     IMPLICIT DOUBLE PRECISION (A-H,O-Z)
      IMPLICIT NONE
C
      INTERFACE
      SUBROUTINE TRACENP (NO,A,IAA,KKA,B,IAB,KKB,Y,Tr)
      IMPLICIT NONE
      INTEGER NO
      DOUBLE PRECISION Tr
      DOUBLE PRECISION, DIMENSION (:), POINTER :: A,B,Y
      INTEGER, DIMENSION (:), POINTER :: IAA,KKA,IAB,KKB
      END SUBROUTINE TRACENP
      END INTERFACE
C
      INTEGER :: N,I,JA,JB,K,L,MODE1,MODE2,ie
      DOUBLE PRECISION DIASUM
      DOUBLE PRECISION, DIMENSION (:), POINTER :: A,B,WR
      INTEGER, DIMENSION (:), POINTER :: IIA,JJA,IIB,JJB
C
C *** INITIALIZATION.
      DDOTS2 = 0.0D0
      IF(IIA(N+1).LE.1 .OR. IIB(N+1).LE.1) RETURN
C *** COMPUTE SCALAR PRODUCT FOR FULL MATRIX.
      ALLOCATE (WR(N),STAT=ie)
         IF(ie.NE.0) CALL XERALL (ie,'DDOTS2','WR',N,0)
      CALL TRACENP (N,A,JJA,IIA,B,JJB,IIB,WR,DDOTS2)
      DEALLOCATE (WR,STAT=ie)
         IF(ie.NE.0) CALL XERALL (ie,'DDOTS2','WR',N,1)
      NULLIFY (WR)
      IF(MODE2.LE.0) RETURN
C *** COMPUTE DIAGONAL SUM.
      DIASUM = 0.0D0
      DO 30 I=1,N
C     LOOP OVER COLUMN INDICES OF MATRIX A.
      DO 20 K=IIA(I),IIA(I+1)-1
      JA     = JJA(K)
C     FOR DIAGONAL TERMS: LOOP OVER COLUMN INDICES OF MATRIX B.
      IF(JA.EQ.I) THEN
         DO 10 L=IIB(I),IIB(I+1)-1
         JB     = JJB(L)
         IF(JA.EQ.JB) THEN
            DIASUM = DIASUM + A(K)*B(L)
            GO TO 30
         ENDIF
   10    CONTINUE
      ENDIF
   20 CONTINUE
   30 CONTINUE
C *** COMPUTE SCALAR PRODUCT FOR UPPER TRIANGLE.
      DDOTS2 = 0.5D0*(DDOTS2-DIASUM) + DIASUM
      RETURN
      END
