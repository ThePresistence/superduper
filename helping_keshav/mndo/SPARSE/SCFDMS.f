      SUBROUTINE SCFDMS (A,LM5,ICALL)
C     *
C     CONTROL ROUTINE FOR SCF CALCULATION.
C     INTEGRAL-DIRECT SPARSE CG-DMS VERSION.
C     *
C     NOTATION. I=INPUT, O=OUTPUT, S=SCRATCH.
C     A(LM5)    AVAILABLE BUFFER (S).
C     ICALL     CONTROL AND ERROR FLAG (I,O).
C     SEE SUBROUTINE SCF FOR DEFINITION OF ICALL.
C     *
      USE LIMIT, ONLY: LM1, LM1M
      IMPLICIT DOUBLE PRECISION (A-H,O-Z)
      INTERFACE
         SUBROUTINE DCARTS (PA,PPA,PSA,IPSA,JPSA,NORBS,LM6,LM4L)
         IMPLICIT NONE
         INTEGER :: NORBS,LM6,LM4L
         DOUBLE PRECISION :: PA(LM4L),PPA(LM6)
         DOUBLE PRECISION, DIMENSION (:), POINTER :: PSA
         INTEGER, DIMENSION (:), POINTER :: IPSA,JPSA
         END SUBROUTINE DCARTS
C
         SUBROUTINE DIRCGS (FA,H,PA,PPA,LM6,LM4L,
     +                      NOPRT,PSA,IPSA,JPSA,ICALL)
         IMPLICIT NONE
         INTEGER LM6,LM4L,ICALL
         LOGICAL NOPRT
         DOUBLE PRECISION PPA(LM6),FA(LM4L),H(LM4L),PA(LM4L)
         DOUBLE PRECISION, DIMENSION (:), POINTER :: PSA
         INTEGER, DIMENSION (:), POINTER :: IPSA,JPSA
         END SUBROUTINE DIRCGS
C
         SUBROUTINE FRAGMS (PSA,JPSA,IPSA)
         IMPLICIT DOUBLE PRECISION (A-H,O-Z)
         DOUBLE PRECISION, POINTER :: PSA(:)
         INTEGER, POINTER :: IPSA(:),JPSA(:)
         END SUBROUTINE FRAGMS
      END INTERFACE
C
      LOGICAL NOPRT
      DOUBLE PRECISION, DIMENSION (:), POINTER :: PSA
      INTEGER, DIMENSION (:), POINTER :: IPSA,JPSA
      SAVE PSA,IPSA,JPSA
      COMMON
     ./AMASS / AMS(LM1)
     ./ATOMC / COORD(3,LM1)
     ./ATOMS / NUMAT,NAT(LM1),NFIRST(LM1),NLAST(LM1)
     ./ATSYM / IELSYM(3,7)
     ./CGRAD / CG(3,LM1+LM1M)
     ./CONSTF/ A0,AFACT,EV,EVCAL,PI,W1,W2,BIGEXP
     ./CONSTN/ ZERO,ONE,TWO,THREE,FOUR,PT5,PT25
     ./DENERG/ DENER
     ./ENERGT/ EE,ENUCLR,EAT,ATHEAT
     ./FLAG3 / KRESET,MPRINT,T2
     ./INOPT2/ IN2(300)
      COMMON
     ./LIMITS/ LM2,LM3,LM4,LM6,LM7,LM8,LM9
     ./LMSCF / LS1,LS2,LS3,LS4,LS5,LS6,LS7,LS8,LS9
     ./LMSPA4/ LM22,LM2I,LM4L
     ./MMCOM1/ EMM
     ./MMDP  / EMMDP
     ./CHCORR/ EHCORR
     ./ORBITS/ NUMB,NORBS,NMOS,NALPHA,NBETA
     ./SKIPA / ISKPA
      DIMENSION A(LM5)
      DIMENSION IEL(3),ISYM(3,LM1)
C     *
C *** INPUT OPTIONS.
      NNHCO  = IN2(20)
C     INTDIR = IN2(55)
C     LINDMS = IN2(56)
      LINFRG = IN2(58)
      KTRIAL = IN2(67)
      NPRINT = IN2(72)
      IATERG = IN2(119)
C *** CHECK WHETHER CALCULATION IS NECESSARY.
      IF(ICALL.EQ.10 .AND. NPRINT.LE.-5) RETURN
C *** CONTROL FLAGS FOR ENERGY AND GRADIENT EVALUATION.
      ICALL1 = ICALL/10
      ICALL2 = ICALL-10*ICALL1
      IF(ICALL2.EQ.2 .AND. ISKPA.LE.0) GO TO 100 
C *** INITIAL DENSITY MATRIX FROM FRAGMENT RHF-SCF CALCULATIONS.
      IF((KTRIAL.GE.0  .AND. LINFRG.GT.0) .OR.
     1   (KTRIAL.GE.30 .AND. KTRIAL.LE.39)) THEN
         CALL FRAGMS (PSA,JPSA,IPSA)
      ENDIF
C *** MOLECULAR MECHANICS CORRECTION FOR PEPTIDE BONDS.
      EMM    = 0.0D0
      IF(NNHCO.GT.0) CALL MMOKF (NNHCO)
C *** SET PRINT OPTIONS.
      NOPRT  = ICALL.GE.20 .OR. KRESET.EQ.1
      NOPRT  = NOPRT .OR.  NPRINT.LT.-4
      NOPRT  = NOPRT .AND. NPRINT.LT.5
C *** SCF PROCEDURE.
      CALL DIRCGS (A(LS6),A(LS7),A(LS8),A(LS3),LM6,LM4L,NOPRT,
     1             PSA,IPSA,JPSA,ICALL)
C     RETURN IN CASE OF SCF FAILURE.
      IF(ICALL.EQ.-1) RETURN
C *** SCF HEAT OF FORMATION.
      IF(IATERG.EQ.1) THEN
         DENER = ATHEAT+EVCAL*(EE+ENUCLR-EAT)+EMM+EMMDP+EHCORR
      ELSEIF(IATERG.EQ.-1) THEN
         DENER = EVCAL*(EE+ENUCLR)+EMM+EMMDP+EHCORR
      ENDIF
C
C *** ENERGY EVALUATION COMPLETED AT THIS POINT.
C *** CHECK OPTIONS FOR EVALUATION OF CARTESIAN GRADIENT.
  100 CONTINUE
      IF(MPRINT.GE.0) CALL CPUSEC (T2)
C     *
C *** COMPUTE FAST FINITE-DIFFERENCE GRADIENT IN CARTESIAN COORDINATES.
C     RETURN IF FULLY NUMERICAL GRADIENT IS REQUESTED (ISKPA=1).
C     RETURN IF NO GRADIENT IS REQUESTED (ICALL2=0).
C     ICALL = (01,11,21,22,31,32) AND ISKPA = 0.
      IF(ISKPA.EQ.0 .AND. ICALL2.NE.0) THEN
         CALL ASSYM (NUMAT,AMS,COORD,ISUB,MSYM,IEL,ISYM,MPRINT)
         CALL DCARTS(A(LS8),A(LS3),PSA,IPSA,JPSA,NORBS,LM6,LM4L)
      ENDIF
      RETURN
      END
