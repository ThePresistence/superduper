C     ******************************************************************
      SUBROUTINE PSCAL (P,LM2,N,MPSCAL,NPRINT,TRACEP,XEL)
C     *
C     SCALE DENSITY MATRIX SUCH THAT IT HAS THE CORRECT TRACE.
C     *
C     NOTATION. I=INPUT, O=OUTPUT, S=SCRATCH.
C     P(LM2,*)  INPUT MATRIX (I).
C     LM2       LEADING DIMENSION (I).
C     N         NUMBER OF ORBITALS (I).
C     MPSCAL    OPTION TO DETERMINE TYPE OF SCALING (I).
C               = 0 NO SCALING.
C               = 1 ADD APPROPRIATE TERM TO EACH DIAGONAL ELEMENT.
C               = 2 MULTIPLY EACH DIAGONAL ELEMENT BY SCALE FACTOR.
C               = 3 MULTIPLY COMPLETE MATRIX BY SCALE FACTOR.
C     NPRINT    PRINTING FLAG (I).
C     TRACEP    ACTUAL VALUE FOR TRACE OF INPUT MATRIX (I).
C     XEL       TARGET VALUE FOR TRACE OF INPUT MATRIX (I).
C     *
C     THIS ROUTINE IS USED TO ENSURE NORMALIZATION OF THE DENSITY  
C     MATRIX WHICH DOES NOT CONTAIN OCCUPATION NUMBER PREFACTORS:
C     P = C * C(transposed)
C     THE NUMBER OF ELECTRONS (XEL) IS THUS TWICE THE TRACE (TRACEP).
C     *
      IMPLICIT DOUBLE PRECISION (A-H,O-Z)
      PARAMETER (PT5=0.5D0)
      DIMENSION P(LM2,N)
      IF(MPSCAL.LE.0 .OR. MPSCAL.GT.3) RETURN
C *** ADD TERM TO EACH DIAGONAL ELEMENT.
      IF(MPSCAL.EQ.1) THEN
         ADD = (PT5*XEL-TRACEP)/DBLE(N)
         DO 10 I=1,N
         P(I,I) = P(I,I)+ADD
   10    CONTINUE
      ENDIF
C *** MULTIPLY EACH DIAGONAL ELEMENT BY SCALE FACTOR.
      IF(MPSCAL.EQ.2) THEN
         FAC = PT5*XEL/TRACEP
         DO 20 I=1,N
         P(I,I) = P(I,I)*FAC
   20    CONTINUE
      ENDIF
C *** MULTIPLY COMPLETE MATRIX BY SCALE FACTOR.
      IF(MPSCAL.EQ.3) THEN
         FAC = PT5*XEL/TRACEP
         DO 40 J=1,N
         DO 30 I=1,N
         P(I,J) = P(I,J)*FAC
   30    CONTINUE
   40    CONTINUE
      ENDIF
C *** DEBUG PRINT.
      IF(NPRINT.GE.7) THEN
         NB6 = 6
         WRITE(NB6,500)
         IF(MPSCAL.EQ.1) THEN
            WRITE(NB6,510) ADD
         ELSE IF(MPSCAL.EQ.2) THEN
            WRITE(NB6,520) FAC
         ELSE IF(MPSCAL.EQ.3) THEN
            WRITE(NB6,530) FAC
         ENDIF
      ENDIF
      RETURN
  500 FORMAT(1X,'THE DENSITY MATRIX HAS BEEN MODIFIED SUCH THAT',
     1       1X,'ITS TRACE REFLECTS THE NUMBER OF ELECTRONS.')
  510 FORMAT(1X,'TERM ADDED TO THE DIAGONAL ELEMENTS:',G20.10)
  520 FORMAT(1X,'SCALE FACTOR FOR THE DIAGONAL ELEMENTS:',G20.10)
  530 FORMAT(1X,'SCALE FACTOR FOR THE COMPLETE MATRIX:',G20.10)
      END
