      SUBROUTINE SCFCG (A,LM5,ICALL)
C     *
C     CONTROL ROUTINE FOR SCF CALCULATION AND CORRELATION TREATMENT.
C     CONTROL ROUTINE FOR FAST GRADIENT EVALUATION.
C     *
C     NON-SPARSE CG-DMS CODE, CONVENTIONAL OR DIRECT INTEGRALS.
C     CODE EQUIVALENT TO SCFCAL, EXCEPT FOR DIFFERENT SCF PROCEDURE.
C     *
C     NOTATION. I=INPUT, O=OUTPUT, S=SCRATCH.
C     A(LM5)    AVAILABLE BUFFER (S).
C     ICALL     CONTROL AND ERROR FLAG (I,O).
C     SEE SUBROUTINE SCF FOR DEFINITION OF ICALL.
C     *
      USE LIMIT, ONLY: LM1, LM1M
      IMPLICIT DOUBLE PRECISION (A-H,O-Z)
C
      PARAMETER (AU2KA=-1185.80676799574D0)
      EXTERNAL DHCORE
      LOGICAL CI,EIGVEC,NOPRT,PRT,REFDAT,UHF
      COMMON
     ./AMASS / AMS(LM1)
     ./ATOMC / COORD(3,LM1)
     ./ATOMS / NUMAT,NAT(LM1),NFIRST(LM1),NLAST(LM1)
     ./CGRAD / CG(3,LM1+LM1M)
     ./CONSTF/ A0,AFACT,EV,EVCAL,PI,W1,W2,BIGEXP
     ./COSMO1/ EGAS,ESOLUT,ESOLV,EDISP,EDIE
     ./COSMO2/ NCOSMO,NPSH,NPSG,NPSTOT,LMNSET
     ./DENERG/ DENER
     ./ENERGT/ EE,ENUCLR,EAT,ATHEAT
     ./FLAG3 / KRESET,MPRINT,T2
     ./FLAG4 / INTSUM
     ./GUGA2 / LCI1,LCI2
     ./INOPT2/ IN2(300)
      COMMON
     ./LIMITS/ LM2,LM3,LM4,LM6,LM7,LM8,LM9
     ./LMCSM1/ LCA1,LCB1,LCC1,LCD1
     ./LMCSM2/ LMCA1,LMCB1,LMCC1,LMCD1,LMCTOT,LMQ,NPS
     ./LMDMS / LD1,LD2,LD3,LD4,LD5
     ./LMGRAD/ LG1,LG2,LG3,LG4,LG5,LG6,LG7,LG8
     ./LMPERT/ LP3,LP4,LP5,LP6,LP9
     ./LMSCF / LS1,LS2,LS3,LS4,LS5,LS6,LS7,LS8,LS9
     ./LMUHF / LU1,LU2,LU3,LU4,LU5,LU6,LU7,LU8
      COMMON
     ./MMCOM1/ EMM
     ./MMDP  / EMMDP
     ./CHCORR/ EHCORR
     ./NBFILE/ NBF(20)
     ./ORBITS/ NUMB,NORBS,NMOS,NALPHA,NBETA
     ./PAR4  / EXCIT(20),NEXCIT,IEXCIT(4,20)
     ./PSDOPT/ RPSOPT(10),IPSOPT(28)
     ./SKIPA / ISKPA
     ./UHF   / UHF
      DIMENSION A(LM5)
      DIMENSION DUM(2)
      DIMENSION IEL(3),ISYM(3,LM1)
      DIMENSION NSET(LMNSET)
      DOUBLE PRECISION, DIMENSION (:), ALLOCATABLE :: AC
C     *
C *** INPUT OPTIONS.
      IOP    = IN2(2)
      INREFD = IN2(10)
      MMINP  = IN2(12)
      NSAV8  = IN2(15)
      NSAV13 = IN2(17)
      NSAV16 = IN2(19)
      NNHCO  = IN2(20)
      ICOSMO = IN2(28)
      INTDIR = IN2(55)
      LINDIA = IN2(57)
      LINFRG = IN2(58)
      KTRIAL = IN2(67)
      KITSCF = IN2(71)
      NPRINT = IN2(72)
      KCI    = IN2(77)
      IATERG = IN2(119)
      MMCOUP = IN2(121)
      IGRAD  = IN2(199)
      IMODE  = IN2(211)
      INOUT  = IN2(212)
      EIGVEC = LINDIA.EQ.1 .AND. KITSCF.NE.-1
C *** CHECK WHETHER CALCULATION IS NECESSARY.
      IF(ICALL.EQ.10 .AND. NPRINT.LE.-5 .AND. KCI.EQ.0
     1               .AND. INREFD.EQ.0  .AND. ICOSMO.GE.0) RETURN
C *** CONTROL FLAGS FOR ENERGY AND GRADIENT EVALUATION.
      ICALL1 = ICALL/10
      ICALL2 = ICALL-10*ICALL1
      IF(ICALL2.EQ.2 .AND. ISKPA.LE.0 .AND. ICOSMO.EQ.0) GO TO 100
C *** INITIAL DENSITY MATRIX FROM FRAGMENT RHF-SCF CALCULATIONS.
      IF((KTRIAL.GE.0 .AND. LINFRG.GT.0) .OR.
     1   (KTRIAL.GE.30 .AND. KTRIAL.LE.39)) THEN
         CALL FRAGMT (A(LS8),LM4)
      ENDIF
C *** CHECK FOR INTEGRAL-DIRECT CALCULATION.
      IF(INTDIR.GT.0) GO TO 10
C *** PREPARATIONS FOR COSMO SOLVATION TREATMENT.
      IF(ICOSMO.GT.0) THEN
         CALL CONSTS (NPS,NSET,LMNSET)
         CALL DYNCSM
         ALLOCATE (AC(LMCTOT))
         CALL AINVRS (AC(LCA1),AC(LCB1),LMCA1,LMCB1,NPS,NSET,LMNSET)
         CALL DMAT   (AC(LCA1),AC(LCB1),AC(LCC1),AC(LCD1),LMCA1,
     1                LMCB1,LMCC1,LMCD1,LMQ,NPS)
      ENDIF
C *** INTEGRAL CALCULATION FOR SCF.
      IF(IOP.GT.0) THEN
         CALL MINDO  (A(LS7),A(LS9),LM4,LM9)
      ELSE IF(INTSUM.LE.LM9 .AND. IMODE.LE.0) THEN
         CALL HCOREP (A(LS7),A(LS9),A(LG1),A(LG2),A(LG3),A(LG4),
     1                LM2,LM4,LM6,LM9)
      ELSE
         CALL HCORE  (A(LS7),A(LS9),A(LG1),A(LG2),A(LG3),A(LG4),
     1                LM2,LM4,LM6,LM9)
      ENDIF
      IF(KTRIAL.GE.20 .AND. KTRIAL.LE.27) GO TO 10
C *** STORE OM2 INTEGRALS FOR USE IN GRADIENT SECTION.
      IF((((IOP.EQ.-6 .OR. IOP.EQ.-8 .OR. IOP.EQ.-22 .OR. IOP.EQ.-23)
     1  .AND. NUMAT.GT.2) .OR. IOP.EQ.-9).AND.INOUT.GT.0) THEN
         CALL OM2SAV (0,A,LM5,0)
      ENDIF
C *** CONTRIBUTIONS FROM EXTERNAL POINT CHARGES.
      IF(MMINP.EQ.2 .AND. MMCOUP.GE.2) THEN
         CALL MMINT (A(LS7),A(LS3),LM4,LM6,ENUCLR)
      ENDIF
C *** FINITE-FIELD CONTRIBUTIONS.
      IF(ICALL.EQ.40) CALL EFPERT (A(LS7),LM4)
C *** CONTRIBUTIONS FROM COSMO SOLVATION MODEL.
      IF(ICOSMO.GT.0) THEN
         CALL ADDHCR (A(LS7),AC(LCD1),AC(LCD1),LM4,LMCD1,LMQ)
         CALL ADDNUC (ENUCLR,AC(LCD1),AC(LCD1),LMCD1,LMQ)
      ENDIF
C *** MOLECULAR MECHANICS CORRECTION FOR PEPTIDE BONDS.
   10 CONTINUE
      EMM    = 0.0D0
      IF(NNHCO.GT.0) CALL MMOKF (NNHCO)
C *** SET PRINT OPTIONS.
      NOPRT  = ICALL.GE.20 .OR. KRESET.EQ.1
      NOPRT  = NOPRT .OR.  NPRINT.LT.-4
      NOPRT  = NOPRT .AND. NPRINT.LT.5
      PRT    = .NOT.NOPRT
C *** SCF PROCEDURE.
      JCALL  = ICALL
      NROOT  = LM3
      IF(NOPRT) NROOT=NMOS
      IF(INTDIR.EQ.0) THEN
       CALL ITERCG (A(LS1),A(LU1),A(LS5),A(LU5),A(LS2),A(LU2),A(LS6),
     1              A(LU6),A(LS7),A(LS8),A(LU8),A(LS3),A(LS9),AC,
     2              A(LD1),A(LD2),A(LD3),A(LD4),A(LD5),
     3              LM2,LM3,LM4,LM6,LM9,LMCD1,LMQ,NROOT,NOPRT,ICALL)
      ELSE
        CALL DIRCG (A(LS1),A(LU1),A(LS5),A(LU5),A(LS2),A(LU2),A(LS6),
     1              A(LU6),A(LS7),A(LS8),A(LU8),A(LS3),A(LU3),AC,
     2              A(LD1),A(LD2),A(LD3),A(LD4),A(LD5),
     3              LM2,LM3,LM4,LM6,LM9,LMCD1,LMQ,NROOT,NOPRT,ICALL)
      ENDIF
C     RETURN IN CASE OF SCF FAILURE.
      IF(ICALL.EQ.-1) GO TO 200
      ICALL  = JCALL
C *** GENERATE REFERENCE DATA.
      REFDAT = ICALL.LE.11 .AND. INREFD.NE.0 .AND. KRESET.EQ.0
      IF(REFDAT .AND. EIGVEC) THEN
         CALL MOREF (A(LS1),A(LS2),LM2,LM3,LM3,NPRINT)
         IF(NEXCIT.GT.0) THEN
            CALL VERTEX (A(LS1),A(LS2),A(LS3),A(LS9),LM2,LM3,LM6,LM9)
         ENDIF
      ENDIF
C *** COSMO SOLVATION ENERGY.
      IF(ICOSMO.GT.0) THEN
         IF(PRT) THEN
            CALL DIELEN (EDIE,A(LS8),A(LU8),A(LS3),AC(LCD1),AC(LCD1),
     1                   LM4,LMCD1,LMQ)
         ENDIF
         IF(PRT .AND. NPRINT.GT.0) THEN
           CALL SCREEN (A(LS8),A(LU8),A(LS3),AC(LCA1),AC(LCA1),AC(LCB1),
     1                  LM4,LMCA1,LMQ,NPS)
         ENDIF
         IF(PRT .OR. REFDAT) THEN
            IF(EIGVEC) CALL ADDDSP (EDISP,A(LS1),A(LS2),AC(LCD1),
     1                              LM2,LM3,LMCD1,ICOSMO)
            CALL COSOLV
         ENDIF
      ENDIF
C *** ELECTROSTATIC POTENTIAL AT EXTERNAL POINTS.
      IF(MMINP.EQ.1) THEN
         CALL QMMPOT (A(LS3),A(LS8),A(LU8),A(LS3+LM6),LM4,LM6)
      ENDIF
C     ELECTRIC FIELD AT EXTERNAL POINTS.
      IF(MMINP.GT.0 .AND. MMCOUP.GE.3) THEN
         CALL QMMF (A(LS3),A(LS8),A(LU8),A(LS3+LM6),LM4,LM6)
      ENDIF
C *** SCF OUTPUT.
      IF(PRT) THEN
         CALL PRTSCF (A(LS1),A(LU1),A(LS2),A(LU2),A(LS6),A(LU6),
     1                A(LS7),A(LS8),A(LU8),LM2,LM3,LM4)
         IF(INREFD.NE.0 .AND. NEXCIT.GT.0 .AND. NPRINT.GE.5) CALL PRTEXC
      ENDIF
      CALL DIPOLE (A(LS8),A(LU8),LM4,PRT)
      IF(PRT) THEN
         IF(ICOSMO.GT.0) CALL PRTCSM (AC(LCD1),LMCD1)
         IF((NSAV13.GT.0 .OR. NSAV16.GT.0) .AND. EIGVEC)
     1   CALL GRAPH  (A(LS1),A(LU1),A(LS5),A(LU5),A(LS2),A(LU2),A(LS6),
     2                A(LU6),A(LS7),A(LS8),A(LU8),A(LS3),LM2,LM3,LM4)
         IF(NSAV8.EQ.1) CALL GRAPH2
         IF(MMINP.EQ.1) CALL QMMOUT
      ELSE IF(REFDAT .AND. EIGVEC) THEN
         CALL SCFRES (A(LS2),A(LS8),A(LU8),LM3,LM4)
      ENDIF
C *** SCF HEAT OF FORMATION.
      IF(IATERG.EQ.1) THEN
         DENER = ATHEAT+EVCAL*(EE+ENUCLR-EAT)+EMM+EMMDP+EHCORR
      ELSEIF(IATERG.EQ.-1) THEN
         DENER = EVCAL*(EE+ENUCLR)+EMM+EMMDP+EHCORR
      ENDIF
      EEP    = EE
      IF(ICOSMO.LT.0 .AND. ICALL.LE.11) EGAS=DENER
C *** CHECK FOR CORRELATION TREATMENT.
      CI     = KCI.GT.0 .OR. (KCI.LT.0 .AND. ICALL.LE.10)
      CI     = CI .AND. .NOT.UHF
      CI     = CI .AND. INTDIR.LE.0 .AND. EIGVEC
      IF(.NOT.CI) GO TO 100
C *** MODIFY ONE-CENTER TWO-ELECTRON INTEGRALS, IF NECESSARY.
      IF(IMODE.LE.0) CALL WSTORE(A(LS9),LM6,-1)
C *** MINIMAL CONFIGURATION INTERACTION.
      IABSCI = ABS(KCI)
      IF(IABSCI.EQ.1) THEN
         CALL CIS (A(LS1),A(LS2),A(LS3),A(LS9),LM2,LM3,LM6,LM9)
         IF(PRT) CALL PRTCIS
      ENDIF
C *** INTEGRAL TRANSFORMATION AND PERTURBATION TREATMENT.
      IF(IABSCI.GE.2 .AND. IABSCI.LE.4) THEN
         NB2 = NBF(2)
         NB3 = NBF(3)
         CALL PERT (A(LS1),A(LS2),A(LP3),A(LP4),A(LP5),A(LP6),A(LS9),
     1              LM2,LM3,LM6,LM7,LM8,LM9,LP9,NB2,NB3,NROOT,NOPRT)
      ENDIF
C *** GUGA-TYPE CONFIGURATION INTERACTION.
      IF(IABSCI.EQ.5) THEN
         CALL GUGA (A(LS1),A(LS2),A(LS6),A(LS7),A(LS9),LM2,LM3,LM4,LM6,
     1              NROOT,NOPRT,A,LM5,ICALL)
         IF(ICALL.EQ.-1) GO TO 200
      ENDIF
C *** FINAL HEAT OF FORMATION.
      IF(KCI.LT.0) EE=EEP
      IF(IATERG.EQ.1) THEN
         DENER = ATHEAT+EVCAL*(EE+ENUCLR-EAT)+EMM+EMMDP+EHCORR
      ELSEIF(IATERG.EQ.-1) THEN
         DENER = EVCAL*(EE+ENUCLR)+EMM+EMMDP+EHCORR
      ENDIF
      IF(ICOSMO.LT.0 .AND. ICALL.LE.11) EGAS=DENER
C
C *** ENERGY EVALUATION COMPLETED AT THIS POINT.
C *** CHECK OPTIONS FOR EVALUATION OF CARTESIAN GRADIENT.
  100 CONTINUE
      IF(MPRINT.GE.0) CALL CPUSEC (T2)
C     RETURN IF NO GRADIENT IS REQUESTED (ICALL2=0).
C     RETURN IF FULLY NUMERICAL GRADIENT IS REQUESTED (ISKPA=1).
      IF(ICALL2.EQ.0 .OR. ISKPA.EQ.1) GO TO 200
C     *
C *** COMPUTE ANALYTIC GRADIENT IN CARTESIAN COORDINATES.
C *** ANALYTICAL OR FAST GRADIENTS IN CARTESIAN COORDINATES.
C     ICALL = (01,11,21,22,31,32) AND ISKPA = (-1,-2,-3).
      IF(ISKPA.LT.0) THEN
         IPSPRT = IN2(90)
         IF(IPSPRT.EQ.0 .AND. MPRINT.LT.0) IPSOPT(5)=-1
         IF(IPSPRT.EQ.0 .AND. MPRINT.GE.5) IPSOPT(5)=1
C        PSDRV CONVENTIONS FOR IMOD:
C        001: COMPUTE ANALYTIC GRADIENT FOR ALL ATOMS.
C        201: COMPUTE ANALYTIC GRADIENT FOR ALL ATOMS AND
C        201: FOR ANY EXTERNAL POINT CHARGES THAT MAY BE PRESENT.
         IMOD = 201
C        PSDRV: FULLY ANALYTIC DRIVER FOR MNDO-TYPE METHODS.
C        PSOMX: HYBRID ANALYTIC DRIVER FOR ALL OTHER METHODS.
         IF(ISKPA.EQ.-1 .OR. ISKPA.EQ.-2) THEN
            CALL PSDRV(IMOD,CG,DUM,DUM,1,A,LM5,A(LS8),A(LU8),
     1                 A(LS1),A(LU1),LM2,A(LS2),A(LU2),A(LCI1),A(LCI2))
         ELSE IF(ISKPA.EQ.-3) THEN
            CALL PSOMX(IMOD,CG,A,LM5,A(LS8),A(LU8),
     1                 A(LS1),A(LU1),LM2,A(LS2),A(LU2),A(LCI1),A(LCI2))
         ENDIF
         IPSOPT(5) = IPSPRT
C        CONVERT FROM ATOMIC UNITS TO KCAL/(MOL*ANGSTROM).
         NUMATM = IN2(120)
         IF(MMINP.LE.0) THEN
            I3N = 3*NUMAT
         ELSE
            I3N = 3*(NUMAT+NUMATM)
         ENDIF
         CALL DSCAL(I3N,AU2KA,CG,1)
C *** COMPUTE FAST FINITE-DIFFERENCE GRADIENT IN CARTESIAN COORDINATES.
C     ICALL = (01,11,21,22,31,32) AND ISKPA = 0.
      ELSE IF(ISKPA.EQ.0) THEN
         CALL ASSYM (NUMAT,AMS,COORD,ISUB,MSYM,IEL,ISYM,MPRINT)
         CALL DCART (A(LS8),A(LU8),LM4,MSYM,IEL,ISYM,DHCORE)
C        THREE-CENTER CONTRIBUTIONS FOR OM2.
         IF(((IOP.EQ.-6.OR.IOP.EQ.-8.OR.IOP.EQ.-22.OR.IOP.EQ.-23)
     1      .AND.NUMAT.GT.2).OR.IOP.EQ.-9) THEN
            IF(INOUT.GT.0) CALL OM2SAV (0,A,LM5,1)
            CALL DCART2 (A(LG1),A(LG2),A(LG3),A(LG4),A(LG5),A(LG6),
     1                   A(LG7),A(LG8),A(LS8),A(LU8),LM2,LM4,MSYM,
     2                   IEL,ISYM)
         ENDIF
C        CONTRIBUTIONS FROM QM/MM INTERACTION TERMS.
         IF(MMINP.GT.0) THEN
            CALL QMMDER (A(LS8),A(LU8),LM4)
         ENDIF
      ENDIF
C *** GRADIENT CONTRIBUTIONS FROM COSMO SOLVATION MODEL.
      IF(ICOSMO.GT.0) THEN
         CALL DGRAD (CG,A(LS8),A(LU8),A(LS3),AC(LCA1),AC(LCA1),AC(LCB1),
     1               LM4,LMCA1,LMQ,NPS)
      ENDIF
  200 CONTINUE
C *** DEALLOCATE COSMO SCRATCH ARRAYS.
      IF(ICOSMO.GT.0) DEALLOCATE (AC)
      RETURN
      END
