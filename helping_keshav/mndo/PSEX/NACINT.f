      SUBROUTINE NACINT (C,E,F,H,W,ONEDEN,ARRAY,CG,CC,
     1                   LM2,LM3,LM4,LM5,LM6,ICALL,SCFCAL)
C     *
C *** INTERFACE TO FULLY NUMERICAL AND (PARTIALLY) ANALYTICAL
C     EVALUATION OF THE SECOND TERM (MO COEFFICIENT AND AO
C     DERIVATIVES) FOR NON-ADIABATIC COUPLING MATRIX ELEMENTS.
C     *
C     ARGUMENTS:
C       C:      MOLECULAR ORBITAL COEFFICIENTS (I).
C       E:      MOLECULAR ORBITAL ENERGIES, EV (I).
C       F:      FOCK MATRIX, EV (S).
C       H:      CORE HAMILTONIAN, EV (S).
C       ONEDEN: ONE-PARTICLE TRANSITION DENSITY MATRIX (I).
C       ARRAY:  UNNAMED COMMON (I,O,S).
C       CG:     FIRST TERM OF THE COUPLING WITHOUT ENERGY DENOMINATOR,
C               KCAL/(MOL*ANG) (I).
C       CC:     FULL NON-ADIABATIC COUPLING VECTOR, 1/ANG (O).
C       ICALL:  CALL NUMBER AS DEFINED IN SCF.f (I,O).
C       SCFCAL: EXTERNAL ROUTINE FOR ENERGY EVALUATION (I).
C
      USE LIMIT, ONLY: LM1, LM1M, LMACT, LMPROP, LMSTAT, LMGRD
      IMPLICIT DOUBLE PRECISION (A-H,O-Z)
      COMMON
     ./CIPRP / CIPROP(LMPROP,LMSTAT),ICISYM(LMSTAT)
     ./ENERGT/ EE,ENUCLR,EAT,ATHEAT
     ./GRDORG/ IGRST(LMGRD),ISTATE,JSTATE
     ./GUGA1 / NCIO,NCIGAM
     ./LMSCF / LS1,LS2,LS3,LS4,LS5,LS6,LS7,LS8,LS9
      DIMENSION C(LM2,LM3)
      DIMENSION E(LM3)
      DIMENSION F(LM4)
      DIMENSION H(LM4)
      DIMENSION W(LM6,LM6)
      DIMENSION ARRAY(LM5)
      DIMENSION ONEDEN(LMACT,NCIO)
      DIMENSION CG(3,LM1+LM1M)
      DIMENSION CC(3,LM1+LM1M)
      EXTERNAL SCFCAL
      ALLOCATABLE CMOSAV(:)
      SAVE CMOSAV,LM2LM3
      DATA LM2LM3/0/
C
C *** INITIALIZATION.
      ICALL2 = MOD(ICALL,10)
C
C *** SAVE MO COEFFICIENTS FOR RESTORE AFTER FULLY NUMERICAL NAC CALCULATION.
C     THIS IS NECESSARY BECAUSE THE PHASES OF THE MOS MUST MATCH THE CI
C     COFFICIENTS.
      IF(ICALL2.EQ.1.AND.INAC.NE.0) THEN
         IF(ALLOCATED(CMOSAV).AND.LM2*LM3.NE.LM2LM3) THEN
            DEALLOCATE(CMOSAV)
         ENDIF
         IF(.NOT.ALLOCATED(CMOSAV)) THEN
            ALLOCATE(CMOSAV(LM2*LM3))
         ENDIF
         LM2LM3 = LM2*LM3
         CMOSAV = ARRAY(LS1:LS1+LM2LM3-1)
      ENDIF
C
C *** CALCULATE SECOND TERM (CSF DERIVATIVES) OF NON-ADIABATIC COUPLING.
      IF(ICALL2.EQ.3.AND.JSTATE.NE.0.AND.ISTATE.NE.JSTATE) THEN
         ENERGI = CIPROP(1,ISTATE)
         ENERGJ = CIPROP(1,JSTATE)
         DELTAE = ENERGJ - ENERGI
         ENUSAV = ENUCLR
         IF(INAC.EQ.0) THEN
            CALL NACANA (C,E,F,H,W,ONEDEN,ARRAY,CG,CC,DELTAE,
     2                   LM2,LM3,LM4,LM5,LM6)
         ELSE
            ICALLN = 0
            CALL NACNUM (C,ONEDEN,ARRAY,CG,CC,DELTAE,
     1                   LM2,LM3,LM5,ICALLN,SCFCAL)
            IF(ICALLN.LT.0) THEN
               WRITE(NB6,'(1X,"ERROR IN SCF CALCULATION.")')
               STOP 'NACINT'
            END IF
            ARRAY(LS1:LS1+LM2LM3-1) = CMOSAV
         ENDIF
         ENUCLR = ENUSAV
      ENDIF
      END
