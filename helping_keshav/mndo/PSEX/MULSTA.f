      SUBROUTINE MULSTA (ARRAY,LM5,ICALL,SCFCAL)
C     *
C     SINGLE POINT CALCULATION OF MULTIPLE CI STATES 
C     INCLUDING ENERGIES, GRADIENTS, AND INTERSTATE
C     COUPLING GRADIENTS AS REQUIRED.
C
C     PROPERTIES CALCULATED HERE CAN BE SAVED VIA MULSAV 
C     FOR USE IN EXTERNAL OPTIMIZATION/DYNAMICS DRIVERS,
C     E.G. CHEMSHELL.
C     *
C     NOTATION. I=INPUT, O=OUTPUT, S=SCRATCH.
C     ARRAY     AVAILABLE BUFFER (S).
C     LM5       DIMENSION OF ARRAY (I).
C     ICALL     CONTROL AND ERROR FLAG (I,O).
C     SCFCAL    EXTERNAL ROUTINE FOR ENERGY EVALUATION (I).
C     *
C     ICALL DEFINES THE TYPE OF CALCULATION IN SUBROUTINE SCF.
C     SEE SUBROUTINE SCF FOR THE LIST OF POSSIBLE VALUES.
C     SEE SUBROUTINE SCFMUL FOR SPECIAL CONVENTIONS.
C     *
C     ICALL  ALSO SERVES AS AN ERROR FLAG FOR SUBROUTINE SCFCAL.
C      -1    RETURNED FROM SCFCAL IF THERE IS NO SCF CONVERGENCE
C     *
C     MULTIPLE STATE INFORMATION IS STORED IN THE FOLLOWING 
C     COMMON BLOCKS:
C
C     ENERGIES: ENERGX [ERGM]
C
C     CARTESIAN GRADIENTS: CGX [CGRADM]
C     CARTESIAN GRADIENT NORMS: CNORMX [CGRADM]
C     INTERNAL GRADIENTS: GRADX [ERGM]
C     INTERNAL GRADIENT NORMS: GNORMX [ERGM]
C
C     CARTESIAN INTERSTATE COUPLINGS: CCX [CNACVM]
C     CARTESIAN INTERSTATE COUPLING NORMS: CCNRMX [CNACVM]
C     INTERNAL INTERSTATE COUPLINGS: GCX [GNACVM]
C     INTERNAL INTERSTATE COUPLING NORMS: GCNRMX [GNACVM]
C
C     THIS LEAVES THE USUAL VARIABLES IN ERG AND CGRAD UNUSED.
C     IF THE 'STATE OF INTEREST' LROOT IS ONE OF THE GRADIENTS
C     IN IGRST, THEN THIS STATE'S INFORMATION WILL BE COPIED
C     INTO ERG AND CGRAD.
C     OTHERWISE THE FIRST STATE IN IGRST WILL BE COPIED
C     *
C     QM+MM CARTESIAN NORMS ARE ALSO CALCULATED IF APPROPRIATE
C     AND PRINTED BUT NOT STORED AT THE MOMENT:
C
C     CARTESIAN QM+MM GRADIENT NORM: CNRMTX - IN SCFMUL
C     CARTESIAN QM+MM INTERSTATE COUPLING NORM: CCNTX - HERE
C     *
      USE LIMIT, ONLY: LM1, LMV, LM1M, LMGRD, LMNAC
      IMPLICIT DOUBLE PRECISION (A-H,O-Z)
      EXTERNAL SCFCAL
      COMMON
     ./ATOMS / NUMAT,NAT(LM1),NFIRST(LM1),NLAST(LM1)
     ./CGRAD / CG(3,LM1+LM1M)
     ./CGRADM/ CGX(3,LM1+LM1M,LMGRD),CNORMX(LMGRD)
     ./CNACVM/ CCX(3,LM1+LM1M,LMNAC),CCNRMX(LMNAC)
     ./DFP   / X(LMV),NVAR
     ./ERG   / E,G(LMV),GNORM,CNORM
     ./ERGM  / ENERGX(LMGRD),GRADX(LMV,LMGRD),GNORMX(LMGRD)
     ./GNACVM/ GCX(LMV,LMNAC),GCNRMX(LMNAC)
     ./GRDORG/ IGRST(LMGRD),ISTATE,JSTATE
     ./INOPT2/ IN2(300)
     ./NBFILE/ NBF(20)
      DIMENSION ARRAY(LM5)
      DIMENSION CCNTX(LMNAC)
C *** FILE NUMBERS.
      NB6    = NBF(6)
C *** INPUT OPTIONS.
      NSAV13 = IN2(17)
      NSAV15 = IN2(18)
      IPRINT = IN2(38)
      MPRINT = IN2(41)
      NPRINT = IN2(72)
      NUMATM = IN2(120)
      LROOT  = IN2(140)
      NCIGRD = IN2(159)
      ICROSS = IN2(160)
C *** INITIALIZATION.
      ICALL1 = ICALL/10
      ICALL2 = ICALL-10*ICALL1
      I3N    = 3*NUMAT
      NUMALL = NUMAT+NUMATM
C     FOR USE WITH MULSAV
      IF(ICALL2.EQ.0) THEN
C        NO GRADIENTS AVAILABLE
         ICALLS = 0
      ELSE IF(ICROSS.EQ.1) THEN
C        MULTIPLE-STATE CARTESIAN AND 'INTERNAL' GRADIENTS AVAILABLE
         ICALLS = 1
      ELSE IF(ICROSS.EQ.2.OR.ICROSS.EQ.7) THEN
C        MULTIPLE-STATE GRADIENTS AND INTERSTATE COUPLING
C        GRADIENTS AVAILABLE
         ICALLS = 2
      ENDIF
C
C *** LOOP OVER ALL CI CALCULATIONS.
C
C     CALCULATING:
C     ENERGX(NCIGRD) - MULTIPLE STATE ENERGIES
C     CGX(3,NUMALL,NCIGRD) - CARTESIAN MULTIPLE STATE GRADIENTS
C     CNORMX(NCIGRD) - CARTESIAN GRADIENT NORMS
C     
C     VIA COMMON BLOCK ERGM:
C     GRADX(NVAR,NCIGRD) - MULTIPLE STATE GRADIENTS FOR OPT
C     GNORMX(NCIGRD) - NORMS OF MULTIPLE STATE GRADIENTS FOR OPT
C
      CALL SCFMUL (ARRAY,LM5,ICALL,SCFCAL,ENERGX,CGX,CNORMX,NUMALL)
      IF(ICALL.EQ.-1) RETURN

C *** INTERSTATE COUPLING GRADIENTS (ICROSS=2 ONLY)
C
C     CALCULATING:
C     CCX(3,NUMALL,IJ) - CARTESIAN INTERSTATE COUPLINGS
C     GCX(NVAR,IJ) - INTERSTATE COUPLING FOR OPT
C     CNORMX(IJ) - CARTESIAN COUPLING NORMS
C     GNORMX(IJ) - NORMS OF COUPLINGS FOR OPT
C     CCNTX(IJ) - CARTESIAN QM+MM COUPLING NORMS
C
      IF((ICROSS.EQ.2.OR.ICROSS.EQ.7) .AND. ICALL2.NE.0) THEN
C        INTERSTATE COUPLING GRADIENTS BETWEEN ALL RELEVANT
C        STATES ARE DETERMINED BY CALLING SCF WITH
C        THE FOLLOWING SPECIAL CONVENTIONS:
C        (1) ICALL=ICALL2=3     (USED IN SCFCAL, LOCAL)
C        (2) IN2(160)=ICROSS+10 (USED IN PSDST1 AND PSOMX, RESET).
         CALL CPUSEC (T1)
         ICALL2 = 3
         DO 30 I=2,NCIGRD
            DO 20 J=1,I-1
               ISTATE = IGRST(I)
               JSTATE = IGRST(J)
               IF(MPRINT.GE.1) THEN
                  WRITE(NB6,300)
                  WRITE(NB6,300)
                  WRITE(NB6,310)
                  WRITE(NB6,400) ISTATE, JSTATE
                  WRITE(NB6,310)
               ENDIF
               IN2(160) = ICROSS+10
               CALL SCF (ARRAY,LM5,ICALL2,SCFCAL)
               IN2(160) = ICROSS
               IF(ICALL2.EQ.-1) RETURN
               IJ = (((I-1)*(I-2))/2) + J
               CALL DCOPY (3*NUMALL,CG,1,CCX(1,1,IJ),1)
               CALL DCOPY (NVAR,G,1,GCX(1,IJ),1)
               CCNRMX(IJ) = CNORM
               GCNRMX(IJ) = GNORM
               IF(NUMATM.GT.0) THEN
                  CCNTX(IJ) = SQRT(DDOT(3*NUMALL,CG,1,CG,1))
               ENDIF
 20         CONTINUE
 30      CONTINUE
         CALL CPUSEC (T2)
      ENDIF

C *** OVERWRITE THE USUAL VARIABLES IN ERG AND CGRAD
C     WITH INFORMATION FROM THE STATE OF INTEREST LROOT
C     PROVIDING LROOT IS IN THE LIST OF GRADIENTS SPECIFIED BY IGRST
C     OTHERWISE OVERWRITE WITH INFO FROM IGRST(1)
      ILRCHK = 1
      DO 60 I = 1, NCIGRD 
         IF (IGRST(I).EQ.LROOT) ILRCHK = I
   60 CONTINUE
      E = ENERGX(ILRCHK)
      CALL DCOPY(3*NUMALL,CGX(1,1,ILRCHK),1,CG,1)
      CALL DCOPY(NVAR,GRADX(1,ILRCHK),1,G,1)
      CNORM = CNORMX(ILRCHK)
      GNORM = GNORMX(ILRCHK)

C *** SAVE RESULTS TO FILES
C     NSAV13 - LIMITED OUTPUT TO MOLDEN
C     NSAV15 - FULL MULTIPLE STATE OUTPUT
      CALL MULSAV (NSAV13,NSAV15,ICALLS,ICALLS)      

C *** PRINTING SECTION.
C     SCFMUL HAS ITS OWN PRINTING SECTION
C     SO HERE WE NEED ONLY PRINT OUT INFORMATION FROM THE 
C     INTERSTATE COUPLING CALCULATIONS
      IF(MPRINT.GE.1 .OR. NPRINT.GE.2) THEN
         IF((ICROSS.EQ.2.OR.ICROSS.EQ.7) .AND. ICALL2.NE.0) THEN
            WRITE(NB6,500) T2-T1
            WRITE(NB6,300)
            WRITE(NB6,310)
            WRITE(NB6,510)
            IF(NUMATM.GT.0) THEN
               WRITE(NB6,520)
            ELSE
               WRITE(NB6,530)
            ENDIF
            DO 80 I = 2, NCIGRD
               DO 70 J = 1, I - 1
                  IJ = (((I-1)*(I-2))/2) + J
                  IF(NUMATM.GT.0) THEN
                     WRITE(NB6,540) IJ, IGRST(I), IGRST(J), 
     1                     CCNRMX(IJ), CCNTX(IJ), GCNRMX(IJ)
                  ELSE
                     WRITE(NB6,540) IJ, IGRST(I), IGRST(J), 
     1                     CCNRMX(IJ), GCNRMX(IJ)
                  ENDIF
   70          CONTINUE
   80       CONTINUE
            WRITE(NB6,310)
         ENDIF
      ENDIF
      RETURN
  300 FORMAT (/  1X)  
  310 FORMAT (   1X,71('-'))
  400 FORMAT (   1X,'CI CALCULATION FOR INTERSTATE COUPLING OF STATES: ',
     1     I2,2X,I2)
  500 FORMAT (/////1X,'TIME FOR INTERSTATE CI RUNS  ',F10.3,' SECONDS')
  510 FORMAT (   1X,'SUMMARY OF INTERSTATE COUPLING CALCULATIONS') 
  520 FORMAT (/  1X,8X,'STATES',19X,
     1              'CARTESIAN     CARTESIAN    OPTIMIZING',
     1        /  1X,'  IJ    I    J',21X,
     1              'QM NORM    QM+MM NORM          NORM'/)
  530 FORMAT (/  1X,8X,'STATES',19X,'CARTESIAN    OPTIMIZING',
     1        /  1X,'  IJ    I    J',21X,'QM NORM          NORM'/)
  540 FORMAT (   1X,I4,2I5,14X,3F14.5)
      END
