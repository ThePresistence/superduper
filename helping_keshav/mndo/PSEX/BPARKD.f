      SUBROUTINE BPARKD (X,G,P,N,ALPHA,FUNCT,IPRINT,
     1                   ARRAY,LM5,ICALL,SCFCAL)
C     *
C     SCALE GEOMETRY STEP ACCORDING TO KLESSINGER OR MARTINEZ
C     SCHEMES AS APPROPRIATE.
C
C     REFERENCES
C     (1) R. IZZO AND M. KLESSINGER, J. COMPUT. CHEM. 21, 52-62 (2000).
C     (2) A. TONIOLO, M. BEN-NUN, AND T. J. MARTINEZ, J. PHYS. CHEM. A
C         106, 4679-4689 (2002).
C     *
      IMPLICIT DOUBLE PRECISION (A-H,O-Z)
      EXTERNAL SCFCAL
      COMMON
     ./CIMAP / IFMAP
     ./CONSTN/ ZERO,ONE,TWO,THREE,FOUR,PT5,PT25
     ./INOPT2/ IN2(300)
     ./INOPT4/ XN4(50)
     ./NBFILE/ NBF(20)
     ./OVERLY/ IOV,JOV,KOV,LOV
      DIMENSION X(N),G(N),P(N),XX(N),GG(N)
      DIMENSION ARRAY(LM5)
C *** FILE NUMBERS.
      NB6    = NBF(6)
C *** INPUT OPTIONS
      IMOMAP = IN2(156)
      MAXMAP = IN2(165)
C *** INITIALIZATION.
      TRAD   = XN4(36)
      SCALE  = XN4(37)
      IF(IPRINT.GT.0) WRITE(NB6,100)      
C *** SCALE AS REQUESTED IN INPUT FILE
      IF(SCALE.GT.ZERO) THEN
         ALPHA = SCALE
      ELSE
         ALPHA = 1.0D0
      ENDIF
C *** CALCULATE STEP SIZE 
      DSIZE = DDOT(N,P,1,P,1)
      OSIZE = SQRT(DSIZE)
      SSIZE = SQRT(DSIZE*ALPHA*ALPHA)
      IF(IPRINT.GE.0) THEN
         IF(SCALE.GT.ZERO) THEN
            WRITE(NB6,110) OSIZE
            WRITE(NB6,120) ALPHA
            WRITE(NB6,130) SSIZE
         ELSE
            WRITE(NB6,110) SSIZE
         ENDIF
      ENDIF
C *** CHECK IF TRUST REGION EXCEEDED   
      IF(TRAD.GT.ZERO .AND. SSIZE.GT.TRAD) THEN
         SKAL = TRAD/SSIZE
         ALPHA = ALPHA * SKAL
         SSIZE = SQRT(DSIZE*ALPHA*ALPHA)
         IF(IPRINT.GE.0) THEN
            WRITE(NB6,140) TRAD 
            WRITE(NB6,150) SKAL
            IF(SCALE.GT.ZERO) WRITE(NB6,160) ALPHA
            WRITE(NB6,170) SSIZE
         ENDIF
      ENDIF
C *** APPLY TOTAL SCALE FACTOR
      CALL DAXPY(N,ALPHA,P,1,X,1)
C *** INITIALIZE ORBITAL MAPPING LOOP
      IMTRY = 0
 10   CONTINUE
C *** CALCULATE NEW POINT.
      IFMAP  = 0
      ICALL  = 21
      LOV    = 2
      CALL COMPFG(N,X,FUNCT,FUNCT,G,ARRAY,LM5,ICALL,SCFCAL)
C     ONLY RETURN HERE IF MAPPING IS NOT THE CAUSE OF FAILURE
      IF(ICALL.EQ.-1 .AND. IFMAP.EQ.0) RETURN
C *** TEST FOR ORBITAL MAPPING FAILURE
C     AND TAKE A SMALLER STEP IF NECESSARY
      IF(IMOMAP.EQ.2 .AND. IFMAP.EQ.1) THEN
C        ORBITAL MAPPING HAS FAILED
         WRITE(NB6, 180)
         IMTRY = IMTRY + 1
         IF(IMTRY.EQ.MAXMAP) THEN
            WRITE(NB6,200)
C           NOTE ICALL IS ALREADY -1
            RETURN
         ENDIF
C        RETRY WITH STEP SIZE HALVED
         ALPHA = ALPHA * 0.5D0
         SSIZE = SQRT(DSIZE*ALPHA*ALPHA)
         WRITE(NB6,190) SSIZE
         CALL DAXPY(N,-ALPHA,P,1,X,1)
         GO TO 10
      ENDIF
C
      RETURN
  100 FORMAT(/  1X,'/////BPARKD: SCALING NEWTON STEP')
  110 FORMAT(/  1X,'NEWTON STEP SIZE       = ',F10.5) 
  120 FORMAT(   1X,'SCALING REQUESTED      = ',F10.5)
  130 FORMAT(   1X,'SCALED STEP SIZE       = ',F10.5)
  140 FORMAT(   1X,'EXCEEDS TRUST RADIUS   = ',F10.5)
  150 FORMAT(   1X,'TRUST SCALING APPLIED  = ',F10.5)
  160 FORMAT(   1X,'OVERALL SCALE FACTOR   = ',F10.5)
  170 FORMAT(   1X,'FINAL STEP SIZE        = ',F10.5)
  180 FORMAT(/  1X,'ORBITAL MAPPING FAILURE REPORTED')
  190 FORMAT(   1X,'RETRY WITH STEP SIZE   = ',F10.5)  
  200 FORMAT(/  1X,'ORBITAL MAPPING HAS FAILED COMPLETELY') 
      END
