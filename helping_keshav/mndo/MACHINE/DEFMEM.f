      SUBROUTINE DEFMEM (A,LEN,MODE,JPRINT)
C     *
C     DYNAMIC MEMORY MANAGEMENT FOR CRAY Y-MP UNDER UNICOS.
C     MACHINE-SPECIFIC ROUTINE ADAPTED FROM PROGRAM GRADSCF
C     WRITTEN BY ANDREW KOMORNICKI.
C     *
C     THIS ROUTINE IS CALLED TWICE FROM SUBROUTINE START.
C     SEE COMMENT LINES IN SUBROUTINE START.
C     *
C     THE FOLLOWING ROUTINES ARE CALLED FROM DEFMEM.
C     LOC     INTRINSIC FUNCTION AVAILABLE UNDER UNICOS.
C     MEMX    USER-PROVIDED FUNCTION WRITTEN IN C (SEE BELOW).
C     MEMLIM  USER-PROVIDED SUBROUTINE WRITTEN IN C (SEE BELOW).
C     *
C     CONVENTIONS FOR FUNCTION MEMX(K) AND SUBROUTINE MEMLIM(K).
C     MEMX( 0) RETURNS THE CURRENT LENGTH OF THE JOB (WORDS).
C     MEMX(+I) INCREASES THE CURRENT MEMORY BY I WORDS.
C     MEMX(-I) DECREASES THE CURRENT MEMORY BY I WORDS.
C     AN ERROR IN MEMX IS INDICATED BY A NEGATIVE FUNCTION VALUE.
C     MEMLIM(MAXL) RETURNS THE MAXIMUM AVAILABLE MEMORY (MAXL WORDS).
C     FUNCTION MEMX AND SUBROUTINE MEMLIM ARE PROVIDED AS C ROUTINES
C     IN A SEPARATE FILE.
C     *
      IMPLICIT INTEGER (A-Z)
      SAVE MAXMEM
      IF(MODE.LT.0) GO TO 10
C *** FIRST FIND OUT HOW MUCH MEMORY WE DO HAVE
      HLM    = MEMX(0)
      FLZ    = HLM
      IBL    = LOC(A)
C *** USE NEW MEMORY ROUTINE
      NETNEW = IBL+LEN-HLM+512
      NEWFL  = FLZ+NETNEW
C *** CHECK FOR OVERFLOW OF SYSTEM LIMITS
      IF(NEWFL.GT.MAXMEM) THEN
         WRITE(0,500) NEWFL,MAXMEM
         WRITE(6,500) NEWFL,MAXMEM
         STOP 'DEFMEM'
      ENDIF
C *** NOW EXPAND FIELD LENGTH AS NEEDED
      OLDFL  = MEMX(NETNEW)
      IF(OLDFL.LT.0) THEN
         WRITE(0,500) NEWFL,MAXMEM
         WRITE(6,500) NEWFL,MAXMEM
         WRITE(0,550)
         WRITE(6,550)
         STOP 'DEFMEM'
      ENDIF
C *** CHECK ON WHAT WE DID
      FLZ    = MEMX(0)
      IF(FLZ.LT.NEWFL) THEN
         WRITE(0,510) NEWFL,FLZ
         WRITE(6,510) NEWFL,FLZ
         STOP 'DEFMEM'
      ENDIF
      IF(JPRINT.EQ.5) WRITE(6,540) LEN,FLZ
      IF(JPRINT.GT.5) WRITE(6,520) LEN,HLM,FLZ,IBL
      RETURN
C *** ENTRY POINT TO INITIALIZE THE NEW MEMORY MACRO
   10 CALL MEMLIM(MAXL)
      IF(MAXL.LE.0) THEN
         MAXMEM = 1000000
      ELSE
         MAXMEM = MAXL-50000
      ENDIF
      IFL    = MEMX(0)
      LM5    = LEN
      LEN    = LEN+MAXMEM-IFL-512
      IF(JPRINT.GE.5) WRITE(6,530) LM5,LEN,IFL,MAXMEM
      RETURN
  500 FORMAT(2X,'REQUEST NEWFL ',I9,'   MAXMEM LIMIT ',I9)
  510 FORMAT(2X,'REQUEST NEWFL ',I9,'   HLM OBTAINED ',I9)
  520 FORMAT(///1X,'DEFMEM DEBUG PRINT',
     1       /  1X,'LEN REQUESTED     ',I9,
     2       /  1X,'HLM INITIAL       ',I9,
     3       /  1X,'FLZ CURRENT       ',I9,
     4       /  1X,'IBL START COMMON  ',I9)
  530 FORMAT(///1X,'DEFMEM INITIALIZATION',
     1       /  1X,'INITIAL LENGTH OF BLANK COMMON',I10,
     2       /  1X,'MAXIMUM LENGTH OF BLANK COMMON',I10,
     3       /  1X,'INITIAL LENGTH OF PROGRAM     ',I10,
     4       /  1X,'MAXIMUM LENGTH OF PROGRAM     ',I10)
  540 FORMAT(///1X,'DEFMEM MEMORY MANAGEMENT',
     1       /  1X,'CURRENT LENGTH OF BLANK COMMON',I10,
     2       /  1X,'CURRENT LENGTH OF PROGRAM     ',I10)
  550 FORMAT(/  1X,'DEFMEM MEMORY MANAGEMENT FAILED. STOP.'//)
      END
