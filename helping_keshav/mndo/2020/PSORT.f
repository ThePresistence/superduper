      SUBROUTINE PSORT (JA,JB,IA,IB,PA2,PB2,PA,PB,LM4,LMP)
C     *
C     EXTRACT DENSITY MATRIX FOR ATOM PAIR I-J.
C     OFFDIAGONAL ELEMENTS ARE MULTIPLIED BY 2.
C     *
      IMPLICIT DOUBLE PRECISION (A-H,O-Z)
      PARAMETER (PT5=0.5D0)
      DIMENSION PA2(LMP),PB2(LMP),PA(LM4),PB(LM4)
C *** ONE-CENTER ELEMENTS FROM ATOM J.
      IJ     = 0
      DO 20 K=JA,JB
      KK     = K*(K-1)/2+JA-1
CDIRX NOVECTOR
C$DIR SCALAR
*VDIR NOVECTOR
*VOCL LOOP,SCALAR
      DO 10 L=JA,K
      IJ     = IJ+1
      KK     = KK+1
      PA2(IJ)= PA(KK)+PA(KK)
      PB2(IJ)= PB(KK)+PB(KK)
   10 CONTINUE
      PA2(IJ)= PA2(IJ)*PT5
      PB2(IJ)= PB2(IJ)*PT5
   20 CONTINUE
C *** LOOP OVER REMAINING ELEMENTS.
      DO 50 K=IA,IB
      KA     = K*(K-1)/2
C     TWO-CENTER ELEMENTS.
      KK     = KA+JA-1
C$DIR SCALAR
*VDIR NOVECTOR
*VOCL LOOP,SCALAR
      DO 30 L=JA,JB
      IJ     = IJ+1
      KK     = KK+1
      PA2(IJ)= PA(KK)+PA(KK)
      PB2(IJ)= PB(KK)+PB(KK)
   30 CONTINUE
C     ONE-CENTER ELEMENTS FOR ATOM I.
      KK     = KA+IA-1
C$DIR SCALAR
*VDIR NOVECTOR
*VOCL LOOP,SCALAR
      DO 40 L=IA,K
      IJ     = IJ+1
      KK     = KK+1
      PA2(IJ)= PA(KK)+PA(KK)
      PB2(IJ)= PB(KK)+PB(KK)
   40 CONTINUE
      PA2(IJ)= PA2(IJ)*PT5
      PB2(IJ)= PB2(IJ)*PT5
   50 CONTINUE
      RETURN
      END
