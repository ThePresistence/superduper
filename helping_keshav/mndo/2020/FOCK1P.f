C     ******************************************************************
      SUBROUTINE FOCK1P (MY1,MY2,J0,F,PA,PB,LM2,LM6)
      USE LIMIT, ONLY: LM1, LMI, LMX, LMZ
      IMPLICIT DOUBLE PRECISION (A-H,O-Z)
C     *
C     ONE-CENTER TWO-ELECTRON CONTRIBUTIONS TO THE FOCK MATRIX.
C     PARALLEL (DIRECT) VERSION.
C     *
C     NOTATION.
C     MY1       FIRST ATOM OF CURRENT NODE.
C     MY2       LAST  ATOM OF CURRENT NODE.
C     J0        OFFSET FOR SECOND INDEX IN F(I,J).
C     F(LM2,*)  PARTIAL FOCK MATRIX FOR ATOMS OF CURRENT NODE.
C     PA(LM6)   ALPHA DENSITY MATRIX FOR ONE-CENTER PAIRS.
C     PB(LM6)   BETA  DENSITY MATRIX FOR ONE-CENTER PAIRS.
C     *
      LOGICAL UHF
      COMMON
     ./ATOMS / NUMAT,NAT(LM1),NFIRST(LM1),NLAST(LM1)
     ./CONSTN/ ZERO,ONE,TWO,THREE,FOUR,PT5,PT25
     ./DPARM4/ REPD(52,LMZ)
     ./DPARM5/ INTIJ(243),INTKL(243),INTREP(243),INTRF1(243),INTRF2(243)
     ./FINDX1/ IP(LMI),IP1(LMI),IP2(LMI)
     ./INDEX / INDX(LMX)
     ./INDEXW/ NW(LM1)
     ./REP   / GSS(LMZ),GPP(LMZ),GSP(LMZ),GP2(LMZ),HSP(LMZ),HPP(LMZ)
     ./UHF   / UHF
      DIMENSION F(LM2,*)
      DIMENSION PA(LM6),PB(LM6)
      DIMENSION PP(45),W(243)
C *** LOOP OVER ATOMS.
      DO 100 II=MY1,MY2
      IA     = NFIRST(II)
      IORBS  = NLAST(II)-IA+1
      NI     = NAT(II)
      NWII   = NW(II)-1
C *** CODE FOR SP BASIS.
C     F(S,S) - CONTRIBUTION FROM GSS.
      KS     = NWII+1
      F(IA,IA-J0) = F(IA,IA-J0)+PB(KS)*GSS(NI)
      IF(IORBS.EQ.1) GO TO 100
      IX     = IA+1
      IY     = IA+2
      IZ     = IA+3
      KX     = NWII+3
      KY     = NWII+6
      KZ     = NWII+10
C     F(S,S) - CONTRIBUTIONS FROM GSP AND HSP.
      QA     = PA(KX)+PA(KY)+PA(KZ)
      QAB    = QA+PB(KX)+PB(KY)+PB(KZ)
      F(IA,IA-J0) = F(IA,IA-J0)+QAB*GSP(NI)-QA*HSP(NI)
C     F(P,P) - CONTRIBUTIONS FROM GSP,HSP,GPP,GP2,HPP.
      FROMSS =(PA(KS)+PB(KS))*GSP(NI)-PA(KS)*HSP(NI)
      QXX    = PA(KX)+PB(KX)
      QYY    = PA(KY)+PB(KY)
      QZZ    = PA(KZ)+PB(KZ)
      F(IX,IX-J0) = F(IX,IX-J0)+FROMSS+PB(KX)*GPP(NI)+(QYY+QZZ)*GP2(NI)
     1                         -(PA(KY)+PA(KZ))*HPP(NI)
      F(IY,IY-J0) = F(IY,IY-J0)+FROMSS+PB(KY)*GPP(NI)+(QXX+QZZ)*GP2(NI)
     1                         -(PA(KX)+PA(KZ))*HPP(NI)
      F(IZ,IZ-J0) = F(IZ,IZ-J0)+FROMSS+PB(KZ)*GPP(NI)+(QXX+QYY)*GP2(NI)
     1                         -(PA(KX)+PA(KY))*HPP(NI)
C     F(S,P) - CONTRIBUTIONS FROM GSP AND HSP.
      TWOHSP = TWO*HSP(NI)
      HSPGSP = HSP(NI)+GSP(NI)
      M      = KS+1
      F(IX,IA-J0) = F(IX,IA-J0)+(PA(M)+PB(M))*TWOHSP-PA(M)*HSPGSP
      M      = KX+1
      F(IY,IA-J0) = F(IY,IA-J0)+(PA(M)+PB(M))*TWOHSP-PA(M)*HSPGSP
      M      = KY+1
      F(IZ,IA-J0) = F(IZ,IA-J0)+(PA(M)+PB(M))*TWOHSP-PA(M)*HSPGSP
C     F(P,P*)- CONTRIBUTIONS FROM GP2 AND HPP.
      TWOHPP = TWO*HPP(NI)
      HPPGP2 = HPP(NI)+GP2(NI)
      M      = KX+2
      F(IY,IX-J0) = F(IY,IX-J0)+(PA(M)+PB(M))*TWOHPP-PA(M)*HPPGP2
      M      = KY+2
      F(IZ,IX-J0) = F(IZ,IX-J0)+(PA(M)+PB(M))*TWOHPP-PA(M)*HPPGP2
      M      = KY+3
      F(IZ,IY-J0) = F(IZ,IY-J0)+(PA(M)+PB(M))*TWOHPP-PA(M)*HPPGP2
C *** CODE FOR SPD BASIS.
C     CONTRIBUTIONS FROM INTEGRALS INVOLVING D ORBITALS.
C     IN THE RHF CASE, THE EXCHANGE CONTRIBUTIONS ARE INCLUDED
C     BY USING MODIFIED RAFFENETTI-TYPE INTEGRALS.
      IF(IORBS.EQ.4) GO TO 100
      IF(UHF) THEN
         DO 10 INTG=1,243
         W(INTG) = REPD(INTREP(INTG),NI)
   10    CONTINUE
      ELSE
         DO 20 INTG=1,243
         INT1 = INTRF1(INTG)
         INT2 = INTRF2(INTG)
         W(INTG) = REPD(INTREP(INTG),NI)
         IF(INT1.GT.0) W(INTG) = W(INTG)-PT25*REPD(INT1,NI)
         IF(INT2.GT.0) W(INTG) = W(INTG)-PT25*REPD(INT2,NI)
   20    CONTINUE
      ENDIF
C     ONE-CENTER MATRIX ELEMENTS OF TOTAL DENSITY.
      DO 30 KLW=1,45
      KL     = KLW+NWII
      PP(KLW)= PA(KL)+PB(KL)
      IF(IP1(KL).NE.IP2(KL)) PP(KLW)=PP(KLW)*TWO
   30 CONTINUE
C     COULOMB CONTRIBUTIONS TO THE FOCK MATRIX.
      DO 40 INTG=1,243
      IJ     = INTIJ(INTG)+NWII
      I      = IP1(IJ)
      J      = IP2(IJ)
      KLW    = INTKL(INTG)
      F(I,J-J0) = F(I,J-J0)+PP(KLW)*W(INTG)
   40 CONTINUE
      IF(.NOT.UHF) GO TO 100
C     EXCHANGE CONTRIBUTIONS TO THE FOCK MATRIX IN THE UHF CASE.
      DO 50 INTG=1,243
      IJ     = INTIJ(INTG)+NWII
      KL     = INTKL(INTG)+NWII
      I      = IP1(IJ)-IA+1
      J      = IP2(IJ)-IA+1
      K      = IP1(KL)-IA+1
      L      = IP2(KL)-IA+1
      IF(I.LT.L) GO TO 50
      IF(I.GE.K) THEN
         JL  = NWII+INDX(MAX(J,L))+MIN(J,L)
         F(I,K-J0) = F(I,K-J0)-PA(JL)*W(INTG)
      ENDIF
      IF(I.GE.L .AND. K.NE.L) THEN
         JK  = NWII+INDX(MAX(J,K))+MIN(J,K)
         F(I,L-J0) = F(I,L-J0)-PA(JK)*W(INTG)
      ENDIF
      IF(J.GE.K .AND. I.NE.J) THEN
         IL  = NWII+INDX(MAX(I,L))+MIN(I,L)
         F(J,K-J0) = F(J,K-J0)-PA(IL)*W(INTG)
      ENDIF
      IF(J.GE.L .AND. I.NE.J .AND. K.NE.L) THEN
         IK  = NWII+INDX(MAX(I,K))+MIN(I,K)
         F(J,L-J0) = F(J,L-J0)-PA(IK)*W(INTG)
      ENDIF
   50 CONTINUE
  100 CONTINUE
      RETURN
      END
