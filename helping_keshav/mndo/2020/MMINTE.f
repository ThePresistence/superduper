      SUBROUTINE MMINTE (H,HM,PA,PB,LM4,LM6)
C     *
C     ELECTROSTATIC CONTRIBUTIONS FROM EXTERNAL POINT CHARGES
C     TO THE ELECTRONIC ENERGY AND THE CORE-CORE REPULSIONS
C     FOR A GIVEN FIXED ELECTRONIC DENSITY IN THE QM REGION.
C     *
C     NOTATION. I=INPUT, O=OUTPUT, S=SCRATCH.
C     H(LM4)    CORE HAMILTONIAN MATRIX (S).
C     HM(LM6)   CONTRIBUTIONS FROM GIVEN POINT CHARGE (S).
C     PA(LM4)   RHF OR UHF-ALPHA DENSITY MATRIX (S).
C     PB(LM4)   UHF-BETA DENSITY MATRIX (S).
C     *
C     RESULTS ARE SAVED IN COMMON BLOCK ENERGT.
C     EEQMMM    CONTRIBUTION TO ELECTRONIC ENERGY (O).
C     ENUCLR    CONTRIBUTION TO CORE-CORE REPULSION ENERGY (O).
C     *
      IMPLICIT DOUBLE PRECISION (A-H,O-Z)
      LOGICAL UHF
      COMMON
     ./CONSTN/ ZERO,ONE,TWO,THREE,FOUR,PT5,PT25
     ./ENERGT/ EEQMMM,ENUCLR,EAT,ATHEAT
     ./INOPT2/ IN2(300)
     ./NBFILE/ NBF(20)
     ./ORBITS/ NUMB,NORBS,NMOS,NALPHA,NBETA
     ./UHF   / UHF
      DIMENSION H(LM4),HM(LM6),PA(LM4),PB(LM4)
C *** FILE NUMBERS.
      NB6    = NBF(6)
      NB11   = NBF(11)
C *** INPUT OPTIONS.
      KTRIAL = IN2(67)
C *** INITIALIZATION.
      EEQMMM = ZERO
      ENUCLR = ZERO
      H(1:LM4) = ZERO
C *** COMPUTE ELECTROSTATIC CONTRIBUTIONS FROM EXTERNAL POINT CHARGES
C     TO THE CORE HAMILTONIAN MATRIX AND THE CORE-CORE REPULSIONS.
      CALL MMINT (H,HM,LM4,LM6,ENUCLR)
C *** READ DENSITY MATRIX FROM FILE NB11.
      IF(KTRIAL.EQ.11) THEN
         NB11 = NBF(11)
         REWIND NB11
         READ(NB11,ERR=100,END=100) PA
         IF(UHF) READ(NB11,ERR=100,END=100) PB
      ELSE
         GO TO 110
      ENDIF
C *** CALCULATE THE CONTRIBUTION TO THE ELECTRONIC ENERGY.
      CALL ESCF (EHA,NORBS,PA,H,LM4)
      IF(UHF) THEN
         CALL ESCF (EHB,NORBS,PB,H,LM4)
      ELSE
         EHB = EHA
      ENDIF
      EEQMMM = TWO*(EHA+EHB)
      RETURN
C *** ERROR EXITS.
  100 CONTINUE
      WRITE(NB6,500) NB11
      STOP 'MMINTE'
  110 CONTINUE
      WRITE(NB6,510)
      STOP 'MMINTE'
  500 FORMAT (///1X,'ERROR OR END-OF-FILE WHEN READING FILE',I3,
     1           1X,'IN SUBROUTINE MMINTE.'/)
  510 FORMAT (///1X,'COMPUTATION WITH FIXED DENSITY (KITSCF=-3)',
     1           1X,'REQUIRES INPUT OF DENSITY (KTRIAL=11).'/)
      END
