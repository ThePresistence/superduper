      SUBROUTINE INTENS (B,APT,X,EIG,I3N,NVIB,LPRINT)
C     *
C     CALCULATION OF INFRARED INTENSITIES.
C     *
C     NOTATION. I=INPUT, O=OUTPUT, S=SCRATCH.
C     B         CARTESIAN NORMAL COORDINATES MULTIPLIED BY CARTESIAN
C               G-MATRIX, IN AMU**(-1/2) (I).
C     APT       CARTESIAN DIPOLE DERIVATIVES, IN ATOMIC UNITS (I).
C     X         BUFFER FOR DIPOLE DERIVATIVES AND IR INTENSITIES (O,S).
C     EIG       HARMONIC WAVENUMBERS, IN CM-1 (I).
C     I3N       NUMBER OF CARTESIAN COORDINATES (I).
C     NVIB      INDEX OF FIRST VIBRATION (I), USUALLY NVIB=7.
C     LPRINT    PRINTING FLAG (I).
C     *

      IMPLICIT DOUBLE PRECISION (A-H,O-Z)
      COMMON /NBFILE/ NBF(20)
      DIMENSION B(I3N,I3N),APT(I3N,3),X(I3N,3),EIG(I3N)
C     THE FOLLOWING CONVERSION FACTORS ARE DERIVED FROM
C     J.PHYS.CHEM.REF.DATA 2,663(1973)
      DATA CFAC/4.803242D0/
      DATA CKM/974.8644D0/
      DATA ZERO/0.0D0/
C *** FILE NUMBERS.
      NB6    = NBF(6)
C     *
C     PRINT CARTESIAN DIPOLE DERIVATIVES.
      IF(LPRINT.GT.0) THEN
         WRITE(NB6,500)
         WRITE(NB6,510)
         WRITE(NB6,520)
         CALL MATPRT(APT,I3N,3,I3N,3)
      ENDIF
      DO 11 I=1,I3N
      DO 10 J=1,3
      X(I,J) = APT(I,J)*CFAC
   10 CONTINUE
   11 CONTINUE
      IF(LPRINT.GT.-1) THEN
         WRITE(NB6,510)
         WRITE(NB6,530)
         CALL MATPRT(X,I3N,3,I3N,3)
      ENDIF
C     *
C     TRANSFORM DIPOLE DERIVATIVES FROM CARTESIAN COORDINATES (APT)
C     TO MASS-WEIGHTED NORMAL COORDINATES (X).
      DO 31 I=1,I3N
      DO 30 J=1,3
      SUM    = ZERO
      DO 20 K=1,I3N
      SUM    = SUM+B(K,I)*APT(K,J)
   20 CONTINUE
      X(I,J) = SUM
   30 CONTINUE
   31 CONTINUE
      IF(LPRINT.GT.0) THEN
         WRITE(NB6,540)
         WRITE(NB6,520)
         CALL MATPRT(X,I3N,3,I3N,3)
      ENDIF
      DO 41 I=1,I3N
      DO 40 J=1,3
      X(I,J) = X(I,J)*CFAC
   40 CONTINUE
   41 CONTINUE
      IF(LPRINT.GT.-2) THEN
         WRITE(NB6,540)
         WRITE(NB6,530)
         CALL MATPRT(X,I3N,3,I3N,3)
      ENDIF
C     *
C     FORM SUMS OF SQUARES OF DIPOLE DERIVATIVES.
      CF2    = CFAC*CFAC
      RCF2   = 1.0D0/CF2
      DO 110 I=1,I3N
      ZIR    = X(I,1)*X(I,1)+X(I,2)*X(I,2)+X(I,3)*X(I,3)
      X(I,1) = ZIR*RCF2
  110 CONTINUE
      XIRMAX = ZERO
      DO 120 I=NVIB,I3N
      XIRMAX = MAX(XIRMAX,X(I,1))
  120 CONTINUE
      IF(LPRINT.GT.0) THEN
         WRITE(NB6,550)
         WRITE(NB6,570)
         DO 130 I=NVIB,I3N
         ZIR = X(I,1)*CF2
         WRITE(NB6,590) I,EIG(I),X(I,1),ZIR
  130    CONTINUE
      ENDIF
C     *
C     CONVERT TO IR INTENSITIES.
      DO 140 I=1,I3N
      X(I,1) = X(I,1)*CKM
  140 CONTINUE
      IF(LPRINT.LT.-4) RETURN
      XIRMAX = XIRMAX*CKM
      WRITE(NB6,560)
      WRITE(NB6,580)
      NVIB1  = NVIB
      IF(LPRINT.LT.-1) NVIB1=1
      IF(XIRMAX.NE.ZERO) THEN
         DO 150 I=NVIB1,I3N
         XIRREL = X(I,1)/XIRMAX
         WRITE(NB6,600) I,EIG(I),X(I,1),XIRREL
 150     CONTINUE
      ELSE
         DO 160 I=NVIB1,I3N
         WRITE(NB6,600) I,EIG(I),X(I,1),ZERO
 160     CONTINUE
      ENDIF
      RETURN
  500 FORMAT(1X)
  510 FORMAT(//12X,'   DIPOLE DERIVATIVES IN     ',
     1       / 12X,'   CARTESIAN COORDINATES     ')
  520 FORMAT(  12X,'      (ATOMIC UNITS)         ')
  530 FORMAT(  12X,'     (DEBYE/ANGSTROM)        ')
  540 FORMAT(//12X,'   DIPOLE DERIVATIVES IN     ',
     1       / 12X,'CARTESIAN NORMAL COORDINATES ')
  550 FORMAT(//15X,'     SUM OF SQUARES OF       ',
     1       / 15X,'   DIPOLE DERIVATIVES IN     ',
     2       / 15X,'CARTESIAN NORMAL COORDINATES ')
  560 FORMAT(//15X,'          INFRARED           ',
     1       / 15X,'  VIBRATIONAL INTENSITIES    ')
  570 FORMAT(/  4X,'MODE',4X,'WAVENUMBER',6X,'A.U.',7X,'(D/A)**2'/)
  580 FORMAT(/  4X,'MODE',4X,'WAVENUMBER',7X,'KM/MOL',5X,'RELATIVE'/)
  590 FORMAT(   1X,I6,2X,F13.5,2F13.8)
  600 FORMAT(   1X,I6,2X,F13.5,F13.3,F13.5)
      END
