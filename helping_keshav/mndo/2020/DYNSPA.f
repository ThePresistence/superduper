      SUBROUTINE DYNSPA (LEN,LFA,LM5X,LM5R)
C     *
C     DYNAMIC MEMORY ALLOCATION FOR DIRECT SCF CALCULATION
C     USING A CONJUGATE GRADIENT DENSITY MATRIX SEARCH
C     IN CONNECTION WITH SPARSE MATRIX TECHNIQUES.
C     *
C     NOTATION.
C     LEN  = AVAILABLE LENGTH OF BLANK COMMON AREA (INPUT VALUE,
C            NOT MODIFIED IN THIS ROUTINE).
C     LFA  = LAST ADDRESS OF BLANK COMMON AREA THAT HAS PREVIOUSLY
C            BEEN RESERVED (INPUT VALUE, NOT MODIFIED IN THIS ROUTINE).
C     LM5X = LAST ADDRESS OF BLANK COMMON AREA THAT IS ACTUALLY USED
C            BY THE ARRAYS DEFINED PRESENTLY (DETERMINED HERE).
C     LM5R = LAST ADDRESS OF BLANK COMMON AREA THAT NEEDS TO REMAIN
C            RESERVED LATER ON (DETERMINED IN THIS ROUTINE).
C     *
      USE LIMIT, ONLY: LM1
      IMPLICIT DOUBLE PRECISION (A-H,O-Z)
      COMMON
     ./ATOMS / NUMAT,NAT(LM1),NFIRST(LM1),NLAST(LM1)
     ./FLAG4 / INTSUM
     ./INOPT2/ IN2(300)
     ./LIMITS/ LM2,LM3,LM4,LM6,LM7,LM8,LM9
     ./LMSCF / LS1,LS2,LS3,LS4,LS5,LS6,LS7,LS8,LS9
     ./LMSPA4/ LM22,LM2I,LM4S
     ./LMUHF / LU1,LU2,LU3,LU4,LU5,LU6,LU7,LU8
     ./NBFILE/ NBF(20)
     ./ORBITS/ NUMB,NORBS,NMOS,NALPHA,NBETA
C     *
C *** FILE NUMBERS.
      NB6    = NBF(6)
C *** INPUT OPTIONS.
      IOP    = IN2(2)
      JPRINT = IN2(42)
C *** INITIALIZATION.
      LM5R   = LFA
C     *
C *** DETERMINE MAXIMUM SIZE OF LOCAL BUFFER FOR DIRECT COMPUTATION
C     OF CORE HAMILTONIAN AND FOCK MATRIX (HFOCKP).
C     MAXIMUM NUMBER OF ORBITALS PER ATOM.
      MAXORB = 1
      DO 10 I=1,NUMAT
      MAXORB = MAX(MAXORB,NLAST(I)-NFIRST(I)+1)
   10 CONTINUE
C     MAXIMUM SIZE OF LOCAL SCF ARRAYS (HFOCKP).
      LM4S   = MAXORB*NORBS
C     MAXIMUM SIZE OF LOCAL BUFFER (HFOCKP).
      LSA = 3*LM4S + LM6
C     CHECK AVAILABLE MEMORY.
      LM5L   = LFA + LSA
      IF(LM5L.GT.LEN) THEN
         WRITE(NB6,900)
         STOP 'DYNSPA'
      ENDIF
      LM5X   = LM5L
C     *
C *** ASSIGN ADDRESSES FOR LOCAL SCF ARRAYS (HFOCKP).
C     THESE LOCAL SCF ARRAYS START AT THE FOLLOWING ADDRESSES.
C     RHF: Q  LS3, F  LS6, H LS7, P  LS8.
C     THE ORDER OF THE ARRAYS IN MEMORY IS INDICATED BY COMMENTS BELOW.
C     THE TRADITIONAL NOTATION IS KEPT FOR THE RELEVANT ARRAYS.
C *** RHF: P(LM4S),F(LM4S),H(LM4S),Q(LM6).
      LS8    = LFA+1
      LS6    = LS8+LM4S
      LS7    = LS6+LM4S
      LS3    = LS7+LM4S
      LU8    = LS8
      LU3    = LS3
C     *
C *** DEFINE NUMBER OF TWO-CENTER TWO-ELECTRON INTEGRALS.
C     FOR PRINTING PURPOSES ONLY.
      INTTOT = LM6*LM6
      INTSUM = 0
      DO 40 I=2,NUMAT
      ITEMP  = NLAST(I)-NFIRST(I)+1
      IW     = (ITEMP*(ITEMP+1))/2
      DO 30 J=1,I-1
      JTEMP  = NLAST(J)-NFIRST(J)+1
      JW     = (JTEMP*(JTEMP+1))/2
      INTSUM = INTSUM+IW*JW
   30 CONTINUE
   40 CONTINUE
      IF(IOP.GT.0) INTSUM=(NUMAT*(NUMAT+1))/2
C     DEFINE BUFFER FOR TWO-ELECTRON INTEGRALS.
C     NO SUCH BUFFER IN INTEGRAL-DIRECT METHODS.
      LM9 = 0
C     *
C *** PRINTING SECTION.
      IF(JPRINT.LT.2) RETURN
C     GENERAL CONTROL VARIABLES.
      IF(LM2.EQ.NORBS) THEN
         WRITE(NB6,510) LM2,LM3,LM4,LM6
      ELSE
         WRITE(NB6,520) NORBS,LM3,LM2,LM4,LM6
      ENDIF
      IF(IOP.GT.0) THEN
         WRITE(NB6,530) INTSUM
      ELSE
         WRITE(NB6,540) INTTOT,INTSUM,LM9
      ENDIF
C     MEMORY ALLOCATION FOR LOCAL ARRAYS.
      WRITE(NB6,610) LS8,LS8+LM4S-1,LM4S,
     1               LS6,LS6+LM4S-1,LM4S,LS7,LS7+LM4S-1,LM4S,
     2               LS3,LS3+LM6 -1,LM6
      RETURN
  510 FORMAT(///1X,'NUMBER OF BASIS ORBITALS                   ',I10,
     1       /  1X,'NUMBER OF EIGENVECTORS COMPUTED            ',I10,
     2       /  1X,'DIMENSION OF LOWER TRIANGLE MATRIX IN SCF  ',I10,
     3       /  1X,'NUMBER OF ONE-CENTER AO PAIRS              ',I10)
  520 FORMAT(///1X,'NUMBER OF BASIS ORBITALS                   ',I10,
     1       /  1X,'NUMBER OF EIGENVECTORS COMPUTED            ',I10,
     2       /  1X,'ROW DIMENSION OF SQUARE MATRICES IN SCF    ',I10,
     3       /  1X,'DIMENSION OF LOWER TRIANGLE MATRIX IN SCF  ',I10,
     4       /  1X,'NUMBER OF ONE-CENTER AO PAIRS              ',I10)
  530 FORMAT(   1X,'NUMBER OF TWO-ELECTRON INTEGRALS           ',I10)
  540 FORMAT(   1X,'NUMBER OF TWO-ELECTRON INTEGRALS (TOTAL)   ',I10,
     1       /  1X,'NUMBER OF TWO-ELECTRON INTEGRALS (UNIQUE)  ',I10,
     2       /  1X,'NUMBER OF TWO-ELECTRON INTEGRALS (STORED)  ',I10)
  610 FORMAT(///1X,'MEMORY ALLOCATION FOR LOCAL RHF-SCF ARRAYS',
     1       // 1X,'    START    FINAL   LENGTH    CONTENTS',
     2       // 1X,3I9,4X,'DENSITY MATRIX',
     3       /  1X,3I9,4X,'FOCK MATRIX',
     4       /  1X,3I9,4X,'CORE HAMILTONIAN MATRIX',
     5       /  1X,3I9,4X,'ONE-CENTER DENSITY MATRIX ELEMENTS')
  900 FORMAT(///1X,'*** ERROR ****',
     1       // 1X,'DUE TO INSUFFICIENT MEMORY THE PROGRAM CANNOT ',
     2             'CARRY OUT THE SCF CALCULATION.')
      END
