      SUBROUTINE MBONDS (P,F,C,LM2,LM4,LMC,NB16,NSAV16,NPRINT,MBO)
C     *
C     CALCULATION OF BOND INDICES AND VALENCIES ACCORDING TO
C     D.R.ARMSTRONG, P.G.PERKINS, AND J.J.P.STEWART,
C     J.CHEM.SOC.DALTON 838 (1973).
C     ADAPTED FROM MOPAC(6.0) WRITTEN BY J.J.P.STEWART.
C     *
C     IMPLEMENTED ONLY FOR RHF WAVEFUNCTIONS (MBO=3 FOR RHF).
C     *
      USE LIMIT, ONLY: LM1
      IMPLICIT DOUBLE PRECISION (A-H,O-Z)
      COMMON
     ./ATOMS / NUMAT,NAT(LM1),NFIRST(LM1),NLAST(LM1)
     ./NBFILE/ NBF(20)
     ./ORBITS/ NUMB,NORBS,NMOS,NALPHA,NBETA
      DIMENSION P(LM4),F(LM2,LM2),C(LMC)
C *** CHECK SPECIAL CASES.
C     FIRST  CASE. RETURN SINCE NO OUTPUT WHATSOEVER IS REQUESTED.
C     SECOND CASE. RETURN FOR UHF AFTER PUTTING A FLAG ON FILE NB16.
      IF(NSAV16.NE.1 .AND. NPRINT.LT.2) RETURN
      IF(MBO.NE.3) THEN
         IF(NSAV16.EQ.1) WRITE(NB16,510) MBO
         RETURN
      ENDIF
C *** STORE LOWER TRIANGLE MATRIX P(LM4) AS SQUARE MATRIX F(LM2,LM2).
      CALL SQUARE (P,F,NORBS,LM2,LM4)
C *** CALCULATE BOND INDICES.
C     NOTE THAT THE RHF DENSITY IS TOO SMALL BY A FACTOR OF 2, BY OUR
C     USUAL CONVENTIONS. THIS AFFECTS THE PREFACTORS IN DO 20,30,40.
      IJ     = 0
      DO 50 I=1,NUMAT
      IA     = NFIRST(I)
      IB     = NLAST(I)
      DO 30 J=1,I
      IJ     = IJ+1
      JA     = NFIRST(J)
      JB     = NLAST(J)
      X      = 0.0D0
      DO 20 IL=IA,IB
      DO 10 IH=JA,JB
      X      = X+F(IL,IH)*F(IL,IH)
   10 CONTINUE
   20 CONTINUE
      C(IJ)  = X*4.0D0
   30 CONTINUE
      X      =-C(IJ)
      DO 40 J=IA,IB
      X      = X+4.0D0*F(J,J)
   40 CONTINUE
      C(IJ)  = X
   50 CONTINUE
C *** PRINT BOND INDICES.
      IF(NPRINT.GE.2) THEN
         NB6 = NBF(6)
         WRITE(NB6,500)
         CALL VECPRT(C,LMC,NUMAT)
      ENDIF
C *** WRITE BOND INDICES TO THE SYBYL MOPAC OUTPUT FILE.
      IF(NSAV16.EQ.1) THEN
         WRITE(NB16,510) MBO
         DO 60 I=1,NUMAT
         INDS   = I*(I-1)/2+1
         INDF   = INDS+I-1
         WRITE(NB16,520) (C(J),J=INDS,INDF)
   60    CONTINUE
      ENDIF
      RETURN
  500 FORMAT(//5X,'BOND ORDERS AND VALENCIES: MOPAC CONVENTIONS'/)
  510 FORMAT(I4)
  520 FORMAT(10F8.4)
      END
