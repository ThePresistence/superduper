      SUBROUTINE INPNMR (JPRINT)
C     *
C     INPUT OF NMR CONTROL RECORDS AND INITIALIZATION.
C     *
C     NOTATION. I=INPUT, O=OUTPUT.
C     JPRINT    PRINTING FLAG (I).
C     *
C     OUTPUT IN COMMON BLOCKS (PSNICS,PSNMRI).
C     INC()     LOGICAL FLAGS FOR NMR COMPUTATION.
C     COORDN()  CARTESIAN COORDINATES OF NICS POINTS (ANGSTROM).
C     NNICS     TOTAL NUMBER OF NICS POINTS.
C     *
      USE LIMIT, ONLY: LM1, LMZ, MXNICS
      IMPLICIT DOUBLE PRECISION (A-H,O-Z)
      PARAMETER (MAXCNT=15)
      LOGICAL INC
      CHARACTER*100 PARMS
      COMMON
     ./ATOMC / COORD(3,LM1)
     ./ATOMS / NUMAT,NAT(LM1),NFIRST(LM1),NLAST(LM1)
     ./CONSTN/ ZERO,ONE,TWO,THREE,FOUR,PT5,PT25
     ./IJWORK/ NNN(LM1),IJKDUM(LM1*11)
     ./INOPT2/ IN2(300)
     ./NBFILE/ NBF(20)
     ./PARM1 / A(3,LM1),NA(LM1),NB(LM1),NC(LM1),NN(LM1),NATOMS
     ./PSNICS/ COORDN(3,MXNICS),NNICS
     ./PSNMRI/ INC(LM1)
C    ./PSNPRD/ XBETAN(LMZ,3),XZETAN(LMZ,3),XZETAS(LMZ,3),XBETAS(LMZ,3),
C    .         XUS(LMZ,3),XSCALN(0:LMZ),XOFFN(0:LMZ,2),IDEFN(0:LMZ)
      DIMENSION ICNT(MAXCNT)
C
C *** FILE NUMBERS.
      NB5    = NBF(5)
      NB6    = NBF(6)
C *** INPUT OPTIONS.
      NMRNUC = IN2(118)
C *** INITIALIZATION.
      NNICS  = 0
      DO 10 I=1,NUMAT
      INC(I) = .TRUE.
      NNN(I) = I
   10 CONTINUE
      IF(NATOMS.GT.NUMAT) THEN
         K  = 0
         DO 15 I=1,NATOMS
         IF(NN(I).NE.99) THEN
            K = K+1
            NNN(I) = K
         ENDIF
   15    CONTINUE
      ENDIF
      IF(NMRNUC.GE.1 .AND. NMRNUC.LE.5) GO TO 200
C
C *** READ NEXT RECORD.
   20 READ(NB5,500,END=300) ICMD,PARMS
      IF(ICMD.EQ.0) GO TO 300
C
C     EXCLUDE ALL CENTERS.
      IF(ICMD.EQ.001) THEN
         DO 30 I=1,NUMAT
         INC(I) = .FALSE.
   30    CONTINUE
C     INCLUDE ALL CENTERS.
      ELSE IF(ICMD.EQ.011) THEN
         DO 40 I=1,NUMAT
         INC(I) = .TRUE.
   40    CONTINUE
C     EXCLUDE SPECIFIC CENTERS.
      ELSE IF(ICMD.EQ.002) THEN
         READ(PARMS,510) ICNT
         DO 50 I=1,MAXCNT
         IF(ICNT(I).GT.0 .AND. ICNT(I).LE.NATOMS) THEN
            NNC = NNN(ICNT(I))
            INC(NNC) = .FALSE.
         ENDIF
   50    CONTINUE
C     INCLUDE SPECIFIC CENTERS.
      ELSE IF(ICMD.EQ.012) THEN
         READ(PARMS,510) ICNT
         DO 60 I=1,MAXCNT
         IF(ICNT(I).GT.0 .AND. ICNT(I).LE.NATOMS) THEN
            NNC = NNN(ICNT(I))
            INC(NNC) = .TRUE.
         ENDIF
   60    CONTINUE
C     EXCLUDE SPECIFIC ELEMENTS.
      ELSE IF(ICMD.EQ.003) THEN
         READ(PARMS,510) ICNT
         DO 80 I=1,MAXCNT
         IF(ICNT(I).GT.0 .AND. ICNT(I).LE.LMZ) THEN
            DO 70 J=1,NUMAT
            IF(NAT(J).EQ.ICNT(I)) THEN
               INC(J) = .FALSE.
            ENDIF
   70       CONTINUE
         ENDIF
   80    CONTINUE
C     INCLUDE SPECIFIC ELEMENTS.
      ELSE IF(ICMD.EQ.013) THEN
         READ(PARMS,510) ICNT
         DO 100 I=1,MAXCNT
         IF(ICNT(I).GT.0 .AND. ICNT(I).LE.LMZ) THEN
            DO 90 J=1,NUMAT
            IF(NAT(J).EQ.ICNT(I)) THEN
               INC(J) = .TRUE.
            ENDIF
   90       CONTINUE
         ENDIF
  100    CONTINUE
C     INPUT OF NICS COORDINATES IN ANGSTROM.
      ELSE IF(ICMD.EQ.101) THEN
         NNICS = NNICS + 1
         IF(NNICS.GT.MXNICS) THEN
            WRITE(NB6,600) MXNICS
            STOP 'INPNMR'
         ENDIF
         READ(PARMS,520) (COORDN(I,NNICS), I=1,3)
C     USE DUMMY ATOMS TO DEFINE NICS COORDINATES IN ANGSTROM.
      ELSE IF(ICMD.EQ.102) THEN
         NNICS = NNICS + 1
         IF(NNICS.GT.MXNICS) THEN
            WRITE(NB6,600) MXNICS
            STOP 'INPNMR'
         ENDIF
         IF(NNICS.EQ.1) THEN
            CALL GMETRY (+1)
         ENDIF
         READ(PARMS,510) ICNT
         DO 110 I=1,MAXCNT
         IF(ICNT(I).GT.0 .AND. ICNT(I).LE.NATOMS) THEN
            IF(NN(ICNT(I)).EQ.99) THEN
               COORDN(1,NNICS) = COORD(1,ICNT(I))
               COORDN(2,NNICS) = COORD(2,ICNT(I))
               COORDN(3,NNICS) = COORD(3,ICNT(I))
            ELSE
               WRITE(NB6,610) ICMD,ICNT(I)
               STOP 'INPNMR'
            ENDIF
         ENDIF
  110    CONTINUE
C     DEFINE NICS COORDINATES IN ANGSTROM, AS NONWEIGHTED MEAN
C     OF THE COORDINATES OF CONSECUTIVE ATOMS ICNT(1)..ICNT(2).
      ELSE IF(ICMD.EQ.103) THEN
         NNICS = NNICS + 1
         IF(NNICS.GT.MXNICS) THEN
            WRITE(NB6,600) MXNICS
            STOP 'INPNMR'
         ENDIF
         IF(NNICS.EQ.1) THEN
            CALL GMETRY (+1)
         ENDIF
         READ(PARMS,510) ICNT
         NICS1 = ICNT(1)
         NICS2 = ICNT(2)
         NICS3 = NICS2-NICS1+1
         IF(NICS1.GT.0 .AND. NICS2.GT.0 .AND. NICS3.GT.0 .AND.
     1      NICS1.LE.NATOMS .AND. NICS2.LE.NATOMS) THEN
            DO 130 K=1,3
            COORDN(K,NNICS) = ZERO
            DO 120 I=NICS1,NICS2
            COORDN(K,NNICS) = COORDN(K,NNICS) + COORD(K,I)
  120       CONTINUE
            COORDN(K,NNICS) = COORDN(K,NNICS) / NICS3
  130       CONTINUE
         ELSE
            WRITE(NB6,620) ICMD,NICS1,NICS2
            STOP 'INPNMR'
         ENDIF
C     DEFINE NICS COORDINATES IN ANGSTROM, AS NONWEIGHTED MEAN
C     OF THE COORDINATES OF EXPLICITLY DEFINED ATOMS ICNT(I).
      ELSE IF(ICMD.EQ.104) THEN
         NNICS = NNICS + 1
         IF(NNICS.GT.MXNICS) THEN
            WRITE(NB6,600) MXNICS
            STOP 'INPNMR'
         ENDIF
         IF(NNICS.EQ.1) THEN
            CALL GMETRY (+1)
         ENDIF
         READ(PARMS,510) ICNT
         DO 150 K=1,3
         NICS3 = 0
         COORDN(K,NNICS) = ZERO
         DO 140 I=1,MAXCNT
         IF(ICNT(I).GT.0 .AND. ICNT(I).LE.NATOMS) THEN
            NICS3 = NICS3+1
            COORDN(K,NNICS) = COORDN(K,NNICS) + COORD(K,ICNT(I))
         ENDIF
  140    CONTINUE
         IF(NICS3.GT.0) THEN
            COORDN(K,NNICS) = COORDN(K,NNICS) / NICS3
         ELSE
            WRITE(NB6,630) ICMD
            STOP 'INPNMR'
         ENDIF
  150    CONTINUE
C *** OBSOLETE OPTIONS KEPT FOR COMPATIBILITY (ICMD.GT.900).
C     READ NMR-SPECIFIC PARAMETERS ON EXTRA LINES, PS CONVENTIONS.
      ELSE IF( ICMD.EQ.907 ) THEN
         READ(PARMS,800) ITYP,SCALET,MAXLT
         IF( ITYP.NE.0 ) THEN
            DO 160 L=0,MAXLT
            READ(NB5,810) NNX,XDUMMY,XCN
  160       CONTINUE
         ENDIF
C     IGNORE OBSOLETE OPTIONS.
      ELSE IF(ICMD.GE.901) THEN
         CONTINUE
C     ERROR EXIT.
      ELSE
         WRITE(NB6,640) ICMD
         STOP 'INPNMR'
      ENDIF
      GO TO 20
C
C *** PREDEFINED INPUT OPTIONS (NMRNUC=1,2,3,4,5).
  200 CONTINUE
C     DEFINE NICS COORDINATES IN ANGSTROM (INCLUDE ALL DUMMY ATOMS).
      IF(NMRNUC.EQ.1 .OR. NMRNUC.EQ.2) THEN
         IF(NATOMS.GT.NUMAT) THEN
            CALL GMETRY (+1)
            DO 220 I=1,NATOMS
            IF(NN(I).EQ.99) THEN
               NNICS = NNICS + 1
               DO 210 K=1,3
               COORDN(K,NNICS) = COORD(K,I)
  210          CONTINUE
            ENDIF
  220       CONTINUE
         ENDIF
      ENDIF
C     INCLUDE ALL ELEMENTS THAT HAVE BEEN PARAMETRIZED.
C     THESE ASSIGNMENTS ARE DONE IN SUBROUTINE NMRCHK.
C     FINAL IDEFN(K) VALUES ARE NOT YET AVAILABLE.
C     IF(NMRNUC.EQ.2 .OR. NMRNUC.EQ.3) THEN
C        DO 230 I=1,NUMAT
C        IF(IDEFN(NAT(I)).GT.0) THEN
C           INC(I) = .TRUE.
C        ELSE
C           INC(I) = .FALSE.
C        ENDIF
C 230    CONTINUE
C     ENDIF
C     INCLUDE CARBON, NITROGEN, AND OXYGEN ATOMS.
      IF(NMRNUC.EQ.4) THEN
         DO 240 I=1,NUMAT
         IF(NAT(I).GE.6 .AND. NAT(I).LE.8) THEN
            INC(I) = .TRUE.
         ELSE
            INC(I) = .FALSE.
         ENDIF
  240    CONTINUE
C     INCLUDE ONLY CARBON ATOMS.
      ELSE IF(NMRNUC.EQ.5) THEN
         DO 250 I=1,NUMAT
         IF(NAT(I).EQ.6) THEN
            INC(I) = .TRUE.
         ELSE
            INC(I) = .FALSE.
         ENDIF
  250    CONTINUE
      ENDIF
C
C *** REMOVE DUMMY ATOMS.
  300 CONTINUE
      IF(NNICS.GT.0 .AND. NATOMS.GT.NUMAT) THEN
         CALL GMETRY (-1)
      ENDIF
C
C *** DEBUG PRINT.
C     STANDARD PRINTING OF NMR DATA IS HANDLED BY NMRCHK.
      IF(JPRINT.GE.5) THEN
C        NUCLEI WHERE THE SHIELDING IS COMPUTED.
C        INC(I) MAY BE REDEFINED IN NMRCHK (FOR NMRNUC=2,3).
         WRITE(NB6,700)
         WRITE(NB6,710) (INC(I),I=1,NUMAT)
C        NICS POINTS WHERE THE SHIELDING IS COMPUTED.
         IF(NNICS.GT.0) THEN
            WRITE(NB6,720)
            DO 400 I=1,NNICS
            WRITE(NB6,730) I,(COORDN(J,I),J=1,3)
  400       CONTINUE
         ENDIF
      ENDIF
      RETURN
  500 FORMAT(I3,A77)
  510 FORMAT(15I5)
  520 FORMAT(3F10.5)
  600 FORMAT(// 1X,'TOO MANY NICS POINTS SPECIFIED, ONLY ',I4,
     1          1X,'POINTS ARE ALLOWED IN INPNMR.')
  610 FORMAT(// 1X,'FAULTY DEFINITION OF NICS COORDINATES: ICMD = ',I3,
     1       /  1X,'THE FOLLOWING CENTER IS NOT A DUMMY ATOM:',I5 )
  620 FORMAT(// 1X,'FAULTY DEFINITION OF NICS COORDINATES: ICMD = ',I3,
     1       /  1X,'ILLEGAL RANGE OF ATOMS SPECIFIED:',2I5 )
  630 FORMAT(// 1X,'FAULTY DEFINITION OF NICS COORDINATES: ICMD = ',I3,
     1       /  1X,'NO ATOMS GIVEN AS INPUT.',2I5 )
  640 FORMAT(/  1X,'UNRECOGNIZED NMR CONTROL RECORD: ICMD = ',I3)
  700 FORMAT(///1X,'NMR INPUT IN INPNMR: ARRAY INC(I).'/)
  710 FORMAT(   1X,20L3)
  720 FORMAT(// 1X,'CARTESIAN COORDINATES OF NICS POINTS (ANGSTROM).',
     1       // 5X,'I',7X,'X',9X,'Y',9X,'Z'/)
  730 FORMAT(   1X,I5,2X,3F10.5)
  800 FORMAT(I3,F10.5,I3,3(I3,F10.5))
  810 FORMAT(6X,I3,2F10.5)
      END
