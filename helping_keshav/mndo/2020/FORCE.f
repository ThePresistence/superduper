      SUBROUTINE FORCE (A,APT,B,C,EIG,WORK,I3N,LMC)
C     *
C     FORCE CONSTANT ANALYSIS IN THE GF-FORMALISM.
C     *
C     NOTATION. I=INPUT, O=OUTPUT, S=SCRATCH.
C     A        CARTESIAN FORCE CONSTANT MATRIX IN ATOMIC UNITS (I).
C     APT      CARTESIAN DIPOLE DERIVATIVES IN ATOMIC UNITS (I).
C     B        EIGENVECTORS OF THE GF-MATRIX (O).
C     C        GENERAL SCRATCH ARRAY (S).
C     EIG      EIGENVALUES OF THE GF-MATRIX WHICH ARE LATER CONVERTED
C              TO HARMONIC VIBRATIONAL WAVENUMBERS (O).
C     WORK     BUFFER FOR DIAGONALIZATION (S).
C     I3N      NUMBER OF CARTESIAN COORDINATES (I).
C     LMC      DIMENSION OF SCRATCH ARRAY C (I).
C     *
      USE LIMIT, ONLY: LM1, LMV
      IMPLICIT DOUBLE PRECISION (A-H,O-Z)
      CHARACTER*2  ELEMNT
      CHARACTER*80 KTITLE,KOMENT
      COMMON
     ./AMASS / AMS(LM1)
     ./ATOMC / COORD(LMV)
     ./ATOMS / NUMAT,NAT(LMV)
     ./CONSTF/ A0,AFACT,EV,EVCAL,PI,W1,W2,BIGEXP
     ./CONSTN/ ZERO,ONE,TWO,THREE,FOUR,PT5,PT25
     ./ELEMTS/ ELEMNT(107)
     ./ERGREF/ H298,H0,HF298,HF0,HAT298,HAT0,D00,EVIB,SCFENR
     ./FLAG1 / KTITLE,KOMENT
     ./INOPT2/ IN2(300)
     ./IJWORK/ ISYM(LMV),NSYM(LMV),IJKDUM(LM1*6)
     ./INFOB / PC(3*LM1),PM(3),PR(3,3)
     ./INFOC / ITYPE,NEG,NTR,NVIB
     ./NBFILE/ NBF(20)
     ./VIBRAT/ WAVEVB(LMV),NSYMVB(LMV)
      DIMENSION A(I3N,I3N),APT(I3N,3),B(I3N,I3N),C(LMC)
      DIMENSION EIG(I3N),WORK(I3N*5)
C *** CONVERSION FACTOR FOR FREQUENCIES FROM ATOMIC UNITS TO CM-1.
C     THE NUMERICAL VALUE OF F2 IS BASED ON
C     F1  = 15.56799404 (MDYN/ANGSTROM)/(HARTREE/BOHR**2) , SEE FMTRX
C     AMU =  1.66056550D-24 G            ATOMIC MASS UNIT
C     C   =  2.99792458D+10 CM/S         SPEED OF LIGHT
C     WHERE THE LAST TWO VALUES ARE TAKEN FROM
C     J.PHYS.CHEM.REF.DATA 2, 663 (1973).
C     F2  = SQRT(F1/AMU) / C
      DATA F2/32297.4043D0/
C *** CONVERSION FACTOR FOR ZERO-POINT ENERGY FROM CM-1 TO KCAL/MOL.
      DATA F3/1.429557D-03/
C *** AN EIGENVALUE OF THE MASS-WEIGHTED FORCE CONSTANT MATRIX
C     (IN ATOMIC UNITS) IS CONSIDERED TO BE
C     -    NEGATIVE IF IT IS LESS THAN -SMALLE,
C     -    ZERO IF ITS ABSOLUTE VALUE IS LESS THAN SMALLE.
C     ***  SMALLE=3.78266E-08 CORRESPONDS TO 1 CM-1.
      DATA SMALLE/3.78226D-08/
C     *
C *** FILE NUMBERS.
      NB6    = NBF(6)
      NB7    = NBF(7)
C *** INPUT OPTIONS.
      IFORM  = IN2(5)
      NSAV7  = IN2(14)
      NSAV13 = IN2(17)
      LPRINT = IN2(40)
      NUMSYM = IN2(76)
      KCI    = IN2(77)
      KMASS  = IN2(205)
C *** WRITE TITLE.
      IF(LPRINT.GT.-5) WRITE(NB6,300) KTITLE
C *** DEFINE ATOMIC MASSES (E.G. ISOTOPIC SUBSTITUTION).
      LMASS  = MIN(0,KMASS)
   10 CALL ISOMAS (LMASS,ISYM,NUMAT,NAT,LPRINT,IFORM,AMSUM)
C *** MOMENTS OF INERTIA AND RELATED QUANTITIES.
      CALL INERT (AMS,COORD,PC,PM,PR,ITYPE,NUMAT,NAT,LPRINT)
C *** SAVE CARTESIAN COORDINATES IN PRINCIPAL COORDINATES.
      IF(NSAV7.EQ.7) THEN
         WORK(1:I3N)  = COORD(1:I3N)
         COORD(1:I3N) = PC(1:I3N)
         CALL GEOSAV (NUMAT,NSAV7,NB7)
         COORD(1:I3N) = WORK(1:I3N)
         WORK(1:I3N)  = ZERO
      ENDIF
C *** COMPUTE HARMONIC FREQUENCIES FROM ORIGINAL FORCE CONSTANTS
C     BEFORE PROJECTION OF THE TRANSLATIONAL-ROTATIONAL COMPONENTS.
      IF(LPRINT.GT.4) THEN
         CALL LINEAR(A,C,I3N,I3N,LMC)
         IJ  = 0
         DO 13 J=1,I3N
         JJ  = (J+2)/3
         WORK(J)= ONE/SQRT(AMS(JJ))
         DO 12 I=1,J
         IJ  = IJ+1
         C(IJ) = A(I,J)*WORK(I)*WORK(J)
   12    CONTINUE
   13    CONTINUE
         CALL TDIAG (C,B,EIG,WORK,LMC,I3N,I3N,I3N)
         TFACT  = F2/(TWO*PI)
         DO 14 I=1,I3N
         EIG(I) = TFACT*SQRT(ABS(EIG(I)))
   14    CONTINUE
         WRITE(NB6,400)
         WRITE(NB6,410) (EIG(I),I=1,I3N)
      ENDIF
C *** PROJECT TRANSLATIONAL AND ROTATIONAL COMPONENTS
C     FROM THE FORCE CONSTANT MATRIX.
      CALL PROPRT (COORD,B,C,I3N,NUMAT,LPRINT)
CMXM  CALL MXM (A,I3N,B,I3N,C,I3N)
CMXM  CALL MXM (B,I3N,C,I3N,A,I3N)
      CALL DGEMM('N','N',I3N,I3N,I3N,ONE,A,I3N,B,I3N,ZERO,C,I3N)
      CALL DGEMM('N','N',I3N,I3N,I3N,ONE,B,I3N,C,I3N,ZERO,A,I3N)
C *** TRANSFER FORCE CONSTANT MATRIX FROM A(I,J) TO C(IJ).
      CALL LINEAR(A,C,I3N,I3N,LMC)
C *** PRINT FORCE CONSTANT MATRIX.
      IF(LPRINT.GT.-1) THEN
         WRITE(NB6,500)
         CALL VECPRT(C,LMC,I3N)
      ENDIF
C *** DIAGONALIZE FORCE CONSTANT MATRIX AND PRINT RESULTS.
      IF(LPRINT.GT.4) THEN
         CALL TDIAG (C,B,EIG,WORK,LMC,I3N,I3N,I3N)
         WRITE(NB6,510)
         CALL MATOUT(B,EIG,I3N,I3N,I3N,I3N)
      ENDIF
C *** CONSTRUCT MASS-WEIGHTED FORCE CONSTANT MATRIX AND DIAGONALIZE.
      IJ     = 0
      DO 21 J=1,I3N
      JJ     = (J+2)/3
      WORK(J)= 1.0D0/SQRT(AMS(JJ))
      DO 20 I=1,J
      IJ     = IJ+1
      C(IJ)  = A(I,J)*WORK(I)*WORK(J)
   20 CONTINUE
   21 CONTINUE
      IF(LPRINT.GT.4) THEN
         WRITE(NB6,520)
         CALL VECPRT(C,LMC,I3N)
      ENDIF
      CALL TDIAG (C,B,EIG,WORK,LMC,I3N,I3N,I3N)
C *** DETERMINE NUMBER OF NEGATIVE AND ZERO EIGENVALUES.
      NEG    = 0
      NTR    = 0
      DO 30 I=1,I3N
      IF(EIG(I).LT.-SMALLE) NEG=NEG+1
      IF(ABS(EIG(I)).LT.SMALLE) NTR=NTR+1
   30 CONTINUE
      NTR    = MIN(NTR,6)
      NVIB   = NEG+NTR+1
C *** CONVERT EIGENVALUES (AU) TO FREQUENCIES (CM-1).
      TFACT  = F2/(TWO*PI)
      DO 40 I=1,I3N
      EIG(I) = TFACT*SQRT(ABS(EIG(I)))
   40 CONTINUE
C *** USE NEGATIVE VALUES TO DENOTE IMAGINARY FREQUENCIES.
      IF(NEG.GT.0) THEN
         DO 50 I=1,NEG
         EIG(I) = -EIG(I)
   50    CONTINUE
      ENDIF
C *** USE ZERO FREQUENCY VALUES FOR TRANSLATIONS AND ROTATIONS.
CRAY  DO 55 I=NEG+1,NEG+NTR
C     EIG(I) = ZERO
C  55 CONTINUE
C *** CALCULATE ZERO-POINT VIBRATIONAL ENERGY.
      EVIB   = ZERO
      DO 60 I=NVIB,I3N
      EVIB   = EVIB+EIG(I)
   60 CONTINUE
      EVIB   = EVIB*F3
C *** DEFINE TRANSLATIONAL AND ROTATIONAL MODES.
      CALL TRDEF  (AMS,AMSUM,B,PC,PM,I3N,NUMAT,NEG,NTR)
      CALL TRANS4 (B,B,PR,I3N,NEG+1,NVIB-1)
C *** PRINT EIGENVECTORS AND HARMONIC FREQUENCIES.
      IF(LPRINT.GT.-1) THEN
         WRITE(NB6,530)
         CALL MATOUT(B,EIG,I3N,I3N,I3N,I3N)
      ENDIF
C *** CONSTRUCT AND PRINT CARTESIAN DISPLACEMENT VECTORS.
      DO 71 I=1,I3N
      II     = (I+2)/3
      FMASS  = ONE/SQRT(AMS(II))
      DO 70 J=1,I3N
      IJ     = I3N*(J-1)+I
      C(IJ)  = FMASS*B(I,J)
   70 CONTINUE
   71 CONTINUE
      IF(LPRINT.GT.-2) THEN
         WRITE(NB6,540)
         CALL MATOUT(C,EIG,I3N,I3N,I3N,I3N)
      ENDIF
C *** ASSIGN SYMMETRY OF EIGENVECTORS.
      IF(ITYPE.EQ.4) THEN
         CALL VECSYM (AMS,COORD,B,EIG,I3N,I3N,NUMAT,ISYM,NSYM,LPRINT)
      ELSE
         CALL VECTYP (AMS,COORD,B,EIG,I3N,I3N,NUMAT,ISYM,NSYM,LPRINT)
      ENDIF
C *** PRINT ZERO-POINT VIBRATIONAL ENERGY.
      IF(LPRINT.GT.-5) THEN
         WRITE(NB6,550) EVIB
         WRITE(NB6,560) NVIB,I3N
         IF(NEG.EQ.1) WRITE(NB6,570)
         IF(NEG.GT.1) WRITE(NB6,580) NEG
      ENDIF
C *** CALCULATE INFRARED INTENSITIES.
      IF(KCI.EQ.0) CALL INTENS (C,APT,WORK,EIG,I3N,NVIB,LPRINT)
C *** SAVE VIBRATIONAL WAVENUMBERS AND SYMMETRY LABELS.
      DO 80 I=1,I3N
      WAVEVB(I) = EIG(I)
      NSYMVB(I) = NSYM(I)
   80 CONTINUE
C *** UniChem(1991) SECTION.
C     THE INFRARED INTENSITIES ARE PRESENTLY NOT CALCULATED FOR
C     KCI.NE.0. IN THIS CASE WE SET ALL INTENSITIES EQUAL TO 1
C     IN ORDER TO ALLOW FOR THE VISUALIZATION OF THE NORMAL MODES.
CRAY  NOTE THAT SUBROUTINE INTENS RETURNS THE INFRARED INTENSITIES
CRAY  IN UNITS OF KM/MOL (NEW CONVENTION IN MNDO93).
C     IF(KCI.NE.0) THEN
C        DO 90 I=1,I3N
C        WORK(I)= ONE
C  90    CONTINUE
C     ENDIF
C     ADJUST LENGTH OF CARTESIAN DISPLACEMENT VECTORS ACCORDING TO
C     MOPAC CONVENTIONS.
C     DO 120 I=NVIB,I3N
C     II     = (I-1)*I3N
C     SUM    = ZERO
C     DO 100 J=1,NUMAT
C     JJ     = II+(J-1)*3
C     SUM    = SUM+SQRT(C(JJ+1)**2+C(JJ+2)**2+C(JJ+3)**2)
C 100 CONTINUE
C     SUM    = ONE/SUM
C     DO 110 J=1,I3N
C     C(J+II) = C(J+II)*SUM
C 110 CONTINUE
C 120 CONTINUE
C     USE ZERO INTENSITIES FOR TRANSLATIONS AND ROTATIONS.
C     DO 130 I=NEG+1,NEG+NTR
C     WORK(I) = ZERO
C 130 CONTINUE
C     DISPLACEMENTS ARE IN ANGSTROM, EIGENVALUES IN CM**(-1).
C     IUNITS = 1
C     IVEC   = I3N
C     UniChem(1991) CALL.
CRAY  CALL CTMPNM (IVEC,I3N,EIG,WORK,WORK(I3N*4+1),
C    1             WORK(I3N*3+1),IUNITS,C)
C     END OF UniChem SECTION.
C
C *** SAVE DATA FOR POSTPROCESSING USING MOLDEN.
      IF(NSAV13.EQ.2) THEN
         NB13 = NBF(13)
         WRITE(NB13,600) '[FR-COORD]', NUMAT
         DO 200 I=1,NUMAT
         II = (I-1)*3
         WRITE(NB13,610) ELEMNT(NAT(I)), (COORD(II+J)/A0,J=1,3)
  200    CONTINUE
         WRITE(NB13,600) '[FREQ]', I3N
         DO 210 I=1,I3N
         WRITE(NB13,620) WAVEVB(I)
  210    CONTINUE
         WRITE(NB13,600) '[FR-NORM-COORD]', I3N
         DO 230 I=1,I3N
         WRITE(NB13,600) 'vibration ', I
         II = (I-1)*I3N
         DO 220 J=1,NUMAT
         JJ = II+(J-1)*3
         WRITE(NB13,620) C(JJ+1), C(JJ+2), C(JJ+3)
  220    CONTINUE
  230    CONTINUE
      ENDIF
C *** End of MOLDEN Section
C
C *** CALCULATE THERMODYNAMIC QUANTITIES.
      CALL HFZERO (EIG,I3N,LPRINT)
      CALL SYMNUM (AMS,PC,ITYPE,NUMAT,ISYM,NUMSYM,LPRINT)
      CALL THERMO (AMSUM,EIG,PM,I3N,NUMSYM,LPRINT)
C     IF(LPRINT.GT.-5) WRITE(NB6,590)
C *** CHECK FOR ADDITIONAL CALCULATION WITH ISOTOPIC SUBSTITUTION.
      LMASS  = LMASS+1
      IF(LMASS.LE.KMASS) GO TO 10
      RETURN
  300 FORMAT(///5X,A)
  400 FORMAT(///5X,'HARMONIC FREQUENCIES IN CM-1 OBTAINED FROM THE ',
     1             'FORCE CONSTANT MATRIX',
     2       /  5X,'BEFORE PROJECTION OF THE TRANSLATIONAL-ROTATIONAL ',
     3             'COMPONENTS'/)
  410 FORMAT(   5X,10F12.5)
  500 FORMAT(///5X,'CARTESIAN FORCE CONSTANT MATRIX IN ATOMIC UNITS',
     1       /  5X,'AFTER PROJECTION OF TRANSLATIONAL-ROTATIONAL ',
     2             'COMPONENTS'/)
  510 FORMAT(///5X,'EIGENVALUES AND EIGENVECTORS OF CARTESIAN FORCE ',
     1             'CONSTANT MATRIX'/)
  520 FORMAT(///5X,'MASS-WEIGHTED CARTESIAN FORCE CONSTANT MATRIX ',
     1             'IN ATOMIC UNITS'/)
  530 FORMAT(///5X,'EIGENVECTORS OF THE MASS-WEIGHTED CARTESIAN ',
     1             'FORCE CONSTANT MATRIX AND HARMONIC FREQUENCIES ',
     2             'IN CM-1.')
  540 FORMAT(///5X,'CARTESIAN DISPLACEMENT VECTORS IN ATOMIC UNITS ',
     1             'AND HARMONIC FREQUENCIES IN CM-1.'/)
  550 FORMAT(///5X,'ZERO-POINT VIBRATIONAL ENERGY',F15.5,' KCAL/MOL')
  560 FORMAT(/  5X,'COMPUTED FROM MODES',I3,' -',I3)
  570 FORMAT(///5X,'THE FORCE CONSTANT MATRIX HAS 1 NEGATIVE ',
     1             'EIGENVALUE.')
  580 FORMAT(///5X,'THE FORCE CONSTANT MATRIX HAS',I3,' NEGATIVE ',
     1             'EIGENVALUES.')
C 590 FORMAT(///)
  600 FORMAT(A,2I5)
  610 FORMAT(A5,5X,3F20.10)
  620 FORMAT(3F20.10)
      END
