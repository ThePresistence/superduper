      SUBROUTINE COROM (ISHELL,JSHELL,NI,NJ,R,FKO,CORE,REPT,S,B)
C     *
C     SEMIEMPIRICAL CORE-ELECTRON ATTRACTIONS (IN EV) FOR OM-METHODS.
C     *
C     NOTATION. I=INPUT, O=OUTPUT.
C     ISHELL    NUMBER OF GAUSSIAN SHELL AT FIRST  ATOM (I).
C     JSHELL    NUMBER OF GAUSSIAN SHELL AT SECOND ATOM (I).
C     NI,NJ     ATOMIC NUMBER FOR CURRENT PAIR (I).
C     R         INTERNUCLEAR DISTANCE, IN AU (I).
C     FKO       KLOPMAN-OHNO SCALING FACTOR FOR CURRENT PAIR (I).
C     CORE()    SEMIEMPIRICAL CORE-ELECTRON ATTRACTIONS EVALUATED FROM
C               SEMIEMPIRICAL TWO-ELECTRON INTEGRALS WITH NEGLECT OF
C               PENETRATION (I).
C     CORE()    CORE-ELECTRON ATTRACTIONS USED IN CORE HAMILTONIAN (O).
C     REPT()    SEMIEMPIRICAL CORE-ELECTRON ATTRACTIONS EVALUATED FROM
C               ANALYTICAL TWO-ELECTRON INTEGRALS WITH NEGLECT OF
C               PENETRATION (I).
C     S(14)     LOCAL OVERLAP INTEGRALS (I).
C     B(14)     LOCAL RESONANCE INTEGRALS (I).
C     *
C     SCRATCH ARRAYS.
C     CORPP()   CORE-VALENCE PSEUDOPOTENTIAL INTEGRALS (S).
C     CORT()    ANALYTICAL CORE-ELECTRON ATTRACTION (S).
C     PEN()     PENETRATION INTEGRALS (S).
C     VALPP()   TWO-CENTER ORTHOGONALIZATION TERMS, FIRST  TYPE (S).
C     VALPP1()  TWO-CENTER ORTHOGONALIZATION TERMS, SECOND TYPE (S).
C     *
C     THE CODE IS STILL RESTRICTED TO AN SP BASIS (20/04/97).
C     SOME PRELIMINARY EXTENSIONS TO D ORBITALS ARE GIVEN AS COMMENTS.
C     *
      USE LIMIT, ONLY: LMZ
      IMPLICIT DOUBLE PRECISION (A-H,O-Z)
      COMMON
     ./CONSTF/ A0,AFACT,EV,EVCAL,PI,W1,W2,BIGEXP
     ./CONSTN/ ZERO,ONE,TWO,THREE,FOUR,PT5,PT25
     ./DPARM / LORBS(LMZ)
     ./INOPT2/ IN2(300)
     ./NBFILE/ NBF(20)
     ./PAROMM/ BETPI(LMZ*8),FVAL1(LMZ),FVAL2(LMZ),GVAL1(LMZ*6)
      DIMENSION CORE(10,2),REPT(10,2),S(14),B(14)
      DIMENSION CORPP(10,2),CORT(10,2)
      DIMENSION PEN(10,2),VALPP(10,2),VALPP1(10,2)
C *** INITIALIZATION.
      IOP    = IN2(2)
      NPRINT = IN2(72)
      IORBS  = LORBS(NI)
      JORBS  = LORBS(NJ)
      IMAX   = 1
      IF(IORBS.GE.4 .OR. JORBS.GE.4) IMAX=4
C     IF(IORBS.GE.9 .OR. JORBS.GE.9) IMAX=10
C *** INITIALIZATION.
      DO 11 J=1,2
      DO 10 I=1,4
      CORPP(I,J) = ZERO
      CORT (I,J) = ZERO
      PEN  (I,J) = ZERO
      VALPP(I,J) = ZERO
      VALPP1(I,J)= ZERO
   10 CONTINUE
   11 CONTINUE
C *** CONTRIBUTION FROM PENETRATION INTEGRALS.
C     CALCULATE ANALYTICAL CORE-ELECTRON ATTRACTIONS.
C     KLOPMAN-OHNO TYPE SCALING.
      CALL CORGAU (ISHELL,JSHELL,NI,NJ,R,CORT)
      DO 21 J=1,2
      DO 20 I=1,IMAX
      PEN(I,J) = FKO*(CORT(I,J)-REPT(I,J))
   20 CONTINUE
   21 CONTINUE
C *** CORE-VALENCE PSEUDOPOTENTIAL CONTRIBUTIONS.
C     OM1: ANALYTICAL ECP COMPUTATION AND KLOPMAN-OHNO SCALING.
      IF(IOP.EQ.-5) THEN
         IF(NI.GT.2 .OR. NJ.GT.2) THEN
            CALL PPECP (ISHELL,JSHELL,ISHELL,JSHELL,NI,NJ,R,CORPP)
            DO 31 J=1,2
            DO 30 I=1,IMAX
            CORPP(I,J) = CORPP(I,J)*FKO
   30       CONTINUE
   31       CONTINUE
         ENDIF
C     OM2: SEMIEMPIRICAL EVALUATION OF THE PSEUDOPOTENTIAL.
      ELSE IF(IOP.EQ.-6 .OR. IOP.EQ.-8 .OR. IOP.EQ.-9
     1           .OR. IOP.EQ.-22 .OR. IOP.EQ.-23) THEN
         CALL CORPP2 (ISHELL,JSHELL,NI,NJ,R,CORPP)
      ENDIF
C *** VALENCE-VALENCE PSEUDOPOTENTIAL CONTRIBUTIONS.
      IF(IOP.EQ.-9) GO TO 35
      FSI    = FVAL1(NI)
      FSJ    = FVAL1(NJ)
      FPI    = FVAL2(NI)
      FPJ    = FVAL2(NJ)
C     CALL BETINT (0,ISHELL,JSHELL,NI,NJ,R,S,B)
      CALL VALPOT (NI,NJ,CORE,S,B,VALPP,VALPP1)
C *** VALPP : SECOND-ORDER TERMS FROM RESONANCE INTEGRALS.
C *** VALPP1: SECOND-ORDER TERMS INVOLVING LOCAL HCORE ELEMENTS.
      VALPP(1,1) = VALPP(1,1)*FSI + VALPP1(1,1)*FPI*PT25
      VALPP(1,2) = VALPP(1,2)*FSJ + VALPP1(1,2)*FPJ*PT25
      IF(IORBS.GE.4) THEN
         VALPP(2,1) = VALPP(2,1)*FSI + VALPP1(2,1)*FPI*PT25
         VALPP(3,1) = VALPP(3,1)*FSI + VALPP1(3,1)*FPI*PT25
      ENDIF
      IF(JORBS.GE.4) THEN
         VALPP(2,2) = VALPP(2,2)*FSJ + VALPP1(2,2)*FPJ*PT25
         VALPP(3,2) = VALPP(3,2)*FSJ + VALPP1(3,2)*FPJ*PT25
      ENDIF
      IF(IORBS.GE.4 .AND. JORBS.GE.4) THEN
         VALPP(4,1) = VALPP(4,1)*FSI + VALPP1(4,1)*FPI*PT25
         VALPP(4,2) = VALPP(4,2)*FSJ + VALPP1(4,2)*FPJ*PT25
      ENDIF
   35 CONTINUE
C *** ADD CONTRIBUTIONS TO CORE-ELECTRON ATTRACTION INTEGRALS.
      DO 41 J=1,2
      DO 40 I=1,IMAX
      CORE(I,J) = CORE(I,J)+PEN(I,J)+CORPP(I,J)+VALPP(I,J)
   40 CONTINUE
   41 CONTINUE
C *** DEBUG PRINT.
      IF(NPRINT.GE.5) THEN
        NB6 = NBF(6)
        RAB = R*A0
        WRITE(NB6,500)
        DO 51 J=1,2
        DO 50 I=1,IMAX
        IF(I.GT.1 .AND. J.EQ.1 .AND. IORBS.EQ.1) GO TO 50
        IF(I.GT.1 .AND. J.EQ.2 .AND. JORBS.EQ.1) GO TO 50
        IF(I.GT.4 .AND. J.EQ.1 .AND. IORBS.LE.4) GO TO 50
        IF(I.GT.4 .AND. J.EQ.2 .AND. JORBS.LE.4) GO TO 50
        TEMP   = CORE(I,J)-PEN(I,J)-CORPP(I,J)-VALPP(I,J)
        TEMP1  = CORE(I,J)-TEMP
        WRITE(NB6,510) NI,NJ,RAB,I,J,CORT(I,J),TEMP,PEN(I,J),CORPP(I,J),
     1                 VALPP(I,J),VALPP1(I,J),TEMP1,CORE(I,J)
   50   CONTINUE
   51   CONTINUE
      ENDIF
      RETURN
  500 FORMAT(1X,'COROM:  NI  NJ     RAB   I J      CORT     CORE-SEMI',
     1          '       PEN         CORPP       VALPP      VALPP1',
     2          '  CORRECTION       CORE')
  510 FORMAT(1X,'COROM:',2I4,F10.5,2I2,9F12.5)
      END
