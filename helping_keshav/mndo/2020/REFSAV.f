      SUBROUTINE REFSAV (I,NB19,ISUB)
C     *
C     SAVE THE REFERENCE DATA FOR MOLECULE (I) ON FILE NB19
C     WHICH IS USED AS INPUT FILE FOR STATISTICAL EVALUATIONS.
C     ALSO SAVE THE REFERENCE DATA FOR MOLECULE (I) ON FILE NB90
C     WHICH IS BINARY AND USED FOR PARAMETERIZATION.
C     *
      USE LIMIT, ONLY: LMF, LMM
      IMPLICIT DOUBLE PRECISION (A-H,O-Z)
      PARAMETER (NB90=90)
      PARAMETER (I99=99)
      PARAMETER (IZ=0)
      CHARACTER*30 HEADER
C     CHARACTER*30 KOMENP
      CHARACTER*30 KOMEXP
      CHARACTER*30 KOMOUT
      CHARACTER*80 KTITLE,KOMENT
      CHARACTER*7  METHOD
      CHARACTER*3  NPTGRP
      CHARACTER*30 TEXT1
      COMMON
     ./FLAG1 / KTITLE,KOMENT
     ./INOPT2/ IN2(300)
     ./NBFILE/ NBF(20)
     ./PAR21 / ID1(7,LMF),LITEXP(LMF)
     ./PAR22 / XEXP(LMF),ERREXP(LMF),WEXP(LMF)
     ./PAR23 / KOMEXP(LMF)
     ./PAR24 / NREF1(LMM),NREF2(LMM)
     ./PAR25 / XCALC(LMF)
     ./QMNAME/ METHOD(-23:6)
     ./SYMALL/ NPTGRP(59)
C    ./FLAP1 / KOMENP
C *** INITIALIZE AND WRITE HEADER.
      IF(I.EQ.1) THEN
         HEADER = ' '
         IOP = IN2(2)
         IF(IOP.GE.-23 .AND. IOP.LE.6) THEN
            HEADER(1:7) = METHOD(IOP)
         ENDIF
C        HEADER(8:30) = KOMENP(1:23)
         REWIND NB19
         WRITE(NB19,500) HEADER
         WRITE(NB19,510) IZ,IZ,IZ,IZ,IZ
      ENDIF
C *** WRITE TITLE LINE AND DATA FOR MOLECULE (I).
      IF(I.GE.1 .AND. I.LE.LMM) THEN
         ID2    = IZ
C        DETERMINE POINT GROUP INDEX FROM KTITLE.
         IF(KTITLE(34:36).NE.'   ') THEN
            DO 10 J=1,59
            IF(KTITLE(34:36).EQ.NPTGRP(J)) THEN
               ID2 = J
               IF(J.EQ.11 .OR. J.EQ.12) ID2=J-2
               GO TO 20
            ENDIF
   10       CONTINUE
         ENDIF
   20    CONTINUE
C        TITLE LINE: POINT GROUP INFORMATION IS INCLUDED VIA
C        ID2         INDEX DETERMINED FROM INPUT (KTITLE),
C        ISUB+1      INDEX DETERMINED FROM COORDINATES (ASSYM).
C        BOTH INDICES REFER TO THE POINT GROUP LABELS IN NPTGRP(59).
C        NOTE THAT ASSYM RECOGNIZES ONLY ABELIAN GROUPS (ISUB=1..7).
         IF(KTITLE(1:30).NE.' ') THEN
            CALL TXCOPY (KTITLE,TEXT1,30)
         ELSE
            CALL TXCOPY (KOMENT,TEXT1,30)
         ENDIF
         WRITE(NB19,520) IZ,ID2,ISUB+1,TEXT1
C        DATA LINES: THERE ARE (NR2-NR1+1) SUCH LINES IN GENERAL.
C        SPECIAL CONVENTION FOR NMR CHEMICAL SHIFTS (ID1(1,NR)=30,31):
C        IF SHIFTS HAVE BEEN AVERAGED FOR NEQUIV EQUIVALENT ATOMS,
C        GENERATE ONLY ONE LINE OF OUTPUT FOR SUCH A SET OF ATOMS,
C        INSTEAD OF NEQUIV LINES (AS IMPLIED BY THE INPUT).
         NR1    = NREF1(I)
         NR2    = NREF2(I)
         DO 30 NR=NR1,NR2
         IF(ID1(1,NR).EQ.30 .OR. ID1(1,NR).EQ.31) THEN
            IF(ID1(5,NR).GT.0 .AND. ID1(5,NR).NE.ID1(4,NR)) GO TO 30
         ENDIF
         ICALL  = ID1(2,NR)+90
         KOMOUT = ' '
         IF(ICALL.EQ.-1 .OR. ICALL.EQ.-2) THEN
            KOMOUT = 'NO SCF'
         ELSE IF(ICALL.EQ.-3) THEN
            KOMOUT = 'UNSUCCESSFUL'
         ELSE IF(ICALL.EQ.-4) THEN
            KOMOUT = 'NO PARAMETERS'
         ELSE
            KOMOUT = KOMEXP(NR)
         ENDIF
         WRITE(NB19,530) (ID1(J,NR),J=1,3),XCALC(NR),XEXP(NR),
     1                    ERREXP(NR),LITEXP(NR),KOMOUT
         IF(WEXP(NR).GT.0.0D0 .AND.
     1      (ID1(2,NR).GE.0 .OR. ID1(2,NR).LT.-90)) THEN
            WRITE(NB90) ID1(1,NR),ID1(2,NR),WEXP(NR),
     1                  XCALC(NR)-XEXP(NR),KOMOUT
         ENDIF
   30    CONTINUE
C *** WRITE LAST LINE AND ATTEMPT TO COPY ANY ADDITIONAL LINES.
      ELSE
         WRITE(NB19,510) I99
         NB5 = NBF(5)
   40    READ(NB5,540,ERR=50,END=50) I1,I2,I3,TEXT1
         IF(I1.NE.I99) THEN
            WRITE(NB19,540) I1,I2,I3,TEXT1
            GO TO 40
         ELSE
            WRITE(NB19,540) I1
            GO TO 50
         ENDIF
   50    CONTINUE
      ENDIF
      RETURN
  500 FORMAT(A)
  510 FORMAT(5I3)
  520 FORMAT(3I3,41X,A)
  530 FORMAT(3I3,1X,3F10.5,I5,5X,A)
  540 FORMAT(3I3,1X,A)
      END
