      SUBROUTINE PRTSCF (CA,CB,EA,EB,FA,FB,H,PA,PB,LM2,LM3,LM4)
C     *
C     PRINT SCF RESULTS.
C     *
      USE LIMIT, ONLY: LM1, LMX
      IMPLICIT DOUBLE PRECISION (A-H,O-Z)
      LOGICAL UHF
      CHARACTER*80 KTITLE,KOMENT
      COMMON
     ./CONSTN/ ZERO,ONE,TWO,THREE,FOUR,PT5,PT25
     ./CONSTF/ A0,AFACT,EV,EVCAL,PI,W1,W2,BIGEXP
     ./ENERGT/ EE,ENUCLR,EAT,ATHEAT
     ./FLAG1 / KTITLE,KOMENT
     ./HALFX / XIIII,XJJJJ,XIJIJ
     ./IJWORK/ NSYM(LMX),ISYM(3,LM1)
     ./INOPT2/ IN2(300)
     ./MMCOM1/ EMM
     ./MMDP  / EMMDP
     ./CHCORR/ EHCORR
     ./NBFILE/ NBF(20)
     ./OCCETS/ ETS
     ./OCCFL / IMOCC,NOCCA,NOCCB,MSUB,MOSUMA,MOSUMB,MOCCA(8),MOCCB(8)
     ./ORBITS/ NUMB,NORBS,NMOS,NALPHA,NBETA
     ./SCFPRT/ EBIND,ENERGY,EIP,EIP2,SZ,S2,ITSAVE
     ./UHF   / UHF
      DIMENSION CA(LM2,LM3),CB(LM2,LM3),EA(LM3),EB(LM3)
      DIMENSION FA(LM4),FB(LM4),H(LM4),PA(LM4),PB(LM4)
C *** FILE NUMBERS.
      NB6    = NBF(6)
C *** INPUT OPTIONS.
      NNHCO  = IN2(20)
      ICOSMO = IN2(28)
      IMMDP  = IN2(30)
      LINDMS = IN2(56)
      LINDIA = IN2(57)
      IMULT  = IN2(66)
      IPUBO  = IN2(69)
      KITSCF = IN2(71)
      NPRINT = IN2(72)
      IATERG = IN2(119)
      INOUT  = IN2(212)
      IHCORR = IN2(226)
C *** PRINT TITLE.
      IF(NPRINT.GT.-5) THEN
         WRITE(NB6,400) KOMENT,KTITLE
         IF(ICOSMO.GT.0) WRITE(NB6,410)
      ENDIF
C *** PRINT CORE HAMILTONIAN AND FOCK MATRIX.
      IF(NPRINT.GE.2) THEN
         IF(INOUT.GT.1) THEN
            NB3 = NBF(3)
            REWIND NB3
            READ(NB3) H
         ENDIF
         WRITE(NB6,500)
         CALL VECPRT(H,LM4,NORBS)
         IF(INOUT.GT.1) READ(NB3) FA
         IF(UHF) THEN
            WRITE(NB6,520)
         ELSE
            WRITE(NB6,510)
         ENDIF
         CALL VECPRT(FA,LM4,NORBS)
         IF(UHF) THEN
            IF(INOUT.GT.1) READ(NB3) FB
            WRITE(NB6,530)
            CALL VECPRT(FB,LM4,NORBS)
         ENDIF
      ENDIF
C *** PRINT EIGENVALUES AND EIGENVECTORS.
      IF(NPRINT.GE.-2 .AND. (LINDMS.EQ.0 .OR. LINDIA.EQ.1) .AND.
     1   KITSCF.NE.-1) THEN
         IF(NPRINT.GE.2) THEN
            NPRSYM = NPRINT
         ELSE
            NPRSYM = -5
         ENDIF
         CALL MOSYM(CA,EA,LM2,LM3,LM3,NSYM,ISYM,ISUB,NPRSYM)
         CALL MOOUT(CA,EA,ISUB,NSYM,NORBS,LM3,LM2,LM3,NPRINT,1)
         IF(UHF) THEN
            CALL MOSYM(CB,EB,LM2,LM3,LM3,NSYM,ISYM,ISUB,NPRSYM)
            CALL MOOUT(CB,EB,ISUB,NSYM,NORBS,LM3,LM2,LM3,NPRINT,2)
         ENDIF
      ENDIF
C *** PRINT DENSITY MATRIX.
      IF(NPRINT.GE.1) THEN
         DO 10 I=1,LM4
         PA(I)  = PA(I)+PB(I)
   10    CONTINUE
         WRITE(NB6,540)
         CALL VECPRT(PA,LM4,NORBS)
         IF(UHF) THEN
            DO 20 I=1,LM4
            PA(I)  = PA(I)-PB(I)*TWO
   20       CONTINUE
            WRITE(NB6,550)
            CALL VECPRT(PA,LM4,NORBS)
            DO 30 I=1,LM4
            PA(I)  = PA(I)+PB(I)
   30       CONTINUE
         ELSE
            DO 40 I=1,LM4
            PA(I)  = PA(I)*PT5
   40       CONTINUE
         ENDIF
      ENDIF
C *** EVALUATE SCF PROPERTIES (IN COMMON BLOCK SCFPRT).
      CALL SCFRES (EA,PA,PB,LM3,LM4)
C *** PRINT THE VALUES OF THESE PROPERTIES.
      IF(NPRINT.GT.-5) THEN
         IF(UHF) THEN
            WRITE(NB6,560) SZ,S2
         ENDIF
         EETOT  = EE+ENUCLR
         IF(IATERG.EQ.1) THEN
            WRITE(NB6,570) ENERGY
         ELSEIF(IATERG.EQ.-1) THEN
            WRITE(NB6,571) ENERGY
         ENDIF
         WRITE(NB6,575) EBIND
         WRITE(NB6,580) EETOT
         WRITE(NB6,590) EE
         IF(IMOCC.EQ.5) WRITE(NB6,595) ETS,EE-ETS
         WRITE(NB6,600) ENUCLR
         IF(NNHCO.GT.0) WRITE(NB6,610) EMM
         IF(IMMDP.NE.-1) THEN
            WRITE(NB6,615) EMMDP
            WRITE(NB6,616) EETOT+EMMDP/EVCAL
         ENDIF
         IF(IHCORR.NE.-1) THEN
            WRITE(NB6,617) EHCORR
            WRITE(NB6,618) EETOT+EMMDP/EVCAL+EHCORR/EVCAL
         ENDIF
         IF(EIP .GT.ZERO) WRITE(NB6,620) EIP
         IF(EIP2.GT.ZERO) WRITE(NB6,630) EIP2
         IF(IMULT.GT.0 .AND. .NOT.UHF .AND. IMOCC.LE.2) THEN
            IF(IMULT.EQ.2) THEN
               SUM = -PT25*XIIII
               WRITE(NB6,640) SUM
               WRITE(NB6,650) XIIII
            ELSE
               SUM = -PT25*XIIII-PT25*XJJJJ-PT5*XIJIJ
               IF(IMULT.EQ.1) SUM = SUM+TWO*XIJIJ
               WRITE(NB6,640) SUM
               WRITE(NB6,660) XIIII,XJJJJ,XIJIJ
            ENDIF
         ENDIF
         WRITE(NB6,670) ITSAVE
      ENDIF
C *** SAVE BOND ORDER MATRIX, OR EIGENVECTORS AND EIGENVALUES.
      IF(IPUBO.EQ.1 .OR. IPUBO.EQ.3) THEN
         NB11 = NBF(11)
         REWIND NB11
         WRITE(NB11) PA
         IF(UHF) WRITE(NB11) PB
         IF(NPRINT.GT.-5) WRITE(NB6,710) NB11
      ENDIF
      IF(IPUBO.EQ.2 .OR. IPUBO.EQ.3) THEN
         NB12 = NBF(12)
         REWIND NB12
         WRITE(NB12) CA
         IF(UHF) WRITE(NB12) CB
         WRITE(NB12) EA
         IF(UHF) WRITE(NB12) EB
         IF(NPRINT.GT.-5) WRITE(NB6,720) NB12
      ENDIF
      RETURN
  400 FORMAT(///5X,A,/5X,A)
  410 FORMAT(/  5X,'COSMO TREATMENT OF SOLVATION EFFECTS.')
  500 FORMAT(///5X,'CORE HAMILTONIAN MATRIX.'/)
  510 FORMAT(///5X,'FOCK MATRIX.'/)
  520 FORMAT(///5X,'FOCK MATRIX FOR ALPHA SPIN.'/)
  530 FORMAT(///5X,'FOCK MATRIX FOR BETA SPIN.'/)
  540 FORMAT(///5X,'DENSITY MATRIX.'/)
  550 FORMAT(///5X,'SPIN DENSITY MATRIX.'/)
  560 FORMAT(///5X,'EXPECTATION VALUE FOR SZ  ',F10.5,
     1       /  5X,'EXPECTATION VALUE FOR S**2',F10.5)
  570 FORMAT(///5X,'SCF HEAT OF FORMATION',F15.5,' KCAL/MOL')
  571 FORMAT(///5X,'TOTAL ENERGY         ',F15.5,' KCAL/MOL')
  575 FORMAT(   5X,'SCF BINDING ENERGY   ',F15.5,' EV')
  580 FORMAT(   5X,'SCF TOTAL ENERGY     ',F15.5,' EV')
  590 FORMAT(   5X,'ELECTRONIC ENERGY    ',F15.5,' EV')
  595 FORMAT(   5X,'ENTROPIC FERMI TERM  ',F15.5,' EV',
     1       /  5X,'ENERGY WITHOUT FERMI ',F15.5,' EV')
  600 FORMAT(   5X,'NUCLEAR ENERGY       ',F15.5,' EV')
  610 FORMAT(   5X,'MM CORRECTION        ',F15.5,' KCAL/MOL')
  615 FORMAT(   5X,'DISPERSION ENERGY    ',F15.5,' KCAL/MOL')
  616 FORMAT(   5X,'E(EETOT)+E(DISP)     ',F15.5,' EV')
  617 FORMAT(   5X,'H CORRECTION ENERGY  ',F15.5,' KCAL/MOL')
  618 FORMAT(   5X,'E(EETOT)+E(DISP+HCOR)',F15.5,' EV')
  620 FORMAT(   5X,'IONIZATION ENERGY    ',F15.5,' EV')
  630 FORMAT(   5X,'IONIZATION ENERGY (2)',F15.5,' EV')
  640 FORMAT(   5X,'HALF ELECTRON TERM   ',F15.5,' EV')
  650 FORMAT(   5X,'INTEGRAL J11         ',F15.5,' EV')
  660 FORMAT(   5X,'INTEGRAL J11         ',F15.5,' EV'/
     1          5X,'INTEGRAL J22         ',F15.5,' EV'/
     2          5X,'INTEGRAL K12         ',F15.5,' EV')
  670 FORMAT(   5X,'SCF CYCLES           ',I9)
  710 FORMAT(// 5X,'THE DENSITY MATRIX HAS BEEN SAVED ON FILE',I3)
  720 FORMAT(// 5X,'THE EIGENVECTORS AND EIGENVALUES HAVE BEEN SAVED',
     1             ' ON FILE',I3)
      END
