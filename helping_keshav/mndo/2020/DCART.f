      SUBROUTINE DCART (PA,PB,LM4,MSYM,IEL,ISYM,DHCORE)
C     *
C     FAST GRADIENTS IN CARTESIAN COORDINATES BY FINITE DIFFERENCE.
C     *
C     NOTATION. I=INPUT, O=OUTPUT.
C     PA(LM4)   RHF OR UHF-ALPHA DENSITY MATRIX (I).
C     PB(LM4)   UHF-BETA DENSITY MATRIX (I).
C     MSYM      NUMBER OF CHARACTERISTIC SYMMETRY ELEMENTS (I).
C     IEL(3)    INTERNAL LABELS FOR THESE SYMMETRY ELEMENTS (I).
C     ISYM(3,*) PERMUTED TABLE OF ATOM NUMBERS AFTER APPLYING
C               THE CORRESPONDING SYMMETRY OPERATIONS (O).
C     DHCORE    EXTERNAL ROUTINE USED IN GRADIENT EVALUATION (I).
C     *
C     THE EXTERNAL REFERENCE DHCORE IS DEFINED IN THE CALLING ROUTINE.
C     DHCORE COMPUTES THE TWO-CENTER INTEGRALS REQUIRED FOR THE
C     GRADIENT CALCULATION.
C     *
      USE LIMIT, ONLY: LM1, LMX, LMP, LM1M
      IMPLICIT DOUBLE PRECISION (A-H,O-Z)
      PARAMETER (LMW=2025)
      EXTERNAL DHCORE
      LOGICAL UHF
      COMMON
     ./ATOMC / COORD(3,LM1)
     ./ATOMS / NUMAT,NAT(LM1),NFIRST(LM1),NLAST(LM1)
     ./ATSYM / IELSYM(3,7)
     ./CGRAD / CG(3,LM1+LM1M)
     ./CONSTF/ A0,AFACT,EV,EVCAL,PI,W1,W2,BIGEXP
     ./CONSTN/ ZERO,ONE,TWO,THREE,FOUR,PT5,PT25
     ./INDEX / INDX(LMX)
     ./INOPT2/ IN2(300)
     ./NBFILE/ NBF(20)
     ./UHF   / UHF
      DIMENSION PA(LM4),PB(LM4)
      DIMENSION IEL(3),ISYM(3,LM1)
      DIMENSION MIP(7),MJP(7),MIJP(7),MELP(7),MIH(4),MJH(4)
      DIMENSION NA(2),NF(2),NL(2),X(3,2)
      DIMENSION H(LMP),PA2(LMP),PB2(LMP),W(LMW)
      DIMENSION DELTA(2),EE(2)
C *** INPUT OPTIONS.
      NNHCO  = IN2(20)
      KCI    = IN2(77)
      INOUT  = IN2(212)
C *** INITIALIZATION.
      DEL    = 1.0D-06
      RDEL   = 5.0D+05
      IJPAIR = INDX(NUMAT)+NUMAT
      DELTA(1)= DEL
      DELTA(2)=-DEL
CMIC$ PROCESS
      DO 20 I=1,NUMAT
      DO 10 J=1,3
      CG(J,I) = ZERO
   10 CONTINUE
   20 CONTINUE
      IF(INOUT.GT.0 .AND. ABS(KCI).GE.2) THEN
         NB1 = NBF(1)
         REWIND NB1
         READ(NB1) PA
         IF(UHF) READ(NB1) PB
      ENDIF
CMIC$ END PROCESS
C
C *** LOOP OVER ATOM PAIRS IJ.
C     ATOMS I AND J ARE IDENTIFIED AT THE BEGINNING OF THE LOOP.
C     ATOM I GETS NUMBER 2 IN THE PAIR I-J SINCE I.GT.J
C     ATOM J GETS NUMBER 1 IN THE PAIR I-J SINCE I.GT.J
CMIC$ DO GLOBAL FOR 64
C$OMP PARALLEL DO SCHEDULE(DYNAMIC)
C$OMP.PRIVATE(DER,EE,EF,EH,ENUCLR,H,I,IA,IB,IJP,J,JA,JB,K,L,LIN,M,MEL,
C$OMP.MELP,MI,MIH,MIJ,MIJP,MIP,MIXZ,MIYZ,MJ,MJH,MJP,MJXZ,MJYZ,MM,
C$OMP.NA,NF,NL,NORBS,PA2,PB2,W,X,XSTORE)
      DO 120 IJ=2,IJPAIR
      I      = NINT(SQRT(TWO*IJ))
      J      = IJ-INDX(I)
      IF(I.EQ.J) GO TO 120
      IA     = NFIRST(I)
      IB     = NLAST(I)
      NA(2)  = NAT(I)
      X(1,2) = COORD(1,I)
      X(2,2) = COORD(2,I)
      X(3,2) = COORD(3,I)
      IJP    = 0
C *** CHECK SYMMETRY EQUIVALENCIES FOR PAIR I-J.
      IF(MSYM.EQ.0) GO TO 60
      MIJP(1)= 0
      MIJP(2)= 0
      DO 30 M=1,MSYM
      MI     = ISYM(M,I)
      MJ     = ISYM(M,J)
      IF(MI.GE.MJ) THEN
         MIJ = INDX(MI)+MJ
      ELSE
         MIJ = INDX(MJ)+MI
      ENDIF
      IF(MIJ.LT.IJ) GO TO 120
      IF(MIJ.EQ.IJ) GO TO 30
      IF(IJP.GT.0 .AND. MIJ.EQ.MIJP(1)) GO TO 30
      IF(IJP.GT.1 .AND. MIJ.EQ.MIJP(2)) GO TO 30
      IJP    = IJP+1
      MIP(IJP)  = MI
      MJP(IJP)  = MJ
      MIJP(IJP) = MIJ
      MELP(IJP) = IEL(M)
   30 CONTINUE
C     SPECIAL SECTION FOR D2H.
      IF(MSYM.LT.3 .OR. IEL(MSYM).GT.3 .OR. IJP.EQ.0) GO TO 60
      MIYZ   = ISYM(1,I)
      MIXZ   = ISYM(2,I)
      MIH(1) = ISYM(3,MIXZ)
      MIH(2) = ISYM(3,MIYZ)
      MIH(3) = ISYM(2,MIYZ)
      MIH(4) = ISYM(3,MIH(3))
      MJYZ   = ISYM(1,J)
      MJXZ   = ISYM(2,J)
      MJH(1) = ISYM(3,MJXZ)
      MJH(2) = ISYM(3,MJYZ)
      MJH(3) = ISYM(2,MJYZ)
      MJH(4) = ISYM(3,MJH(3))
      DO 50 M=1,4
      MI     = MIH(M)
      MJ     = MJH(M)
      IF(MI.GE.MJ) THEN
         MIJ = INDX(MI)+MJ
      ELSE
         MIJ = INDX(MJ)+MI
      ENDIF
      IF(MIJ.LT.IJ) GO TO 120
      IF(MIJ.EQ.IJ) GO TO 50
      DO 40 MM=1,IJP
      IF(MIJ.EQ.MIJP(MM)) GO TO 50
   40 CONTINUE
      IJP    = IJP+1
      MIP(IJP)  = MI
      MJP(IJP)  = MJ
      MIJP(IJP) = MIJ
      MELP(IJP) = M+3
   50 CONTINUE
C *** END OF SYMMETRY CHECKS.
   60 JA     = NFIRST(J)
      JB     = NLAST(J)
      NA(1)  = NAT(J)
      NF(1)  = 1
      NL(1)  = JB-JA+1
      NF(2)  = NL(1)+1
      NL(2)  = NF(2)+IB-IA
      NORBS  = NL(2)
      LIN    = INDX(NORBS)+NORBS
      X(1,1) = COORD(1,J)
      X(2,1) = COORD(2,J)
      X(3,1) = COORD(3,J)
C *** FORM DENSITY MATRIX FOR ATOM PAIR I-J
      CALL PSORT (JA,JB,IA,IB,PA2,PB2,PA,PB,LM4,LMP)
C
C *** LOOP OVER THREE CARTESIAN COORDINATES.
      DO 110 K=1,3
C *** CHECK FOR GRADIENT COMPONENTS WHICH ARE ZERO BY SYMMETRY.
      IF(MSYM.GT.0) THEN
         DO 70 M=1,MSYM
         IF(ISYM(M,I).NE.I .OR. ISYM(M,J).NE.J) GO TO 70
         MEL = IEL(M)
         IF(IELSYM(K,MEL).EQ.-1) GO TO 110
   70    CONTINUE
      ENDIF
C
C *** FINITE DIFFERENCE CALCULATION.
      XSTORE = X(K,2)
      DO 90 L=1,2
      X(K,2) = XSTORE+DELTA(L)
C     TWO-CENTER INTEGRALS.
      CALL DHCORE (H,W,LIN,LMW,I,J,NA,NF,NL,X,ENUCLR)
C     TWO-CENTER ONE-ELECTRON ENERGY.
      EH     = ZERO
      DO 80 M=1,LIN
      EH     = EH+H(M)*(PA2(M)+PB2(M))
   80 CONTINUE
C     TWO-CENTER TWO-ELECTRON ENERGY.
      EF     = ZERO
      CALL FOCK2D(EF,H,PA2,PB2,W,LIN,LMW,NF,NL)
      IF(UHF) THEN
         CALL FOCK2D(EF,H,PB2,PA2,W,LIN,LMW,NF,NL)
         EF = EF*PT5
      ENDIF
      EE(L)  = EH+EF+ENUCLR
   90 CONTINUE
      X(K,2) = XSTORE
      DER    = (EE(1)-EE(2))*EVCAL*RDEL
CMIC$ GUARD
C$OMP CRITICAL (DCART1)
      CG(K,I) = CG(K,I)+DER
      CG(K,J) = CG(K,J)-DER
C *** IMPOSE SYMMETRY EQUIVALENCIES.
      IF(IJP.GT.0) THEN
         DO 100 MM=1,IJP
         MI  = MIP(MM)
         MJ  = MJP(MM)
         MEL = MELP(MM)
         CG(K,MI) = CG(K,MI)+DER*IELSYM(K,MEL)
         CG(K,MJ) = CG(K,MJ)-DER*IELSYM(K,MEL)
  100    CONTINUE
      ENDIF
C$OMP END CRITICAL (DCART1)
CMIC$ END GUARD
  110 CONTINUE
  120 CONTINUE
C$OMP END PARALLEL DO
C *** MOLECULAR MECHANICS CORRECTION FOR PEPTIDE BONDS.
      IF(NNHCO.GT.0) CALL MMOKG (CG,DEL,LM1,NNHCO)
      RETURN
      END
