      SUBROUTINE KEYCHK (IMOPAC)
C     *
C     CHECK FOR KEYWORDS IN INPUT FILE.
C     PRINT AVAILABLE KEYWORDS AND ECHO INPUT FILE (IF REQUESTED).
C     *
C     NOTATION. I=INPUT,O=OUTPUT.
C     IMOPAC    FLAG FOR KEYWORD-BASED INPUT (O).
C               = 0  NO KEYWORDS.
C               =-1  ONLY MNDO97 KEYWORDS.
C               = 1  MOPAC KEYWORDS, POSSIBLY WITH MNDO97 KEYWORDS.
C     *
      IMPLICIT DOUBLE PRECISION (A-H,O-Z)
      CHARACTER*800 KEYWRD
      CHARACTER*80  LINE(3)
      COMMON
     ./KEYWRD/ KEYWRD
     ./NBFILE/ NBF(20)
      DIMENSION ICOL(3)
      DATA ICOL/80,80,30/
C *** FILE NUMBERS.
      NB5    = NBF(5)
      NB6    = NBF(6)
C *** INITIALIZATION.
      IMOPAC = 0
      ILOWA  = ICHAR('a')
      ILOWZ  = ICHAR('z')
      ICAPA  = ICHAR('A')
      ICAPZ  = ICHAR('Z')
C *** CHECK FIRST THREE LINES FOR CHARACTERS A-Z AND a-z.
C     CHECK COLUMNS 1-80 FOR LINES 1-2, COLUMNS 1-30 FOR LINE 3.
C     THIS IS TAKEN AS CRITERION FOR KEYWORDS.
      DO 20 N=1,3
      READ(NB5,500,ERR=100,END=110) LINE(N)
      NCOL   = ICOL(N)
      DO 10 I=1,NCOL
      ILINE  = ICHAR(LINE(N)(I:I))
      IF((ILINE.GE.ILOWA .AND. ILINE.LE.ILOWZ) .OR.
     1   (ILINE.GE.ICAPA .AND. ILINE.LE.ICAPZ)) THEN
         IMOPAC = 1
         GO TO 30
      ENDIF
   10 CONTINUE
   20 CONTINUE
   30 CONTINUE
      REWIND NB5
C *** CHECK TYPE OF KEYWORDS USED.
C     GETTXT PROVIDES THE KEYWORD STRING (MOPAC CONVENTIONS).
C     KEYDEF ANALYSES THE KEYWORD STRING (IMOPAC=-1 OR +1).
      IF(IMOPAC.EQ.1) THEN
         KEYMOD = -1
         CALL GETTXT (KEYWRD,KEYLEN,ICALL)
         IF(ICALL.EQ.-9) STOP 'KEYCHK'
         CALL KEYDEF (KEYWRD(:KEYLEN),IMOPAC,KEYMOD)
         REWIND NB5
      ENDIF
C *** THE KEYWORD STRING MAY BE LONGER IN MNDO97 (IMOPAC=-1)
C     THAN IN MOPAC (IMOPAC=1). IF NECESSARY, GET THE FULL
C     KEYWORD STRING AND ITS LENGTH.
      IF(IMOPAC.EQ.-1 .AND. KEYLEN.GT.160) THEN
         CALL KEYGET (KEYWRD,KEYLEN,ICALL)
         IF(ICALL.EQ.-9) STOP 'KEYCHK'
         REWIND NB5
      ENDIF
C *** CHECK FOR GENERAL PRINTING REQUESTS.
      JPRINT = 0
      IF(IMOPAC.NE.0) THEN
         II  = INDEX(KEYWRD(:KEYLEN),'JPRINT')
         IF(II.GT.0) THEN
            JPRINT = NINT(READA(KEYWRD(:KEYLEN),II,0))
         ENDIF
         IF(INDEX(KEYWRD(:KEYLEN),'ECHO').GT.0) THEN
            JPRINT = MAX(JPRINT,6)
         ENDIF
      ELSE
         READ(LINE(2),510) JPRINT
      ENDIF
C *** PRINT AVAILABLE KEYWORDS (IF REQUESTED).
      IF(JPRINT.GE.7) THEN
         KEYMOD = 1
         CALL KEYDEF (KEYWRD(:KEYLEN),IMOPAC,KEYMOD)
      ENDIF
C *** PRINT STANDARD INPUT FILE (IF REQUESTED).
      IF(JPRINT.GE.6) THEN
         WRITE(NB6,530)
         DO 70 I=1,10000
            READ(NB5,500,END=80) LINE(1)
            DO 40 J=80,2,-1
            IF(LINE(1)(J:J).NE.' ') GO TO 50
   40       CONTINUE
            J = 1
   50       DO 60 K=1,J
            IF(ICHAR(LINE(1)(K:K)).LT.32) LINE(1)(K:K)='*'
   60       CONTINUE
            WRITE(NB6,520) LINE(1)(1:J)
   70    CONTINUE
   80    WRITE(NB6,540)
         REWIND NB5
      ENDIF
C *** PRINT TYPE OF INPUT FILE (IF REQUESTED).
      IF(JPRINT.GE.5) THEN
         WRITE(NB6,550) IMOPAC
         IF(IMOPAC.NE.0) WRITE(NB6,560) KEYLEN
      ENDIF
      RETURN
C *** ERROR EXIT.
  100 WRITE(NB6,600)
      STOP 'KEYCHK'
  110 WRITE(NB6,610) N
      STOP 'KEYCHK'
  500 FORMAT(A)
  510 FORMAT(18X,I2)
  520 FORMAT(1X,A)
  530 FORMAT(/  1X,'ECHO INPUT FILE'/)
  540 FORMAT(/  1X,'END OF INPUT FILE'/)
  550 FORMAT(/  1X,'TYPE OF INPUT: IMOPAC =',I3)
  560 FORMAT(/  1X,'LENGTH OF KEYWORD STRING: KEYLEN =',I4,' BYTES')
  600 FORMAT(// 1X,'ERROR IN FIRST THREE LINES OF INPUT FILE'//)
  610 FORMAT(// 1X,'ERROR - END OF INPUT FILE AT LINE',I2//)
      END
