      SUBROUTINE MOINT3 (C,CC,C12,G,GG,W,WW,IMOCI,IPROD,NSYM,LM2,LM3,
     1                   LM6,LM7,LM8,LM9,LM10,NB2,NB3,IA,IB,JB,IEN2,
     2                   IOUT2,NNTOT,CA,CB,KK,LL,IABSCI)
C     *
C     PARTIAL AO-MO TRANSFORMATION FOR TWO-ELECTRON INTEGRALS.
C     *
      IMPLICIT DOUBLE PRECISION (A-H,O-Z)
      COMMON
     ./CONSTF/ A0,AFACT,EV,EVCAL,PI,W1,W2,BIGEXP
     ./CONSTN/ ZERO,ONE,TWO,THREE,FOUR,PT5,PT25
     ./NBFILE/ NBF(20)
      DIMENSION C(LM2,LM3),CC(LM7),W(LM9)
      DIMENSION IMOCI(LM3),IPROD(8,8),NSYM(LM3)
      DIMENSION C12(LM6),WW(LM6)
      DIMENSION G(LM8),GG(LM10,LM10)
C     INITIALIZATION.
      REV    = ONE/EV
      SQ2    = SQRT(TWO)
      SQ3    = SQRT(THREE)
      LMAX   = LM7/LM6
      KKS    = NSYM(KK)
      LLS    = NSYM(LL)
      NN     = 0
      NNTOT  = 0
C     *
C     MATRIX ELEMENTS FOR SINGLE EXCITATIONS.
C     *
      CALL CCPROD (C(1,KK),C(1,LL),C12,LM2,LM6)
      CALL WWSTEP (C12,CC,WW,LM7,NB3,LM6,LMAX)
      DO 10 I=1,IB
      II     = IMOCI(I)
      IF(II.EQ.LL) GO TO 10
      IS     = NSYM(II)
      NSIK   = IPROD(IS,KKS)
      NSIL   = IPROD(IS,LLS)
      IF(NSIK.NE.1 .AND. I.GE.IA) GO TO 10
      IF(NSIL.NE.1 .AND. I.LT.IA) GO TO 10
      IF(I.LT.IA) THEN
C        MO I IS OCCUPIED.
         CALL CCPROD (C(1,KK),C(1,II),C12,LM2,LM6)
         WNN = -DDOT (LM6,C12,1,WW,1)
         IF(II.EQ.KK) THEN
            CALL CCPROD (C(1,LL),C(1,LL),C12,LM2,LM6)
            WNN = -WNN-DDOT (LM6,C12,1,WW,1)
         ENDIF
      ELSE
C        MO I IS UNOCCUPIED.
         CALL CCPROD (C(1,LL),C(1,II),C12,LM2,LM6)
         WNN = DDOT (LM6,C12,1,WW,1)
      ENDIF
C     STORE THE MATRIX ELEMENT COMPUTED.
      NN     = NN+1
      W(NN)  = CB*SQ2*WNN*REV
   10 CONTINUE
C     *
C     MATRIX ELEMENTS FOR DOUBLE EXCITATIONS.
C     *
      JMAX   = LM8/LM6
      IF(JMAX.GT.JB) JMAX=JB
C     OUTER IJ-LOOP.
      DO 130 I=IA,IB
      II     = IMOCI(I)
      IS     = NSYM(II)
C     COMPUTE JMAX SETS OF (IJ,AB) INTEGRALS.
      MM     = 1-LM6
      DO 20 J=1,JMAX
      IJ     = IMOCI(J)
      MM     = MM+LM6
      CALL CCPROD (C(1,II),C(1,IJ),C12,LM2,LM6)
      CALL WWSTEP (C12,CC,G(MM),LM7,NB3,LM6,LMAX)
   20 CONTINUE
C     RESUME IJ-LOOP.
      DO 120 J=1,JB
      IJ     = IMOCI(J)
      JS     = NSYM(IJ)
      NSIJ   = IPROD(IS,JS)
C     INNER KL-LOOP.
      DO 110 K=IA,I
      IK     = IMOCI(K)
      KS     = NSYM(IK)
      IEQK   = 0
      IF(I.GT.K) IEQK=1
      DO 100 L=1,J
      IL     = IMOCI(L)
      LS     = NSYM(IL)
      NSKL   = IPROD(KS,LS)
      IF(NSIJ.NE.NSKL) GO TO 100
      IF(II.EQ.LL .AND. IJ.EQ.KK .AND. IK.EQ.LL .AND. IL.EQ.KK) GOTO 100
      JEQL   = 0
      IF(J.GT.L) JEQL=1
      IGO    = 1+IEQK+2*JEQL
      IGOGO  = 0
C     COMPUTE INTEGRAL (IJ,KL), I.E. (IU,JV) IN NOTES.
      IF(J.LE.JMAX) THEN
         MM  = 1+LM6*(J-1)
         CALL CCPROD (C(1,IK),C(1,IL),C12,LM2,LM6)
         WN1 = DDOT  (LM6,C12,1,G(MM),1)
      ELSE
         CALL CCPROD (C(1,II),C(1,IJ),C12,LM2,LM6)
         CALL WWSTEP (C12,CC,WW,LM7,NB3,LM6,LMAX)
         CALL CCPROD (C(1,IK),C(1,IL),C12,LM2,LM6)
         WN1 = DDOT  (LM6,C12,1,WW,1)
      ENDIF
      IF(I.EQ.K .OR. J.EQ.L) GO TO 200
C     COMPUTE INTEGRAL (IL,KJ), I.E. (IV,JU) IN NOTES.
      IF(L.LE.JMAX) THEN
         MM  = 1+LM6*(L-1)
         CALL CCPROD (C(1,IK),C(1,IJ),C12,LM2,LM6)
         WN2 = DDOT  (LM6,C12,1,G(MM),1)
      ELSE
         CALL CCPROD (C(1,II),C(1,IL),C12,LM2,LM6)
         CALL WWSTEP (C12,CC,WW,LM7,NB3,LM6,LMAX)
         CALL CCPROD (C(1,IK),C(1,IJ),C12,LM2,LM6)
         WN2 = DDOT  (LM6,C12,1,WW,1)
      ENDIF
  200 CONTINUE
      GO TO (210,220,230,240),IGO
C     EXCITATIONS J,J-I,I.
  210 WNN    = CA*WN1
      IF(II.NE.LL .AND. IJ.NE.KK) GO TO 250
      IF(II.EQ.LL) THEN
         CALL WINT (IJ,KK,IJ,KK,WN3,C,CC,C12,WW,LM2,LM3,LM7,NB3,LM6)
      ELSE
         CALL WINT (LL,II,LL,II,WN3,C,CC,C12,WW,LM2,LM3,LM7,NB3,LM6)
      ENDIF
      WNN    = WNN+CB*WN3
      GO TO 250
C     EXCITATIONS J,J-I,K.
  220 WNN    = CA*SQ2*WN1
      IF(IJ.NE.KK) GO TO 250
      IF(II.EQ.LL .OR. IK.EQ.LL) THEN
         CALL WINT (LL,LL,II,IK,WN3,C,CC,C12,WW,LM2,LM3,LM7,NB3,LM6)
         CALL WINT (KK,KK,II,IK,WN4,C,CC,C12,WW,LM2,LM3,LM7,NB3,LM6)
         WNN = WNN+CB*SQ2*(WN1+WN3-TWO*WN4)
      ELSE
         CALL WINT (LL,II,LL,IK,WN3,C,CC,C12,WW,LM2,LM3,LM7,NB3,LM6)
         WNN = WNN+CB*SQ2*WN3
      ENDIF
      GO TO 250
C     EXCITATIONS J,L-I,I.
  230 WNN    = -CA*SQ2*WN1
      IF(II.NE.LL) GO TO 250
      IF(IJ.EQ.KK .OR. IL.EQ.KK) THEN
         CALL WINT (KK,KK,IJ,IL,WN3,C,CC,C12,WW,LM2,LM3,LM7,NB3,LM6)
         CALL WINT (LL,LL,IJ,IL,WN4,C,CC,C12,WW,LM2,LM3,LM7,NB3,LM6)
         WNN = WNN-CB*SQ2*(WN1+WN3-TWO*WN4)
      ELSE
         CALL WINT (IJ,KK,IL,KK,WN3,C,CC,C12,WW,LM2,LM3,LM7,NB3,LM6)
         WNN = WNN-CB*SQ2*WN3
      ENDIF
      GO TO 250
C     EXCITATIONS J,L-I,K.
C     NOTE. WN1 IS (IU,KL), WN2 IS (IK,LU) IN NOTES.
  240 WNN    = -CA*(WN2+WN1)
      IGOGO  = 1
      IF(IJ.NE.KK .AND. IL.NE.KK) GO TO 250
      IF(II.NE.LL .AND. IK.NE.LL) GO TO 250
      CALL WINT (IJ,IL,II,IK,WN3,C,CC,C12,WW,LM2,LM3,LM7,NB3,LM6)
      WNN    = WNN-CB*(WN1-TWO*WN3)
      GO TO 250
  245 WNN    = CA*SQ3*(WN2-WN1)
      IGOGO  = 0
      IF(IJ.NE.KK .AND. IL.NE.KK) GO TO 250
      IF(II.NE.LL .AND. IK.NE.LL) GO TO 250
      WNN    = WNN-CB*SQ3*WN1
C     STORE THE MATRIX ELEMENT COMPUTED.
  250 NN     = NN+1
      IF(NN.GT.LM9) THEN
         IF(NNTOT.EQ.0) REWIND NB2
         WRITE(NB2) W
         NNTOT = NNTOT+LM9
         NN  = 1
      ENDIF
      W(NN)  = WNN*REV
      IF(IGOGO.EQ.1) GO TO 245
  100 CONTINUE
  110 CONTINUE
  120 CONTINUE
  130 CONTINUE
      IF(IABSCI.EQ.3) GO TO 540
C     *
C     MATRIX ELEMENTS FOR TRIPLE EXCITATIONS.
C     *
C *** FIRST SUM. TYPES 8B,9,10.
      JMAX   = LM8/LM6+IA-1
      IF(JMAX.GT.IB) JMAX=IB
C     COMPUTE JMAX SETS OF (IJ,AB) INTEGRALS.
      MM     = 1-LM6
      DO 290 J=IA,JMAX
      IJ     = IMOCI(J)
      MM     = MM+LM6
      IF(IJ.EQ.LL) GO TO 290
      CALL CCPROD (C(1,LL),C(1,IJ),C12,LM2,LM6)
      CALL WWSTEP (C12,CC,G(MM),LM7,NB3,LM6,LMAX)
  290 CONTINUE
C     START THE LOOP.
      DO 300 I=1,JB
      II     = IMOCI(I)
      IF(II.EQ.KK) GO TO 300
      IS     = NSYM(II)
      NSILL  = IPROD(IS,LLS)
      DO 310 J=IA,IB
      IJ     = IMOCI(J)
      IF(IJ.EQ.LL) GO TO 310
      JS     = NSYM(IJ)
      DO 320 K=IA,J
      IK     = IMOCI(K)
      IF(IK.EQ.LL) GO TO 320
      KS     = NSYM(IK)
      NSJK   = IPROD(JS,KS)
      IF(NSILL.NE.NSJK) GO TO 320
C     COMPUTE FIRST INTEGRAL, I.E. (IU,LV) IN NOTES.
      IF(J.LE.JMAX) THEN
         MM  = 1+LM6*(J-IA)
         CALL CCPROD (C(1,II),C(1,IK),C12,LM2,LM6)
         WN1 = DDOT  (LM6,C12,1,G(MM),1)
      ELSE
         CALL CCPROD (C(1,LL),C(1,IJ),C12,LM2,LM6)
         CALL WWSTEP (C12,CC,WW,LM7,NB3,LM6,LMAX)
         CALL CCPROD (C(1,II),C(1,IK),C12,LM2,LM6)
         WN1 = DDOT  (LM6,C12,1,WW,1)
      ENDIF
      IF(J.EQ.K) GO TO 340
C     COMPUTE SECOND INTEGRAL, I.E. (IV,LU) IN NOTES.
      IF(K.LE.JMAX) THEN
         MM = 1+LM6*(K-IA)
         CALL CCPROD (C(1,II),C(1,IJ),C12,LM2,LM6)
         WN2 = DDOT  (LM6,C12,1,G(MM),1)
      ELSE
         CALL CCPROD (C(1,LL),C(1,IK),C12,LM2,LM6)
         CALL WWSTEP (C12,CC,WW,LM7,NB3,LM6,LMAX)
         CALL CCPROD (C(1,II),C(1,IJ),C12,LM2,LM6)
         WN2 = DDOT  (LM6,C12,1,WW,1)
      ENDIF
  340 CONTINUE
      IGOGO  = 0
      IF(J.GT.K) GO TO 350
C     EXCITATIONS I,KMO,KMO-J,J,LMO.
      WNN    = -CB*SQ2*WN1
      GO TO 370
C     EXCITATIONS I,KMO,KMO-J,K,LMO.
  350 WNN    = -CB*(WN2+WN1)
      IGOGO  = 1
      GO TO 370
  360 WNN    = CB*SQ3*(WN2-WN1)
      IGOGO  = 0
C     STORE THE MATRIX ELEMENT COMPUTED.
  370 NN     = NN+1
      IF(NN.GT.LM9) THEN
         IF(NNTOT.EQ.0) REWIND NB2
         WRITE(NB2) W
         NNTOT = NNTOT+LM9
         NN  = 1
      ENDIF
      W(NN)  = WNN*REV
      IF(IGOGO.EQ.1) GO TO 360
  320 CONTINUE
  310 CONTINUE
  300 CONTINUE
C *** SECOND SUM. TYPES 8A,12,13.
      JMAX   = LM8/LM6
      IF(JMAX.GT.JB) JMAX=JB
C     COMPUTE JMAX SETS OF (IJ,AB) INTEGRALS.
      MM     = 1-LM6
      DO 390 J=1,JMAX
      IJ     = IMOCI(J)
      MM     = MM+LM6
      IF(IJ.EQ.KK) GO TO 390
      CALL CCPROD (C(1,KK),C(1,IJ),C12,LM2,LM6)
      CALL WWSTEP (C12,CC,G(MM),LM7,NB3,LM6,LMAX)
  390 CONTINUE
C     START THE LOOP.
      DO 400 I=IA,IB
      II     = IMOCI(I)
      IF(II.EQ.LL) GO TO 400
      IS     = NSYM(II)
      NSIKK  = IPROD(IS,KKS)
      DO 410 J=1,JB
      IJ     = IMOCI(J)
      IF(IJ.EQ.KK) GO TO 410
      JS     = NSYM(IJ)
      DO 420 K=1,J
      IK     = IMOCI(K)
      IF(IK.EQ.KK) GO TO 420
      KS     = NSYM(IK)
      NSJK   = IPROD(JS,KS)
      IF(NSIKK.NE.NSJK) GO TO 420
C     COMPUTE FIRST INTEGRAL, I.E. (KJ,IU) IN NOTES.
      IF(J.LE.JMAX) THEN
         MM  = 1+LM6*(J-1)
         CALL CCPROD (C(1,II),C(1,IK),C12,LM2,LM6)
         WN1 = DDOT  (LM6,C12,1,G(MM),1)
      ELSE
         CALL CCPROD (C(1,KK),C(1,IJ),C12,LM2,LM6)
         CALL WWSTEP (C12,CC,WW,LM7,NB3,LM6,LMAX)
         CALL CCPROD (C(1,II),C(1,IK),C12,LM2,LM6)
         WN1 = DDOT  (LM6,C12,1,WW,1)
      ENDIF
      IF(J.EQ.K) GO TO 440
C     COMPUTE SECOND INTEGRAL, I.E. (KI,JU) IN NOTES.
      IF(K.LE.JMAX) THEN
         MM  = 1+LM6*(K-1)
         CALL CCPROD (C(1,II),C(1,IJ),C12,LM2,LM6)
         WN2 = DDOT  (LM6,C12,1,G(MM),1)
      ELSE
         CALL CCPROD (C(1,KK),C(1,IK),C12,LM2,LM6)
         CALL WWSTEP (C12,CC,WW,LM7,NB3,LM6,LMAX)
         CALL CCPROD (C(1,II),C(1,IJ),C12,LM2,LM6)
         WN2 = DDOT  (LM6,C12,1,WW,1)
      ENDIF
  440 CONTINUE
      IGOGO  = 0
      IF(J.GT.K) GO TO 450
C     EXCITATIONS J,J,KMO-I,LMO,LMO.
      WNN=CB*SQ2*WN1
      GO TO 470
C     EXCITATIONS J,K,KMO-I,LMO,LMO.
  450 WNN=CB*(TWO*WN2-WN1)
      IGOGO  = 1
      GO TO 470
  460 WNN    = -CB*SQ3*WN1
      IGOGO  = 0
C     STORE THE MATRIX ELEMENT COMPUTED.
  470 NN     = NN+1
      IF(NN.GT.LM9) THEN
         IF(NNTOT.EQ.0) REWIND NB2
         WRITE(NB2) W
         NNTOT = NNTOT+LM9
         NN  = 1
      ENDIF
      W(NN)  = WNN*REV
      IF(IGOGO.EQ.1) GO TO 460
  420 CONTINUE
  410 CONTINUE
  400 CONTINUE
C *** THIRD SUM. TYPE 8C.
      DO 500 I=1,JB
      II     = IMOCI(I)
      IF(II.EQ.KK) GO TO 500
      IS     = NSYM(II)
      DO 510 J=IA,IB
      IJ     = IMOCI(J)
      IF(IJ.EQ.LL) GO TO 510
      JS     = NSYM(IJ)
      NSIJ   = IPROD(IS,JS)
      IF(NSIJ.NE.1) GO TO 510
      CALL WINT (LL,LL,II,IJ,WN1,C,CC,C12,WW,LM2,LM3,LM7,NB3,LM6)
      CALL WINT (LL,II,LL,IJ,WN2,C,CC,C12,WW,LM2,LM3,LM7,NB3,LM6)
      CALL WINT (KK,KK,II,IJ,WN3,C,CC,C12,WW,LM2,LM3,LM7,NB3,LM6)
      CALL WINT (KK,II,KK,IJ,WN4,C,CC,C12,WW,LM2,LM3,LM7,NB3,LM6)
C     EXCITATIONS I,KMO,KMO-J,LMO,LMO.
      WNN    = TWO*WN1-WN2-TWO*WN3+WN4
      NN     = NN+1
      IF(NN.GT.LM9) THEN
         IF(NNTOT.EQ.0) REWIND NB2
         WRITE(NB2) W
         NNTOT = NNTOT+LM9
         NN  = 1
      ENDIF
      W(NN)  = CB*SQ2*WNN*REV
  510 CONTINUE
  500 CONTINUE
C     *
C     NUMBER OF NONZERO MATRIX ELEMENTS.
C     *
  540 NNTOT  = NNTOT+NN
      IF(NNTOT.GT.LM9) WRITE(NB2) W
      LREC   = 1+(NNTOT-1)/LM9
      IF(IOUT2.GE.0) THEN
         NB6 = NBF(6)
         WRITE(NB6,710) NNTOT
         IF(IOUT2.GE.5 .AND. LREC.GT.1) THEN
            WRITE(NB6,720) NB2,LREC,LM9
         ENDIF
      ENDIF
C     *
C     COMPUTE COULOMB AND EXCHANGE INTEGRALS.
C     *
      DO 610 I=1,IB
      DO 600 J=1,IB
      GG(J,I)= ZERO
  600 CONTINUE
  610 CONTINUE
      IF(IEN2.EQ.0) RETURN
C     COULOMB INTEGRALS (II,JJ).
      DO 630 I=1,IB
      II     = IMOCI(I)
      CALL CCPROD (C(1,II),C(1,II),C12,LM2,LM6)
      CALL WWSTEP (C12,CC,WW,LM7,NB3,LM6,LMAX)
      DO 620 J=1,I
      IJ     = IMOCI(J)
      CALL CCPROD (C(1,IJ),C(1,IJ),C12,LM2,LM6)
      WNN    = DDOT  (LM6,C12,1,WW,1)
      GG(I,J)= WNN*REV
  620 CONTINUE
  630 CONTINUE
C     EXCHANGE INTEGRALS (IJ,IJ).
      IF(IB.EQ.1) RETURN
      DO 650 I=2,IB
      II     = IMOCI(I)
      IMINUS = I-1
      DO 640 J=1,IMINUS
      IJ     = IMOCI(J)
      CALL CCPROD (C(1,II),C(1,IJ),C12,LM2,LM6)
      CALL WWSTEP (C12,CC,WW,LM7,NB3,LM6,LMAX)
      WNN    = DDOT  (LM6,C12,1,WW,1)
      GG(J,I)= WNN*REV
  640 CONTINUE
  650 CONTINUE
      RETURN
  710 FORMAT(/1X,'THERE ARE',I7,' NONZERO MATRIX ELEMENTS.'/)
  720 FORMAT( 1X,'THEY ARE STORED ON FILE',I3,' IN',I4,' RECORDS OF',
     1            I5,' WORDS.'/)
      END
