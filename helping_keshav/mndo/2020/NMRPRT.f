      SUBROUTINE NMRPRT (NMR,IOP)
C     *
C     COMPUTE NMR CHEMICAL SHIFTS FROM NMR SHIELDINGS AND PRINT RESULTS.
C     *
      USE LIMIT, ONLY: LM1, LMZ, MXNICS
      IMPLICIT DOUBLE PRECISION (A-H,O-Z)
      LOGICAL INC
      CHARACTER*2 ELEMNT
      COMMON
     ./ATOMC / COORD(3,LM1)
     ./ATOMS / NUMAT,NAT(LM1),NFIRST(LM1),NLAST(LM1)
     ./CONSTN/ ZERO,ONE,TWO,THREE,FOUR,PT5,PT25
     ./ELEMTS/ ELEMNT(107)
     ./NBFILE/ NBF(20)
     ./PSDOPT/ RPSOPT(10),IPSOPT(28)
     ./PSNICS/ COORDN(3,MXNICS), NNICS
     ./PSNMRI/ INC(LM1)
     ./PSNMRR/ SHIFT(2,LM1+MXNICS),SIGMA(3,3,LM1+MXNICS)
     ./PSNPRD/ XBETAN(LMZ,15),XSCALN(0:LMZ),OFFN(0:LMZ,2),IDEFN(0:LMZ)
C     TWO-PARAMETER FITS FOR STANDARD MNDO (J=1 GAS, J=2 LIQUID).
      DIMENSION OFMNDO(LMZ,2),SCMNDO(LMZ,2),SHIFTM(2)
      SAVE OFMNDO,SCMNDO
      DATA OFMNDO / 33.396D0, 4*0.0D0, -61.500D0,-346.500D0, -39.560D0,
     1                       76*0.0D0,  38.498D0,  26.284D0,
     2              32.692D0, 4*0.0D0, -63.660D0,-398.700D0, -46.940D0,
     3                       76*0.0D0,  57.520D0,  47.920D0/
      DATA SCMNDO /-0.6277D0, 4*0.0D0, -0.7619D0, -0.5698D0, -0.4806D0,
     1                       76*0.0D0, -0.8678D0, -0.6269D0,
     2             -0.6221D0, 4*0.0D0, -0.7172D0, -0.5402D0, -0.4411D0,
     3                       76*0.0D0, -1.2587D0, -1.0758D0/
C *** FILE NUMBERS.
      NB6    = NBF(6)
C *** INITIALIZATION.
      IPRINT = IPSOPT(5)
C *** COMPUTE NMR CHEMICAL SHIFTS.
      DO 10 I=1,NUMAT
      IF(INC(I)) THEN
         AVE = (SIGMA(1,1,I)+SIGMA(2,2,I)+SIGMA(3,3,I))/THREE
         NI  = NAT(I)
         IF(NI.EQ.1) CALL NNEIGH (I,NI,NUMAT,COORD,NAT)
         SHIFT(1,I) = -AVE + OFFN(NI,1)
         SHIFT(2,I) = -AVE + OFFN(NI,2)
      ENDIF
   10 CONTINUE
      DO 20 I=1,NNICS
      NICS = NUMAT+I
      AVE  = (SIGMA(1,1,NICS)+SIGMA(2,2,NICS)+SIGMA(3,3,NICS))/THREE
      SHIFT(1,NICS) = -AVE + OFFN(0,1)
      SHIFT(2,NICS) = -AVE + OFFN(0,2)
   20 CONTINUE
C *** PRINT NMR CHEMICAL SHIFTS.
      IF(IPRINT.GE.-1) THEN
C        CHECK FOR NONZERO OFFSET VALUES.
         NNINC = 0
         NOFFG = 0
         NOFFL = 0
         DO 30 I=1,NUMAT
         IF(INC(I)) THEN
            NNINC = NNINC+1
            NI    = NAT(I)
            IF(OFFN(NI,1).NE.ZERO) NOFFG=NOFFG+1
            IF(OFFN(NI,2).NE.ZERO) NOFFL=NOFFL+1
         ENDIF
   30    CONTINUE
         NOFFN = NOFFL+NOFFG
C        PRINT RESULTS.
         IF(NNINC.GT.0 .AND. NOFFN.GT.0) THEN
            WRITE(NB6,500)
            WRITE(NB6,510)
            WRITE(NB6,520)
            IF(IPRINT.GE.0) THEN
               WRITE(NB6,530)
               WRITE(NB6,540)
            ENDIF
            WRITE(NB6,550)
            WRITE(NB6,560)
            DO 40 I=1,NUMAT
            IF(INC(I)) THEN
               NI  = NAT(I)
               IF(NI.EQ.1) CALL NNEIGH (I,NI,NUMAT,COORD,NAT)
               AVE = -SHIFT(1,I)+OFFN(NI,1)
               WRITE(NB6,570) I,ELEMNT(NAT(I)),AVE,
     1                      (OFFN(NI,K),SHIFT(K,I),K=1,2)
            ENDIF
   40       CONTINUE
         ENDIF
         IF(NNICS.GT.0) THEN
            WRITE(NB6,580)
            WRITE(NB6,590)
            DO 50 I=1,NNICS
            AVE = -SHIFT(1,I+NUMAT)+OFFN(0,1)
            WRITE(NB6,570) I,'NICS',AVE,OFFN(0,1),SHIFT(1,I+NUMAT)
   50       CONTINUE
         ENDIF
         WRITE(NB6,600)
      ENDIF
C *** SPECIAL PRINTING SECTION FOR STANDARD MNDO.
      IF(NMR.LT.0 .AND. IOP.EQ.0 .AND. IPRINT.GE.-1) THEN
         IF(NNINC.GT.0) THEN
            WRITE(NB6,610)
            DO 60 I=1,NUMAT
            IF(INC(I)) THEN
               AVE = (SIGMA(1,1,I)+SIGMA(2,2,I)+SIGMA(3,3,I))/THREE
               NI  = NAT(I)
               IF(NI.EQ.1) CALL NNEIGH (I,NI,NUMAT,COORD,NAT)
               SHIFTM(1) = AVE*SCMNDO(NI,1) + OFMNDO(NI,1)
               SHIFTM(2) = AVE*SCMNDO(NI,2) + OFMNDO(NI,2)
               WRITE(NB6,620) I,ELEMNT(NAT(I)),AVE,
     1                      (SCMNDO(NI,K),OFMNDO(NI,K),SHIFTM(K),K=1,2)
            ENDIF
   60       CONTINUE
            WRITE(NB6,600)
         ENDIF
      ENDIF
      RETURN
  500 FORMAT(//,' CONVERSION OF THE COMPUTED SHIELDINGS INTO NMR',
     1       ' CHEMICAL SHIFTS USING METHOD-SPECIFIC OFFSETS.')
  510 FORMAT(' OFFSETG AND SHIFTG REFER TO EXPERIMENTAL REFERENCE',
     1       ' DATA FROM THE GAS PHASE.')
  520 FORMAT(' OFFSETL AND SHIFTL REFER TO EXPERIMENTAL REFERENCE',
     1       ' DATA FROM THE LIQUID PHASE.')
  530 FORMAT(' REFERENCE MOLECULES (GAS PHASE): METHANE (H,C),',
     1       ' NITROGEN (N), WATER (O).')
  540 FORMAT(' REFERENCE MOLECULES (LIQUID PHASE): TETRAMETHYLSILANE',
     1       ' (H,C), NITROMETHANE (N), WATER (O).')
  550 FORMAT(' ALL VALUES IN P.P.M.'/)
  560 FORMAT(' ATOM TYPE   SHIELDING     OFFSETG      SHIFTG',
     1       '     OFFSETL      SHIFTL'/)
  570 FORMAT(1X,I4,1X,A4,5(2X,F10.3))
  580 FORMAT(//,' CONVERSION OF THE COMPUTED SHIELDINGS INTO NICS',
     1       ' VALUES (P.P.M.)')
  590 FORMAT(' ATOM TYPE   SHIELDING     OFFSETG      SHIFTG'/)
  600 FORMAT()
  610 FORMAT(//,' WHEN USING STANDARD MNDO PARAMETERS, A TWO-PARAMETER',
     1       ' FIT (SHIFT = SCMNDO * SHIELDING + OFMNDO)',/,
     2       ' YIELDS BETTER RESULTS THAN A ONE-PARAMETER FIT',
     3       ' (SHIFT = -SHIELDING + OFFSET).',/,
     4       ' RESULTS FROM SUCH A TWO-PARAMETER FIT (P.P.M.)',//,
     5       ' ATOM TYPE   SHIELDING     SCMNDOG     OFMNDOG',
     6       '      SHIFTG     SCMNDOL     OFMNDOL      SHIFTL'/)
  620 FORMAT(1X,I4,1X,A4,7(2X,F10.3))
      END
