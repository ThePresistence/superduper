      SUBROUTINE CIS (C,EIG,CKL,W,LM2,LM3,LM6,LM9)
C     *
C     MINIMAL CONFIGURATION INTERACTION
C     *
C     NOTATION. I=INPUT, O=OUTPUT, S=SCRATCH.
C     C(LM2,*)  EIGENVECTORS (I).
C     EIG(LM3)  ORBITAL ENERGIES (I).
C     CKL(LM6)  PRODUCTS OF COEFFICIENTS FOR ONE-CENTER PAIRS (S).
C     W(LM9)    TWO-ELECTRON INTEGRALS (I).
C     *
      IMPLICIT DOUBLE PRECISION (A-H,O-Z)
      COMMON
     ./CCIS  / K,L,NC,LROOT
     ./CONSTF/ A0,AFACT,EV,EVCAL,PI,W1,W2,BIGEXP
     ./CONSTN/ ZERO,ONE,TWO,THREE,FOUR,PT5,PT25
     ./ENERGT/ ENERGY,ENUCLR,EAT,ATHEAT
     ./FLAG3 / KRESET,MPRINT,T2
     ./HALFX / XI,XJ,XK
     ./INOPT2/ IN2(300)
     ./MMCOM1/ EMM
     ./MMDP  / EMMDP
     ./ORBITS/ NUMB,NORBS,NMOS,NALPHA,NBETA
     ./XCI   / E(6),ENGYCI(3),VECTCI(3,3),ECI(3),BKCAL(3)
      DIMENSION C(LM2,LM3),EIG(LM3),CKL(LM6),W(LM9)
      DIMENSION EDUM(6),Q(15)
C *** INPUT OPTIONS.
      IMULT  = IN2(66)
      IATERG = IN2(119)
      IMODE  = IN2(211)
C *** CALCULATE THE REQUIRED TWO-ELECTRON INTEGRALS.
      IF(IMODE.GT.0) THEN
         XKKKK = SPCG (C(1,K),C(1,K),C(1,K),C(1,K),W,LM2,LM9)
         XLLLL = SPCG (C(1,L),C(1,L),C(1,L),C(1,L),W,LM2,LM9)
         XKKLL = SPCG (C(1,K),C(1,K),C(1,L),C(1,L),W,LM2,LM9)
         XKLKL = SPCG (C(1,K),C(1,L),C(1,K),C(1,L),W,LM2,LM9)
         XKKKL = SPCG (C(1,K),C(1,K),C(1,K),C(1,L),W,LM2,LM9)
         XKLLL = SPCG (C(1,K),C(1,L),C(1,L),C(1,L),W,LM2,LM9)
      ELSE
         XKKKK = SPCW (C(1,K),C(1,K),C(1,K),C(1,K),CKL,W,LM2,LM6)
         XLLLL = SPCW (C(1,L),C(1,L),C(1,L),C(1,L),CKL,W,LM2,LM6)
         XKKLL = SPCW (C(1,K),C(1,K),C(1,L),C(1,L),CKL,W,LM2,LM6)
         XKLKL = SPCW (C(1,K),C(1,L),C(1,L),C(1,K),CKL,W,LM2,LM6)
         XKKKL = SPCW (C(1,K),C(1,K),C(1,K),C(1,L),CKL,W,LM2,LM6)
         XKLLL = SPCW (C(1,K),C(1,L),C(1,L),C(1,L),CKL,W,LM2,LM6)
      ENDIF
C *** CALCULATE DIFFERENCE OF ORBITAL ENERGIES.
      EDIFF  = EIG(L)-EIG(K)
C *** 2*2 OR 3*3 CLOSED-SHELL CI.
      IF(IMULT.EQ.0) THEN
         E(1)   = ENERGY
         E(2)   = XKLKL
         E(3)   = E(1)+TWO*EDIFF-FOUR*XKKLL+TWO*XKLKL+XKKKK+XLLLL
         IF(NC.GT.2) THEN
            E(4)   = ZERO
            E(5)   = (XKKKL-XKLLL)*SQRT(TWO)
            E(6)   = E(1)+EDIFF-XKKLL+TWO*XKLKL
         ENDIF
C *** 3*3 CI FOR THE FIRST EXCITED SINGLET (HALF ELECTRON METHOD).
C     IN THE FIRST CONFIGURATION, K AND L ARE SINGLY OCCUPIED (HALF
C     ELECTRON SCF MOS). THE OTHER TWO CONFIGURATIONS ARE THE CLOSED
C     SHELLS WITH THE OCCUPATIONS KK AND LL.
      ELSE IF(IMULT.EQ.1) THEN
         H12    = PT5*SQRT(TWO)*(XKKKL-XKLLL)
         E(1)   = ENERGY+PT25*(XI+XJ-XKKKK-XLLLL)-1.5D0*(XK-XKLKL)
         E(2)   = H12
         E(3)   = E(1)-EDIFF-XKKLL-XKLKL+PT5*(XKKKK+XLLLL)
         E(4)   =-H12
         E(5)   = XKLKL
         E(6)   = E(3)+TWO*EDIFF
C *** 2*2 CI FOR DOUBLET (HALF ELECTRON METHOD).
C     CASE 1. EXCITATION FROM K TO L (DOUBLY TO SINGLY OCCUPIED).
C     CASE 2. EXCITATION FROM K TO L (SINGLY OCCUPIED TO UNOCCUPIED).
      ELSE IF(IMULT.EQ.2) THEN
         IF(L.LE.NUMB) THEN
            E(1)   = ENERGY
            E(2)   = PT5*XKLLL
            E(3)   = E(1)+EDIFF+PT5*XLLLL-XKKLL+PT5*XKLKL
         ELSE
            E(1)   = ENERGY
            E(2)   =-PT5*XKKKL
            E(3)   = E(1)+EDIFF+PT5*XKKKK-XKKLL+PT5*XKLKL
         ENDIF
      ENDIF
C *** SOLVE THE CI-MATRIX.
      LINCI  = (NC*(NC+1))/2
      DO I=1,LINCI
         EDUM(I)= E(I)
      ENDDO
      CALL TDIAG (EDUM,VECTCI,ENGYCI,Q,LINCI,NC,NC,NC)
      ENERGY = ENGYCI(LROOT)
      IF(KRESET.EQ.1) RETURN
      IF(IATERG.EQ.1) THEN
         DO I=1,NC
            J      = (I*(I+1))/2
            BKCAL(I) = E(J)
            BKCAL(I) = ATHEAT+EVCAL*(BKCAL(I) +ENUCLR-EAT)+EMM+EMMDP
            ECI(I)   = ATHEAT+EVCAL*(ENGYCI(I)+ENUCLR-EAT)+EMM+EMMDP
         ENDDO
      ELSEIF(IATERG.EQ.-1) THEN
         DO I=1,NC
            J      = (I*(I+1))/2
            BKCAL(I) = E(J)
            BKCAL(I) = EVCAL*(BKCAL(I) +ENUCLR)+EMM+EMMDP
            ECI(I)   = EVCAL*(ENGYCI(I)+ENUCLR)+EMM+EMMDP
         ENDDO
      ENDIF
      RETURN
      END
