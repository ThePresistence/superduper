      SUBROUTINE INMULS (IFORM,JPRINT)
C     *
C     INPUT OF SPECIFIC OPTIONS FOR SINGLE-POINT
C     MULTIPLE SURFACE CALCULATIONS.
C     *
      USE LIMIT, ONLY: LMGRD, LMNAC
      IMPLICIT DOUBLE PRECISION (A-H,O-Z)
      COMMON
     ./GRDORG/ IGRST(LMGRD),ISTATE,JSTATE,ISTMUL(LMGRD)
     ./INOPT2/ IN2(300)
     ./NBFILE/ NBF(20)
C *** FILE NUMBERS.
      NB5    = NBF(5)
      NB6    = NBF(6)
C *** INPUT VARIABLES.
      JOP    = IN2(3)
      IMULT  = IN2(66)
      LROOT  = IN2(140)
      MULTCI = IN2(142)
      NCIGRD = IN2(159)
      ICROSS = IN2(160)
C *** CHECK LIMIT OF COUPLINGS IS NOT EXCEEDED
      NUMICS = (NCIGRD*(NCIGRD-1))/2
      IF((ICROSS.EQ.2.OR.ICROSS.EQ.7) .AND. NUMICS.GT.LMNAC) GOTO 1000
C *** READ INPUT AND INITIALIZE COMMON BLOCKS.
      IF(NCIGRD.GE.1 .AND. NCIGRD.LE.LMGRD) THEN
         IF(IFORM.LE.0) THEN
            READ(NB5,500) (IGRST(I),I=1,NCIGRD)
         ELSE
            READ(NB5,*)   (IGRST(I),I=1,NCIGRD)
         ENDIF
      ELSE
         IN2(159) = 2
         IGRST(1) = 1
         IGRST(2) = 2
      ENDIF
C *** SPECIAL CASE OF TWO STATES WITH VARYING MULTIPLICITIES
      IF(MULTCI.EQ.-1) THEN
         IF(IFORM.LE.0) THEN
            READ(NB5,500) (ISTMUL(I),I=1,2)
         ELSE
            READ(NB5,*)   (ISTMUL(I),I=1,2)
         ENDIF
C        DEFAULT CASE
         IF(ISTMUL(1).LE.0) ISTMUL(1) = IMULT
         IF(ISTMUL(2).LE.0) ISTMUL(2) = IMULT
C        RESET MULTCI IN THE CASE THAT MULTIPLICITIES ARE THE SAME
         IF(ISTMUL(1).EQ.ISTMUL(2)) IN2(142) = ISTMUL(1)
      ENDIF
C *** IF OPTIMIZING, CHECK THAT LROOT IS ONE OF THE STATES
C     SPECIFIED IN IGRST, OTHERWISE LROOT WILL BE IGNORED IN MULSTA
      ILRCHK = 1
      DO 10 I = 1, IN2(159)
         IF (IGRST(I).EQ.LROOT) ILRCHK = I
   10 CONTINUE
C *** SET STATE LABELS TO ZERO TO FLAG START OF CALCULATION.
      ISTATE = 0
      JSTATE = 0
C *** PRINTING SECTION.
      IF(JPRINT.GT.-5) THEN
         WRITE(NB6,100)
         WRITE(NB6,130) IN2(159),(IGRST(I),I=1,IN2(159))
         IF(MULTCI.EQ.-1) WRITE(NB6,135) (ISTMUL(I),I=1,IN2(159))
         IF(NCIGRD.LE.1 .OR. NCIGRD.GT.LMGRD) WRITE(NB6,140)
         IF(JOP.GE.0) THEN
            IF(IGRST(ILRCHK).EQ.LROOT) THEN
               WRITE(NB6,150) LROOT
            ELSE
               WRITE(NB6,160) LROOT, IGRST(ILRCHK)
            ENDIF
         ENDIF
      ENDIF
      RETURN
C *** ERROR SECTION
 1000 CONTINUE
      WRITE(NB6,2000) NCIGRD, NUMICS, LMNAC
      STOP 'INMULS'
C
  100 FORMAT(///1X,'*** OPTIONS FOR MULTIPLE SURFACE CALCULATION ***')
  130 FORMAT(// 1X,'NUMBER OF CI GRADIENT VECTORS COMPUTED:  ',I3,
     1       /  1X,'LABELS OF THE ASSOCIATE CI STATES:    ',10I3)
  135 FORMAT(   1X,'SPIN MULTIPLICITIES OF THE CI STATES: ',10I3)
  140 FORMAT(/  1X,'OPTIONS AND CI STATES SELECTED BY DEFAULT')
  150 FORMAT(/  1X,'STATE ',I2,' WILL BE OPTIMIZED')
  160 FORMAT(/  1X,'WARNING: LROOT=',I2,' NOT COMPATIBLE WITH ',
     1             'GRADIENTS SELECTED',
     1       /  1X,'         STATE ',I2,' WILL BE OPTIMIZED INSTEAD')
  500 FORMAT(20I4)
 2000 FORMAT(///1X,'ERROR: NCIGRD=',I2,' IMPLIES ',I2,' COUPLING PAIRS',
     1       /  1X,'       THIS EXCEEDS THE LIMIT OF ',I2)
      END
