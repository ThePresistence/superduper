      SUBROUTINE GETHES (HESS,XPARAM,SVEC,TVEC,IGTHES,LMH,NVAR,IPRINT,
     1                   ARRAY,LM5,ICALL,SCFCAL)
C     *
C     DEFINITION OF THE HESSIAN. OPTIONS:
C     IGTHES=0 : DIAGONAL MATRIX, DGHSX (DEFAULT FOR MIN SEARCH)
C     IGTHES=1 : NUMERICAL CALCULATION  (DEFAULT FOR TS SEARCH)
C     IGTHES=2 : INPUT FROM FILE (INITIAL HESSIAN ONLY)
C     IGTHES=3 : NUMERICAL CALCULATION  (CENTRAL DIFFERENCES)
C     IGTHES=4 : NUMERICAL DIAGONAL MATRIX (TWO POINTS ONLY)
C     IGTHES=5 : UNIT MATRIX
C
C     NB: IN THE CASE OF YARKONY CONICAL INTERSECTION SEARCH
C     ONLY THE AVERAGE ENERGY AND GEOMETRICAL CONSTRAINT GRADIENT
C     CONTRUBITIONS ARE USED, EVEN WHEN EXTRAPOLATABLE FUNCTIONS ARE
C     SWITCHED ON. THIS IS BECAUSE THE FINITE DIFFERENCE PROCEDURE
C     CAN TAKE THE GEOMETRY AWAY FROM THE SEAM, WHERE EXTRAPOLATABLE
C     FUNCTIONS ARE NO LONGER VALID.
C     *
      USE LIMIT, ONLY: LMV, LM1, LM1M, LMYL
      IMPLICIT DOUBLE PRECISION (A-H,O-Z)
      EXTERNAL SCFCAL
      LOGICAL PRT
      COMMON
     ./CIMAP / IFMAP
     ./CONSTN/ ZERO,ONE,TWO,THREE,FOUR,PT5,PT25
     ./FLPOCM/ GRAD(LMV),D(LMV),ALPHA,FIP1
     ./INOPT2/ IN2(300)
     ./NBFILE/ NBF(20)
     ./PARM3 / LOC(LMV),NVAR3
     ./YARLAG/ YLVAL(LMYL),YLGRD(LMYL),YLD(LMYL),NYL
     ./YARUPD/ YARV(3,LM1+LM1M,LMYL),YARS(LMV,2),YARG(LMV,LMYL,2),IXTRAP
      DIMENSION HESS(LMH,LMH)
      DIMENSION XPARAM(NVAR),XD(NVAR),SVEC(NVAR),TVEC(NVAR)
      DIMENSION ARRAY(LM5)
C     DGHSS: HESSIAN DIAGONAL FOR BOND LENGTH    (IGTHES=0).
C     DGHSA: HESSIAN DIAGONAL FOR BOND ANGLE     (IGTHES=0).
C     DGHSD: HESSIAN DIAGONAL FOR DIHEDRAL ANGLE (IGTHES=0).
      DATA DGHSS,DGHSA,DGHSD /1000.D0,500.D0,200.D0/
C     XINC : STEP SIZE FOR HESSIAN CALCULATION.
C     XINC : SHOULD BE IN THE RANGE FROM 10**(-2) TO 10**(-4).
C     XINC : 10**(-3) SEEMS TO BE A REASONABLE COMPROMISE.
      DATA XINC /1.0D-03/
C *** FILE NUMBERS.
      NB6    = NBF(6)
C     INPUT OPTIONS
      IGEOM  = IN2(4)
      IMOMAP = IN2(156)
      ICROSS = IN2(160)
C *** INITIALIZATION.
      PRT    = IPRINT.GE.0
C     WE ASSUME WE START WITH CORRECTLY MAPPED ORBITALS
      IFMAP  = 0
C *** OPTION IGTHES=0.
      IF(IGTHES.EQ.0) THEN
         IF(PRT) WRITE(NB6,300)
C        IN THE CASE OF YARKONY CI SEARCH, A DIAGONAL MATRIX
C        IS A VALID OPTION AND SHOULD NOT BE OVERRIDDEN
         IF(ICROSS.NE.5) IGTHES=1
         DO 11 J=1,NVAR
         DO 10 I=1,NVAR
         HESS(I,J)=ZERO
   10    CONTINUE
   11    CONTINUE
         DO 20 I=1,NVAR
            IF(IGEOM.EQ.0) THEN
               IF(MOD(LOC(I),3).EQ.1) HESS(I,I)=DGHSS
               IF(MOD(LOC(I),3).EQ.2) HESS(I,I)=DGHSA
               IF(MOD(LOC(I),3).EQ.0) HESS(I,I)=DGHSD
            ELSE
               HESS(I,I)=1000.0D0
            ENDIF
   20    CONTINUE
      ENDIF
C *** OPTION IGTHES=2.
      IF(IGTHES.EQ.2) THEN
         IF(PRT) WRITE(NB6,310)
C        BUG FIX: IF THE HESSIAN IS RESET LATER USE IGTHES=1
C        BUT DON'T LET IT OVERWRITE THE FILE INFORMATION NOW
         IGTHES=1
         RETURN
      ENDIF
C *** OPTIONS IGTHES=1,3.
      IF((IGTHES.EQ.1).OR.(IGTHES.EQ.3)) THEN
         IF(PRT) THEN
            IF(IGTHES.EQ.1) WRITE(NB6,320)
            IF(IGTHES.EQ.3) WRITE(NB6,330)
         ENDIF
         IF(ICROSS.EQ.5 .AND. IGTHES.EQ.1) THEN
            CALL DCOPY (NVAR,YARS,1,TVEC,1)
            IF(NYL.GT.2) THEN
               DO 30 J=3,NYL
                  CALL DAXPY (NVAR,YLVAL(J),YARG(1,J,1),1,TVEC,1)
  30           CONTINUE
            ENDIF
         ENDIF
         DO 70 I=1,NVAR
         XPARAM(I) = XPARAM(I)+XINC
         CALL COMPFG (NVAR,XPARAM,FDUM,FDUM,SVEC,ARRAY,LM5,
     1                ICALL,SCFCAL)
         IF(IMOMAP.EQ.2 .AND. IFMAP.EQ.1) GOTO 1000
         IF(ICALL.EQ.-1) RETURN
         IF(ICROSS.EQ.5) THEN
            CALL DCOPY (NVAR,YARS,1,SVEC,1)
            IF(NYL.GT.2) THEN
               DO 40 J=3,NYL
                  CALL DAXPY (NVAR,YLVAL(J),YARG(1,J,1),1,SVEC,1)
  40           CONTINUE
            ENDIF
         ENDIF
         XPARAM(I) = XPARAM(I) - XINC
         IF(IGTHES.EQ.3) THEN
            XPARAM(I) = XPARAM(I)-XINC
            CALL COMPFG (NVAR,XPARAM,FDUM,FDUM,TVEC,ARRAY,LM5,
     1                   ICALL,SCFCAL)
            IF(IMOMAP.EQ.2 .AND. IFMAP.EQ.1) GOTO 1000
            IF(ICALL.EQ.-1) RETURN
            IF(ICROSS.EQ.5) THEN
               CALL DCOPY (NVAR,YARS,1,TVEC,1)
               IF(NYL.GT.2) THEN
                  DO 45 J=3,NYL
                     CALL DAXPY (NVAR,YLVAL(J),YARG(1,J,1),1,TVEC,1)
   45             CONTINUE
               ENDIF
            ENDIF
            XPARAM(I) = XPARAM(I)+XINC
            DO 50 J=1,NVAR
            HESS(I,J) = (SVEC(J)-TVEC(J))/(XINC+XINC)
   50       CONTINUE
         ELSE
            DO 60 J=1,NVAR
               IF(ICROSS.EQ.5) THEN
                  HESS(I,J) = (SVEC(J)-TVEC(J))/XINC
               ELSE
                  HESS(I,J) = (SVEC(J)-GRAD(J))/XINC
               ENDIF
   60       CONTINUE
         ENDIF
   70    CONTINUE
C        SYMMETRIZATION.
         DO 90 I=1,NVAR
         DO 80 J=1,I-1
         HESS(I,J) = (HESS(I,J)+HESS(J,I))*PT5
         HESS(J,I) = HESS(I,J)
   80    CONTINUE
   90    CONTINUE
      ENDIF
C *** OPTION IGTHES=4.
C     ANALOGUE OF HESSIAN ESTIMATION IN FLEPO ROUTINE
      IF(IGTHES.EQ.4) THEN
         IF(PRT) THEN
            WRITE(NB6,360)
         ENDIF
         DO 200 J=1,NVAR
            DO 210 I=1,NVAR
               HESS(I,J) = ZERO
  210       CONTINUE
  200    CONTINUE
         IF(ICROSS.EQ.5) THEN
            CALL DCOPY (NVAR,YARS,1,TVEC,1)
            IF(NYL.GT.2) THEN
               DO 220 J=3,NYL
                  CALL DAXPY (NVAR,YLVAL(J),YARG(1,J,1),1,TVEC,1)
  220          CONTINUE
            ENDIF
         ELSE
            CALL DCOPY (NVAR,GRAD,1,TVEC,1)
         ENDIF
         DO 230 I=1,NVAR
            XD(I) = XPARAM(I)-SIGN(XINC,TVEC(I))
  230    CONTINUE
         CALL COMPFG (NVAR,XD,FDUM,FDUM,SVEC,ARRAY,LM5,
     1                ICALL,SCFCAL)
         IF(IMOMAP.EQ.2 .AND. IFMAP.EQ.1) GOTO 1000
         IF(ICALL.EQ.-1) RETURN
         IF(ICROSS.EQ.5) THEN
            CALL DCOPY (NVAR,YARS,1,SVEC,1)
            IF(NYL.GT.2) THEN
               DO 240 J=3,NYL
                  CALL DAXPY (NVAR,YLVAL(J),YARG(1,J,1),1,SVEC,1)
  240          CONTINUE
            ENDIF
         ENDIF
         DO 250 I=1,NVAR
            DELG = TVEC(I)-SVEC(I)
            DELX = XPARAM(I)-XD(I)
            HESS(I,I) = DELG/DELX
C           DIAGONAL ELEMENTS MUST BE POSITIVE
            IF(HESS(I,I).LT.ZERO) THEN
               HESS(I,I) = ABS(TVEC(I))/XINC
            ENDIF
C           MINIMUM THRESHOLD SET TO UNIT MATRIX ELEMENT
            HESS(I,I) = MAX(HESS(I,I),ONE)
  250    CONTINUE
      ENDIF
C *** OPTION IGTHES=5.
C     IDENTITY MATRIX (EQUIVALENT TO IHESS=3)
C     USEFUL FOR MIMICKING STEEPEST DESCENT.
      IF(IGTHES.EQ.5) THEN
         IF(PRT) WRITE(NB6,370)
         DO 270 J=1,NVAR
         DO 260 I=1,NVAR
         IF (I .EQ. J) THEN
            HESS(I, J) = ONE
         ELSE
            HESS(I, J) = ZERO
         ENDIF
  260    CONTINUE
  270    CONTINUE
      ENDIF
C *** FOR YARKONY, ANOTHER POINT CALCULATION REQUIRED
C     TO SET YARV, YARS, ETC. CORRECTLY
      IF(ICROSS.EQ.5 .AND. IGTHES.GT.0) THEN
         CALL COMPFG (NVAR,XPARAM,FIP1,FIP1,GRAD,ARRAY,LM5,
     1                ICALL,SCFCAL)
         IF(IMOMAP.EQ.2 .AND. IFMAP.EQ.1) GOTO 1000
         IF(ICALL.EQ.-1) RETURN
      ENDIF
      RETURN
C *** ERROR EXIT.
C 110 WRITE(NB6,340)
C     STOP 'GETHES'
C 120 WRITE(NB6,350)
C     STOP 'GETHES'
 1000 WRITE(NB6,2000)
      STOP 'GETHES'
  300 FORMAT(/1X,'DIAGONAL MATRIX USED AS INITIAL HESSIAN'/)
  310 FORMAT(/1X,'INITIAL HESSIAN READ FROM FILE'/)
  320 FORMAT(/1X,'HESSIAN CALCULATED NUMERICALLY'/)
  330 FORMAT(/1X,'HESSIAN CALCULATED DOUBLE NUMERICALLY'/)
  360 FORMAT(/1X,'DIAGONAL HESSIAN CALCULATED NUMERICALLY'/)
  370 FORMAT(/1X,'IDENTITY MATRIX USED AS INITIAL HESSIAN'/)
C 340 FORMAT(///1X,'ERROR WHEN READING FILE 23',
C    1       /  1X,'FOR INITIAL HESSIAN MATRIX',
C    2       /  1X,'IN SUBROUTINE GETHES.'/)
C 350 FORMAT(///1X,'END-OF-FILE ENCOUNTERED WHEN READING FILE 23',
C    1       /  1X,'FOR INITIAL HESSIAN MATRIX',
C    2       /  1X,'IN SUBROUTINE GETHES.'/)
 2000 FORMAT(// 1X,'ORBITAL MAPPING FAILURE DURING CALCULATION',
     1       /  1X,'OF INITIAL HESSIAN MATRIX',
     2       /  1X,'IN SUBROUTINE GETHES.'/)
      END
