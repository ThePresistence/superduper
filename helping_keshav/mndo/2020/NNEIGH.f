      SUBROUTINE NNEIGH (IAT,NI,NUMAT,COORD,NAT)
C     *
C     DETERMINE NEAREST NEIGHBOUR OF ATOM IAT AND SET FLAG (NI).
C     SPECIAL CONVENTION FOR TREATING N-H AND O-H BONDS IN NMR.
C     *
C     NOTATION. I=INPUT, O=OUTPUT.
C     IAT       NUMBER OF ATOM TO BE CHECKED (I).
C     NI        SET TO 85/86 IF IAT IS A HYDROGEN ATOM BOUND TO N/O,
C               OTHERWISE SET TO 1 (O).
C     NUMAT     NUMBER OF ATOMS (I).
C     COORD()   CARTESIAN COORDINATES (I).
C     NAT()     ATOMIC NUMBERS (I).
C     *
      IMPLICIT DOUBLE PRECISION (A-H,O-Z)
      DIMENSION COORD(3,NUMAT),NAT(NUMAT)
      RIJ(I,J) = (COORD(1,I)-COORD(1,J))**2+(COORD(2,I)-COORD(2,J))**2
     1          +(COORD(3,I)-COORD(3,J))**2
C     INITIAIZATION.
      RRMIN  = 1.0D10
      IIMIN  = 1
C     FIND NEAREST NEIGHBOUR (ATOM IIMIN).
      DO 10 I=1,NUMAT
      IF(I.NE.IAT) THEN
         RR = RIJ(I,IAT)
         IF(RR.LT.RRMIN) THEN
            RRMIN = RR
            IIMIN = I
         ENDIF
      ENDIF
   10 CONTINUE
C     ATOMIC NUMBER OF NEAREST NEIGHBOUR.
      NEAR = NAT(IIMIN)
C     SET OUTPUT FLAG NI.
      NI   = 1
      IF(NAT(IAT).EQ.1 .AND. NEAR.EQ.7) NI=85
      IF(NAT(IAT).EQ.1 .AND. NEAR.EQ.8) NI=86
      RETURN
      END
