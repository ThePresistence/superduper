      SUBROUTINE GETSYM
C     *
C     INPUT FOR SYMMETRY DATA.
C     ADAPTED FROM MOPAC(6.0) WRITTEN BY J.J.P.STEWART.
C     *
C     ON EXIT
C     NDEP    = NUMBER OF SYMMETRY RELATIONS.
C     IDEPFN  = ARRAY OF REFERENCE FUNCTION INDICES.
C     LOCPAR  = ARRAY OF REFERENCE ATOM LOCATIONS.
C     LOCDEP  = ARRAY OF DEPENDENT ATOM LOCATIONS.
C     *
C     LOCDEP IS THE ATOM WHOSE COORDINATES DEPEND ON THE COORDINATES OF
C     LOCPAR.
C     LOCPAR IS THE ATOM WHOSE COORDINATES ARE USED TO CALCULATE THOSE
C     OF LOCDEP.
C     IDEPFN POINTS TO THE PARTICULAR FUNCTION TO BE USED.
C     *
      USE LIMIT, ONLY: LMS
      IMPLICIT DOUBLE PRECISION (A-H,O-Z)
      CHARACTER LINE*80
      COMMON
     ./NBFILE/ NBF(20)
     ./PARM2 / NDEP,LOCPAR(LMS),IDEPFN(LMS),LOCDEP(LMS)
      DIMENSION IVALUE(40),VALUE(40)
C *** FILE NUMBERS.
      NB5    = NBF(5)
C *** INITIALIZATION.
      NDEP   = 0
C *** READ INPUT DATA.
   20 READ(NB5,'(A)',END=70) LINE
      IF(LINE.EQ.' ') GO TO 70
      CALL NUCHAR(LINE,VALUE,NVALUE)
C     CONVERT TO INTEGER VALUES.
      DO 30 I=1,NVALUE
      IVALUE(I) = INT(VALUE(I))
   30 CONTINUE
C     CHECK FOR COMPLETE SYMMETRY SPECIFICATION.
      IF(NVALUE.EQ.0) GO TO 70
      IF(IVALUE(1).EQ.0) GO TO 70
      IF(IVALUE(2).EQ.0) GO TO 70
      IF(IVALUE(3).EQ.0) GO TO 70
C     FILL THE LOCDEP ARRAY.
      DO 40 I=3,NVALUE
         IF(IVALUE(I).EQ.0) GO TO 50
         NDEP=NDEP+1
         LOCDEP(NDEP)=IVALUE(I)
         LOCPAR(NDEP)=IVALUE(1)
         IDEPFN(NDEP)=IVALUE(2)
   40 CONTINUE
   50 CONTINUE
      GO TO 20
   70 RETURN
      END
