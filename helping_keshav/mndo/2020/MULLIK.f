      SUBROUTINE MULLIK (P,S,POP,POPI,QI,LM2,NORBS,NPRINT)
C     *
C     MULLIKEN POPULATION ANALYSIS.
C     *
      USE LIMIT, ONLY: LM1, LMZ
      IMPLICIT DOUBLE PRECISION (A-H,O-Z)
      CHARACTER*2 ELEMNT
      COMMON
     ./ATOMS / NUMAT,NAT(LM1),NFIRST(LM1),NLAST(LM1)
     ./ELEMTS/ ELEMNT(107)
     ./NBFILE/ NBF(20)
     ./PARDER/ CORE(LMZ),EHEAT(LMZ),EISOL(LMZ)
      DIMENSION P(LM2,LM2),S(LM2,LM2),POP(LM2),POPI(LM2),QI(LM2)
C *** FILE NUMBERS.
      NB6    = NBF(6)
C *** INITIALIZATION.
      IF(NPRINT.GE.2) THEN
         WRITE(NB6,500)
         ITMAX = 0
         DO 10 I=1,NUMAT
         ITMAX = MAX(ITMAX,NLAST(I)-NFIRST(I)+1)
   10    CONTINUE
         IF(ITMAX.LE.1) THEN
            WRITE(NB6,510)
         ELSE IF(ITMAX.LE.4) THEN
            WRITE(NB6,520)
         ELSE
            WRITE(NB6,525)
         ENDIF
      ENDIF
C *** TRACE OF (PS).
C     IMPLEMENTED AS A DOT PRODUCT SINCE BOTH MATRICES ARE SYMMETRIC.
      DO 20 I=1,NORBS
      POP(I) = DDOT(NORBS,P(1,I),1,S(1,I),1)
   20 CONTINUE
C *** LOOP OVER ALL ATOMS.
      DO 40 I=1,NUMAT
      NI     = NAT(I)
      IA     = NFIRST(I)
      IB     = NLAST(I)
C *** CALCULATE ATOMIC POPULATIONS (POPI) AND CHARGES (QI).
      POPI(I)= 0.0D0
      DO 30 J=IA,IB
      POPI(I)= POPI(I)+POP(J)
   30 CONTINUE
      QI(I)  = CORE(NI)-POPI(I)
C *** PRINT ATOMIC POPULATIONS.
      IF(NPRINT.GE.2) THEN
       IF((IB-IA).LT.4) THEN
         WRITE(NB6,530) ELEMNT(NI),I,QI(I),POPI(I),(POP(J),J=IA,IB)
       ELSE
         IP   = IA+3
         POPD = POP(IA+4)+POP(IA+5)+POP(IA+6)+POP(IA+7)+POP(IA+8)
         WRITE(NB6,530) ELEMNT(NI),I,QI(I),POPI(I),(POP(J),J=IA,IP),POPD
       ENDIF
      ENDIF
   40 CONTINUE
      RETURN
  500 FORMAT(///5X,'MULLIKEN POPULATION ANALYSIS.')
  510 FORMAT(/  5X,'ATOM',4X,'I',8X,'CHARGE',6X,'DENSITY',8X,'S'/)
  520 FORMAT(/  5X,'ATOM',4X,'I',8X,'CHARGE',6X,'DENSITY',8X,'S',
     1          10X,'PX',10X,'PY',10X,'PZ'/)
  525 FORMAT(/  5X,'ATOM',4X,'I',8X,'CHARGE',6X,'DENSITY',8X,'S',
     1          10X,'PX',10X,'PY',10X,'PZ',10X,'DSUM'/)
  530 FORMAT(   6X,A2,3X,I3,3X,7F12.5)
      END
