      SUBROUTINE FORHE (E,NORBS,NSH,MSH,NEL,NOSH)
C     *
C     DEFINE HALF-ELECTRON OPEN SHELLS.
C     *
C     NOTATION. I=INPUT, O=OUTPUT.
C     E(I)      ORBITAL ENERGIES IN EV (I).
C     NORBS     NUMBER OF ORBITALS (I).
C     NSH       NUMBER OF HALF-ELECTRON OPEN SHELLS (O).
C     MSH(J)    TOTAL NUMBER OF ORBITALS IN SHELL J (O).
C     NEL(J)    TOTAL NUMBER OF ELECTRONS IN SHELL J (O).
C     NOSH(K,J) INDICES FOR ORBITALS IN SHELL J (K=1,MSH(J)) (O).
C     *
C     INPUT FROM COMMON BLOCKS.
C     OCCA(I)   PREDEFINED MO OCCUPATION NUMBERS.
C     *
      USE LIMIT, ONLY: LMX
      IMPLICIT DOUBLE PRECISION (A-H,O-Z)
      COMMON
     ./CONSTN/ ZERO,ONE,TWO,THREE,FOUR,PT5,PT25
     ./OCCNM / OCCA(LMX),OCCB(LMX)
      DIMENSION MSH(10),NEL(10),NOSH(5,10)
      DIMENSION E(NORBS)
      DATA DE/0.100D0/
C *** DEFINE OCCUPATION NUMBERS FOR QUASI-DEGENERATE LEVELS.
C     DISTRIBUTE ELECTRONS EVENLY OVER SUCH LEVELS.
      NSH    = 0
      DO 10 J=1,10
      MSH(J) = 0
      NEL(J) = 0
   10 CONTINUE
      I      = 1
   30 IF(I.GT.NORBS) GO TO 100
      IC     = 1
      A      = OCCA(I)
      IF(A.GT.0.1D0 .AND. A.LT.0.9D0) THEN
         NSH = NSH+1
         NOSH(1,NSH) = I
         I2  = I+1
         DO 40 I1=I,NORBS-1
         I2  = I1+1
         EE  = E(I2)-E(I1)
         OO  = ABS(OCCA(I2)-OCCA(I1))
         IF(EE.GT.DE .OR. OO.GT.DE) GO TO 50
         IC  = IC+1
         NOSH(IC,NSH) = I2
         A   = A+OCCA(I2)
   40    CONTINUE
   50    MSH(NSH) = IC
         NEL(NSH) = IDINT(A*TWO)
         I   = I2
      ELSE
         I   = I+1
      ENDIF
      GO TO 30
  100 CONTINUE
      RETURN
      END
