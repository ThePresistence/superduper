      SUBROUTINE GETTXT (KEYWRD,KEYLEN,ICALL)
C     *
C     READ FIRST LINES OF AN INPUT FILE WITH MOPAC KEYWORDS.
C     KEYWORD AND TITLE LINES FOR INPUT MODE IMOPAC=1.
C     ADAPTED FROM MOPAC(6.0) WRITTEN BY J.J.P.STEWART.
C     *
C     NOTATION. I=INPUT, O=OUTPUT.
C     KEYWRD    CHARACTER STRING FOR KEYWORDS (O).
C     KEYLEN    LENGTH OF CHARACTER STRING KEYWRD, IN BYTES (O).
C               INITIALIZED TO THE MAXIMUM AVAILABLE LENGTH.
C               ACTUAL LENGTH DETERMINED DURING INPUT.
C     ICALL     ERROR FLAG (O).
C               = 0 NORMAL RETURN.
C               =-9 END-OF-FILE ENCOUNTERED.
C     *
      IMPLICIT DOUBLE PRECISION (A-H,O-Z)
      PARAMETER (NB92=92)
      CHARACTER*(*) KEYWRD
      CHARACTER*80 KOMENT,KTITLE,GETNAM,OLDKEY
      CHARACTER*50 FILEN
      CHARACTER*1 CH,CH2
      COMMON
     ./FLAG1 / KTITLE,KOMENT
     ./NBFILE/ NBF(20)
      DIMENSION IS(3)
C *** FILE NUMBERS.
      NB5    = NBF(5)
      NB6    = NBF(6)
C *** INITIALIZATION.
      ICALL  = 0
      IS(1)  = 161
      IS(2)  = 81
      IS(3)  = 1
      KEYWRD = ' '
      KOMENT = ' '
      KTITLE = ' '
      KEYLEN = LEN(KEYWRD)
C *** CHECK WHETHER CHARACTER STRING IS LONG ENOUGH.
      IF(KEYLEN.LT.241) THEN
         WRITE(NB6,520) KEYLEN
         STOP 'GETTXT'
      ENDIF
C *** READ FIRST KEYWORD LINE.
      READ(NB5,'(A)',END=130,ERR=110) KEYWRD(:80)
      OLDKEY = KEYWRD(1:80)
      CALL UPPCAS(KEYWRD(1:80),80)
      IF(INDEX(KEYWRD,'SETUP').NE.0) THEN
         I=INDEX(KEYWRD,'SETUP=')
         IF(I.NE.0)THEN
            J=INDEX(KEYWRD(I:),' ')
            FILEN=OLDKEY(I+6:I+J-1)
         ELSE
            FILEN='SETUP'
         ENDIF
         OPEN(UNIT=NB92,FILE=GETNAM(FILEN),
     1        STATUS='UNKNOWN',FORM='FORMATTED')
         REWIND NB92
         READ(NB92,'(A)',END=40,ERR=40) KEYWRD(81:160)
         CALL UPPCAS(KEYWRD(81:160),80)
         READ(NB92,'(A)',END=10,ERR=10) KEYWRD(161:240)
         CALL UPPCAS(KEYWRD(161:240),80)
   10    CONTINUE
         READ(NB5,'(A)',END=130,ERR=120) KTITLE,KOMENT
C *** READ SECOND AND THIRD KEYWORD LINE (OPTION ' +').
C *** READ TWO LINES OF TEXT THEREAFTER (KTITLE,KOMENT)
      ELSE IF(INDEX(KEYWRD(1:80),' +').NE.0) THEN
         READ(NB5,'(A)',END=130,ERR=110) KEYWRD(81:160)
         OLDKEY=KEYWRD(81:160)
         CALL UPPCAS(KEYWRD(81:160),80)
         IF(INDEX(KEYWRD(81:160),'SETUP').NE.0) THEN
            I=INDEX(KEYWRD,'SETUP=')
            IF(I.NE.0)THEN
               J=INDEX(KEYWRD(I:),' ')
               FILEN=OLDKEY(I-74:I+J-80)
            ELSE
               FILEN='SETUP'
            ENDIF
            OPEN(UNIT=NB92,FILE=GETNAM(FILEN),
     1           STATUS='UNKNOWN',FORM='FORMATTED')
            REWIND NB92
            READ(NB92,'(A)',END=20,ERR=20) KEYWRD(161:240)
            CALL UPPCAS(KEYWRD(161:240),80)
   20       CONTINUE
         ELSE IF(INDEX(KEYWRD(81:160),' +').NE.0) THEN
            READ(NB5,'(A)',END=130,ERR=110) KEYWRD(161:240)
            CALL UPPCAS(KEYWRD(161:240),80)
         ENDIF
         READ(NB5,'(A)',END=130,ERR=120) KTITLE,KOMENT
C *** READ SECOND AND THIRD KEYWORD LINE (OPTION '&').
C *** READ ONE LINE OF TEXT THEREAFTER (KTITLE)
      ELSE IF(INDEX(KEYWRD(:80),'&').NE.0) THEN
         READ(NB5,'(A)',END=130,ERR=110) KEYWRD(81:160)
         OLDKEY=KEYWRD(81:160)
         CALL UPPCAS(KEYWRD(81:160),80)
         IF(INDEX(KEYWRD(81:160),'SETUP').NE.0) THEN
            I=INDEX(KEYWRD,'SETUP=')
            IF(I.NE.0) THEN
               J=INDEX(KEYWRD(I:),' ')
               FILEN=OLDKEY(I-74:I+J-80)
            ELSE
               FILEN='SETUP'
            ENDIF
            OPEN(UNIT=NB92,FILE=GETNAM(FILEN),
     1           STATUS='UNKNOWN',FORM='FORMATTED')
            REWIND NB92
            READ(NB92,'(A)',END=30,ERR=30) KEYWRD(161:240)
            CALL UPPCAS(KEYWRD(161:240),80)
            READ(NB5,'(A)',END=130,ERR=120) KTITLE
   30       CONTINUE
         ELSE IF(INDEX(KEYWRD(81:160),'&').NE.0) THEN
            READ(NB5,'(A)',END=130,ERR=110) KEYWRD(161:240)
         ELSE
            READ(NB5,'(A)',END=130,ERR=120) KTITLE
         ENDIF
C *** READ TWO LINES OF TEXT (KTITLE,KOMENT)
C *** IF THERE IS ONLY ONE KEYWORD LINE.
      ELSE
         READ(NB5,'(A)',END=130,ERR=120) KTITLE,KOMENT
      ENDIF
      GO TO 50
   40 WRITE(NB6,'(A)') ' SETUP FILE MISSING OR CORRUPT'
C *** ADD BLANKS ACCORDING TO MOPAC CONVENTIONS.
   50 DO 80 J=1,3
      IF(KEYWRD(IS(J):IS(J)).NE.' ') THEN
        CH=KEYWRD(IS(J):IS(J))
        KEYWRD(IS(J):IS(J))=' '
        DO 60 I=IS(J)+1,239
        CH2=KEYWRD(I:I)
        KEYWRD(I:I)=CH
        CH=CH2
        IF(KEYWRD(I+1:I+2).EQ.'  ') THEN
           KEYWRD(I+1:I+1)=CH
           GO TO 70
        ENDIF
   60   CONTINUE
      WRITE(NB6,'(A,I2,A)')' LINE',J,' OF KEYWORDS DOES NOT HAVE ENOUGH'
      WRITE(NB6,'(A)') ' SPACES FOR PARSING. PLEASE CORRECT LINE.'
        STOP 'GETTXT'
   70   CONTINUE
      ENDIF
   80 CONTINUE
C *** DETERMINE ACTUAL LENGTH OF KEYWRD.
      DO 90 I=KEYLEN,2,-1
      IF(KEYWRD(I:I).NE.' ') GO TO 100
   90 CONTINUE
  100 IF(I.LT.KEYLEN) KEYLEN=I+1
C *** CLOSE SETUP FILE.
      IF(INDEX(KEYWRD,'SETUP').NE.0) THEN
         CLOSE (NB92)
      ENDIF
      RETURN
C *** ERROR EXITS.
  110 WRITE(NB6,500)
      STOP 'GETTXT'
  120 WRITE(NB6,510)
      STOP 'GETTXT'
  130 CONTINUE
      ICALL = -9
      RETURN
  500 FORMAT(// 1X,'ERROR WHEN READING FIRST LINES (KEYWORDS).'/)
  510 FORMAT(// 1X,'ERROR WHEN READING FIRST LINES (TITLE).'/)
  520 FORMAT(// 1X,'ERROR: STRING FOR MOPAC KEYWORDS IS TOO SHORT.',
     1       /  1X,'AVAILABLE LENGTH: KEYLEN =',I3,', MINIMUM: 241.',
     2       /  1X,'THE PROGRAM CODE NEEDS TO BE CHANGED.'/)
      END
