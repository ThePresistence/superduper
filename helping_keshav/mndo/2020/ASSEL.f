      SUBROUTINE ASSEL(IEL,NUMAT,AMS,COORD,CG,ISYM,M,IERROR)
C     *
C     DEFINE SYMMETRY RELATIONS BETWEEN THE ATOMS
C     WITH RESPECT TO A SYMMETRY ELEMENT.
C     *
C     NOTATION. I=INPUT,O=OUTPUT.
C     IEL       INTERNAL LABEL (1..7) FOR GIVEN SYMMETRY ELEMENT (I).
C     NUMAT     NUMBER OF ATOMS (I).
C     AMS()     ATOMIC MASSES (I).
C     COORD()   CARTESIAN COORDINATES OF THE ATOMS (I).
C     CG(3)     CARTESIAN COORDINATES OF THE CENTER OF GRAVITY (I).
C     ISYM(M,*) PERMUTATED TABLE OF ATOM NUMBERS AFTER APPLYING
C               SYMMETRY OPERATION AT SYMMETRY ELEMENT IEL (O).
C     M         RUNNING INDEX (1..3) FOR SYMMETRY OPERATIONS (I).
C     IERROR    ERROR FLAG, IERROR=1 SIGNALS ERROR (O).
C     *
      IMPLICIT DOUBLE PRECISION (A-H,O-Z)
      DIMENSION AMS(NUMAT),COORD(3,NUMAT),CG(3),ISYM(3,NUMAT)
      DIMENSION IELSYM(3,7)
      DATA IELSYM/-1, 1, 1, 1,-1, 1, 1, 1,-1,
     1             1,-1,-1,-1, 1,-1,-1,-1, 1,-1,-1,-1/
      DATA SMALL /1.0D-07/
      DATA SMALLM/1.0D-07/
C *** INITIALIZATION.
      IERROR = 0
      DO 10 I=1,NUMAT
      ISYM(M,I) = 0
   10 CONTINUE
C *** LOOP OVER ALL ATOMS.
      DO 70 I=1,NUMAT
      IF(ISYM(M,I).NE.0) GO TO 70
C *** CASE 1. ATOM I LIES IN THE SYMMETRY ELEMENT.
      DO 20 K=1,3
      IF(IELSYM(K,IEL).EQ.1) GO TO 20
      CK     = COORD(K,I)-CG(K)
      IF(ABS(CK).GT.SMALL) GO TO 30
   20 CONTINUE
      ISYM(M,I) = I
      GO TO 70
C *** CASE 2. ATOM I IS RELATED TO ATOM J BY SYMMETRY.
   30 IF(I.EQ.NUMAT) GO TO 60
      AMSI   = AMS(I)
      IPLUS  = I+1
      DO 50 J=IPLUS,NUMAT
      IF(ABS(AMSI-AMS(J)).GT.SMALLM) GO TO 50
      DO 40 K=1,3
      CKI   = COORD(K,I)-CG(K)
      CKJ   = COORD(K,J)-CG(K)
      DELTA = CKI-CKJ*IELSYM(K,IEL)
      IF(ABS(DELTA).GT.SMALL) GO TO 50
   40 CONTINUE
      ISYM(M,I) = J
      ISYM(M,J) = I
      GO TO 70
   50 CONTINUE
   60 IERROR = 1
   70 CONTINUE
      RETURN
      END
