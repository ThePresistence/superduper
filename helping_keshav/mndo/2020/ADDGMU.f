      SUBROUTINE ADDGMU (F1,P1,P2,LM4)
C     *
C     ADD GSBP CONTRIBUTION TO THE FOCK MATRIX THAT STEMS
C     FROM THE INTERACTION OF THE MULLIKEN CHARGE DISTRIBUTION
C     WITH THE REACTION FIELD POTENTIAL
C     *
C     F_MUNU -= 1/2 S_MUNU SUM_A^ATOMS Q_A * [ GAMMA_MUA + GAMMA_NUA ]
C     *
C     F(LM4)    FOCK MATRIX (I,O)
C     P(LM4)    DENSITY MATRIX (I)
C     S(LM4)    AO OVERLAP MATRIX (I)
C     LM4       NUMBER OF BASIS FUNCTION PAIRS (I)
      USE LIMIT, ONLY: LM1, LMX, LMI, LMZ
      IMPLICIT DOUBLE PRECISION (A-H,O-Z)
      COMMON
     ./ATOMS / NUMAT,NAT(LM1),NFIRST(LM1),NLAST(LM1)
     ./INDEX / INDX(LMX)
     ./ATOMC / COORD(3,LM1)
     ./CONSTN/ ZERO,ONE,TWO,THREE,FOUR,PT5,PT25
     ./INOPT2/ IN2(300)
     ./PARDER/ CORE(LMZ),EHEAT(LMZ),EISOL(LMZ)
     ./QMMM7 / GSBP1(LM1),GSBP2(LM1,LM1)
      DIMENSION F1(LM4),P1(LM4),P2(LM4),CRG(NUMAT)

C     CALCULATE MULLIKEN CHARGES FIRST
      DO 10 I=1,NUMAT
       NI   = NAT(I)
       IA   = NFIRST(I)
       IB   = NLAST(I)
       QI   = ZERO
       DO 20 J=IA,IB
         LL   = INDX(J)+J
         QI   = QI + P1(LL)+P2(LL)
   20  CONTINUE
       CRG(I) = CORE(NI)-QI
   10 CONTINUE
      NORBS = NLAST(NUMAT)

C     ADD GSBP CONTRIBUTION TO FOCK MATRIX
      DO 30 I=1,NUMAT
        IA    = NFIRST(I)
        IB    = NLAST(I)
        QSUM  = ZERO
        DO 40 J=1,NUMAT
          QSUM = QSUM + CRG(J)*GSBP2(I,J)
   40   CONTINUE
        DO 50 J=IA,IB
          LL   = INDX(J)+J
          F1(LL) = F1(LL) - QSUM
   50   CONTINUE
   30 CONTINUE


      RETURN
      END


