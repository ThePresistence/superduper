      SUBROUTINE DYNGUG (LEN,LM5,LM5X,LM5R)
C     *
C     DYNAMIC MEMORY ALLOCATION AND INITIALIZATION FOR GUGA-CI.
C     *
C     NOTATION.
C     LEN  = AVAILABLE LENGTH OF BLANK COMMON AREA (INPUT VALUE,
C            NOT MODIFIED IN THIS ROUTINE).
C     LM5  = LAST ADDRESS OF BLANK COMMON AREA THAT HAS PREVIOUSLY
C            BEEN RESERVED (INPUT VALUE, NOT MODIFIED IN THIS ROUTINE).
C     LM5X = LAST ADDRESS OF BLANK COMMON AREA THAT IS ACTUALLY USED
C            BY THE ARRAYS DEFINED PRESENTLY (DETERMINED HERE).
C     LM5R = LAST ADDRESS OF BLANK COMMON AREA THAT NEEDS TO REMAIN
C            RESERVED LATER ON (DETERMINED IN THIS ROUTINE).
C     *
C     SOME OF THE GUGA-CI ARRAYS ARE ASSIGNED TO THE BLANK COMMON AREA
C     SINCE THEY ARE USED BOTH IN THE GUGA-CI ROUTINES AND THE ANALYTIC
C     DERIVATIVE ROUTINES.
C     ARRAY            CONTENTS
C     ONELM(NCIO)      LAGRANGIAN MULTIPLIERS
C     TWOPD(NCIGAM)    TWO-PARTICLE DENSITY MATRIX
C     *
C     THE DYNAMIC MEMORY ALLOCATION EMPLOYS THE FOLLOWING ORDER.
C     ARRAY: ONELM(),TWOPD()
C     START: LCI1....LCI2...
C     *
      USE LIMIT, ONLY: LMACT
      IMPLICIT DOUBLE PRECISION (A-H,O-Z)
      COMMON
     ./GUGA1 / NCIO,NCIGAM
     ./GUGA2 / LCI1,LCI2,LCI3,LCI4
     ./INOPT2/ IN2(300)
     ./NBFILE/ NBF(20)
     ./ORBITS/ NUMB,NORBS,NMOS,NALPHA,NBETA
C *** FILE NUMBERS.
      NB6    = NBF(6)
C *** INPUT VARIABLES.
      JPRINT = IN2(42)
C *** DETERMINE FIELD LENGTHS.
      NCIO   = IN2(131)+IN2(132)
      ITEMP  = (NCIO*(NCIO+1))/2
      NCIGAM = (ITEMP*(ITEMP+1))/2
      NSQR   = NORBS*NORBS
C *** ASSIGN MEMORY.
      LCI1   = LM5+1
      LCI2   = LCI1+LMACT*NCIO
      LCI3   = LCI2+NCIGAM
      LCI4   = LCI3+NSQR
      LM5X   = LCI4+NSQR-1
      LM5R   = LM5X
C *** PRINTING SECTION.
      IF(JPRINT.GE.2 .OR. LM5X.GT.LEN) THEN
         WRITE(NB6,500) NCIO,NCIGAM,NSQR,NSQR
         WRITE(NB6,510) LCI1,LCI2-1,NCIO,LCI2,LCI3-1,NCIGAM,
     1                  LCI3,LCI4-1,NSQR,LCI4,LM5X,NSQR
      ENDIF
C *** ERROR EXIT.
      IF(LM5X.GT.LEN) THEN
         WRITE(NB6,520) LEN,LM5X
         STOP 'DYNGUG'
      ENDIF
      RETURN
  500 FORMAT(///1X,'SIZE OF ONE-PARTICLE DENSITY MATRIX          ',I10,
     1       /  1X,'SIZE OF TWO-PARTICLE DENSITY MATRIX          ',I10,
     2       /  1X,'SIZE OF NATURAL ORBITAL MO COEFFICIENTS      ',I10,
     3       /  1X,'SIZE OF CI-CORRECTED AO-BASIS DENSITY MATRIX ',I10)
  510 FORMAT(///1X,'MEMORY ALLOCATION FOR GUGA-CI CALCULATION    ',
     1       // 1X,'    START    FINAL   LENGTH    CONTENTS      ',
     2       // 1X,3I9,4X,'LAGRANGIAN MULTIPLIERS                ',
     3       /  1X,3I9,4X,'TWO-PARTICLE DENSITY MATRIX           ',
     4       /  1X,3I9,4X,'NATURAL ORBITAL MO COEFFICIENTS       ',
     5       /  1X,3I9,4X,'CI-CORRECTED AO-BASIS DENSITY MATRIX  ')
  520 FORMAT(///1X,'*** ERROR ***',
     1       // 1X,'DUE TO INSUFFICIENT MEMORY THE PROGRAM CANNOT ',
     2             'CARRY OUT THE GUGA-CI CALCULATION.',
     3       /  1X,'AVAILABLE LENGTH OF BLANK COMMON             ',I10,
     4       /  1X,'MINIMUM FIELD LENGTH REQUIRED FOR GUGA-CI    ',I10/)
      END
