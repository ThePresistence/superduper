      SUBROUTINE MODPAR (X,NFX,NSX,N)
C     *
C     MODIFY SELECTED PARAMETERS IN THE WORKING ARRAYS.
C     SAVE PREVIOUS VALUES OF PARAMETERS (IF REQUESTED).
C     *
C     NOTATION. I=INPUT, O=OUTPUT.
C     X(I)      NEW VALUE OF PARAMETER (I).
C     NFX(I)    TYPE OF PARAMETER - CONVENTIONS SEE BELOW  (I).
C     NSX(I)    ATOMIC NUMBER OF THE CORRESPONDING ELEMENT (I).
C     N         MAXIMUM INDEX OF PARAMETER (I).
C     *
C     SPECIFICATIONS FOR PARAMETERS (NF,NS).
C     NF IS THE TYPE OF PARAMETER. CONVENTIONS IN STANDARD NOTATION:
C       1 USS  ,   2 UPP  ,   3 ZS   ,   4 ZP   ,   5 BETAS,   6 BETAP,
C       7 ALPHA,   8 BETPI,   9 BETSH,  10 BETPH,  11 ALPS ,  12 ALPP ,
C      13 ALPPI,  14 ALPSH,  15 ALPPH,  16 FVAL1,  17 FVAL2,  18 GVAL1,
C      19 GVAL2,  20 FG   ,  21 UDD  ,  22 ZD   ,  23 BETAD,  24 ZSN  ,
C      25 ZPN  ,  26 ZDN  ,  27 POCOR,  28 GSCAL,  29 NUMAO,  30      ,
C      31 FN11 ,  32 FN21 ,  33 FN31 ,  34 FN12 ,  35 FN22 ,  36 FN32 ,
C      37 FN13 ,  38 FN23 ,  39 FN33 ,  40 FN14 ,  41 FN24 ,  42 FN34 ,
C      43 SOLV1,  44 SOLV2,  45 SOLV3,  46 SOLV4,  47 SOLV5,  48 SOLV6,
C      49 ZSCOR,  50 FSCOR,  51 BSCOR,  52 ASCOR,  53 DELTA,  54 OMEGA,
C      55 XSCAL,  56 XOFFL,  57 XOFFG,  58 ZSSCF,  59 ZPSCF,  60 ZDSCF,
C      61 BSSCF,  62 BPSCF,  63 BDSCF,  64 XUSS ,  65 XUPP ,  66 XUDD ,
C      67 ZSNMR,  68 ZPNMR,  69 ZDNMR,  70 BSNMR,  71 BPNMR,  72 BDNMR,
C      73 PDDG1,  74 PDDG2,  75 PDDG3,  76 PDDG4,  77 C6   ,  78 R0   ,
C      79 D3S6 ,  80 D3S8 ,  81 D3A1 ,  82 D3A2 ,  83      ,  84      ,
C      91 GSS  ,  92 GPP  ,  93 GSP  ,  94 GP2  ,  95 HSP  ,  96 HPP  ,
C      97      ,  98      ,  99 EHEAT, 100 CORE , 101      , 102      ,
C     103      , 104      , 105      , 106      , 107      , 108      ,
C     121 F0DD , 122 F2DD , 123 F4DD , 124 F0SD , 125 G2SD , 126 F0PD ,
C     127 F2PD , 128 G1PD , 129 G3PD , 130      , 131      , 132      ,
C     201 ALP01, 202 ALP02, 203 ALP03, 204 ALP04, 205 ALP05, 206 ALP06,
C     207 ALP07, 208 ALP08, 209 ALP09, 210 ALP10, etc up to  286 ALP86.
C     NS DENOTES THE POSITION OF THE PARAMETER IN ITS ARRAY, I.E.
C     USUALLY THE ATOMIC NUMBER.
C     *
      USE LIMIT, ONLY: LMZ
      IMPLICIT DOUBLE PRECISION (A-H,O-Z)
C     PARAMETER (SMALL=1.0D-08)
      LOGICAL AM1PM3,NOMNDO,OMX
      COMMON
     ./ABOND / ALPB(LMZ,LMZ),MALPB(LMZ)
     ./AMPGAU/ GUESS1(LMZ,4,3),GSCAL(LMZ),IMP(LMZ)
     ./COSMO6/ SOLV1(LMZ,6)
     ./DPARM / LORBS(LMZ)
     ./DPARM1/ UDD(LMZ,3)
     ./DPARM2/ ZSN(LMZ,3)
     ./DPARM3/ F0DD(LMZ,9)
     ./DPARM8/ JF0SD(LMZ),JG2SD(LMZ),JGSP(LMZ),JHSP(LMZ)
      COMMON
     ./INOPT2/ IN2(300)
     ./MULTIP/ DD(6,0:LMZ),PO(9,0:LMZ)
     ./NBFILE/ NBF(20)
     ./PARDER/ TORE(LMZ),EHEAT(LMZ),EISOL(LMZ)
     ./PAROMM/ BETPI(LMZ,12),ZSCOR(LMZ,4)
     ./PAROPT/ USS(LMZ,7)
     ./PDDG1 / DA(LMZ,2),PA(LMZ,2)
     ./PSNPRD/ XBETAN(LMZ,3),XZETAN(LMZ,3),XZETAS(LMZ,3),XBETAS(LMZ,3),
     .         XUS(LMZ,3),XSCALN(0:LMZ),XOFFN(0:LMZ,2),IDEFN(0:LMZ)
     ./QMMM3 / DELTAM(LMZ,2)
     ./REP   / GSS(LMZ,6)
     ./MMDPP / C6(LMZ),R0(LMZ),D3S6,D3S8,D3A1,D3A2
C     COMMON
C    ./ATORB / IOS(LMZ,3)
C    ./DNBND / III(LMZ,2)
      DIMENSION X(N),NFX(N),NSX(N)
      DIMENSION NEW(LMZ),NPO(LMZ)
C *** INITIALIZATION.
      IOP    = IN2(2)
      AM1PM3 = IOP.EQ.-2 .OR. IOP.EQ.-7 .OR. IOP.EQ.-12 .OR. IOP.EQ.-17
      KSTOP  = 0
      NSMIN  = LMZ
      NSMAX  = 0
      DO 10 I=1,LMZ
      NEW(I) = 0
      NPO(I) = 0
   10 CONTINUE
C *** CURRENT CONVENTION: PO(9,NS)=PO(1,NS) UNLESS READ IN EXPLICITLY.
C     EARLIER CONVENTION: PO(9,NS)=PO(1,NS) UNLESS PREDEFINED ANYWHERE.
C     THE FOLLOWING LINES (COMMENTS) SHOW THE EARLIER CODE WHERE ANY
C     PREDEFINED VALUE OF PO(9,NS).NE.PO(1,NS), E.G. FROM BLOCK DATA,
C     IS USED. THE CURRENT CODE WILL EMPLOY AN INDEPENDENT VALUE OF
C     PO(9,NS) ONLY IF READ FROM INPUT FILE VIA NF=27 (SEE BELOW).
C     THE DEFAULT NPO(NS)=0 CORRESPONDS TO PO(9,NS)=PO(1,NS).
C     DO 20 I=1,N
C     NS     = NSX(I)
C     IF(ABS(PO(9,NS)-PO(1,NS)).GT.SMALL) THEN
C        NPO(NS) = 1
C     ENDIF
C  20 CONTINUE
C *** LOOP OVER INPUT PARAMETERS AND REPLACE PARAMETER VALUES.
      DO 30 I=1,N
      NF     = NFX(I)
      NS     = NSX(I)
      NSMIN  = MIN(NSMIN,NS)
      NSMAX  = MAX(NSMAX,NS)
      A      = X(I)
C     STANDARD PARAMETERS.
      IF(NF.LE.7) THEN
         USS(NS,NF) = A
C     OM1 AND OM2 PARAMETERS.
      ELSE IF(NF.GE.8 .AND. NF.LE.19) THEN
         BETPI(NS,NF-7) = A
C     PARAMETERS FOR d ORBITALS.
      ELSE IF(NF.GE.21 .AND. NF.LE.23) THEN
         UDD(NS,NF-20) = A
C     AUXILIARY ORBITAL EXPONENTS FOR ONE-CENTER TERMS.
      ELSE IF(NF.GE.24 .AND. NF.LE.26) THEN
         ZSN(NS,NF-23) = A
C     ADDITIVE TERM FOR CORE.
      ELSE IF(NF.EQ.27) THEN
         PO(9,NS) = A
         NPO(NS)  = 1
C-AK  SCALING OF GAUSSIAN CORE-CORE TERMS IN AM1/D-PHOT.
      ELSE IF(NF.EQ.28) THEN
         GSCAL(NS) = A
C-AK  NUMBER OF ATOMIC ORBITALS.
      ELSE IF(NF.EQ.29) THEN
         LORBS(NS) = NINT(A)
C-AK  UNSUPPORTED TYPE (SUPERSEDED BY NUMAO).
      ELSE IF(NF.EQ.30) THEN
         WRITE(NB6,600)
         STOP 'MODPAR'
C     AM1 AND PM3 CORE REPULSION TERMS (GAUSSIANS).
      ELSE IF(NF.GE.31 .AND. NF.LE.42 .AND. AM1PM3) THEN
         K123 = MOD(NF-1,3)+1
         KFN  = (NF-28)/3
         GUESS1(NS,KFN,K123) = A
         IMP(NS) = MAX(IMP(NS),KFN)
C     PARAMETERS FOR SOLVATION MODELS.
      ELSE IF(NF.GE.43 .AND. NF.LE.48) THEN
         SOLV1(NS,NF-42) = A
C     PARAMETERS FOR CORE ELECTRONS.
      ELSE IF(NF.GE.49 .AND. NF.LE.52) THEN
         ZSCOR(NS,NF-48) = A
C     PARAMETERS FOR ELECTROSTATIC POTENTIALS.
      ELSE IF(NF.GE.53 .AND. NF.LE.54) THEN
         DELTAM(NS,NF-52) = A
C     NMR PARAMETERS: SCALE FACTORS.
      ELSE IF(NF.EQ.55) THEN
         XSCALN(NS) = A
C     NMR PARAMETERS: OFFSET.
      ELSE IF(NF.GE.56 .AND. NF.LE.57) THEN
         XOFFN(NS,NF-55) = A
C     NMR PARAMETERS: ORBITAL EXPONENTS FOR SCF.
      ELSE IF(NF.GE.58 .AND. NF.LE.60) THEN
         XZETAS(NS,NF-57) = A
         IDEFN(NS) = MAX(IDEFN(NS),2)
C     NMR PARAMETERS: BETA RESONANCE TERMS FOR SCF.
      ELSE IF(NF.GE.61 .AND. NF.LE.63) THEN
         XBETAS(NS,NF-60) = A
         IDEFN(NS) = MAX(IDEFN(NS),2)
C     NMR PARAMETERS: ONE-CENTER ENERGIES.
      ELSE IF(NF.GE.64 .AND. NF.LE.66) THEN
         XUS(NS,NF-63) = A
         IDEFN(NS) = 3
C     NMR PARAMETERS: ORBITAL EXPONENTS FOR NMR.
      ELSE IF(NF.GE.67 .AND. NF.LE.69) THEN
         XZETAN(NS,NF-66) = A
         IDEFN(NS) = MAX(IDEFN(NS),1)
C     NMR PARAMETERS: BETA RESONANCE TERMS FOR NMR.
      ELSE IF(NF.GE.70 .AND. NF.LE.72) THEN
         XBETAN(NS,NF-69) = A
         IDEFN(NS) = MAX(IDEFN(NS),1)
C     CORE REPULSION PDDG TERMS: PREFACTORS.
      ELSE IF(NF.GE.73 .AND. NF.LE.74) THEN
         PA(NS,NF-72) = A
         IDEFN(NS) = MAX(IDEFN(NS),1)
C     CORE REPULSION PDDG TERMS: DISTANCES.
      ELSE IF(NF.GE.75 .AND. NF.LE.76) THEN
         DA(NS,NF-74) = A
         IDEFN(NS) = MAX(IDEFN(NS),1)
C     PARAMETERS FOR DISPERSION CORRECTION (D1 AND D2)
      ELSE IF(NF.EQ.77) THEN
         C6(NS) = A
      ELSE IF(NF.EQ.78) THEN
         R0(NS) = A
C     PARAMETERS FOR DISPERSION CORRECTION (D3)
      ELSE IF(NF.EQ.79) THEN
         D3S6 = A
      ELSE IF(NF.EQ.80) THEN
         D3S8 = A
      ELSE IF(NF.EQ.81) THEN
         D3A1 = A
      ELSE IF(NF.EQ.82) THEN
         D3A2 = A
C     ONE-CENTER TWO-ELECTRON INTEGRALS (sp).
      ELSE IF(NF.GE.91 .AND. NF.LE.96) THEN
         GSS(NS,NF-90) = A
         IF(NF.EQ.93) JGSP(NS) = 1
         IF(NF.EQ.95) JHSP(NS) = 1
C     EXPERIMENTAL HEAT OF FORMATION FOR THE ATOM.
      ELSE IF(NF.EQ.99) THEN
         EHEAT(NS) = A
C     CORE CHARGE.
      ELSE IF(NF.EQ.100) THEN
         TORE(NS) = A
C     MAIN QUANTUM NUMBERS FOR SP AND D.
C     ELSE IF(NF.GE.101 .AND. NF.LE.102) THEN
C        III(NS,NF-100) = NINT(A)
C     GROUND-STATE OCCUPATION NUMBERS FOR S,P,D.
C     ELSE IF(NF.GE.103 .AND. NF.LE.105) THEN
C        IOS(NS,NF-102) = NINT(A)
C     SLATER-CONDON PARAMETERS INVOLVING D ORBITALS.
      ELSE IF(NF.GE.121 .AND. NF.LE.129) THEN
         F0DD(NS,NF-120) = A
         IF(NF.EQ.124) JF0SD(NS) = 1
         IF(NF.EQ.125) JG2SD(NS) = 1
C     MNDO/d BOND PARAMETERS FOR CORE REPULSION.
      ELSE IF(NF.GE.201 .AND. NF.LE.(200+LMZ)) THEN
         NJ  = NF-200
         ALPB(NJ,NS) = A
         MALPB(NS)   = MAX(MALPB(NS),NJ)
C     INPUT ERROR.
      ELSE
         KSTOP = KSTOP+1
      ENDIF
C     CHECK TYPE OF INPUT PARAMETER.
C     NFTYP = 2 FOR ZS,ZP,ZD.
C     NFTYP = 3 FOR USS,UPP,UDD,ZSN,ZPN,ZDN,GSS,GPP,GSP,GP2,HSP.
C     NFTYP = 1 OTHERWISE.
      NFTYP = 1
      IF(NF.EQ.3 .OR. NF.EQ.4 .OR. NF.EQ.22) THEN
         NFTYP = 2
      ELSE IF(NF.EQ.1 .OR. NF.EQ.2 .OR. NF.EQ.21) THEN
         NFTYP = 3
      ELSE IF(NF.GE.24 .AND. NF.LE.26) THEN
         NFTYP = 3
      ELSE IF(NF.GE.91 .AND. NF.LE.95) THEN
         NFTYP = 3
      ENDIF
      NEW(NS)  = MAX(NEW(NS),NFTYP)
   30 CONTINUE
C *** ERROR EXIT.
      IF(KSTOP.GT.0) THEN
         NB6 = NBF(6)
         WRITE(NB6,500)
         IF(KSTOP.GT.1) WRITE(NB6,510) KSTOP
         STOP 'MODPAR'
      ENDIF
C *** COMPUTE DEPENDENT PARAMETERS.
      OMX    = IOP.EQ.-5 .OR. IOP.EQ.-6 .OR. IOP.EQ.-8 .OR. IOP.EQ.-9
     1                   .OR. IOP.EQ.-22 .OR. IOP.EQ.-23
      NOMNDO = IOP.GT.0  .OR. OMX
      DO 40 NS=NSMIN,NSMAX
      IF(NEW(NS).GE.2) THEN
         GSS(NS,6) = 0.5D0*(GSS(NS,2)-GSS(NS,4))
         IF(OMX) THEN
            CALL GTOHYF (NS,0,0)
         ENDIF
         IF(.NOT.NOMNDO) THEN
            CALL INIGHD(NS)
            CALL DDPOHY(NS)
            IF(NPO(NS).EQ.0) PO(9,NS)=PO(1,NS)
         ENDIF
         IF(NEW(NS).GE.3) THEN
            EISOL(NS) = EATOM(NS,0,0,0)
         ENDIF
      ENDIF
   40 CONTINUE
      RETURN
  500 FORMAT(///1X,'ERROR IN SUBROUTINE MODPAR.',
     1       /  1X,'ASSIGNMENT OF INPUT PARAMETERS IMPOSSIBLE.')
  510 FORMAT(   1X,'NUMBER OF ERRORS',I10)
  600 FORMAT(///1X,'ERROR IN SUBROUTINE MODPAR.',
     1       /  1X,'SETTING NUMBER OF ORBITALS USING PARAMETER XX30'
     2       /  1X,'(IPAR=30) IS NO LONGER SUPPORTED. PLEASE USE'
     3       /  1X,'PARAMETER NUMAO (IPAR=29) INSTEAD.')
      END
