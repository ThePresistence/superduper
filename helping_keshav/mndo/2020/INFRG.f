      SUBROUTINE INFRG (NUMAT,NFRAGS,NCHRGS)
C     *
C     READ FRAGMENT DATA (PRELIMINARY VERSION).
C     *
C     NOTATION. I=INPUT, O=OUTPUT.
C     NUMAT     NUMBER OF ATOMS IN MOLECULE (I).
C     NFRAGS    FRAGMENT ASSIGNMENT OF ATOMS IN MOLECULE (I).
C     NCHRGS    FORMAL CHARGES OF ATOMS IN MOLECULE (I).
C     *
      IMPLICIT DOUBLE PRECISION (A-H,O-Z)
C     CHARACTER*80 KTITLE,KOMENT
      COMMON
C    ./FLAG1 / KTITLE,KOMENT
     ./INOPT2/ IN2(300)
     ./NBFILE/ NBF(20)
      DIMENSION NFRAGS(NUMAT),NCHRGS(NUMAT)
C *** FILE NUMBERS.
      NB5    = NBF(5)
      NB6    = NBF(6)
C *** INPUT OPTIONS.
      JPRINT = IN2(42)
      LINFRG = IN2(58)
      INPFRG = IN2(59)
      KHARGE = IN2(65)
      KTRIAL = IN2(67)
C *** CHECK WHETHER INPUT OF FRAGMENT DATA IS REQUIRED.
      IF(INPFRG.LT.0) RETURN
      IF(LINFRG.LE.0 .AND. KTRIAL/10.NE.3) RETURN
C *** INITIALIZE ARRAYS.
      DO 10 I=1,NUMAT
      NFRAGS(I) = 1
      NCHRGS(I) = 0
   10 CONTINUE
C *** READ FRAGMENT ASSIGNMENTS.
      READ(NB5,500,ERR=100,END=100) (NFRAGS(I),I=1,NUMAT)
C *** READ FORMAL ATOMIC CHARGES.
      NCHRG  = 0
   20 CONTINUE
      READ(NB5,500,ERR=100,END=100) N,NDUMMY
      IF(N.GT.0 .AND. N.LE.NUMAT) THEN
         NCHRGS(N) = NDUMMY
         NCHRG     = NCHRG+NDUMMY
         GO TO 20
      ENDIF
C *** PRINTING SECTION.
      IF(JPRINT.GE.2) THEN
         WRITE(NB6,600) NFRAGS
         WRITE(NB6,610) NCHRGS
      ENDIF
C *** CHECK FOR SIMPLE INPUT ERRORS.
      IF(KHARGE.NE.NCHRG) THEN
         WRITE(NB6,620) KHARGE,NCHRG
         STOP 'INFRG'
      ENDIF
      RETURN
C *** ERROR EXIT.
  100 CONTINUE
      WRITE(NB6,630)
      STOP 'INFRG'
  500 FORMAT(10I5)
  600 FORMAT(///1X,'FRAGMENT ASSIGNMENTS FOR ATOMS',
     1       / (1X,10I5))
  610 FORMAT(// 1X,'FORMAL CHARGES ASSIGNED TO ATOMS',
     1       / (1X,10I5))
  620 FORMAT(///1X,'INCOMPATIBLE INPUT FOR CHARGES:',
     1       /  1X,'KHARGE (DIRECTLY FROM INPUT)',I10,
     2       /  1X,'NCHRG  (FROM FRAGMENT DATA) ',I10,/)
  630 FORMAT(///1X,'ERROR DURING INPUT OF FRAGMENT DATA'/)
      END
