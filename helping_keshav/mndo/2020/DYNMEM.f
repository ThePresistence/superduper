      SUBROUTINE DYNMEM (LEN,LM5)
C     *
C     CONTROL ROUTINE FOR DYNAMIC MEMORY ALLOCATION.
C     *
C     LEN = AVAILABLE LENGTH OF BLANK COMMON AREA (INPUT VALUE,
C           NOT MODIFIED IN THIS ROUTINE).
C     LM5 = LAST ADDRESS OF BLANK COMMON AREA THAT IS ACTUALLY USED
C           FOR THE CURRENT MOLECULE (DETERMINED HERE).
C     *
C     THIS ROUTINE IS CALLED AFTER COMPLETING THE INPUT FOR A GIVEN
C     MOLECULE IN ORDER TO ALLOCATE THE AVAILABLE BUFFER SPACE.
C     A MORE DETAILED MEMORY ALLOCATION IS CARRIED OUT LATER FOR
C     -  THE ANALYTIC DERIVATIVE CODE
C     -  THE COSMO SOLVATION CODE (SEE DYNCSM)
C     WHICH USE DYNAMICALLY ALLOCATABLE BUFFER.
C     *
C     INPUT COMMON BLOCKS (PROVIDING INFORMATION THAT IS REQUIRED IN
C     THE MEMORY ALLOCATION PROCESS).
C     ATOMS     NUMAT,NAT,NFIRST,NLAST: MOLECULAR DATA
C     CIPARM    ICI1,ICI2: SIZE OF ACTIVE CI SPACE
C     COSMO2    NPSTOT: NUMBER OF SURFACE SEGMENTS
C     DFP       NVAR: NUMBER OF GEOMETRICAL VARIABLES
C     INOPT1    IN1: INPUT OPTIONS (NEVER CHANGED)
C     INOPT2    IN2: INPUT OPTIONS (ACTUAL VALUES, MAY BE UPDATED)
C     NBFILE    NBF: FILE NUMBERS
C     ORBITS    NUMB,NORBS,NMOS,NALPHA,NBETA: ORBITAL DATA
C     SKIPA     ISKPA: FLAG FOR ANALYTIC DERIVATIVES
C     UHF       UHF: LOGICAL FLAG FOR UHF TREATMENT
C     *
C     OUTPUT COMMON BLOCKS (DEFINED IN ROUTINES CALLED FROM DYNMEM).
C     FLAG4     INTSUM: NUMBER OF TWO-ELECTRON INTEGRALS
C     GUGA1     NCIO,NCIGAM: SIZE OF GUGA-CI ARRAYS
C     GUGA2     LCI1(2): ADDRESSES FOR GUGA-CI TREATMENT
C     INDEX     INDX(LMX): CANONICAL PAIR INDICES
C     LIMITS    LM2,LM3,LM4,LM6,LM7,LM8,LM9: SIZE OF SCF ARRAYS
C     LMCSM1    LC1(4): ADDRESSES FOR COSMO TREATMENT
C     LMCSM2    LMC1(7): LENGTHS OF COSMO ARRAYS
C     LMDIIS    LDIIS1(9): ADDRESSES FOR DIIS TREATMENT
C     LMDMS     LD1(5): ADDRESSES FOR DENSITY MATRIX SEARCH
C     LMFOR     LF1(9): ADDRESSES FOR OPTIMIZER AND FORCE CONSTANTS
C     LMGRAD    LG1(8): ADDRESSES FOR OM2 ENERGY AND GRADIENT
C     LMPERT    LP1(5): ADDRESSES FOR PERTURBATION TREATMENT
C     LMSCF     LS1(9): ADDRESSES FOR RHF CALCULATION
C     LMUHF     LU1(9): ADDRESSES FOR UHF CALCULATION
C     PSDOPT    IPSOPT(7): BUFFER LENGTH FOR ANALYTIC DERIVATIVES
C     *
      IMPLICIT DOUBLE PRECISION (A-H,O-Z)
      LOGICAL NOCI,OKCI
      COMMON
     ./INOPT2/ IN2(300)
C     *
C *** INPUT OPTIONS.
      IOP    = IN2(2)
      IDIIS  = IN2(9)
      NMR    = IN2(13)
      ICOSMO = IN2(28)
      IPSANA = IN2(29)
      INTDIR = IN2(55)
      LINDMS = IN2(56)
      LINDIA = IN2(57)
      IABSCI = ABS(IN2(77))
      IVBSE  = IN2(213)
C     CI CURRENTLY NOT AVAILABLE FOR DIRECT METHODS (INTDIR.GT.0).
C     ALSO NOT FEASIBLE WITHOUT EIGENVECTORS (LINDMS.GT.0, LINDIA.EQ.0).
      NOCI   = INTDIR.GT.0 .OR. (LINDMS.GT.0 .AND. LINDIA.EQ.0)
      OKCI   = .NOT.NOCI
C     *
C *** DYNAMIC MEMORY ALLOCATION.
C     LM5X   = LAST ADDRESS THAT IS ACTUALLY USED FOR A GIVEN TASK
C     LM5Y   = LAST ADDRESS THAT MUST REMAIN RESERVED FOR LATER TASKS
C     LM5    = LAST ADDRESS USED OVERALL (UPDATED FOR EACH TASK)
C     LM5R   = LAST ADDRESS RESERVED OVERALL (UPDATED FOR EACH TASK)
C     *
C *** GENERAL INITIALIZATION.
      CALL DYNDEF
C *** GEOMETRY OPTIMIZATION AND FORCE CONSTANTS.
      CALL DYNFRC (LEN,LFA,LFB)
      LM5R   = LFA
C *** SCF TREATMENT.
      IF(INTDIR.LE.0 .AND. LINDMS.LE.3) THEN
         CALL DYNSCF (LEN,LFA,LM5X,LM5Y)
      ELSE IF(INTDIR.GT.0 .AND. LINDMS.LE.3) THEN
         CALL DYNDIR (LEN,LFA,LM5X,LM5Y)
      ELSE IF(INTDIR.GT.3 .AND. LINDMS.GT.3) THEN
         CALL DYNSPA (LEN,LFA,LM5X,LM5Y)
      ENDIF
      LM5    = LM5X
      LM5R   = MAX(LM5R,LM5Y)
C     CONJUGATE GRADIENT DENSITY MATRIX SEARCH.
      IF(LINDMS.GE.1 .AND. LINDMS.LE.3) THEN
         CALL DYNDMS (LEN,LM5,LM5X,LM5Y)
         LM5  = MAX(LM5 ,LM5X)
         LM5R = MAX(LM5R,LM5Y)
      ENDIF
      LM5A = LM5
C     OM2 ORTHOGONALIZATION CORRECTIONS.
      IF(IOP.EQ.-6 .OR. IOP.EQ.-8 .OR. IOP.EQ.-9
     1  .OR. IOP.EQ.-22 .OR. IOP.EQ.-23) THEN
         CALL DYNOM2 (LEN,LM5,LM5X,LM5Y)
         LM5  = MAX(LM5 ,LM5X)
         LM5R = MAX(LM5R,LM5Y)
         LM5A = MAX(LM5A,LM5R)
C     VALENCE BOND TREATMENT.
      ELSE IF(IVBSE.NE.0) THEN
         CALL DYNVB  (LEN,LM5,LM5X,LM5Y)
         LM5  = MAX(LM5 ,LM5X)
         LM5R = MAX(LM5R,LM5Y)
         LM5A = MAX(LM5A,LM5R)
      ENDIF
C     DIIS PROCEDURE.
      IF(IDIIS.GE.0) THEN
         CALL DYNSCR (LEN,LM5A,LM5X,LM5Y)
         LM5  = MAX(LM5 ,LM5X)
         LM5R = MAX(LM5R,LM5Y)
      ENDIF
C *** CORRELATED TREATMENTS.
C     INTEGRAL TRANSFORMATION AND PERTURBATION TREATMENT.
C     THERE ARE SPECIAL CONVENTIONS FOR SUBROUTINE DYNPT:
C     MEMORY ALLOCATION DOES NOT START AT ADDRESS LM5+1,
C     BUT REFERS DIRECTLY TO ADDRESSES DEFINED IN DYNSCF.
      IF(OKCI .AND. IABSCI.GE.2 .AND. IABSCI.LE.4) THEN
         CALL DYNPT (LEN,LM5,LM5X,LM5Y)
         LM5  = MAX(LM5 ,LM5X)
         LM5R = MAX(LM5R,LM5Y)
      ENDIF
C     GUGA-CI TREATMENT.
      IF(OKCI .AND. IABSCI.EQ.5) THEN
         CALL DYNGUG (LEN,LM5R,LM5X,LM5Y)
         LM5  = MAX(LM5 ,LM5X)
         LM5R = MAX(LM5R,LM5Y)
      ENDIF
C *** ANALYTIC DERIVATIVES.
      IF(IPSANA.NE.0 .OR. NMR.NE.0 .OR. IN2(121).EQ.4) THEN
         CALL DYNANA (LEN)
      ENDIF
C *** ACCOUNT FOR THE SPECIAL CASE THAT THE MEMORY REQUIREMENTS
C     FOR FORCE CONSTANT ANALYSIS MIGHT BE LARGER THAN THOSE FOR
C     THE QUANTUM-CHEMICAL CALCULATION.
      LM5 = MAX(LM5,LFB)
      RETURN
      END
