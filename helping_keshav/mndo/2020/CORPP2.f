      SUBROUTINE CORPP2 (ISHELL,JSHELL,NI,NJ,R,CORPP)
C     *
C     CORE-VALENCE PSEUDOPOTENTIAL (IN EV) FOR OM2.
C     *
C     NOTATION. I=INPUT, O=OUTPUT.
C     ISHELL    INDEX OF FIRST  GAUSSIAN SHELL (I).
C     JSHELL    INDEX OF FIRST  GAUSSIAN SHELL (I).
C     NI,NJ     ATOMIC NUMBERS (I).
C     R         INTERNUCLEAR DISTANCE IN ATOMIC UNITS (I).
C     CORPP()   CORE-VALENCE PSEUDOPOTENTIAL IN EV (O).
C     *
      USE LIMIT, ONLY: LMZ
      IMPLICIT DOUBLE PRECISION (A-H,O-Z)
      COMMON
     ./CONSTN/ ZERO,ONE,TWO,THREE,FOUR,PT5,PT25
     ./INOPT2/ IN2(300)
     ./NBFILE/ NBF(20)
     ./PAROPT/ USS(LMZ),UPP(LMZ),ZS(LMZ),ZP(LMZ),BS(LMZ),BP(LMZ),
     .         ALP(LMZ)
     ./PAROMM/ BETPI(LMZ),BETSH(LMZ),BETPH(LMZ),ALPS(LMZ),ALPP(LMZ),
     .         DUM(LMZ*7),ZSCOR(LMZ),FSCOR(LMZ),BSCOR(LMZ),ASCOR(LMZ)
      DIMENSION CORPP(10,2)
C *** INITIALIZATION.
      NPRINT = IN2(72)
      DO 10 I=1,4
      CORPP(I,1) = ZERO
      CORPP(I,2) = ZERO
   10 CONTINUE
C *** START LOOP FOR CALCULATING THE PSEUDOPOTENTIAL.
C     CONVENTIONS. ATOM I IN ORIGIN, ATOM J ON POSITIVE Z-AXIS.
C     IATOM=1   VALENCE AOS OF ATOM I, CORE    AOS OF ATOM J.
C     IATOM=2   CORE    AOS OF ATOM I, VALENCE AOS OF ATOM J.
C     NOTE.     VALENCE AOS - LABEL A, CORE    AOS - LABEL B.
      DO 20 IATOM=1,2
      IF(IATOM.EQ.1) THEN
         NA     = NI
         NB     = NJ
         NSHELL = ISHELL
      ELSE
         NA     = NJ
         NB     = NI
         NSHELL = JSHELL
      ENDIF
C *** CHECK WHETHER ATOM B HAS CORE ELECTRONS.
C     IF NOT, THERE IS NO PSEUDOPOTENTIAL.
      IF(NB.LE.2) GO TO 20
      ZB     = ZSCOR(NB)
      FB     = FSCOR(NB)
C *** COMPUTE TWO-CENTER CORE-VALENCE OVERLAPS.
      CALL SPGTO2 (NSHELL,ZB,R,S1,S2)
C *** COMPUTE PSEUDOPOTENTIAL FROM CORE ELECTRONS.
C     EXTENDED ZERNER-TYPE PSEUDOPOTENTIAL.
C     PARAMETER FB=FSCORE(NB) IN EV.
      BAS    = BS(NA)
      AAS    = ALPS(NA)
      BBS    = BSCOR(NB)
      CBS    = ASCOR(NB)
      R2     = R*R
      SQR    = SQRT(R)
      T1     = PT5*(BAS+BBS)*SQR*EXP(-(AAS+CBS)*R2)
      AIM11  = -(S1*T1+T1*S1)
      AIM12  = - S1*S1*FB
      CORPP(1,IATOM) = AIM11+AIM12
      IF(NA.GT.2) THEN
         BAP    = BP(NA)
         AAP    = ALPP(NA)
         T3     = PT5*(BAP+BBS)*SQR*EXP(-(AAP+CBS)*R2)
         AIM21  = -(S1*T3+T1*S2)
         AIM22  = - S1*S2*FB
         AIM31  = -(S2*T3+T3*S2)
         AIM32  = - S2*S2*FB
         CORPP(2,IATOM) = AIM21+AIM22
         CORPP(3,IATOM) = AIM31+AIM32
         IF(IATOM.EQ.2) THEN
            CORPP(2,IATOM) = -CORPP(2,IATOM)
         ENDIF
      ENDIF
C     DEBUG PRINT.
      IF(NPRINT.GT.5) THEN
         NB6 = NBF(6)
         WRITE(NB6,500) NB,ZB,FB,BBS,CBS
         WRITE(NB6,510) S1,S2,(CORPP(II,IATOM),II=1,3)
      ENDIF
   20 CONTINUE
      RETURN
  500 FORMAT(' CORPP2: NB,ZB,FB,BBS,CBS',I4,4F12.5)
  510 FORMAT(' CORPP2: S1,S2,CORPP(1-3)',4X,5F12.5)
      END
