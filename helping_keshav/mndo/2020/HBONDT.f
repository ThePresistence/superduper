      SUBROUTINE HBONDT (JPRINT)
C     *
C     CHECK FOR HYDROGEN BONDS X...H-Y AND FIND THEIR NUMBER (NHBOND).
C     A.GOLDBLUM, J.COMPUT.CHEM. 8, 835 (1987).
C     *
C     A HYDROGEN BOND X...H-Y IS IDENTIFIED BY THE FOLLOWING CRITERIA.
C     - ATOMS X AND Y MUST BE NITROGEN, OXYGEN, OR FLUORINE.
C     - THE DISTANCE X...H MUST BE IN THE RANGE OF RHXMIN...RHXMIN.
C     - THE DISTANCE H-Y MUST BE THE SHORTEST DISTANCE INVOLVING H.
C     - THE ANGLE X...H-Y MUST BE LARGER THAN ANGMIN.
C     DEFAULT VALUES.
C     RHXMIN = 1.1 ANGSTROM, RHXMAX = 5.0 ANGSTROM, ANGMIN = 90 DEGREE.
C     *
      USE LIMIT, ONLY: LM1
      IMPLICIT DOUBLE PRECISION (A-H,O-Z)
      LOGICAL NOPRT
      CHARACTER*2 ELEMNT
      COMMON
     ./ATOMC / COORD(3,LM1)
     ./ATOMS / NUMAT,NAT(LM1),NFIRST(LM1),NLAST(LM1)
     ./CONSTF/ A0,AFACT,EV,EVCAL,PI,W1,W2,BIGEXP
     ./CONSTN/ ZERO,ONE,TWO,THREE,FOUR,PT5,PT25
     ./ELEMTS/ ELEMNT(107)
     ./HBONDC/ IHBOND,NHBOND,RHXMIN,RHXMAX,ANGMIN
     ./INOPT2/ IN2(300)
     ./NBFILE/ NBF(20)
C *** INPUT OPTION.
      IOP    = IN2(2)
      IHBOND = IN2(21)
C *** INITIALIZATION.
      RHXMIN = 1.1D0
      RHXMAX = 5.0D0
      ANGMIN = 90.0D0
      IF(IHBOND.EQ.1) THEN
         RHXMIN = 1.2D0
         RHXMAX = 2.5D0
         ANGMIN = 100.0D0
      ELSE IF(IHBOND.GE.2) THEN
         RHXMIN = 1.1D0
         RHXMAX = DBLE(IHBOND)*0.1D0
         ANGMIN = 90.0D0
      ENDIF
      IF(IOP.EQ.-3 .OR. IOP.EQ.-13) THEN
         NOPRT = JPRINT.LT.0
      ELSE
         NOPRT = JPRINT.LT.5
         IF(NOPRT) RETURN
      ENDIF
      IF(.NOT.NOPRT) THEN
         NB6 = NBF(6)
         WRITE(NB6,500) RHXMIN,RHXMAX,ANGMIN
      ENDIF
C *** INITIALIZE COUNTERS AND MIN/MAX DISTANCES.
      NHBOND = 0
      NHBNHN = 0
      NHBNHO = 0
      NHBOHN = 0
      NHBOHO = 0
      N12TOT = 0
      N12NHN = 0
      N12NHO = 0
      N12OHN = 0
      N12OHO = 0
      N15TOT = 0
      N15NHN = 0
      N15NHO = 0
      N15OHN = 0
      N15OHO = 0
      RHYMAX = 0.0D0
      RHYMIN = 100.0D0
      SHXMAX = 0.0D0
      SHXMIN = 100.0D0
C *** LOOP OVER ATOM PAIRS.
      DO 30 I=2,NUMAT
      NI     = NAT(I)
      IF(NI.GT.1 .AND. (NI.LT.7 .OR. NI.GT.9)) GO TO 30
      DO 20 J=1,I-1
      IF(NI.EQ.1) THEN
         IH  = I
         IX  = J
      ELSE
         IH  = J
         IX  = I
         IF(NAT(IH).GT.1) GO TO 20
      ENDIF
      IF(NAT(IX).LT.7 .OR. NAT(IX).GT.9) GO TO 20
C *** CHECK DISTANCE X...H
      RHX    = (COORD(1,IX)-COORD(1,IH))**2+(COORD(2,IX)-COORD(2,IH))**2
     1        +(COORD(3,IX)-COORD(3,IH))**2
      RHX    = SQRT(RHX)
      IF(RHX.LT.RHXMIN .OR. RHX.GT.RHXMAX) GO TO 20
C *** FIND SMALLEST DISTANCE H-Y
      RMIN2  = RHX*RHX
      IY     = 0
      DO 10 K=1,NUMAT
      IF(K.EQ.IH .OR. K.EQ.IX) GO TO 10
      R2     = (COORD(1,K)-COORD(1,IH))**2+(COORD(2,K)-COORD(2,IH))**2
     1        +(COORD(3,K)-COORD(3,IH))**2
      IF(R2.GE.RMIN2) GO TO 10
      RMIN2  = R2
      IY     = K
   10 CONTINUE
      IF(IY.EQ.0) GO TO 20
      IF(NAT(IY).LT.7 .OR. NAT(IY).GT.9) GO TO 20
      RHY    = SQRT(RMIN2)
      RXY2   = (COORD(1,IY)-COORD(1,IX))**2+(COORD(2,IY)-COORD(2,IX))**2
     1        +(COORD(3,IY)-COORD(3,IX))**2
      RHX2   = RHX*RHX
C *** CHECK ANGLE X...H-Y
      ANG    = (RHX2+RMIN2-RXY2)/(TWO*RHX*RHY)
      IF(ANG.LT.-ONE) ANG=-ONE
      IF(ANG.GT. ONE) ANG= ONE
      ANG    = ACOS(ANG)
      ANGLE  = ANG*AFACT
      IF(ANGLE.LE.ANGMIN) GO TO 20
C *** ALL TESTS FOR A HYDROGEN BOND ARE SATISFIED.
      NHBOND = NHBOND+1
      IF(ANGLE.LT.120.0D0) N12TOT = N12TOT+1
      IF(ANGLE.GT.150.0D0) N15TOT = N15TOT+1
      IF(NAT(IX).EQ.7 .AND. NAT(IY).EQ.7) THEN
         NHBNHN = NHBNHN+1
         IF(ANGLE.LT.120.0D0) N12NHN = N12NHN+1
         IF(ANGLE.GT.150.0D0) N15NHN = N15NHN+1
      ENDIF
      IF(NAT(IX).EQ.7 .AND. NAT(IY).EQ.8) THEN
         NHBNHO = NHBNHO+1
         IF(ANGLE.LT.120.0D0) N12NHO = N12NHO+1
         IF(ANGLE.GT.150.0D0) N15NHO = N15NHO+1
      ENDIF
      IF(NAT(IX).EQ.8 .AND. NAT(IY).EQ.7) THEN
         NHBOHN = NHBOHN+1
         IF(ANGLE.LT.120.0D0) N12OHN = N12OHN+1
         IF(ANGLE.GT.150.0D0) N15OHN = N15OHN+1
      ENDIF
      IF(NAT(IX).EQ.8 .AND. NAT(IY).EQ.8) THEN
         NHBOHO = NHBOHO+1
         IF(ANGLE.LT.120.0D0) N12OHO = N12OHO+1
         IF(ANGLE.GT.150.0D0) N15OHO = N15OHO+1
      ENDIF
      RHYMIN = MIN(RHYMIN,RHY)
      RHYMAX = MAX(RHYMAX,RHY)
      SHXMIN = MIN(SHXMIN,RHX)
      SHXMAX = MAX(SHXMAX,RHX)
C *** PRINT HYDROGEN BOND THAT HAS BEEN IDENTIFIED.
      IF(.NOT.NOPRT) THEN
C        WRITE(NB6,510) NHBOND,IX,IH,IY,RHX,RHY,ANGLE,
         WRITE(NB6,510)        IX,IH,IY,RHX,RHY,ANGLE,
     1                  ELEMNT(NAT(IX)),ELEMNT(NAT(IH)),ELEMNT(NAT(IY))
      ENDIF
   20 CONTINUE
   30 CONTINUE
C *** PRINT OVERALL STATISTICS.
      IF(.NOT.NOPRT) THEN
         WRITE(NB6,520)
         WRITE(NB6,530) NHBOND,N12TOT,NHBOND-N12TOT-N15TOT,N15TOT,
     1                  NHBNHN,N12NHN,NHBNHN-N12NHN-N15NHN,N15NHN,
     2                  NHBNHO,N12NHO,NHBNHO-N12NHO-N15NHO,N15NHO,
     3                  NHBOHN,N12OHN,NHBOHN-N12OHN-N15OHN,N15OHN,
     4                  NHBOHO,N12OHO,NHBOHO-N12OHO-N15OHO,N15OHO
         WRITE(NB6,540) RHYMIN,RHYMAX,SHXMIN,SHXMAX
      ENDIF
      RETURN
  500 FORMAT(///1X,'IDENTIFICATION OF HYDROGEN BONDS X...H-Y USING ',
     1             'THE FOLLOWING CRITERIA',
     2       // 1X,'MINIMUM DISTANCE X...H  ',F10.5,' ANGSTROM',
     3       /  1X,'MAXIMUM DISTANCE X...H  ',F10.5,' ANGSTROM',
     4       /  1X,'MINIMUM ANGLE    X...H-Y',F10.5,' DEGREE',
     5       // 1X,'LABELS (IX,IH,IY) REFER TO THE LIST OF REAL ATOMS',
     6          1X,'WITHOUT DUMMY ATOMS',
     7       // 5X,'I   IX   IH   IY     RHX       RHY       ANGLE'/)
C 510 FORMAT(1X,4I5,2F10.5,F12.5,5X,A2,'.. ',A2,'- ',A2)
  510 FORMAT(6X,3I5,2F10.2,F12.0,5X,A2,'.. ',A2,'- ',A2)
  520 FORMAT(// 1X,'NUMBER OF H-BONDS WITH ANGLES   ',
     1             '   0-180   0-120 120-150 150-180'/)
  530 FORMAT(   1X,'NUMBER OF HYDROGEN BONDS, X..H-Y',4I8,
     1       /  1X,'NUMBER OF HYDROGEN BONDS, N..H-N',4I8,
     2       /  1X,'NUMBER OF HYDROGEN BONDS, N..H-O',4I8,
     3       /  1X,'NUMBER OF HYDROGEN BONDS, O..H-N',4I8,
     4       /  1X,'NUMBER OF HYDROGEN BONDS, O..H-O',4I8)
  540 FORMAT(// 1X,'DISTANCES WITHIN H-BONDS FOUND (IN ANGSTROM)',
     1       /  1X,'MINIMUM COVALENT DISTANCE H-Y ',F10.5,
     2       /  1X,'MAXIMUM COVALENT DISTANCE H-Y ',F10.5,
     3       /  1X,'MINIMUM H-BOND   DISTANCE H..X',F10.5,
     4       /  1X,'MAXIMUM H-BOND   DISTANCE H..X',F10.5)
      END
