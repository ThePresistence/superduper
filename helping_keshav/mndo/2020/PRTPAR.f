      SUBROUTINE PRTPAR (IOP,NMR,JPRINT)
C     *
C     CONTROL THE PRINTING OF THE ACTUAL PARAMETERS.
C     *
      USE LIMIT, ONLY: LM1, LMZ
      IMPLICIT DOUBLE PRECISION (A-H,O-Z)
      COMMON
     ./ATOMS / NUMAT,NAT(LM1),NFIRST(LM1),NLAST(LM1)
     ./NBFILE/ NBF(20)
      DIMENSION NATELM(LMZ)
C *** FILE NUMBERS.
      NB6    = NBF(6)
C *** DETERMINE THE ELEMENTS PRESENT IN THE MOLECULE.
C     NELM     NUMBER OF ELEMENTS PRESENT.
C     NATELM   CORRESPONDING ATOMIC NUMBERS (NELM VALUES).
      NELM   = 0
      DO 10 I=1,LMZ
      NATELM(I) = 0
   10 CONTINUE
      DO 20 I=1,NUMAT
      NI     = NAT(I)
      NATELM(NI) = NI
   20 CONTINUE
      DO 30 I=1,LMZ
      IF(NATELM(I).GT.0) THEN
         NELM = NELM+1
         IF(NELM.LT.I) NATELM(NELM) = NATELM(I)
      ENDIF
   30 CONTINUE
C *** PRINT THE STANDARD PARAMETERS.
      WRITE(NB6,500)
      DO 50 N=1,NELM
      NI     = NATELM(N)
      CALL PAROUT(IOP,NMR,NI)
   50 CONTINUE
C *** DEBUG PRINT.
      IF(JPRINT.GT.5) THEN
         DO 60 N=1,NELM
         NI  = NATELM(N)
         CALL DDPOUT(NI,JPRINT)
   60    CONTINUE
      ENDIF
      RETURN
  500 FORMAT(///1X,'PARAMETER VALUES USED IN THE CALCULATION.',
     1       // 1X,' NI  TYPE        VALUE     UNIT')
      END
