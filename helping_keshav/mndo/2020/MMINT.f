      SUBROUTINE MMINT (H,HM,LM4,LM6,ENUCLR)
C     *
C     ELECTROSTATIC CONTRIBUTIONS FROM EXTERNAL POINT CHARGES
C     TO THE CORE HAMILTONIAN AND THE CORE-CORE REPULSIONS.
C     *
C     NOTATION. I=INPUT, O=OUTPUT, S=SCRATCH.
C     H(LM4)    CORE HAMILTONIAN MATRIX (I,O).
C     HM(LM6)   CONTRIBUTIONS FROM GIVEN POINT CHARGE (S).
C     ENUCLR    CORE-CORE REPULSION ENERGY (I,O).
C     *
C     THE SEQUENTIAL VERSION DOES NOT NEED THE SCRATCH ARRAY HM(LM6).
C     IT CAN COLLECT THE CONTRIBUTIONS DIRECTLY IN H(LM4) BY USING:
C     CALL QMINTC (H,LM4,ENUCQM,COORDM(1,M),PTCHG,2,0)
C     CALL QMINT  (H,LM4,ENUCQM,COORDM(1,M),PTCHG,2,0)
C     *
      USE LIMIT, ONLY: LM1M, LMI
      IMPLICIT DOUBLE PRECISION (A-H,O-Z)
      COMMON
     ./CONSTN/ ZERO,ONE,TWO,THREE,FOUR,PT5,PT25
     ./FINDX1/ IP(LMI),IP1(LMI),IP2(LMI)
     ./INOPT2/ IN2(300)
     ./QMMM1 / COORDM(3,LM1M),CHARGM(LM1M)
      DIMENSION H(LM4),HM(LM6)
C *** INPUT OPTIONS.
      NUMATM  = IN2(120)
      MMPOT   = IN2(122)
C *** LOOP OVER EXTERNAL POINT CHARGES.
C.OMP PARALLEL DO SCHEDULE(STATIC)
C.OMP.PRIVATE(ENUCQM,HM,PTCHG)
      DO 20 M=1,NUMATM
      PTCHG  = CHARGM(M)
      IF(PTCHG.NE.ZERO) THEN
         HM(1:LM6) = ZERO
         IF(MMPOT.LE.2) THEN
            CALL QMINTC (HM,LM6,ENUCQM,COORDM(1,M),PTCHG,1,0)
         ELSE
            CALL QMINT  (HM,LM6,ENUCQM,COORDM(1,M),PTCHG,1,0)
         ENDIF
C.OMP CRITICAL
         ENUCLR = ENUCLR+ENUCQM
         DO 10 I=1,LM6
         H(IP(I)) = H(IP(I)) + HM(I)
   10    CONTINUE
C.OMP END CRITICAL
      ENDIF
   20 CONTINUE
C.OMP END PARALLEL DO
      RETURN
      END
