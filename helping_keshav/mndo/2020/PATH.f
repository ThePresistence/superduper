      SUBROUTINE PATH (A,LM5,SCFCAL)
C     *
C     REACTION PATH CALCULATION WITH ONE PATH VARIABLE.
C     *
      USE LIMIT, ONLY: LM1, LMV, LMR
      IMPLICIT DOUBLE PRECISION (A-H,O-Z)
      EXTERNAL SCFCAL
      CHARACTER*80  KTITLE,KOMENT
      COMMON
     ./CONSTF/ A0,AFACT,EV,EVCAL,PI,W1,W2,BIGEXP
     ./DFP   / X(LMV),NVAR
     ./DIPOL / DIP(7)
     ./ERG   / ENERGY,GRAD(LMV),GNORM,CNORM
     ./FLAG1 / KTITLE,KOMENT
     ./INOPT2/ IN2(300)
     ./LMFOR / LF1,LF2,LF3,LF4,LF5,LF6,LF7,LF8,LFA
     ./NBFILE/ NBF(20)
     ./PARM1 / AA(LMV),NA(LM1,4),NATOMS
     ./PARM4 / RC(LMR),LREACT,LTOTAL
     ./PARM6 / LPT,LPT1,LPT2
      DIMENSION A(LM5)
      DIMENSION HEATS(LMR),DIPS(LMR)
      DIMENSION CNORMS(LMR),GNORMS(LMR)
C *** FILE NUMBERS.
      NB6    = NBF(6)
C *** INPUT OPTIONS.
      JOP    = IN2(3)
      IGEOM  = IN2(4)
      MAXEND = IN2(32)
      MIDDLE = IN2(37)
C *** CHECK FOR RESTART.
C     THE CURRENT VALUE OF LPT IS SAVED ON THE RESTART FILE.
      LPT    = 1
      IF(IN2(37).GT.0) THEN
         IF(JOP.EQ.0) CALL DFPSAV (A(LF1),LFA,1,MIDDLE)
         IF(JOP.EQ.1) CALL TSSAV  (A(LF1),A(LF2),NVAR,1,MIDDLE)
      ENDIF
C *** LOOP OVER ALL POINTS ON THE REACTION PATH.
      L1     = (LREACT+2)/3
      L2     = LREACT-3*(L1-1)
      LMIN   = MAX(1,LPT)
      LMAX   = LTOTAL
      DO 20 L=LMIN,LTOTAL
      LPT    = L
      RCV    = RC(L)
      AA(LREACT) = RCV
      IF(IGEOM.EQ.0 .AND. L2.GT.1) RCV=RCV*AFACT
      WRITE(NB6,500) KOMENT,KTITLE
      WRITE(NB6,510) L2,L1,RCV
      CALL SYMTRY(+1)
      CALL GMETRY(-1)
C *** DO THE CALCULATION FOR A GIVEN POINT.
C     SINGLE SCF CALCULATION.
      IF(JOP.EQ.-1 .OR. MAXEND.EQ.1) THEN
         ICALL = 0
         CALL SCF (A,LM5,ICALL,SCFCAL)
         IF(ICALL.EQ.-1) THEN
            LMAX = L-1
            GO TO 30
         ENDIF
C     SINGLE SCF AND GRADIENT CALCULATION.
      ELSE IF(JOP.EQ.-2) THEN
         ICALL = 1
         CALL SCF (A,LM5,ICALL,SCFCAL)
         IF(ICALL.EQ.-1) THEN
            LMAX = L-1
            GO TO 30
         ENDIF
C     OPTIMIZE THE MOLECULAR GEOMETRY.
      ELSE
         CALL GEOOPT (A,LM5,ICALL,SCFCAL)
         IF(ICALL.EQ.-1 .OR. ICALL.EQ.-8) THEN
            LMAX = L-1
            GO TO 30
         ENDIF
         IF(IN2(45).EQ.0) IN2(45)=2
      ENDIF
C *** STORE RESULTS.
      HEATS (L) = ENERGY
      DIPS  (L) = DIP(1)
      CNORMS(L) = CNORM
      GNORMS(L) = GNORM
   20 CONTINUE
C *** PRINTING SECTION.
   30 CONTINUE
      IF(LMIN.LE.LMAX) WRITE(NB6,520)
      DO 40 L=LMIN,LMAX
      RCV    = RC(L)
      IF(IGEOM.EQ.0 .AND. L2.GT.1) RCV=RCV*AFACT
      WRITE(NB6,530) L,RCV,HEATS(L),DIPS(L),GNORMS(L),CNORMS(L)
   40 CONTINUE
      RETURN
  500 FORMAT(///1X,A,/1X,A)
  510 FORMAT(/  1X,'REACTION COORDINATE: VARIABLE',I2,' OF ATOM',I3,
     1       /  1X,'ITS CURRENT VALUE IS ',F10.5//)
  520 FORMAT(///1X,'SUMMARY OF RESULTS FROM REACTION PATH CALCULATION.',
     1       // 3X,'I    REACTION     HEAT OF    DIPOLE   OPTIMIZED',
     2             '   CARTESIAN',
     3       /  3X,'    COORDINATE   FORMATION   MOMENT    GRADIENT',
     4             '    GRADIENT',
     5       /  3X,'                (KCAL/MOL)  (DEBYE)      NORM  ',
     6             '      NORM'/)
  530 FORMAT(   1X,I3,F11.3,F13.5,F9.3,F11.3,F12.3)
      END
