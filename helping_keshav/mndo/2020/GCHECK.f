      SUBROUTINE GCHECK (PRT)
C     *
C     CHECK AFTER THE INITIAL FUNCTION EVALUATION IN AN OPTIMIZATION
C     WHETHER THE CONVERGENCE CRITERIA FOR THE GRADIENT ARE SATISFIED.
C     RETURN WITH THE APPROPRIATE VALUE OF NSUCC IF THIS IS THE CASE.
C     PRT IS A LOGICAL PRINTING FLAG.
C     *
      USE LIMIT, ONLY: LMV
      IMPLICIT DOUBLE PRECISION (A-H,O-Z)
C     *
C     AFTER THE CALL TO SUBROUTINE SCF, COMMON BLOCK /ERG/ CONTAINS
C     FOR                                            ICALL   ISKPA
C     ENERGY = CURRENT ENERGY                     10,20,21    0,1
C     GG(I)  = CURRENT GRADIENT                      21,22    0,1
C     GNORM  = CURRENT GRADIENT NORM FOR GG(I)       21,22    0,1
C     CNORM  = CURRENT CARTESIAN GRADIENT NORM       21,22    0
C     THE GRADIENT INFORMATION CAN BE TAKEN DIRECTLY FROM /ERG/ SINCE
C     ICALL=21 IN THE INITIAL FUNCTION EVALUATION (FLEPO AND TSOPT).
C     *
      LOGICAL PRT
      COMMON
     ./CONINF/ ESTAT0,ESTAT1,GF2NRM
     ./DFP   / X(LMV),NVAR
     ./ERG   / ENERGY,GG(LMV),GNORM,CNORM
     ./INOPT2/ IN2(300)
     ./NBFILE/ NBF(20)
     ./OPTCRT/ NSUCC
     ./OPTTOL/ TOLEND(4),TOLIN(4),XMAXST
C *** FILE NUMBERS.
      NB6    = NBF(6)
C *** INPUT OPTIONS.
      JOP    = IN2(3)
      ICONV  = MOD(IN2(44),10)
      ICROSS = IN2(160)
      LINITG = IN2(48)
C *** INITIALIZATION.
      NSUCC  = -1
      IF(LINITG.LT.0) RETURN
      IF(LINITG.EQ.0) THEN
         TOL3 = TOLEND(3)/10
      ELSE
         TOL3 = TOLEND(3)/LINITG
      ENDIF
C *** GEOMETRY OPTIMIZATION FOR ENERGY MINIMUM.
      IF(JOP.EQ.0 .OR. JOP.EQ.3 .OR. JOP.EQ.5) THEN
         IF(ICROSS.EQ.4) THEN
            IF(GF2NRM.LT.TOL3) THEN
               IF(PRT) WRITE(NB6,490) GF2NRM
               NSUCC = 6
            ENDIF
         ELSE IF(ICONV.EQ.3 .AND. GNORM.LT.TOL3) THEN
            IF(PRT) WRITE(NB6,500) GNORM
            NSUCC = 5
         ELSE
            DO 10 I=1,NVAR
            IF(ABS(GG(I)).GT.TOL3) RETURN
   10       CONTINUE
            IF(PRT) WRITE(NB6,510) GNORM
            NSUCC = 0
         ENDIF
      ELSE
C *** TRANSITION STATE SEARCH.
         DO 20 I=1,NVAR
         IF(ABS(GG(I)).GT.TOL3) RETURN
   20    CONTINUE
         IF(PRT) WRITE(NB6,510) GNORM
         NSUCC  = 0
      ENDIF
C *** PRINT FINAL MESSAGE.
      IF(PRT .AND. NSUCC.GE.0) WRITE(NB6,520)
      RETURN
  490 FORMAT(/  1X,'TEST ON PROJECTED EXCITED STATE GRADIENT ',
     1             'NORM SATISFIED, NORM =',F8.5)
  500 FORMAT(/  1X,'TEST ON GNORM SATISFIED, GNORM =',F8.5)
  510 FORMAT(/  1X,'TEST ON G SATISFIED, GNORM =',F8.5)
  520 FORMAT(/  1X,'INITIAL FUNCTION EVALUATION: CONVERGED GRADIENT.',
     1       /  1X,'NO GEOMETRY OPTIMIZATION REQUIRED.')
      END
