      SUBROUTINE RENORM (E,C,NGAUSS,LGAUSS)
C     *
C     RENORMALIZATION OF CONTRACTED GAUSSIAN BASIS ORBITALS (SPD).
C     USEFUL WHEN THE BASIS IS DEFINED WITH A LIMITED NUMBER OF DIGITS.
C     *
C     NOTATION.  I=INPUT, O=OUTPUT.
C     E(NGAUSS)  EXPONENTS OF PRIMITIVES IN CONTRACTED ORBITAL (I,O).
C     C(NGAUSS)  COEFFICIENTS OF PRIMITIVES IN CONTRACTED ORBITAL (I,O).
C     NGAUSS     NUMBER OF PRIMITIVES (I).
C     LGAUSS     L QUANTUM NUMBER (I). 0 S, 1 P, 2 D.
C     *
      IMPLICIT DOUBLE PRECISION (A-H,O-Z)
      DIMENSION E(NGAUSS),C(NGAUSS)
      IF(NGAUSS.LE.1) THEN
         C(1)   = 1.0D0
      ELSE
         EXPO   = LGAUSS+1.5D0
         SUM    = C(1)**2
         DO 20 I=2,NGAUSS
         SUM    = SUM+C(I)**2
         DO 10 J=1,I-1
         DUM    = 2.0D0*SQRT(E(I)*E(J)) / (E(I)+E(J))
         SUM    = SUM + C(I)*C(J)*2.0D0*DUM**EXPO
   10    CONTINUE
   20    CONTINUE
         FF     = 1.0D0/SQRT(SUM)
         DO 30 I=1,NGAUSS
         C(I)   = C(I)*FF
   30    CONTINUE
      ENDIF
      RETURN
      END
