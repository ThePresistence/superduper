      SUBROUTINE PRTDIA (A,LM4,N,MODE)
C     *
C     PRINT DIAGONAL ELEMENTS OF INPUT MATRIX.
C     *
C     NOTATION. I=INPUT, O=OUTPUT, S=SCRATCH.
C     A         INPUT MATRIX (I).
C     LM4       LINEAR DIMENSION OF INPUT MATRIX (I).
C     N         NUMBER OF ELEMENTS TO BE PRINTED (I).
C     MODE      TYPE OF INPUT MATRIX (I).
C               = 0 LINEARLY PACKED ARRAY.
C               = M ROW DIMENSION IN CASE OF SQUARE STORAGE.
C     *
      IMPLICIT DOUBLE PRECISION (A-H,O-Z)
      PARAMETER (JBLOCK=10)
      COMMON
     ./NBFILE/ NBF(20)
      DIMENSION A(LM4)
C *** FILE NUMBERS.
      NB6    = NBF(6)
C *** CHECK FOR SIMPLE INPUT ERRORS.
      IF(MODE.GT.0) THEN
         IF(N.GT.MODE) THEN
            WRITE(NB6,500) MODE,N
            RETURN
         ENDIF
      ENDIF
C *** PRINT DIAGONAL ELEMENTS.
C     ALTERNATIVE OUTPUT FORMAT WITH ROW COUNTER
C     GIVEN ON COMMENT LINES (SEE 700 AND 710).
      WRITE(NB6,510)
C     JBLOCK : NUMBER OF ENTRIES PER LINE.
C     KBLOCK : NUMBER OF LINES.
      KBLOCK = 1+(N-1)/JBLOCK
      WRITE(NB6,600) (J,J=1,JBLOCK)
C     WRITE(NB6,700) (J,J=1,JBLOCK)
C     LOOP OVER LINES.
      DO 10 K=1,KBLOCK
      KK     = (K-1)*JBLOCK
      KA     = 1+KK
      KB     = MIN(N,KA+JBLOCK-1)
      IF(MODE.LE.0) THEN
         WRITE(NB6,610) (A((J*(J+1))/2),J=KA,KB)
      ELSE
         WRITE(NB6,610) (A(MODE*(J-1)+J),J=KA,KB)
      ENDIF
C     ALTERNATIVE OUTPUT FORMAT WITH ROW COUNTER.
C     IF(MODE.LE.0) THEN
C        WRITE(NB6,710) KK,(A((J*(J+1))/2),J=KA,KB)
C     ELSE
C        WRITE(NB6,710) KK,(A(MODE*(J-1)+J),J=KA,KB)
C     ENDIF
   10 CONTINUE
      RETURN
  500 FORMAT(/5X,'ERROR IN SUBROUTINE PRTDIA.',
     1       /5X,'ROW DIMENSION OF MATRIX TO BE PRINTED:',I10,
     2       /5X,'NUMBER OF DIAGONAL ELEMENTS REQUESTED:',I10,
     3       /5X,'IGNORE PRINT REQUEST AND RETURN.')
  510 FORMAT( 5X,'DIAGONAL ELEMENTS ONLY.'/)
  600 FORMAT( 1X,I9,9I12)
  610 FORMAT( 1X,10F12.5)
C 700 FORMAT( 9X,I5,9I12)
C 710 FORMAT( 1X,I4,10F12.5)
      END
