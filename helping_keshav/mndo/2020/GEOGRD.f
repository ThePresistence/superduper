      SUBROUTINE GEOGRD (CG,G,NUMAT,NVAR)
C     *
C     COMPUTE GRADIENT USED IN GEOMETRY OPTIMIZATION ROUTINES
C     FROM AVAILABLE CARTESIAN GRADIENT.
C     *
C     NOTATION. I=INPUT, O=OUTPUT, S=SCRATCH.
C     CG        CARTESIAN GRADIENT (I).
C     G         GRADIENT FOR  GEOMETRY OPTIMIZATION (O).
C     NUMAT     NUMBER OF ATOMS (I).
C     NVAR      NUMBER OF GEOMETRICAL VARIABLES (I).
C     *
C     CASE IGEOM=0, OPTIMIZATION IN INTERNAL COORDINATES:
C     TRANSFORM GRADIENT FROM CARTESIAN TO INTERNAL COORDINATES
C     USING A NUMERICAL FINITE-DIFFERENCE PROCEDURE.
C     *
C     CASE IGEOM=1, OPTIMIZATION IN CARTESIAN COORDINATES:
C     COPY AVAILABLE CARTESIAN GRADIENT TO OUTPUT ARRAY (IF KSYM=0).
C     TRANSFORM GRADIENT NUMERICALLY IF THERE IS SYMMETRY (KSYM=1).
C     *
      USE LIMIT, ONLY: LM1, LMV
      IMPLICIT DOUBLE PRECISION (A-H,O-Z)
C
      COMMON
     ./ATOMC / COORD(3,LM1)
     ./INOPT2/ IN2(300)
     ./PARM1 / A(LMV),NABC(LM1,4),NATOMS
     ./PARM3 / LOC(LMV),NVAR3
      DIMENSION CG(3,NUMAT),G(NVAR)
      DIMENSION COLD(3,NUMAT),CMINUS(3,NUMAT),DELTA(3,2)
C     PREDEFINED DISPLACEMENTS.
      DATA DELTA/1.0D-05,3.49066D-05,8.72665D-05,3*1.0D-05/
C *** INPUT OPTIONS.
      IGEOM  = IN2(4)
      KSYM   = IN2(75)
C *** EVALUATE INTERNAL OR CARTESIAN GRADIENT FOR GEOMETRY OPTIMIZATION.
      IF(IGEOM.EQ.1 .AND. KSYM.EQ.0) THEN
         DO 10 I=1,NVAR
         K      = LOC(I)
         L      = K-3*((K-1)/3)
         G(I)   = CG(L,(K+2)/3)
   10    CONTINUE
      ELSE
         COLD(1:3,1:NUMAT) = COORD(1:3,1:NUMAT)
         DO 20 I=1,NVAR
         K      = LOC(I)
         L      = K-3*((K-1)/3)
         DEL    = DELTA(L,IGEOM+1)
         AK     = A(K)
         A(K)   = AK-DEL
         CALL SYMTRY (-1)
         CALL GMETRY (-1)
         CMINUS(1:3,1:NUMAT) = COORD(1:3,1:NUMAT)
         A(K)   = AK+DEL
         CALL SYMTRY (-1)
         CALL GMETRY (-1)
         A(K)   = AK
         COORD(1:3,1:NUMAT) = COORD(1:3,1:NUMAT)-CMINUS(1:3,1:NUMAT)
         G(I)   = DDOT((3*NUMAT),CG,1,COORD,1)/(2.0D0*DEL)
   20    CONTINUE
         COORD(1:3,1:NUMAT) = COLD(1:3,1:NUMAT)
      ENDIF
      RETURN
      END
