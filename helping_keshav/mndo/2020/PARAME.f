      SUBROUTINE PARAME
C     *
C     PARAMETERS FOR MNDO(/d) CALCULATIONS WITH EXTENDED BASIS SETS.
C     *
C     CONVENTIONS.
C     - THE BASIC PARAMETERS ARE UNCHANGED (S FOR H-He, SP FOR Li-Ne).
C     - THE NEW PARAMETERS RERER TO ADDITIONAL POLARIZATION FUNCTIONS:
C       P FOR H-He, D FOR Li-Ne.
C     - VALUES FOR THE NEW PARAMETERS ARE TAKEN FROM BLOCK /MNDOD/
C       WHERE THEY HAVE BEEN STORED UPON INPUT.
C     - IMMD(I)=2: P PARAMETERS FOR H -He.
C       IMMD(I)=3: D PARAMETERS FOR Li-Ne.
C     *
      USE LIMIT, ONLY: LMZ
      IMPLICIT DOUBLE PRECISION (A-H,O-Z)
      COMMON
     ./CONSTN/ ZERO,ONE,TWO,THREE,FOUR,PT5,PT25
     ./DPARM / LORBS(LMZ)
     ./DPARM1/ UDD(LMZ),ZD(LMZ),BETAD(LMZ)
     ./DPARM2/ ZSN(LMZ),ZPN(LMZ),ZDN(LMZ)
     ./DPARM3/ F0DD(LMZ),F2DD(LMZ),F4DD(LMZ),F0SD(LMZ),G2SD(LMZ),
     .         F0PD(LMZ),F2PD(LMZ),G1PD(LMZ),G3PD(LMZ)
     ./DPARM7/ IF0SD(LMZ),IG2SD(LMZ),IGSP(LMZ),IHSP(LMZ)
     ./DPARM8/ JF0SD(LMZ),JG2SD(LMZ),JGSP(LMZ),JHSP(LMZ)
      COMMON
     ./MNDOD / USSD(LMZ),UPPD(LMZ),ZSD(LMZ),ZPD(LMZ),
     .         BETASD(LMZ),BETAPD(LMZ),ALPD(LMZ),
     .         GSSD(LMZ),GPPD(LMZ),GSPD(LMZ),GP2D(LMZ),
     .         HSPD(LMZ),UDDD(LMZ),ZDD(LMZ),BETADD(LMZ),
     .         ZSND(LMZ),ZPND(LMZ),ZDND(LMZ),POCORD(LMZ),
     .         F0SDD(LMZ),G2SDD(LMZ),IMMD(LMZ)
     ./MULTIP/ DD(6,0:LMZ),PO(9,0:LMZ)
     ./PAROPT/ USS(LMZ),UPP(LMZ),ZS(LMZ),ZP(LMZ),
     .         BETAS(LMZ),BETAP(LMZ),ALP(LMZ)
     ./REP   / GSS(LMZ),GPP(LMZ),GSP(LMZ),GP2(LMZ),HSP(LMZ),
     .         HPP(LMZ)
C    ./PARDER/ CORE(LMZ),EHEAT(LMZ),EISOL(LMZ)
C *** LOOP OVER ATOMS.
      DO 10 I=1,10
C     PARAMETERS FOR 2p POLARIZATION FUNCTIONS OF H-He.
      IF(IMMD(I).EQ.2 .AND. I.LE.2) THEN
         UPP(I)   = UPPD(I)
         ZP(I)    = ZPD(I)
         BETAP(I) = BETAPD(I)
         GSP(I)   = GSPD(I)
         GPP(I)   = GPPD(I)
         GP2(I)   = GP2D(I)
         HSP(I)   = HSPD(I)
         HPP(I)   = PT5*(GPPD(I)-GP2D(I))
         LORBS(I) = 4
         CALL DDPOHY(I)
         PO(9,I) = PO(1,I)
C     PARAMETERS FOR 3d POLARIZATION FUNCTIONS OF Li-Ne.
      ELSE IF(IMMD(I).EQ.3 .AND. I.GT.2) THEN
         UDD(I)   = UDDD(I)
         ZD(I)    = ZDD(I)
         BETAD(I) = BETADD(I)
         ZSN(I)   = ZSND(I)
         ZPN(I)   = ZPND(I)
         ZDN(I)   = ZDND(I)
         F0SD(I)  = F0SDD(I)
         G2SD(I)  = G2SDD(I)
         JF0SD(I) = IF0SD(I)
         JG2SD(I) = IG2SD(I)
         JGSP(I)  = IGSP(I)
         JHSP(I)  = IHSP(I)
         LORBS(I) = 9
         CALL INIGHD(I)
         CALL DDPOHY(I)
         PO(9,I) = PO(1,I)
      ENDIF
C     EISOL UNCHANGED BY POLARIZATION FUNCTIONS.
C     EISOL(I) = EATOM(I,0,0,0)
   10 CONTINUE
      RETURN
      END
