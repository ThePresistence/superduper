      SUBROUTINE UPDHES (HESS,SVEC,TVEC,LMH,NVAR,IUPD,ICROSS,IPRINT)
C     *
C     UPDATE OF THE HESSIAN FOR EIGENVECTOR FOLLOWING.
C     *
      USE LIMIT, ONLY: LMV, LM1, LM1M, LMYL
      IMPLICIT DOUBLE PRECISION (A-H,O-Z)
      COMMON
     ./CONSTN/ ZERO,ONE,TWO,THREE,FOUR,PT5,PT25
     ./FLPOCM/ GRAD(LMV),D(LMV),ALPHA,FIP1
     ./OPTEF / OLDG(LMV),VMODE(LMV),DD
     ./YARLAG/ YLVAL(LMYL),YLGRD(LMYL),YLD(LMYL),NYL
     ./YARUPD/ YARV(3,LM1+LM1M,LMYL),YARS(LMV,2),YARG(LMV,LMYL,2),IXTRAP
      DIMENSION HESS(LMH,LMH),SVEC(NVAR),TVEC(NVAR)
C     *
C     THE UPDATE DEPENDS ON CURRENT GRADIENTS (GRAD), OLD GRADIENTS
C     (OLDG), AND THE CORRECTION VECTOR (D) USED ON THE LAST CYCLE.
C     *
C     THREE UPDATING PROCEDURES ARE AVAILABLE.
C     IUPD=1: THE POWELL UPDATE:
C             THIS PRESERVES THE SYMMETRIC CHARACTER OF THE HESSIAN
C             WHILST ALLOWING ITS EIGENVALUE STRUCTURE TO CHANGE.
C             IT IS THE DEFAULT UPDATE FOR A TRANSITION STATE SEARCH.
C     IUPD=2: THE BFGS UPDATE:
C             THIS UPDATE HAS THE IMPORTANT CHARACTERISTIC OF RETAINING
C             POSITIVE DEFINITENESS (NOTE: THIS IS NOT RIGOROUSLY
C             GUARANTEED, BUT CAN BE CHECKED FOR BY THE PROGRAM).
C             IT IS THE DEFAULT UPDATE FOR A MINIMUM SEARCH.
C     IUPD=3: MURTAGH/SARGENT UPDATE:
C             FOR USE PRIMARILY TO EMULATE YARKONY'S CI SEARCH METHOD
C             THIS RANK ONE UPDATE IS PROBLEMATIC BECAUSE
C             POSITIVE DEFINITINESS IS NOT MAINTAINED IN GENERAL
C             BFGS IS THEREFORE PREFERRED FOR CI SEARCHES IN PRACTICE
C
C     NOTE IN THE CASE OF THE YARKONY CI SEARCH THAT BOTH THE OLD AND
C     THE NEW COMPONENTS ARE MULTIPLIED BY THE SAME (NEW) LAGRANGE
C     MULTIPLIERS. SEE EQ. (7a') OF CONINT REF. 6
C     *
C *** INITIALIZATION.
      IF(IUPD.EQ.0) RETURN
      DO 15 I=1,NVAR
      TVEC(I)=ZERO
      DO 10 J=1,NVAR
      TVEC(I)=TVEC(I)+HESS(I,J)*D(J)
   10 CONTINUE
   15 CONTINUE
      IF(ICROSS.EQ.5) THEN
C        INCLUDE GRAD DIFF VECTOR AND GRADIENT OF THE INTERSTATE
C        COUPLING ONLY IF THEY HAVE BEEN ORTHOGONALISED FOR THE
C        LAST TWO FUNCTION EVALUATIONS
         IF(IXTRAP.EQ.2) THEN
            INCGH = 1
         ELSE
            INCGH = 3
         ENDIF
      ENDIF
C *** POWELL UPDATE
      IF(IUPD.EQ.1) THEN
         DO 20 I=1,NVAR
            IF(ICROSS.EQ.5) THEN
               TVEC(I)=YARS(I,1)-YARS(I,2)-TVEC(I)
               IF(NYL.GT.2 .OR. INCGH.EQ.1) THEN
                  DO 25 J=INCGH,NYL
                    TVEC(I)=(YLVAL(J)*(YARG(I,J,1)-YARG(I,J,2)))+TVEC(I)
   25             CONTINUE
               ENDIF
            ELSE
               TVEC(I)=GRAD(I)-OLDG(I)-TVEC(I)
            ENDIF
   20    CONTINUE
         DDS=DD*DD
         DDTD=DDOT(NVAR,TVEC,1,D,1)
         DDTD=DDTD/DDS
         DO 40 I=2,NVAR
         DO 30 J=1,I-1
         TEMP=TVEC(I)*D(J) + D(I)*TVEC(J) - D(I)*DDTD*D(J)
         HESS(I,J)=HESS(I,J)+TEMP/DDS
         HESS(J,I)=HESS(I,J)
   30    CONTINUE
   40    CONTINUE
         DO 45 I=1,NVAR
         TEMP=D(I)*(TWO*TVEC(I) - D(I)*DDTD)
         HESS(I,I)=HESS(I,I)+TEMP/DDS
   45    CONTINUE
      ENDIF
C *** BFGS UPDATE.
      IF(IUPD.EQ.2) THEN
         DO 50 I=1,NVAR
            IF(ICROSS.EQ.5) THEN
               SVEC(I)=YARS(I,1)-YARS(I,2)
               IF(NYL.GT.2 .OR. INCGH.EQ.1) THEN
                  DO 55 J=INCGH,NYL
                    SVEC(I)=(YLVAL(J)*(YARG(I,J,1)-YARG(I,J,2)))+SVEC(I)
   55             CONTINUE
               ENDIF
            ELSE
               SVEC(I)=GRAD(I)-OLDG(I)
            ENDIF
   50    CONTINUE
         DDS=DDOT(NVAR,SVEC,1,D,1)
C        IF DDS IS NEGATIVE, RETENTION OF POSITIVE DEFINITENESS IS NOT
C        GUARANTEED. PRINT A WARNING AND SKIP UPDATE THIS CYCLE.
         DDTD=DDOT(NVAR,D,1,TVEC,1)
         DO 70 I=2,NVAR
         DO 60 J=1,I-1
         TEMP= (SVEC(I)*SVEC(J))/DDS - (TVEC(I)*TVEC(J))/DDTD
         HESS(I,J)=HESS(I,J)+TEMP
         HESS(J,I)=HESS(I,J)
   60    CONTINUE
   70    CONTINUE
         DO 75 I=1,NVAR
         TEMP= (SVEC(I)*SVEC(I))/DDS - (TVEC(I)*TVEC(I))/DDTD
         HESS(I,I)=HESS(I,I)+TEMP
   75    CONTINUE
      ENDIF
C *** MURTAGH/SARGENT UPDATE
      IF(IUPD.EQ.3) THEN
         DO 200 I=1,NVAR
            IF(ICROSS.EQ.5) THEN
               TVEC(I)=YARS(I,1)-YARS(I,2)-TVEC(I)
               IF(NYL.GT.2 .OR. INCGH.EQ.1) THEN
                  DO 205 J=INCGH,NYL
                    TVEC(I)=(YLVAL(J)*(YARG(I,J,1)-YARG(I,J,2)))+TVEC(I)
  205             CONTINUE
               ENDIF
            ELSE
               TVEC(I)=GRAD(I)-OLDG(I)-TVEC(I)
            ENDIF
  200    CONTINUE
         DDTD = DDOT(NVAR,D,1,TVEC,1)
         DO 220 I=2,NVAR
         DO 210 J=1,I-1
         TEMP= (TVEC(I)*TVEC(J))/DDTD
         HESS(I,J)=HESS(I,J)+TEMP
         HESS(J,I)=HESS(I,J)
  210    CONTINUE
  220    CONTINUE
         DO 230 I=1,NVAR
         TEMP= (TVEC(I)*TVEC(I))/DDTD
         HESS(I,I)=HESS(I,I)+TEMP
  230    CONTINUE
      ENDIF
      RETURN
C 100 FORMAT( 1X,'WARNING: HEREDITARY POSITIVE DEFINITENESS ENDANGERED')
C 110 FORMAT( 1X,'UPDATE SKIPPED THIS CYCLE')
      END
