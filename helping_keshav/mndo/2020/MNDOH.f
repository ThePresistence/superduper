      SUBROUTINE MNDOH (I,J,RHX,ENUC,ENUCLR,SCALE)
C     *
C     SPECIAL CORE-CORE REPULSIONS FOR HYDROGEN BONDS, ATOMS I...J.
C     K.Y.BURSTEIN AND A.N.ISAEV, THEORET.CHIM.ACTA 64, 397 (1984).
C     A.GOLDBLUM, J.COMPUT.CHEM. 8, 835 (1987).
C     *
C     A HYDROGEN BOND X...H-Y IS IDENTIFIED BY THE FOLLOWING CRITERIA.
C     - ATOMS X AND Y MUST BE NITROGEN, OXYGEN, OR FLUORINE.
C     - THE DISTANCE X...H MUST BE IN THE RANGE OF RHXMIN...RHXMAX.
C     - THE DISTANCE H-Y MUST BE THE SHORTEST DISTANCE INVOLVING H.
C     - THE ANGLE X...H-Y MUST BE LARGER THAN ANGMIN.
C     *
C     APPLICATION OF THESE CRITERIA WILL FAIL IN THE CASE OF SYMMETRIC
C     HYDROGEN BONDS X...H...Y SINCE X...H WILL NOT BE IDENTIFIED AS A
C     HYDROGEN BOND WHEN THE DISTANCE X...H IS EQUAL TO OR SLIGHTLY
C     LESS THAN THE DISTANCE H...Y. TO COVER SUCH CASES, IT WOULD BE
C     NECESSARY TO INTRODUCE AN ADDITIONAL CRITERION (RHXTOL) DEFINING
C     THE ALLOWED TOLERANCES, E.G.: R(H...Y) < R(X...H) + RHXTOL.
C     SINCE THIS APPROACH IS NOT SPECIFIED IN THE ORIGINAL LITERATURE,
C     WE ONLY GIVE THE CORRESPONDING CODE AS COMMENTS (LABEL CTOL).
C     NOTE THAT THIS CODE ON COMMENT LINES HAS NOT BEEN TESTED PROPERLY.
C     *
      USE LIMIT, ONLY: LM1
      IMPLICIT DOUBLE PRECISION (A-H,O-Z)
CTOL  PARAMETER (RHXTOL=0.2D0)
      COMMON
     ./ATOMC / COORD(3,LM1)
     ./ATOMS / NUMAT,NAT(LM1),NFIRST(LM1),NLAST(LM1)
     ./CONSTF/ A0,AFACT,EV,EVCAL,PI,W1,W2,BIGEXP
     ./CONSTN/ ZERO,ONE,TWO,THREE,FOUR,PT5,PT25
     ./HBONDC/ IHBOND,NHBOND,RHXMIN,RHXMAX,ANGMIN
C *** INITIALIZATION.
      IF(RHX.LT.RHXMIN .OR. RHX.GT.RHXMAX) RETURN
      IF(NAT(I).EQ.1) THEN
         IH  = I
         IX  = J
      ELSE
         IH  = J
         IX  = I
      ENDIF
      IF(NAT(IH).NE.1) RETURN
      IF(NAT(IX).LT.7 .OR. NAT(IX).GT.9) RETURN
C *** FIND SMALLEST DISTANCE H-Y
      RMIN2  = RHX*RHX
CTOL  RMIN2  = RHX*RHX*TWO
      IY     = 0
      DO 10 K=1,NUMAT
      IF(K.EQ.IH .OR. K.EQ.IX) GO TO 10
      R2     = (COORD(1,K)-COORD(1,IH))**2+(COORD(2,K)-COORD(2,IH))**2
     1        +(COORD(3,K)-COORD(3,IH))**2
      IF(R2.GE.RMIN2) GO TO 10
      RMIN2  = R2
      IY     = K
   10 CONTINUE
      IF(IY.EQ.0) RETURN
      IF(NAT(IY).LT.7 .OR. NAT(IY).GT.9) RETURN
      RHY    = SQRT(RMIN2)
      RXY2   = (COORD(1,IY)-COORD(1,IX))**2+(COORD(2,IY)-COORD(2,IX))**2
     1        +(COORD(3,IY)-COORD(3,IX))**2
      RHX2   = RHX*RHX
C *** CHECK ANGLE X...H-Y
      ANG    = (RHX2+RMIN2-RXY2)/(TWO*RHX*RHY)
      IF(ANG.LT.-ONE) ANG=-ONE
      IF(ANG.GT. ONE) ANG= ONE
      ANG    = ACOS(ANG)
      ANG    = ANG*AFACT
      IF(ANG.LE.ANGMIN) RETURN
CTOL  CHECK FOR TOLERANCE IN DISTANCES X...H AND H...Y
CTOL  IF(RHY.GE.(RHX+RHXTOL)) RETURN
C *** ALL TESTS FOR A HYDROGEN BOND ARE SATISFIED.
      SCALHX = ONE+EXP(-TWO*RHX2)
      ENUCLR = ENUCLR+ENUC*(SCALHX-SCALE)
      RETURN
      END
