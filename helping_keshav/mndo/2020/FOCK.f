      SUBROUTINE FOCK (F,PA,PB,Q,W,LM4,LM6)
C     *
C     TWO-ELECTRON CONTRIBUTIONS TO MNDO-TYPE FOCK MATRIX.
C     VECTORIZED CODE.
C     *
C     NOTATION. I=INPUT, O=OUTPUT, S=SCRATCH.
C     F(LM4)    FOCK MATRIX (I,O).
C     PA(LM4)   RHF OR UHF-ALPHA DENSITY MATRIX (I).
C     PB(LM4)   UHF-BETA DENSITY MATRIX (I).
C     Q(LM6)    SCRATCH ARRAY FOR ONE-CENTER PAIR TERMS (S).
C     W(LM6,*)  TWO-ELECTRON INTEGRALS (I).
C     *
      USE LIMIT, ONLY: LM1, LMI, LME, LMX
      IMPLICIT DOUBLE PRECISION (A-H,O-Z)
      LOGICAL UHF
      COMMON
     ./ATOMS / NUMAT,NAT(LM1),NFIRST(LM1),NLAST(LM1)
     ./CONSTN/ ZERO,ONE,TWO,THREE,FOUR,PT5,PT25
     ./DELEMT/ NELMD,NFOCK
     ./FINDX1/ IP(LMI),IP1(LMI),IP2(LMI)
     ./FINDX2/ JP1(LME),JP2(LME),JP3(LME),JX(LM1),IJMAX
     ./INDEX / INDX(LMX)
     ./INDEXW/ NW(LM1)
     ./UHF   / UHF
      DIMENSION F(LM4),PA(LM4),PB(LM4),Q(LM6),W(LM6,LM6)
C
C *** COULOMB CONTRIBUTIONS.
C     ONE-CENTER EXCHANGE CONTRIBUTIONS ARE IMPLICITLY INCLUDED FOR RHF.
CMIC$ DO GLOBAL FOR 64
C$OMP PARALLEL DO SCHEDULE(STATIC)
      DO 10 KL=1,LM6
      Q(KL)  = (PA(IP(KL))+PB(IP(KL)))
      IF(IP1(KL).NE.IP2(KL)) Q(KL)=Q(KL)*TWO
   10 CONTINUE
C$OMP END PARALLEL DO
C$OMP PARALLEL DO SCHEDULE(STATIC)
C$OMP.PRIVATE(SUM)
CMIC$ DO GLOBAL
      DO 30 IJ=1,LM6
      SUM    = ZERO
CDIR$ NOBL
      DO 20 KL=1,LM6
      SUM    = SUM+Q(KL)*W(KL,IJ)
   20 CONTINUE
      F(IP(IJ)) = F(IP(IJ))+SUM
   30 CONTINUE
C$OMP END PARALLEL DO
C
C *** TWO-CENTER EXCHANGE CONTRIBUTIONS.
C     OFFDIAGONAL TWO-CENTER TERMS (IJ,KL).
C     NFOCK  =  5 FOR AN SP  BASIS.
C     NFOCK  = 10 FOR AN SPD BASIS.
CMIC$ DO GLOBAL
C$OMP PARALLEL DO SCHEDULE(DYNAMIC)
C$OMP.PRIVATE(I,J,IK,JL,K,KLMIN,KLMAX,KLW,L)
      DO 70 N=1,NUMAT-1
      KLMIN  = JX(N)
      KLMAX  = JX(N+1)-1
      DO 60 KL=KLMIN,KLMAX
      K      = JP1(KL)
      L      = JP2(KL)
      KLW    = JP3(KL)
      DO 50 NPASS=1,NFOCK
CDIR$ IVDEP
C$DIR NO_RECURRENCE
*VDIR NODEP
*VOCL LOOP,NOVREC
      DO 40 IJ=KLMAX+NPASS,IJMAX,NFOCK
      I      = JP1(IJ)
      J      = JP2(IJ)
      IK     = INDX(I)+K
      JL     = INDX(J)+L
      F(IK)  = F(IK)-PA(JL)*W(JP3(IJ),KLW)
   40 CONTINUE
   50 CONTINUE
   60 CONTINUE
   70 CONTINUE
C$OMP END PARALLEL DO
      IF(.NOT.UHF) RETURN
C *** ONE-CENTER EXCHANGE CONTRIBUTIONS FOR UHF.
C     OFFDIAGONAL ONE-CENTER TERMS (II,KK) FOR SP-BASIS.
CMIC$ PROCESS
CDIR$ IVDEP
C$DIR NO_RECURRENCE
*VDIR NODEP
*VOCL LOOP,NOVREC
      DO 80 I=1,NUMAT
      IA     = NFIRST(I)
      IORBS  = NLAST(I)-IA+1
      IF(IORBS.EQ.4) THEN
         IJ     = NW(I)-1
         IXS    = INDX(IA+1)+IA
         IYS    = INDX(IA+2)+IA
         IZS    = INDX(IA+3)+IA
         IYX    = IYS+1
         IZX    = IZS+1
         IZY    = IZS+2
         F(IXS) = F(IXS)-PA(IXS)*W(IJ+3,IJ+1)
         F(IYS) = F(IYS)-PA(IYS)*W(IJ+6,IJ+1)
         F(IZS) = F(IZS)-PA(IZS)*W(IJ+10,IJ+1)
         F(IYX) = F(IYX)-PA(IYX)*W(IJ+6,IJ+3)
         F(IZX) = F(IZX)-PA(IZX)*W(IJ+10,IJ+3)
         F(IZY) = F(IZY)-PA(IZY)*W(IJ+10,IJ+6)
      ENDIF
   80 CONTINUE
CMIC$ END PROCESS
C     DIAGONAL ONE-CENTER TERMS (IJ,IJ), GENERAL CODE.
CMIC$ DO GLOBAL FOR 64
CDIR$ IVDEP
C$DIR NO_RECURRENCE
*VDIR NODEP
*VOCL LOOP,NOVREC
C$OMP PARALLEL DO SCHEDULE(STATIC)
      DO 90 IJ=1,LM6
      F(IP(IJ)) = F(IP(IJ)) - PA(IP(IJ))*W(IJ,IJ)
   90 CONTINUE
C$OMP END PARALLEL DO
CMIC$ PROCESS
      DO 110 NPASS=1,NFOCK
CDIR$ IVDEP
C$DIR NO_RECURRENCE
*VDIR NODEP
*VOCL LOOP,NOVREC
      DO 100 IJ=NPASS,IJMAX,NFOCK
      I      = JP1(IJ)
      J      = JP2(IJ)
      IF(I.NE.J) THEN
         II  = INDX(I)+I
         JJ  = INDX(J)+J
         IJW = JP3(IJ)
         F(II) = F(II)-PA(JJ)*W(IJW,IJW)
      ENDIF
  100 CONTINUE
  110 CONTINUE
C     ALL ONE-CENTER TERMS ARE NOW INCLUDED FOR AN SP-BASIS.
C     THE FOLLOWING CODE COVERS ANY OFFDIAGONAL TERMS FOR AN SPD-BASIS.
      DO 140 N=1,NUMAT
      IA     = NFIRST(N)
      IORBS  = NLAST(N)-IA+1
      IF(IORBS.LE.4) GO TO 140
      KLMIN  = JX(N)
      KLMAX  = KLMIN+IORBS*IORBS-1
      KSTART = JP1(KLMIN)
      DO 130 KL=KLMIN,KLMAX
      K      = JP1(KL)
      L      = JP2(KL)
C     ONLY ELEMENTS F(IK) WITH I.GE.K ARE NEEDED. THEREFORE,
C     THE LOOP OVER IJ CAN START AT IJMIN.GE.KLMIN.
      IJMIN  = KLMIN+IORBS*(K-KSTART)
      DO 120 IJ=IJMIN,KLMAX
C     DIAGONAL TERMS HAVE BEEN INCLUDED ABOVE ALSO FOR AN SPD-BASIS.
      IF(JP3(IJ).EQ.JP3(KL)) GO TO 120
      WIJKL  = W(JP3(IJ),JP3(KL))
      IF(WIJKL.NE.ZERO) THEN
         I   = JP1(IJ)
         J   = JP2(IJ)
         IK  = INDX(I)+K
         JL  = INDX(MAX(J,L))+MIN(J,L)
         F(IK) = F(IK)-PA(JL)*WIJKL
      ENDIF
  120 CONTINUE
  130 CONTINUE
  140 CONTINUE
CMIC$ END PROCESS
      RETURN
      END
