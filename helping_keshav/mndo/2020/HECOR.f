      SUBROUTINE HECOR(C,EA,Q,W,LM2,LM3,LM6,LM9,NPRINT)
C     *
C     HALF-ELECTRON CORRECTION FOR UP TO TEN QUASI-DEGENERATE LEVELS
C     WITH PARTIAL OCCUPATION (DEFINED IN SUBROUTINE OCCDEF).
C     *
      IMPLICIT DOUBLE PRECISION (A-H,O-Z)
      COMMON
     ./CONSTN/ ZERO,ONE,TWO,THREE,FOUR,PT5,PT25
     ./ENERGT/ EE,ENUCLR,EAT,ATHEAT
     ./INOPT2/ IN2(300)
     ./NBFILE/ NBF(20)
     ./ORBITS/ NUMB,NORBS,NMOS,NALPHA,NBETA
      DIMENSION C(LM2,LM3),EA(LM3),Q(LM6),W(LM9)
      DIMENSION MSH(10),NEL(10),NOSH(5,10),COR(10),CIJ(10,10)
C *** FILE NUMBERS.
      NB6    = NBF(6)
C *** INITIALIZATION.
      IMODE  = IN2(211)
      EEOLD  = EE
C *** DEFINE HALF-ELECTRON OPEN SHELLS.
C     NSH         NUMBER OF OPEN SHELLS.
C     MSH(J)      TOTAL NUMBER OF ORBITALS IN SHELL J.
C     NEL(J)      TOTAL NUMBER OF ELECTRONS IN SHELL J.
C     NOSH(K,J)   INDICES FOR ORBITALS IN SHELL J (K=1,MSH(J)).
      CALL FORHE (EA,NORBS,NSH,MSH,NEL,NOSH)
      IF(NSH.EQ.0) RETURN
      IF(NPRINT.GT.5) THEN
C        WRITE(NB6,500) NSH
         DO 10 I=1,NSH
         M   = MSH(I)
         WRITE(NB6,510) I,M,NEL(I),(NOSH(J,I),J=1,M)
   10    CONTINUE
      ENDIF
C *** CHECK STORAGE OF TWO-ELECTRON INTEGRALS.
      IF(IMODE.GT.0) THEN
         WRITE(NB6,520) IMODE
         RETURN
      ENDIF
C *** PROVIDE APPROPRIATE ONE-CENTER TWO-ELECTRON INTEGRALS.
      CALL WSTORE (W,LM6,-1)
C *** EVALUATE HALF-ELECTRON CORRECTIONS.
C *** FIRST SECTION. INTRASHELL CONTRIBUTIONS FROM EACH OPEN SHELL.
C     THIS IS THE TOTAL CORRECTION IF THERE IS ONLY ONE OPEN SHELL.
      ADD    = ZERO
      DO 20 I=1,NSH
      M      = MSH(I)
      N      = NEL(I)
C     ONE SHELL, ONE ORBITAL, ONE ELECTRON.
      IF(M.EQ.1) THEN
         N1  = NOSH(1,I)
         IF(N.EQ.1) THEN
           X11 = SPCW(C(1,N1),C(1,N1),C(1,N1),C(1,N1),Q,W,LM2,LM6)
           COR(I) = -PT25*X11
           EA(N1) = EA(N1)+TWO*COR(I)
C          WRITE(NB6,'(5X,''I='',I3,3X,''INTEGRAL X11  '', 9F7.3)')I,X11
         ELSE
           WRITE(NB6,530) M,N
         ENDIF
C     ONE SHELL, TWO ORBITALS, ONE TO THREE ELECTRONS.
      ELSE IF(M.EQ.2) THEN
         N1  = NOSH(1,I)
         N2  = NOSH(2,I)
         X11 = SPCW(C(1,N1),C(1,N1),C(1,N1),C(1,N1),Q,W,LM2,LM6)
         X22 = SPCW(C(1,N2),C(1,N2),C(1,N2),C(1,N2),Q,W,LM2,LM6)
         X12 = SPCW(C(1,N1),C(1,N1),C(1,N2),C(1,N2),Q,W,LM2,LM6)
         Y12 = SPCW(C(1,N1),C(1,N2),C(1,N1),C(1,N2),Q,W,LM2,LM6)
C        WRITE(NB6,'(5X,''I='',I3,3X,''INTEGRAL X11,X22,X12,Y12  '',
C    1             9F7.3)')I, X11,X22,X12,Y12
         EHE = 0.0625D0*N*N*(X11+X22+FOUR*X12 - TWO*Y12)
         IF(N.EQ.1) THEN
            COR(I)= - EHE
         ELSE IF(N.EQ.2) THEN
            COR(I)= + X12-Y12-EHE
         ELSE IF(N.EQ.3) THEN
            COR(I)=  PT5*(X11+X22)+TWO*X12-Y12 -EHE
         ELSE IF(N.GT.3.OR.N.LT.1) THEN
            WRITE(NB6,530) M,N
         ENDIF
         EA(N1) = EA(N1)+COR(I)*TWO/N
         EA(N2) = EA(N1)
C     ONE SHELL, THREE ORBITALS, ONE TO FIVE ELECTRONS.
      ELSE IF(M.EQ.3) THEN
         N1  = NOSH(1,I)
         N2  = NOSH(2,I)
         N3  = NOSH(3,I)
         X11 = SPCW(C(1,N1),C(1,N1),C(1,N1),C(1,N1),Q,W,LM2,LM6)
         X22 = SPCW(C(1,N2),C(1,N2),C(1,N2),C(1,N2),Q,W,LM2,LM6)
         X33 = SPCW(C(1,N3),C(1,N3),C(1,N3),C(1,N3),Q,W,LM2,LM6)
         X12 = SPCW(C(1,N1),C(1,N1),C(1,N2),C(1,N2),Q,W,LM2,LM6)
         X13 = SPCW(C(1,N1),C(1,N1),C(1,N3),C(1,N3),Q,W,LM2,LM6)
         X23 = SPCW(C(1,N2),C(1,N2),C(1,N3),C(1,N3),Q,W,LM2,LM6)
         Y12 = SPCW(C(1,N1),C(1,N2),C(1,N1),C(1,N2),Q,W,LM2,LM6)
         Y13 = SPCW(C(1,N1),C(1,N3),C(1,N1),C(1,N3),Q,W,LM2,LM6)
         Y23 = SPCW(C(1,N2),C(1,N3),C(1,N2),C(1,N3),Q,W,LM2,LM6)
C        WRITE(NB6,'(5X,''I='',I3,3X,''INTEGRAL XII,XIJ,YIJ, '',
C    1         9F7.3)')I,X11,X22,X33,X12,X13,X23,Y12,Y13,Y23

         EHE   = (ONE/36.D0)*N*N *(X11+X22+X33 + FOUR*(X12+X13+X23)
     1                                         - TWO *(Y12+Y13+Y23))
         IF(N.EQ.1) THEN
            COR(I) = - EHE
         ELSE IF(N.EQ.2) THEN
            COR(I) = + (X12+X13+X23- Y12-Y13-Y23)/THREE - EHE
         ELSE IF(N.EQ.3) THEN
            COR(I) = + (X12+X13+X23- Y12-Y13-Y23) - EHE
         ELSE IF(N.EQ.4) THEN
            COR(I) = + ((X11+X22+X33)+5.0D0*(X12+X13+X23))/THREE
     1               -  (Y12+Y13+Y23) - EHE
         ELSE IF(N.EQ.5) THEN
            COR(I) = + (TWO *(X11+X22+X33)+8.0D0*(X12+X13+X23)
     1               -  FOUR*(Y12+Y13+Y23))/THREE - EHE
         ELSE
            WRITE(NB6,530) M,N
         ENDIF
         EA(N1) = EA(N1)+COR(I)*TWO/N
         EA(N2) = EA(N1)
         EA(N3) = EA(N1)
      ENDIF
      ADD    = ADD + COR(I)
   20 CONTINUE
C *** SECOND SECTION. GENERAL CASE - MORE THAN ONE OPEN SHELL.
C     THE INTRASHELL CONTRIBUTIONS HAVE BEEN EVALUATED ABOVE (ADD).
C     THE INTERSHELL CONTRIBUTIONS BETWEEN DIFFERENT OPEN SHELLS
C     ARE INCLUDED BELOW (SUMCIJ).
      SUMCIJ = ZERO
      IF(NSH.GE.2) THEN
         DO 90 I=2,NSH
         MI  = MSH(I)
         NI  = NEL(I)
         NFI = MIN(NI,2*MI-NI)
         DO 80 J=1,I-1
         MJ  = MSH(J)
         NJ  = NEL(J)
         NFJ = MIN(NJ,2*MJ-NJ)
         AIJ = ZERO
         DO 50 I1=1,MI
         N1  = NOSH(I1,I)
         DO 40 J1=1,MJ
         N2  = NOSH(J1,J)
         TTT = SPCW(C(1,N1),C(1,N2),C(1,N1),C(1,N2),Q,W,LM2,LM6)
         AIJ = AIJ+TTT
   40    CONTINUE
   50    CONTINUE
C        AIJ     AVERAGE EXCHANGE INTEGRAL BETWEEN SHELLS I AND J.
C        NFI     NALPHA-NBETA FOR ELECTRONS IN SHELL I, HIGH-SPIN CASE.
C        NFJ     NALPHA-NBETA FOR ELECTRONS IN SHELL J, HIGH-SPIN CASE.
C        CIJ     CONTRIBUTION TO HALF-ELECTRON CORRECTION
C                FROM INTERACTION BETWEEN SHELLS I AND J.
C        CORB1   CONTRIBUTION TO ORBITAL ENERGY SHIFT IN SHELL I
C                FROM INTERACTION BETWEEN SHELLS I AND J.
C        CORB2   CONTRIBUTION TO ORBITAL ENERGY SHIFT IN SHELL J
C                FROM INTERACTION BETWEEN SHELLS I AND J.
         AIJ = AIJ/(MI*MJ)
         CIJ(I,J) = -NFI*NFJ*AIJ*PT5
         SUMCIJ = CIJ(I,J)+SUMCIJ
         CORB1  = CIJ(I,J)/NI
         CORB2  = CIJ(I,J)/NJ
         DO 60 I1=1,MI
         N1  = NOSH(I1,I)
         EA(N1) = EA(N1)+CORB1
   60    CONTINUE
         DO 70 J1=1,MJ
         N2  = NOSH(J1,J)
         EA(N2) = EA(N2)+CORB2
   70    CONTINUE
   80    CONTINUE
   90    CONTINUE
      ENDIF
C *** TOTAL HALF-ELECTRON CORRECTION TO ELECTRONIC ENERGY.
      EE     = EE+ADD+SUMCIJ
C *** DEBUG PRINT.
      IF(NPRINT.GT.5) THEN
         WRITE(NB6,500) NSH
         WRITE(NB6,540)
         DO 100 I=1,NSH
         WRITE(NB6,550) I,MSH(I),NEL(I),COR(I)
  100    CONTINUE
         IF(NSH.GE.2) THEN
            WRITE(NB6,560)
            DO 120 I=2,NSH
            DO 110 J=1,I-1
            WRITE(NB6,570) I,J,CIJ(I,J)
  110       CONTINUE
  120       CONTINUE
         ENDIF
         WRITE(NB6,580) EEOLD
         WRITE(NB6,590) EE
         WRITE(NB6,600) ADD+SUMCIJ
         WRITE(NB6,610) ADD
         IF(NSH.GE.2) WRITE(NB6,620) SUMCIJ
      ENDIF
      RETURN
  500 FORMAT(///5X,'NUMBER OF OPEN SHELLS',3X,I2)
  510 FORMAT(/  5X,'NSH        M      NEL      MO NUMBERS',
     1       /  5X,13(I3,6X))
  520 FORMAT(// 5X,'NO HALF-ELECTRON CORRECTION CAN BE COMPUTED',
     1       /  5X,'IN SUBROUTINE HECOR WITH OPTION IMODE =',I5)
  530 FORMAT(// 5X,'NO HALF-ELECTRON CORRECTION CAN BE COMPUTED.',
     1       /  5X,'NUMBER OF ORBITALS IN THE SHELL ',I5,
     2       /  5X,'NUMBER OF ELECTRONS IN THE SHELL',I5,
     3       /  5X,'THESE DATA ARE INCOMPATIBLE.')
  540 FORMAT(// 5X,'HALF-ELECTRON CORRECTIONS FROM EACH SHELL',
     1       // 5X,'NSH        M      NEL      HE-CORRECTION')
  550 FORMAT(   5X,3(I3,6X),2X,F10.5)
  560 FORMAT(// 5X,'CONTRIBUTIONS FROM INTERACTIONS BETWEEN SHELLS',
     1       // 5X,'  I        J               HE-CORRECTION')
  570 FORMAT(   5X,2(I3,6X),11X,F10.5)
  580 FORMAT(   5X,'UNCORRECTED ELECTRON ENERGY     ',F12.3,' EV')
  590 FORMAT(   5X,'CORRECTED ELECTRON ENERGY       ',F12.3,' EV')
  600 FORMAT(   5X,'TOTAL HALF-ELECTRON CORRECTION  ',F12.3,' EV')
  610 FORMAT(   5X,'INTRASHELL CONTRIBUTION         ',F12.3,' EV')
  620 FORMAT(   5X,'INTERSHELL CONTRIBUTION         ',F12.3,' EV')
      END
