      SUBROUTINE DYNINT
C     *
C     DEFINITION OF PAIR INDICES.
C     *
C     NOTATION.
C     IP(LMI)    INDICES OF UNIQUE ONE-CENTER AO PAIRS = I*(I-1)/2+J
C     IP1(LMI)   INDEX OF FIRST  AO IN THE ONE-CENTER PAIR = I (COUL)
C     IP2(LMI)   INDEX OF SECOND AO IN THE ONE-CENTER PAIR = J (COUL)
C     JP1(LME)   INDEX OF FIRST  AO IN THE ONE-CENTER PAIR = I (EXCH)
C     JP2(LME)   INDEX OF SECOND AO IN THE ONE-CENTER PAIR = J (EXCH)
C     JP3(LME)   COULOMB PAIR INDEX FOR GIVEN EXCHANGE PAIR INDEX
C     JX(LM1)    FIRST EXCHANGE PAIR INDEX FOR GIVEN ATOM
C     JXLAST     LAST  EXCHANGE PAIR INDEX FOR LAST  ATOM
C     NW(LM1)    FIRST COULOMB  PAIR INDEX FOR GIVEN ATOM
C     *
      USE LIMIT, ONLY: LM1, LMI, LME, LMX
      IMPLICIT DOUBLE PRECISION (A-H,O-Z)
      COMMON
     ./ATOMS / NUMAT,NAT(LM1),NFIRST(LM1),NLAST(LM1)
     ./FINDX1/ IP(LMI),IP1(LMI),IP2(LMI)
     ./FINDX2/ JP1(LME),JP2(LME),JP3(LME),JX(LM1),JXLAST
     ./INDEX / INDX(LMX)
     ./INDEXW/ NW(LM1)
C *** DEFINE PAIR INDICES AND PAIR FACTORS.
C     SECTION FOR COULOMB PART.
      K      = 0
      DO 30 II=1,NUMAT
      NW(II) = K+1
      IA     = NFIRST(II)
      IB     = NLAST(II)
      DO 20 I=IA,IB
      ID     = INDX(I)
      DO 10 J=IA,I
      K      = K+1
      IP(K)  = ID+J
      IP1(K) = I
      IP2(K) = J
   10 CONTINUE
   20 CONTINUE
   30 CONTINUE
C     SECTION FOR EXCHANGE PART.
      K      = 0
      DO 60 II=1,NUMAT
      JX(II) = K+1
      NWII   = NW(II)-1
      IA     = NFIRST(II)
      IB     = NLAST(II)
      DO 50 I=IA,IB
      I4     = I-IA+1
      DO 40 J=IA,IB
      K      = K+1
      J4     = J-IA+1
      JP1(K) = I
      JP2(K) = J
      IF(I.GE.J) THEN
         JP3(K) = NWII+INDX(I4)+J4
      ELSE
         JP3(K) = NWII+INDX(J4)+I4
      ENDIF
   40 CONTINUE
   50 CONTINUE
   60 CONTINUE
      JXLAST = K
      RETURN
      END
