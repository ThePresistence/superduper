      SUBROUTINE KEYWRT (KEYWRD,IMOPAC,KEY96,LEN96,LM96)
C     *
C     GENERATE MNDO97 KEYWORDS FROM STANDARD NUMERICAL INPUT.
C     THE CODE COVERS ONLY KEYWORDS WITH INTEGER VALUES.
C     *
C     NOTATION. I=INPUT, O=OUTPUT.
C     KEYWRD    KEYWORD STRING GENERATED (O).
C     IMOPAC    TYPE OF INPUT (I).
C     KEY96     AVAILABLE MNDO97 KEYWORDS WITH INTEGER VALUES (I).
C     LEN96     LENGTH OF THESE KEYWORDS IN BYTES (I).
C     LM96      NUMBER OF SUCH KEYWORDS.
C     *
      IMPLICIT DOUBLE PRECISION (A-H,O-Z)
      CHARACTER*(*) KEYWRD
      CHARACTER*10  IVALUE
      CHARACTER*6   KEY96
      COMMON
     ./INOPT1/ IN1(300)
     ./NBFILE/ NBF(20)
      DIMENSION KEY96(LM96),LEN96(LM96)
C *** FILE NUMBERS.
      NB6    = NBF(6)
C *** INITIALIZATION.
      KEYWRD = ' '
      KEYLEN = LEN(KEYWRD)
      MAXCOL = 80
      MXLINE = (KEYLEN-1)/MAXCOL+1
      ICOL   = 0
      ILINE  = 0
C *** SPECIAL CONVENTIONS FOR MOPAC-TYPE INPUT.
      IF(IMOPAC.EQ.1) THEN
         KEYLEN = 240
         MXLINE = 3
         ICOL   = 6
         KEYWRD(2:6) = 'MOPAC'
         IF(IN1(75).NE.0) THEN
            KEYWRD(7:10) = ' SYM'
            ICOL = 10
         ENDIF
      ENDIF
C *** LOOP OVER MNDO97 KEYWORDS WITH INTEGER VALUES.
C     INCLUDE KEYWORDS WITH NONZERO INTEGER VALUES.
      DO 30 I=1,LM96
      IF(IN1(I).EQ.0) GO TO 30
      IF(IMOPAC.EQ.1 .AND. I.EQ.75) GO TO 30
C     WRITE NONZERO INTEGER VALUE TO INTERNAL FILE.
C     NON-BLANK CHARACTERS ARE FOUND IN IVALUE(J:10).
      WRITE(IVALUE,'(I10)') IN1(I)
      DO 10 J=1,10
      IF(IVALUE(J:J).NE.' ') GO TO 20
   10 CONTINUE
   20 LENVAL = 10-J+1
C     KEYWORD WILL OCCUPY KEYWRD(ICOL1:ICOL2), LENI BYTES.
C     THERE IS ONE BLANK BETWEEN KEYWORDS (ICOL1=ICOL+2).
      LENI   = LEN96(I)+1+LENVAL
      ICOL1  = ICOL+2
      ICOL2  = ICOL1+LENI-1
C     KCOL2 SPECIFIES THE LAST COLUMN THAT WOULD BE OCCUPIED
C     BY THE KEYWRD ON THE CURRENT LINE (ILINE+1).
      KCOL2  = ICOL2-ILINE*MAXCOL
C     CHECK WHETHER KEYWORD WOULD EXTEND BEYOND CURRENT LINE.
C     MAXCOL COLUMNS PER LINE, KEYWORDS ALLOWED UP TO COLUMN
C     MAXCOL-3, TO LEAVE SPACE FOR CONTINUATION KEYWORD (' +').
C     START NEXT LINE IF THE KEYWORD DOES NOT FIT ON THE LINE,
C     STOP IF THE MAXIMUM NUMBER OF LINES (MXLINE) IS EXCEEDED.
      IF(KCOL2.GT.(MAXCOL-3)) THEN
         ILINE = ILINE+1
         IF(ILINE.GE.MXLINE) THEN
            WRITE(NB6,500) MXLINE,MAXCOL,KEYLEN
            GO TO 40
         ENDIF
         KEYWRD(ICOL1:ICOL1+1) = ' +'
         ICOL1 = ILINE*MAXCOL+2
         ICOL2 = ICOL1+LENI-1
      ENDIF
C     WRITE THE KEYWORD.
      KEYWRD(ICOL1:ICOL2) = KEY96(I)(:LEN96(I))//'='//IVALUE(J:10)
      ICOL = ICOL2
   30 CONTINUE
C *** CHECK IF NOTHING HAS BEEN WRITTEN.
      IF(KEYWRD.EQ.' ') THEN
         KEYWRD = ' IOP=0'
         ICOL2  = 6
      ENDIF
C *** DEBUG PRINT (NPRINT=IN1(72)).
   40 CONTINUE
      IF(IN1(72).GT.5) THEN
         ILINE = ILINE+1
         WRITE(NB6,510) ICOL2,ILINE
         DO 50 I=1,ILINE
         ICOL1  = (I-1)*MAXCOL+1
         ICOL2  = ICOL1+MAXCOL-1
         WRITE(NB6,520) KEYWRD(ICOL1:ICOL2)
   50    CONTINUE
      ENDIF
      RETURN
  500 FORMAT(///1X,'KEYWRT: AUTOMATIC GENERATION OF A KEYWORD STRING',
     1          1X,'FAILED, TOO MANY ENTRIES.',
     2       /  1X,'LIMITS:',I3,' LINES WITH',I3,' COLUMNS EACH.',
     3       /  1X,'CONTINUE WITH AN INCOMPLETE KEYWORD STRING'/)
  510 FORMAT(// 1X,'KEYWRT: AUTOMATIC GENERATION OF A KEYWORD STRING',
     1       /  1X,'WITH A TOTAL OF',I4,' BYTES ON',I3,' LINE(S).'/)
  520 FORMAT(   1X,A)
      END
