      SUBROUTINE GRID (A,LM5,SCFCAL)
C     *
C     GRID CALCULATION WITH TWO GRID VARIABLES.
C     *
C     NOTATION. I=INPUT, O=OUTPUT, S=SCRATCH.
C     A(LM5)    AVAILABLE BUFFER (S).
C     SCFCAL    EXTERNAL ROUTINE FOR ENERGY EVALUATION (I).
C     *
      USE LIMIT, ONLY: LM1, LMV, LMG
      IMPLICIT DOUBLE PRECISION (A-H,O-Z)
      EXTERNAL SCFCAL
      CHARACTER*80  KTITLE,KOMENT
      COMMON
     ./CONSTF/ A0,AFACT,EV,EVCAL,PI,W1,W2,BIGEXP
     ./DFP   / X(LMV),NVAR
     ./DIPOL / DIP(7)
     ./ERG   / ENERGY,GRAD(LMV),GNORM,CNORM
     ./FLAG1 / KTITLE,KOMENT
     ./INOPT2/ IN2(300)
     ./LMFOR / LF1,LF2,LF3,LF4,LF5,LF6,LF7,LF8,LFA
     ./NBFILE/ NBF(20)
     ./PARM1 / AA(LMV),NA(LM1,4),NATOMS
     ./PARM5 / RC1(LMG),RC2(LMG),LGRID1,LTOT1,LGRID2,LTOT2
     ./PARM6 / LPT,LPT1,LPT2
      DIMENSION A(LM5)
      DIMENSION HEATS(LMG,LMG),DIPS(LMG,LMG)
      DIMENSION CNORMS(LMG,LMG),GNORMS(LMG,LMG)
C *** FILE NUMBERS.
      NB6    = NBF(6)
C *** INPUT OPTIONS.
      JOP    = IN2(3)
      IGEOM  = IN2(4)
      MAXEND = IN2(32)
      MIDDLE = IN2(37)
      KGEOM  = IN2(68)
C *** CHECK FOR RESTART.
C     GET CURRENT VALUES OF LPT1 AND LPT2 FROM THE RESTART FILE.
      LPT1   = 1
      LPT2   = 1
      IF(IN2(37).GT.0) THEN
         IF(JOP.EQ.0) CALL DFPSAV (A(LF1),LFA,1,MIDDLE)
         IF(JOP.EQ.1) CALL TSSAV  (A(LF1),A(LF2),NVAR,1,MIDDLE)
      ENDIF
C *** LOOP OVER ALL POINTS ON THE REACTION PATH.
      L1     = (LGRID1+2)/3
      L2     = LGRID1-3*(L1-1)
      LMIN   = MAX(1,LPT1)
      LMAX   = LTOT1
      M1     = (LGRID2+2)/3
      M2     = LGRID2-3*(M1-1)
      MMIN   = MAX(1,LPT2)
      MMAX   = LTOT2
      MLST   = LTOT2
      DO 30 L=LMIN,LTOT1
      LPT1   = L
      RC1V   = RC1(L)
      AA(LGRID1) = RC1V
      IF(IGEOM.EQ.0 .AND. L2.GT.1) RC1V=RC1V*AFACT
      IF(LMIN.GT.1 .AND. L.GT.LMIN) MMIN=1
      DO 20 M=MMIN,LTOT2
      LPT2   = M
      IF(KGEOM.EQ.3 .AND. MOD(L,2).EQ.0) THEN
         RC2V = RC2(LTOT2+1-M)
      ELSE
         RC2V = RC2(M)
      ENDIF
      AA(LGRID2) = RC2V
      IF(IGEOM.EQ.0 .AND. M2.GT.1) RC2V=RC2V*AFACT
      WRITE(NB6,500) KOMENT,KTITLE
      WRITE(NB6,510) L2,L1,RC1V,M2,M1,RC2V
      CALL SYMTRY(+1)
      CALL GMETRY(-1)
C *** DO THE CALCULATION FOR A GIVEN POINT.
C     SINGLE SCF CALCULATION.
      IF(JOP.EQ.-1 .OR. MAXEND.EQ.1) THEN
         ICALL = 0
         CALL SCF (A,LM5,ICALL,SCFCAL)
         IF(ICALL.EQ.-1) THEN
            LMAX = L
            MLST = M-1
            GO TO 40
         ENDIF
C     SINGLE SCF AND GRADIENT CALCULATION.
      ELSE IF(JOP.EQ.-2) THEN
         ICALL = 1
         CALL SCF (A,LM5,ICALL,SCFCAL)
         IF(ICALL.EQ.-1) THEN
            LMAX = L
            MLST = M-1
            GO TO 40
         ENDIF
C     OPTIMIZE THE MOLECULAR GEOMETRY.
      ELSE
         CALL GEOOPT (A,LM5,ICALL,SCFCAL)
         IF(ICALL.EQ.-1 .OR. ICALL.EQ.-8) THEN
            LMAX = L
            MLST = M-1
            GO TO 40
         ENDIF
         IF(IN2(45).EQ.0) IN2(45)=2
      ENDIF
C *** STORE RESULTS.
      HEATS (L,M) = ENERGY
      DIPS  (L,M) = DIP(1)
      CNORMS(L,M) = CNORM
      GNORMS(L,M) = GNORM
   20 CONTINUE
   30 CONTINUE
C *** PRINTING SECTION.
   40 CONTINUE
      IF((LMAX+MLST).GT.1) WRITE(NB6,520)
      DO 60 L=LMIN,LMAX
      RC1V   = RC1(L)
      IF(IGEOM.EQ.0 .AND. L2.GT.1) RC1V=RC1V*AFACT
      IF(L.EQ.LMAX) MMAX=MLST
      DO 50 M=MMIN,MMAX
      RC2V   = RC2(M)
      IF(IGEOM.EQ.0 .AND. M2.GT.1) RC2V=RC2V*AFACT
      WRITE(NB6,530) L,M,RC1V,RC2V,HEATS(L,M),DIPS(L,M),
     1             GNORMS(L,M),CNORMS(L,M)
   50 CONTINUE
   60 CONTINUE
      RETURN
  500 FORMAT(///1X,A,/1X,A)
  510 FORMAT(/  1X,'FIRST  REACTION COORDINATE: VARIABLE',I2,' OF ATOM',
     1              I3,', CURRENT VALUE', F10.5,
     2       /  1X,'SECOND REACTION COORDINATE: VARIABLE',I2,' OF ATOM',
     3              I3,', CURRENT VALUE', F10.5//)
  520 FORMAT(///1X,'SUMMARY OF RESULTS FROM GRID CALCULATION.',
     1       // 3X,'L  M    REACTION     REACTION     HEAT OF   ',
     2             ' DIPOLE   OPTIMIZED   CARTESIAN',
     3       /  3X,'       COORDINATE   COORDINATE   FORMATION  ',
     4             ' MOMENT    GRADIENT    GRADIENT',
     5       /  3X,'           L            M       (KCAL/MOL)  ',
     6             '(DEBYE)      NORM        NORM'/)
  530 FORMAT(   1X,2I3,F11.3,F13.3,F13.5,F9.3,F11.3,F12.3)
      END
