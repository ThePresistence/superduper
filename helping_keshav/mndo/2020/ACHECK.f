      SUBROUTINE ACHECK (ICALL)
C     *
C     CHECK ON AVAILABLE SEMIEMPIRICAL PARAMETERS.
C     *
C     NOTATION. I=INPUT, O=OUTPUT.
C     ICALL     ERROR FLAG (O).
C     ICALL     = 0 NO ERRORS.
C               =-4 PARAMETERS ARE MISSING.
C     *
      USE LIMIT
      IMPLICIT DOUBLE PRECISION (A-H,O-Z)
      CHARACTER*7 METHOD
      COMMON
     ./ATOMS / NUMAT,NAT(LM1),NFIRST(LM1),NLAST(LM1)
     ./IJWORK/ NEW(LM1*12)
     ./INOPT2/ IN2(300)
     ./MNDOEL/ IMM(LMZ)
     ./NBFILE/ NBF(20)
     ./PAR20 / XXPAR(LMP),NFPAR(LMP),NSPAR(LMP),NNPAR
     ./PARAVL/ IMPAR(LMZ)
     ./QMNAME/ METHOD(-23:6)
     ./DFTAVA/ IDFTAT(LMZ)
      DIMENSION MINDOP(55)
      DATA MINDOP/21*1,  1,0,1,0,1,0,1,      1,0,1,0,0,0,0,1,
     1               1,0,1,1,1,0,0,0,1,  1,0,1,1,1,0,0,1,1,1/
C *** FOR EVALUATION PURPOSES SULFUR, PHOSPHOROUS, ZINK WERE
C     MARKED UNAVAILABLE DUE TO MISSING JORGENSEN PARAMETERS
C     FOR THESE ELEMENTS
      DATA IDFTAT/1,0,0,0,0,1,1,1,0,0,0,0,0,0,0,0,0,0,
     1            0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,
     3            50*0/
C *** FILE NUMBERS.
      NB6    = NBF(6)
C *** INPUT OPTIONS.
      IOP    = IN2(2)
      IPAROK = IN2(11)
C *** GENERAL CHECK FOR ATOMIC NUMBERS.
      ICALL  = 0
      IER    = 0
      DO 10 I=1,NUMAT
      NI     = NAT(I)
      IF(NI.LT.1 .OR. (NI.GT.LMZ .AND. NI.NE.99)) THEN
         IER = IER+1
         IF(IER.EQ.1) WRITE(NB6,400) LMZ
         WRITE(NB6,410) NI
      ENDIF
   10 CONTINUE
      IF(IER.GT.0) STOP 'ACHECK'
C *** SET FLAGS FOR EXPLICIT INPUT OF NEW PARAMETERS.
      DO 20 I=1,LMZ
      NEW(I) = 0
   20 CONTINUE
      IF(IPAROK.GE.1 .AND. IPAROK.LE.3) THEN
         DO 30 I=1,NNPAR
         NI  = NSPAR(I)
         IF(NI.GE.1 .AND. NI.LE.LMZ) THEN
            NEW(NI) = 1
         ENDIF
   30    CONTINUE
      ENDIF
C *** SPECIAL CHECK FOR AM1 AND PM3 (IPAROK).
      IF(IOP.EQ.-2 .OR. IOP.EQ.-7) THEN
         DO 40 I=1,NUMAT
         NI  = NAT(I)
         IF(IMPAR(NI).EQ.0 .AND. NEW(NI).EQ.0) THEN
            IF(IPAROK.NE.-1 .OR. IMM(NI).EQ.0) THEN
               IER = IER+1
               WRITE(NB6,500) METHOD(IOP)(1:3),I,NI
            ELSE IF(IPAROK.EQ.-1) THEN
               WRITE(NB6,510) METHOD(IOP)(1:3),I,NI
            ENDIF
         ENDIF
   40    CONTINUE
C *** GENERAL CHECK FOR ALL OTHER AVAILABLE METHODS.
      ELSE IF(IOP.GE.-23 .AND. IOP.LE.2) THEN
         DO 50 I=1,NUMAT
         NI  = NAT(I)
         IF(IMPAR(NI).EQ.0 .AND. NEW(NI).EQ.0) THEN
            IER = IER+1
            WRITE(NB6,500) METHOD(IOP),I,NI
         ENDIF
   50    CONTINUE
      ENDIF
C *** SPECIAL CHECK ON MINDO/3 BOND PARAMETERS.
      IF(IOP.EQ.1) THEN
         DO 70 I=1,NUMAT
         NI     = NAT(I)
         IF(NI.LT.14) GO TO 70
         DO 60 J=1,I
         NJ     = NAT(J)
         IF(NJ.EQ.1 .OR. NJ.EQ.6 .OR. NJ.EQ.NI) GO TO 60
         CALL BONTYP(NI,NJ,NBOND)
         IF(MINDOP(NBOND).EQ.0) THEN
            IER = IER+1
            WRITE(NB6,520) METHOD(IOP),I,J,NI,NJ
         ENDIF
   60    CONTINUE
   70    CONTINUE
      ENDIF
C *** SPECIAL CHECK FOR SECOND-ROW ELEMENTS IN CNDO/2.
C     PARAMETERS ARE AVAILABLE, INTEGRALS NOT IMPLEMENTED.
      IF(IOP.EQ.2) THEN
         DO 80 I=1,NUMAT
         NI  = NAT(I)
         IF(NI.GE.11 .AND. NI.LE.18) THEN
            IER = IER+1
            WRITE(NB6,530) METHOD(IOP),I,NI
         ENDIF
   80    CONTINUE
      ENDIF
C *** CHECK FOR AVAILABILITY OF SCC-DFTB PARAMETERS
      IF(IOP.EQ.5 .OR. IOP.EQ.6) THEN
        DO I=1,NUMAT
          IF (IDFTAT(NAT(I)).EQ.0 .AND. NAT(I).NE.99) THEN
            IER = IER+1
            WRITE(NB6,500) METHOD(IOP),I,NAT(I)
          ENDIF
        ENDDO
      ENDIF
C *** SET FLAG FOR MISSING PARAMETERS.
      IF(IER.GT.0) ICALL=-4
      RETURN
  400 FORMAT(/1X,'MAXIMUM ATOMIC NUMBER IN THIS PROGRAM',I5)
  410 FORMAT( 1X,'INPUT ATOMIC NUMBER WHICH IS TOO HIGH',I5)
  500 FORMAT(/1X,A,': PARAMETERS PRESENTLY NOT AVAILABLE FOR ',
     1             'ATOM',I3,' WITH ATOMIC NUMBER',I3)
  510 FORMAT(/1X,A,': MNDO PARAMETERS REQUESTED (OPTION IPAROK) FOR ',
     1             'ATOM',I3,' WITH ATOMIC NUMBER',I3)
  520 FORMAT(/1X,A,': PARAMETERS PRESENTLY NOT AVAILABLE FOR ',
     1             'ATOM PAIR',2I3,' WITH ATOMIC NUMBERS',2I3)
  530 FORMAT(/1X,A,': INTEGRALS NOT IMPLEMENTED FOR SECOND-ROW ',
     1             'ATOM',I3,' WITH ATOMIC NUMBER',I3)
      END
