      SUBROUTINE SCFRES (EA,PA,PB,LM3,LM4)
C     *
C     EVALUATE AND SAVE SCF RESULTS (IN COMMON BLOCK SCFPRT).
C     *
      IMPLICIT DOUBLE PRECISION (A-H,O-Z)
      LOGICAL UHF
      COMMON
     ./CONSTF/ A0,AFACT,EV,EVCAL,PI,W1,W2,BIGEXP
     ./CONSTN/ ZERO,ONE,TWO,THREE,FOUR,PT5,PT25
     ./ENERGT/ EE,ENUCLR,EAT,ATHEAT
     ./HALFX / XIIII,XJJJJ,XIJIJ
     ./INOPT2/ IN2(300)
     ./MMCOM1/ EMM
     ./MMDP  / EMMDP
     ./CHCORR/ EHCORR
     ./ORBITS/ NUMB,NORBS,NMOS,NALPHA,NBETA
     ./SCFPRT/ EBIND,ENERGY,EIP,EIP2,SZ,S2,ITSAVE
     ./UHF   / UHF
      DIMENSION EA(LM3)
      DIMENSION PA(LM4),PB(LM4)
C *** INPUT OPTIONS.
      LINDMS = IN2(56)
      LINDIA = IN2(57)
      IMULT  = IN2(66)
      KITSCF = IN2(71)
      IATERG = IN2(119)
C *** SPIN EXPECTATION VALUES FOR UHF.
      IF(UHF) THEN
         CALL ESCF (S2,NORBS,PA,PB,LM4)
         SZ  = (NALPHA-NBETA)*PT5
         S2  = SZ*SZ+(NALPHA+NBETA)*PT5-TWO*S2
      ENDIF
C *** ENERGIES.
      EETOT  = EE+ENUCLR
      EBIND  = EETOT-EAT
      IF(IATERG.EQ.1) THEN
         ENERGY = ATHEAT+EVCAL*EBIND
      ELSEIF(IATERG.EQ.-1) THEN
         ENERGY = EVCAL*EETOT
      ENDIF
      ENERGY = ENERGY+EMM+EMMDP+EHCORR
C *** IONIZATION POTENTIALS.
      EIP    = ZERO
      EIP2   = ZERO
      IF((LINDMS.LE.0 .OR. LINDIA.EQ.1) .AND. KITSCF.NE.-1) THEN
         EIP =-EA(NUMB)
      ENDIF
      IF(IMULT.GT.0 .AND. .NOT.UHF) THEN
         IF(IMULT.EQ.1) THEN
            EIP2=-EA(NUMB-1)
            EIP = EIP +PT5*XIIII-1.5D0*XIJIJ
            EIP2= EIP2+PT5*XJJJJ-1.5D0*XIJIJ
         ELSE IF(IMULT.EQ.2) THEN
            EIP = EIP +PT5*XIIII
         ELSE IF(IMULT.EQ.3) THEN
            EIP2=-EA(NUMB-1)
            EIP = EIP +PT5*XIIII+PT5*XIJIJ
            EIP2= EIP2+PT5*XJJJJ+PT5*XIJIJ
         ELSE
            EIP = ZERO
         ENDIF
      ENDIF
      END
