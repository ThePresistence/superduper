      SUBROUTINE NMRCHK (NMR)
C     *
C     INITIAL CHECKS AND PRINTING FOR NMR CALCULATION.
C     *
C     NOTATION. I=INPUT, O=OUTPUT.
C     NMR       INPUT OPTION FOR NMR (I).
C     *
      USE LIMIT, ONLY: LM1, LMZ, MXNICS
      IMPLICIT DOUBLE PRECISION (A-H,O-Z)
      LOGICAL INC
      CHARACTER*2 ELEMNT
      COMMON
     ./ATOMC / COORD(3,LM1)
     ./ATOMS / NUMAT,NAT(LM1),NFIRST(LM1),NLAST(LM1)
     ./ELEMTS/ ELEMNT(107)
     ./IJWORK/ IELMT(LM1),IWANT(LM1),NNN(LM1),IJKDUM(LM1*9)
     ./INOPT2/ IN2(300)
     ./NBFILE/ NBF(20)
     ./PARM1 / A(3,LM1),NA(LM1),NB(LM1),NC(LM1),NN(LM1),NATOMS
     ./PSNICS/ COORDN(3,MXNICS),NNICS
     ./PSNMRI/ INC(LM1)
     ./PSNPRD/ XBETAN(LMZ,3),XZETAN(LMZ,3),XZETAS(LMZ,3),XBETAS(LMZ,3),
     .         XUS(LMZ,3),XSCALN(0:LMZ),XOFFN(0:LMZ,2),IDEFN(0:LMZ)
      IF(NMR.EQ.0) RETURN
C *** FILE NUMBERS.
      NB6    = NBF(6)
C *** INPUT OPTIONS.
      JPRINT = IN2(42)
      NPRINT = IN2(72)
      NMRNUC = IN2(118)
C *** UPDATE FLAGS FOR NMR COMPUTATION.
C     INCLUDE ALL ELEMENTS THAT HAVE BEEN PARAMETRIZED.
      IF(NMRNUC.EQ.2 .OR. NMRNUC.EQ.3) THEN
         DO 10 I=1,NUMAT
         IF(IDEFN(NAT(I)).GT.0) THEN
            INC(I) = .TRUE.
         ELSE
            INC(I) = .FALSE.
         ENDIF
   10    CONTINUE
      ENDIF
C *** PRINT NMR INPUT DATA.
      IF(JPRINT.GT.0) THEN
         WRITE(NB6,400)
         NNINC = 0
         DO 20 K=1,NUMAT
         IF(INC(K)) NNINC=NNINC+1
         NNN(K) = K
   20    CONTINUE
         IF(NATOMS.GT.NUMAT) THEN
            K = 0
            DO 30 I=1,NATOMS
            IF(NN(I).NE.99) THEN
               K = K+1
               NNN(K) = I
            ENDIF
   30       CONTINUE
         ENDIF
C        NUCLEI WHERE THE SHIELDING IS COMPUTED.
         IF(NNINC.GT.0) THEN
            WRITE(NB6,410)
            IF(NATOMS.GT.NUMAT) WRITE(NB6,420)
            WRITE(NB6,430)
            DO 40 K=1,NUMAT
            IF(INC(K)) WRITE(NB6,450) NNN(K),(COORD(J,K),J=1,3)
   40       CONTINUE
         ENDIF
C        NICS POINTS WHERE THE SHIELDING IS COMPUTED.
         IF(NNICS.GT.0) THEN
            WRITE(NB6,440)
            DO 50 I=1,NNICS
            WRITE(NB6,450) I,(COORDN(J,I),J=1,3)
   50       CONTINUE
         ENDIF
      ENDIF
C *** PRINT NMR PARAMETERS.
      IF(NMR.GT.0 .AND. JPRINT.GE.5) THEN
         WRITE(NB6,500) NMR
         CALL PRTPAR (0,NMR,JPRINT)
      ENDIF
C *** CONTROL SCF OUTPUT.
      IF(NPRINT.GE.-5) THEN
         WRITE(NB6,510)
         WRITE(NB6,520)
         IF(NPRINT.GE.-2 .AND. NPRINT.LE.0) THEN
            WRITE(NB6,530)
            IN2(72) = -3
         ENDIF
      ENDIF
C *** CHECK NMR PARAMETERS FOR CONSISTENCY.
      IF(NPRINT.LE.-5 .OR. NMR.LT.0) RETURN
      DO 60 I=1,LMZ
      IELMT(I) = 0
      IWANT(I) = 0
   60 CONTINUE
      DO 70 I=1,NUMAT
      IELMT(NAT(I)) = 1
      IF(INC(I)) IWANT(NAT(I)) = 1
   70 CONTINUE
      NWANT = 0
      NELMT = 0
      DO 80 I=1,LMZ
      IF(IDEFN(I).LE.0) THEN
         IF(IELMT(I).EQ.1) THEN
            NELMT = NELMT+1
            IELMT(NELMT) = I
         ENDIF
         IF(IWANT(I).EQ.1) THEN
            NWANT = NWANT+1
            IWANT(NWANT) = I
         ENDIF
      ENDIF
   80 CONTINUE
      IF(NELMT.GT.0) THEN
         WRITE(NB6,540) (ELEMNT(IELMT(I)),I=1,NELMT)
         IF(NWANT.GT.0) WRITE(NB6,550) (ELEMNT(IWANT(I)),I=1,NWANT)
      ENDIF
      RETURN
  400 FORMAT(///1X,'INPUT FOR NMR COMPUTATION.')
  410 FORMAT(   1X,'ATOMS FOR WHICH NMR SHIELDINGS ARE COMPUTED.')
  420 FORMAT(   1X,'THE NUMBERING INCLUDES DUMMY ATOMS.')
  430 FORMAT(/  1X,'CARTESIAN COORDINATES OF THESE ATOMS (ANGSTROM).',
     1       // 5X,'I',7X,'X',9X,'Y',9X,'Z'/)
  440 FORMAT(// 1X,'CARTESIAN COORDINATES OF NICS POINTS (ANGSTROM).',
     1       // 5X,'I',7X,'X',9X,'Y',9X,'Z'/)
  450 FORMAT(   1X,I5,2X,3F10.5)
  500 FORMAT(///1X,'SPECIFIC NMR PARAMETERS ARE EMPLOYED AS REQUESTED',
     1          1X,'BY INPUT OPTION NMR =',I3,'.',
     2       /  1X,'THESE PARAMETERS ARE INCLUDED IN THE FOLLOWING',
     3          1X,'LIST.')
  510 FORMAT(///1X,'CALCULATION WITH SPECIFIC NMR PARAMETERS.')
  520 FORMAT(/  1X,'THESE PARAMETERS HAVE BEEN OPTIMIZED TO REPRODUCE',
     1          1X,'NMR CHEMICAL SHIFTS.',
     2       /  1X,'THEY DO NOT YIELD RELIABLE RESULTS FOR OTHER',
     3          1X,'PROPERTIES.')
  530 FORMAT(   1X,'THEREFORE, ONLY A MINIMAL SCF OUTPUT IS PROVIDED.',
     1       /  1X,'OPTION NPRINT.GT.0 WILL GENERATE DETAILED OUTPUT.')
  540 FORMAT(///1X,'STANDARD PARAMETERS WILL BE USED FOR THE',
     1          1X,'FOLLOWING ELEMENTS',
     2       /  1X,'WHICH HAVE NOT YET BEEN PARAMETRIZED FOR NMR.',
     3       //(1X,10(3X,A2)))
  550 FORMAT(// 1X,'NMR SHIELDINGS ARE REQUESTED FOR THE FOLLOWING',
     1          1X,'OF THESE ELEMENTS.',
     2       //(1X,10(3X,A2)))
      END
