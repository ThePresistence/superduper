      SUBROUTINE FRAGMT (PA,LM4)
C     *
C     CONTROL ROUTINE FOR FRAGMENT RHF-SCF CALCULATIONS.
C     USED TO GET INITIAL BLOCK-DIAGONAL GUESS OF THE DENSITY MATRIX.
C     THE RESULTING MATRIX IS STORED IN TRIANGULAR FORM.
C     *
C     NOTATION. I=INPUT, O=OUTPUT, S=SCRATCH.
C     PA(LM4)   STANDARD DENSITY MATRIX IN BUFFER FOR MOLECULE (O).
C     *
      USE LIMIT, ONLY: LM1
      IMPLICIT DOUBLE PRECISION (A-H,O-Z)
      COMMON
     ./ATOMC / COORD(3,LM1)
     ./ATOMS / NUMAT,NAT(LM1),NFIRST(LM1),NLAST(LM1)
     ./FRGMT1/ NFRAGS(LM1),NCHRGS(LM1)
     ./INOPT2/ IN2(300)
     ./NBFILE/ NBF(20)
     ./ORBITS/ NUMB,NORBS,NMOS,NALPHA,NBETA
      DIMENSION PA(LM4)
C     AUTOMATIC LOCAL ARRAYS FOR STORING MOLECULAR DATA.
      DIMENSION COORDS(3,NUMAT),NATS(NUMAT),NFS(NUMAT)
      DIMENSION NNCHG(LM1)
C     ALLOCATABLE WORKING ARRAY FOR FRAGMENT RHF-SCF CALCULATIONS.
      DOUBLE PRECISION, ALLOCATABLE :: ARRAY(:)
C     *
C *** CHECK WHETHER DENSITY MATRIX IS AVAILABLE.
      IF(IN2(67).LT.0) RETURN
C *** FILE NUMBERS.
      NB6    = NBF(6)
C *** DEFINE LOCAL VARIABLES FOR THIS ROUTINE.
      IOP    = IN2(2)
      JPRINT = IN2(42)
      INPFRG = IN2(59)
      KHARGE = IN2(65)
      NPRINT = IN2(72)
      NORBSS = NORBS
C
C *** SPECIAL CASE: THERE IS ONLY A SINGLE FRAGMENT.
      IF(INPFRG.LT.0) THEN
C        DYNAMIC MEMORY ALLOCATION FOR SCF TREATMENT.
         CALL DYNFRG (LM5X,LM2F,LM4F,LM6F,LM9F,
     1                LS1F,LS2F,LS3F,LS5F,LS6F,LS7F,LS8F,LS9F,
     2                LG1F,LG2F,LG3F,LG4F,JPRINT)
C        ALLOCATE REQUIRED MEMORY.
         ALLOCATE (ARRAY(LM5X),STAT=IERR)
C        PERFORM RHF-SCF CALCULATION.
         CALL SCFFRG (ARRAY,LM5X,NITER,LM2F,LM4F,LM6F,LM9F,
     1                LS1F,LS2F,LS3F,LS5F,LS6F,LS7F,LS8F,LS9F,
     2                LG1F,LG2F,LG3F,LG4F)
C        COPY FRAGMENT DENSITY INTO MOLECULAR DENSITY ARRAY.
         PA(1:LM4F) = ARRAY(LS8F:LS8F+LM4F-1)
C        DEALLOCATE REQUIRED MEMORY.
         DEALLOCATE (ARRAY,STAT=IERR)
         GO TO 100
      ENDIF
C
C *** GENERAL CASE: THERE IS MORE THAN ONE FRAGMENT.
C *** SAVE MOLECULAR INPUT DATA.
      NUMATS = NUMAT
      DO 10 I=1,NUMAT
      COORDS(1,I) = COORD(1,I)
      COORDS(2,I) = COORD(2,I)
      COORDS(3,I) = COORD(3,I)
      NATS(I)     = NAT(I)
      NFS(I)      = NFIRST(I)
   10 CONTINUE
C *** GET NUMBER OF FRAGMENTS (NFRAG) AND FRAGMENT CHARGES (NNCHG).
      NCHRG  = 0
      NFRAG  = 1
      DO 15 I=1,NUMAT
      NFRAG  = MAX(NFRAG,NFRAGS(I))
   15 CONTINUE
      DO 20 I=1,NFRAG
      NNCHG(I) = 0
   20 CONTINUE
      DO 30 I=1,NUMAT
      IFRAG  = NFRAGS(I)
      NCHRG  = NCHRG+NCHRGS(I)
      NNCHG(IFRAG) = NNCHG(IFRAG)+NCHRGS(I)
   30 CONTINUE
      IF(JPRINT.GE.2) THEN
         WRITE(NB6,510) NFRAG,NCHRG
      ENDIF
C     *
C *** INITIALIZE DENSITY MATRIX OF MOLECULE.
      PA(1:LM4) = 0.0D0
C     *
C *** LOOP OVER FRAGMENTS AND GENERATE DENSITY MATRIX.
      LM5MAX = 0
      DO 70 IFRAG=1,NFRAG
C *** EXTRACT FRAGMENT DATA FROM MOLECULAR DATA.
      CALL DEFFRG (IFRAG,COORDS,NUMATS,NATS,NFRAGS,NCHRGS,JPRINT)
C *** DYNAMIC MEMORY ALLOCATION FOR CURRENT FRAGMENT.
      CALL DYNFRG (LM5X,LM2F,LM4F,LM6F,LM9F,
     1             LS1F,LS2F,LS3F,LS5F,LS6F,LS7F,LS8F,LS9F,
     2             LG1F,LG2F,LG3F,LG4F,JPRINT)
C *** IF NEEDED: ALLOCATE MEMORY FOR FRAGMENT RHF-SCF.
      IF(LM5X.GT.LM5MAX) THEN
         IF(IFRAG.GT.1) DEALLOCATE (ARRAY,STAT=IERR)
         ALLOCATE (ARRAY(LM5X),STAT=IERR)
         LM5MAX = MAX(LM5MAX,LM5X)
      ENDIF
C *** FURTHER INITIALIZATION FOR CURRENT FRAGMENT.
C     DEFINITION OF PAIR INDICES.
      CALL DYNINT
C     DEFINITION OF GAUSSIAN BASIS SET.
      IF(IOP.EQ.-5 .OR. IOP.EQ.-6 .OR. IOP.EQ.-8 .OR. IOP.EQ.-9
     .             .OR. IOP.EQ.-22.OR. IOP.EQ.-23) THEN
         CALL GTOMIN (0,0,JPRINT)
         IF(IOP.EQ.-5) CALL ECPDEF (JPRINT)
      ENDIF
C *** PERFORM RHF-SCF CALCULATION FOR FRAGMENT.
      IN2(65) = NNCHG(IFRAG)
      CALL SCFFRG (ARRAY,LM5X,NITER,LM2F,LM4F,LM6F,LM9F,
     1             LS1F,LS2F,LS3F,LS5F,LS6F,LS7F,LS8F,LS9F,
     2             LG1F,LG2F,LG3F,LG4F)
      IF(JPRINT.GE.2 .AND. JPRINT.LT.5) THEN
         WRITE(NB6,570) IFRAG,NITER
      ENDIF
C *** COPY FRAGMENT DENSITY INTO MOLECULAR DENSITY ARRAY.
C     USE SQUARE FRAGMENT DENSITY MATRIX FOR CONVENIENCE.
      CALL SQUARE (ARRAY(LS8F),ARRAY(LS8F),NORBS,LM2F,LM4F)
      CALL COPFRG (IFRAG,NUMATS,NFS,NFRAGS,ARRAY(LS8F),LM2F,PA,LM4)
   70 CONTINUE
C *** DEALLOCATE MEMORY REQUIRED FOR RHF-SCF FRAGMENT CALCULATIONS.
      DEALLOCATE (ARRAY,STAT=IERR)
C     *
C *** REDEFINE MOLECULAR INPUT DATA.
      IF(JPRINT.GE.2) THEN
         WRITE(NB6,620)
      ENDIF
      NUMAT  = NUMATS
      DO 90 I=1,NUMAT
      COORD(1,I) = COORDS(1,I)
      COORD(2,I) = COORDS(2,I)
      COORD(3,I) = COORDS(3,I)
      NAT(I)     = NATS(I)
      NFIRST(I)  = NFS(I)
   90 CONTINUE
C     REDEFINE MOLECULAR CONTROL VARIABLES.
      IN2(65) = KHARGE
      CALL INPUTS
C     DEFINITION OF PAIR INDICES.
      CALL DYNINT
C     DEFINITION OF GAUSSIAN BASIS SET.
      IF(IOP.EQ.-5 .OR. IOP.EQ.-6 .OR. IOP.EQ.-8 .OR. IOP.EQ.-9
     1             .OR. IOP.EQ.-22.OR. IOP.EQ.-23) THEN
         CALL GTOMIN (0,0,JPRINT)
         IF(IOP.EQ.-5) CALL ECPDEF (JPRINT)
      ENDIF
C     *
C *** PRINT INITIAL FRAGMENT-BASED DENSITY MATRIX.
  100 CONTINUE
      IF(JPRINT.GE.5 .OR. NPRINT.GE.5) THEN
         IF(INPFRG.LT.0) WRITE(NB6,500) NITER
         WRITE(NB6,610)
         CALL VECPRT (PA,LM4,NORBSS)
      ENDIF
C *** RESET KTRIAL.
      IN2(67) = -1
      RETURN
  500 FORMAT(///1X,'GENERATION OF AN INITIAL DENSITY MATRIX.',
     1       /  1X,'ONE SINGLE FRAGMENT, NUMBER OF SCF ITERATIONS',I6)
  510 FORMAT(///1X,'RHF CALCULATIONS FOR FRAGMENTS TO GENERATE INITIAL',
     1          1X,'BLOCK-DIAGONAL DENSITY MATRIX.',
     2       // 1X,'TOTAL NUMBER OF FRAGMENTS              ',I10,
     3       /  1X,'TOTAL CHARGE OF FULL SYSTEM            ',I10)
  570 FORMAT(   1X,'FRAGMENT',I4,' DONE, NUMBER OF SCF ITERATIONS',I6)
  610 FORMAT(///1X,'INITIAL DENSITY MATRIX',
     1          1X,'IN LINEARLY PACKED TRIANGULAR FORMAT'/)
  620 FORMAT(///1X,'RHF CALCULATIONS FOR FRAGMENTS HAVE BEEN FINISHED.',
     1       /  1X,'REDEFINITION OF CONTROL VARIABLES FOR MOLECULE.')
      END
