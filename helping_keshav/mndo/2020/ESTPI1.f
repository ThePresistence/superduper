      FUNCTION ESTPI1 (N,EVAL,D,E,X,ANORM)
C
C     AUTHOR -
C        STEPHEN T. ELBERT (AMES LABORATORY-USDOE), 5 DEC 1986.
C        MINOR COSMETIC CHANGES BY W. THIEL, JANUARY 1991.
C
C     PURPOSE -
C        EVALUATE SYMMETRIC TRIDIAGONAL MATRIX PERFORMANCE INDEX
C        FOR 1 EIGENVECTOR
C
C     METHOD -
C        THIS ROUTINE FORMS THE 1-NORM OF THE RESIDUAL MATRIX A*X-X*EVAL
C        WHERE  A  IS A SYMMETRIC TRIDIAGONAL MATRIX STORED
C        IN THE DIAGONAL (D) AND SUB-DIAGONAL (E) VECTORS, EVAL IS THE
C        EIGENVALUE OF AN EIGENVECTOR OF  A,  NAMELY  X.
C        THIS NORM IS SCALED BY MACHINE ACCURACY FOR THE PROBLEM SIZE.
C        ALL NORMS APPEARING IN THE COMMENTS BELOW ARE 1-NORMS.
C
C     ON ENTRY -
C        N      - INTEGER
C                 THE ORDER OF THE MATRIX  A.
C        EVAL   - W.P. REAL
C                 THE EIGENVALUE CORRESPONDING TO VECTOR  X.
C        D      - W.P. REAL (N)
C                 THE DIAGONAL VECTOR OF  A.
C        E      - W.P. REAL (N)
C                 THE SUB-DIAGONAL VECTOR OF  A.
C        X      - W.P. REAL (N)
C                 AN EIGENVECTOR OF  A.
C        ANORM  - W.P. REAL
C                 THE NORM OF  A  IF IT HAS BEEN PREVIOUSLY COMPUTED.
C
C     ON EXIT -
C        ANORM  - W.P. REAL
C                 THE NORM OF  A, COMPUTED IF INITIALLY ZERO.
C        ESTPI1 - W.P. REAL
C           !!A*X-X*EVAL!! / (EPSLON(10*N)*!!A!!*!!X!!);
C           WHERE EPSLON(X) IS THE SMALLEST NUMBER SUCH THAT
C              X + EPSLON(X) .NE. X
C
C           ESTPI1 .LT. 1 == SATISFACTORY PERFORMANCE
C                  .GE. 1 AND .LE. 100 == MARGINAL PERFORMANCE
C                  .GT. 100 == POOR PERFORMANCE
C           (SEE LECT. NOTES IN COMP. SCI. VOL.6 PP 124-125)
C
      DOUBLE PRECISION ESTPI1
      DOUBLE PRECISION ANORM,EVAL,RNORM,SIZE,XNORM
      DOUBLE PRECISION D(N), E(N), X(N)
      DOUBLE PRECISION EPSLON, ONE, ZERO
      PARAMETER (ZERO = 0.0D0, ONE = 1.0D0)
C
      ESTPI1 = ZERO
      IF( N .LE. 1 ) RETURN
      SIZE = 10 * N
      IF (ANORM .EQ. ZERO) THEN
C        COMPUTE NORM OF  A
         ANORM = MAX( ABS(D(1)) + ABS(E(2))
     *               ,ABS(D(N)) + ABS(E(N)))
         DO 110 I = 2, N-1
            ANORM = MAX( ANORM, ABS(E(I))+ABS(D(I))+ABS(E(I+1)))
  110    CONTINUE
         IF(ANORM .EQ. ZERO) ANORM = ONE
      END IF
C     COMPUTE NORMS OF RESIDUAL AND EIGENVECTOR
      XNORM = ABS(X(1)) + ABS(X(N))
      RNORM = ABS( (D(1)-EVAL)*X(1) + E(2)*X(2))
     *       +ABS( (D(N)-EVAL)*X(N) + E(N)*X(N-1))
      DO 120 I = 2, N-1
         XNORM = XNORM + ABS(X(I))
         RNORM = RNORM + ABS(E(I)*X(I-1) + (D(I)-EVAL)*X(I)
     *                       + E(I+1)*X(I+1))
  120 CONTINUE
      ESTPI1 = RNORM / (EPSLON(SIZE)*ANORM*XNORM)
      RETURN
      END
