      SUBROUTINE PDBSAV (NB9)
C     *
C     WRITE A PDB-COMPATIBLE OUTPUT FILE WHICH MAY BE USED AS INPUT FOR
C     EVALUATION PROGRAMS THAT ACCEPT THE PDB FORMAT.
C     *
      USE LIMIT, ONLY: LM1
      IMPLICIT DOUBLE PRECISION (A-H,O-Z)
      CHARACTER*80  KTITLE,KOMENT
      CHARACTER*48  REMARK
      CHARACTER*2   ELEMNT
      CHARACTER*7   METHOD
      COMMON
     ./ATOMC / COORD(3,LM1)
     ./ATOMS / NUMAT,NAT(LM1),NFIRST(LM1),NLAST(LM1)
     ./ELEMTS/ ELEMNT(107)
     ./FLAG1 / KTITLE,KOMENT
     ./INOPT1/ IN1(300)
     ./QMNAME/ METHOD(-23:6)
C *** SPECIFY METHOD AS REMARK.
      IOP = IN1(2)
      IF(IOP.GE.-23 .AND. IOP.LE.6) THEN
         REMARK = METHOD(IOP)
      ELSE
         REMARK = ' '
      ENDIF
C *** WRITE FILE NB9.
      WRITE(NB9,500)
      WRITE(NB9,510) KTITLE(:48)
      WRITE(NB9,520) REMARK
      DO 10 I=1,NUMAT
      NI     = NAT(I)
      WRITE(NB9,530) I,ELEMNT(NI),COORD(1,I),COORD(2,I),COORD(3,I)
   10 CONTINUE
      WRITE(NB9,540)
      RETURN
  500 FORMAT('HEADER')
  510 FORMAT('COMPND',5X,A)
  520 FORMAT('REMARK',5X,A)
  530 FORMAT('ATOM  ',1X,I4,1X,A4,1X,'XXX',5X,'1',4X,3F8.3)
  540 FORMAT('END   ')
      END
