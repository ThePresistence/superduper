      SUBROUTINE GTOHYF (NI,IGTO,LMAX)
C     *
C     CALCULATE DEPENDENT PARAMETERS FOR MINIMAL GAUSSIAN BASIS.
C     THE PRESENT VERSION IS RESTRICTED TO SP BASIS SETS.
C     *
C     NOTATION. I=INPUT, O=OUTPUT.
C     NI        ATOMIC NUMBER (I).
C     IGTO      TYPE OF MINIMAL BASIS (I).
C               = 0     ECP BASIS FOR USE IN OM1 OR OM2.
C               = 1..6  STOnG BASIS SETS.
C     LMAX      MAXIMUM L QUANTUM NUMBER IN BASIS SET (I).
C               DEFAULT LMAX=0 FOR NI.LE.2, LMAX=1 OTHERWISE.
C     *
C     FOR INTERNAL CONSISTENCY, THE DEFINITION OF THE GAUSSIAN BASIS
C     (PRIMITIVES VIA ECPSET/STONG/RENORM AND SUBSEQUENT SCALING)
C     MUST BE THE SAME HERE AND IN ANY OTHER ROUTINE WHICH DEFINES
C     THE BASIS SET (GTOMIN).
C     *
      USE LIMIT, ONLY: LMZ
      IMPLICIT DOUBLE PRECISION (A-H,O-Z)
      PARAMETER (DIPFAC=5.0832D0)
      COMMON
     ./CONSTF/ A0,AFACT,EV,EVCAL,PI,W1,W2,BIGEXP
     ./CONSTN/ ZERO,ONE,TWO,THREE,FOUR,PT5,PT25
     ./DIPOL1/ HYF(LMZ),HYFPD(LMZ)
     ./DNBND / III(LMZ),IIID(LMZ)
     ./MULTIP/ DD(6,0:LMZ),PO(9,0:LMZ)
     ./PAROPT/ USS(LMZ),UPP(LMZ),Z(LMZ),PARDUM(LMZ*4)
     ./REP   / GSS(LMZ),GPP(LMZ),GSP(LMZ),GP2(LMZ),HSP(LMZ),
     .         HPP(LMZ)
      DIMENSION E(6),CS(6),CP(6)
C *** INITIALIZATION.
      DD(2,NI) = ZERO
      DD(3,NI) = ZERO
      HYF(NI)  = ZERO
      IF(LMAX.LT.0) RETURN
      IF(LMAX.EQ.0 .AND. NI.LE.2) RETURN
C *** SET CONTROL VARIABLES.
      IF(IGTO.GE.1 .AND. IGTO.LE.6) THEN
         NGAUSS = IGTO
      ELSE
         NGAUSS = 3
      ENDIF
      NGS    = NGAUSS
      NMAIN  = III(NI)
C *** DEFINE PRIMITIVES.
      IF(IGTO.LE.0 .AND. NI.GT.2) THEN
         CALL ECPSET (E,CS,CP,NGS,NI)
      ELSE
         CALL STONG  (E,CS,CP,NGS,NMAIN)
      ENDIF
C *** RENORMALIZE PRIMITIVES.
      CALL RENORM (E,CS,NGS,0)
      CALL RENORM (E,CP,NGS,1)
C *** SCALE GAUSSIAN BASIS FUNCTIONS.
      FAC    = (TWO/PI)**0.75D0
      FAC2   = FAC*TWO
      Z2     = Z(NI)**2
      DO 10 IG=1,NGS
      E(IG)  = E(IG)*Z2
      CS(IG) = CS(IG)*FAC *E(IG)**0.75D0
      CP(IG) = CP(IG)*FAC2*E(IG)**1.25D0
   10 CONTINUE
C *** CALCULATE EQUIVALENT DIPOLE AND QUADRUPOLE LENGTHS.
C *** PRECALCULATE HYBRIDIZATION TERMS FOR DIPOLE MOMENT.
      PIFAC1 = PT5*PI*SQRT(PI)
      PIFAC2 = PT5*PI**0.75D0
      SUM1   = ZERO
      SUM2   = ZERO
      DO 30 K=1,NGS
      DO 20 L=1,NGS
      SUM1   = SUM1 + CS(K)*CP(L)*SQRT(ONE/(E(K)+E(L))**5)
      SUM2   = SUM2 + CS(K)*CP(L)*SQRT(ONE/(E(K)+E(L))**7)
   20 CONTINUE
   30 CONTINUE
      DD(2,NI) = PIFAC1*SUM1
      DD(3,NI) = PIFAC2*SQRT(SUM2)
      HYF(NI)  = DD(2,NI)*DIPFAC
C *** CALCULATE EQUIVALENT ADDITIVE KLOPMAN-OHNO TERMS.
      D1       = DD(2,NI)
      Q2       = DD(3,NI)
      FG1      = HSP(NI)
      FG2      = PT5*(GPP(NI)-GP2(NI))
      IF(FG1.NE.ZERO .AND. FG2.NE.ZERO) THEN
         PO(2,NI) = POIJ(1,D1,FG1)
         PO(3,NI) = POIJ(2,Q2*SQRT(TWO),FG2)
      ENDIF
      RETURN
      END
