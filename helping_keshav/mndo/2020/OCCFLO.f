      SUBROUTINE OCCFLO (EA,EB,LM2)
C     DETERMINE FRACTIONAL OCCUPATION NUMBERS OF THE ORBITALS
C     WITH FLOATING OCCUPATION AND THE FERMI ENERGY, I.E. THE
C     UPPER LIMIT OF THE INTEGRALS OVER THE GAUSSIAN FUNCTIONS
C     ASSOCIATED WITH THE FRACTIONALLY OCCUPIED MOLECULAR ORBITALS,
C     SUCH THAT THE SUM OF THESE INTEGRALS IS EQUAL TO THE NUMBER
C     OF ELECTRONS.
C     G.GRANUCCI, A.TONIOLO, CHEM.PHYS.LETT. 325, 79-85 (2000).
C
C     PLEASE NOTE THAT ARRAY OCCA IN COMMON BLOCK OCCNM CONTAINS THE
C     OCCUPATION NUMBERS OF THE ALPHA ORBITALS. FOR A CLOSED-SHELL
C     SCF CALCULATION, THE OCCUPATION PATTERN OF THE BETA ORBITALS
C     IS IDENTICAL AND THEREFORE NOT STORED. THUS THE VALUES IN OCCA
C     NEED TO BE MULTIPLIED BY TWO TO MEET THE CONVENTIONS IN THE
C     PAPER CITED ABOVE.
C
C     CONSIDERING THIS, FROM EQ. (3) OF THE PAPER IT FOLLOWS THAT
C     OCCA(I) = 0.5 + 0.5 * ERF((EFERMI-EA(I)) / (SQRT(2)*DOMEGA)).
C
C     MEANING OF THE VARIABLES IN COMMON BLOCK OCCFL2:
C       DOMEGA     GAUSSIAN WIDTH, ARBITRARY PARAMETER.
C       EFERMI     FERMI ENERGY, SEE ABOVE.
C       NFLOAT     NUMBER OF MOS WITH FLOATING OCCUPATION.
C       NDOCC      NUMBER OF DOUBLY OCCUPIED ORBITALS
C                  WITH FIXED OCCUPATION.
C       NUMOCC     TOTAL NUMBER OF OCCUPIED ORBITALS
C                  (WITH FIXED OR FLOATING OCCUPATION).
C
C     OTHER USEFUL VARIABLES:
C       NOCCA      LAST ORBITAL WITH FLOATING OCCUPATION.
C
      USE LIMIT, ONLY: LMX
      IMPLICIT DOUBLE PRECISION (A-H,O-Z)
      COMMON
     ./CONSTN/ ZERO,ONE,TWO,THREE,FOUR,PT5,PT25
     ./INOPT2/ IN2(300)
     ./INOPT4/ XN4(50)
     ./NBFILE/ NBF(20)
     ./OCCFL / IMOCC,NOCCA,NOCCB,MSUB,MOSUMA,MOSUMB,MOCCA(8),MOCCB(8)
     ./OCCFL2/ DOMEGA,EFERMI,NFLOAT,NDOCC,NUMOCC
     ./OCCNM / OCCA(LMX),OCCB(LMX)
     ./ORBITS/ NUMB,NORBS,NMOS,NALPHA,NBETA
      DIMENSION EA(LM2),EB(LM2)
      PARAMETER (MXITFL = 200)
      PARAMETER (TINY   = 1D-10)
      LOGICAL   PRT
C *** FILE NUMBERS, INPUT OPTIONS, AND FLAGS.
      NB6    = NBF(6)
      NPRINT = IN2(72)
      NFLOAT = IN2(216)
      NDOCC  = IN2(217)
      DOMEGA = XN4(32)
      PRT    = NPRINT.GE.2
C *** IMPOSE DEFAULT VALUES.
      IF(NFLOAT.LE.0) NFLOAT=NORBS
      IF(NDOCC.LE.0)  NDOCC =(NALPHA+NBETA-NFLOAT)/2
      NOCCA = NFLOAT+NDOCC
      IF(NOCCA.GT.NORBS) THEN
         WRITE(NB6,600) NFLOAT,NDOCC,NORBS
         STOP 'OCCFLO'
      ENDIF
C *** ALWAYS DEFINE NEW STARTING VALUE FOR THE FERMI ENERGY.
C     THE BENEFIT OF REUSING THE PREVIOUS FERMI ENERGY IS SMALL,
C     BUT ON THE OTHER HAND, DOING SO MAY PREVENT CONVERGENCE
C     OF THE NEWTON-RAPHSON PROCEDURE BELOW IF THE FERMI ENERGY
C     VARIES STRONGLY BETWEEN CONSECUTIVE SCF ITERATIONS.
      CALL EFINIT
C *** USE NEWTON'S METHOD TO FIND THE FERMI ENERGY.
      IF(PRT) WRITE(NB6,100)
      DNOCCA = PT5 * DBLE(NALPHA+NBETA)
      DO IT=1,MXITFL
         CALL SUMOCC(FUNC, DERIV)
         IF(PRT) WRITE(NB6,110) IT,EFERMI,FUNC,DERIV
         DELTEF = (DNOCCA - FUNC) / DERIV
         IF(DABS(DELTEF).LT.TINY) THEN
            IF(PRT) WRITE(NB6,*)
            RETURN
         ENDIF
         EFERMI = EFERMI + DELTEF
      END DO
      WRITE(NB6,610) MXITFL
      STOP 'OCCFLO'
  100 FORMAT(/1X,"ITER",10X,"FERMI ENERGY",13X,
     1           "FUNCTION VALUE",11X,"DERIVATIVE")
  110 FORMAT(1X,I4,3F25.15)
  500 FORMAT(1X,"FERMI ENERGY OF",F15.5,
     1       1X,"EV OUTSIDE THE USEFUL RANGE, RESETTING.")
  600 FORMAT(/1X,"NFLOAT=",I4," PLUS NDOCC=",I4," EXCEEDS NORBS=",I4)
  610 FORMAT(/1X,"FERMI ENERGY DID NOT CONVERGE WITHIN",I4,
     1           " NEWTON-RAPHSON ITERATIONS.")
      CONTAINS

         SUBROUTINE EFINIT
C ***    INITIALIZE EFERMI FROM ORBITAL ENERGIES.
         IMPLICIT NONE
         INTEGER IFERMI
         IF(MOD(NALPHA+NBETA,2).EQ.0) THEN
C           FOR AN EVEN NUMBER OF ELECTRONS, USE THE ARITHMETIC MEAN
C           OF THE HOMO AND LUMO (OR SOMO) ENERGIES.
            IFERMI = (NALPHA+NBETA) / 2
            EFERMI = PT5*(EA(IFERMI)+EA(IFERMI+1))
         ELSE
C           FOR AN ODD NUMBER OF ELECTRONS, USE THE ENERGY OF THE
C           UNPAIRED ELECTRON.
            IFERMI = (NALPHA+NBETA+1) / 2
            EFERMI = EA(IFERMI)
         ENDIF
         END SUBROUTINE EFINIT


         SUBROUTINE SUMOCC(FUNC, DERIV)
C ***    CALCULATE THE SUM OF THE FRACTIONAL OCCUPATION NUMBERS (FUNC)
C        AND THE DERIVATIVE WITH RESPECT TO THE FERMI ENERGY (DERIV).
         IMPLICIT NONE
         DOUBLE PRECISION, PARAMETER :: PI = 3.141592653589793D0
         DOUBLE PRECISION FUNC, DERIV, FAC, FRAC
         INTEGER I
         FUNC  = DBLE(NDOCC)
         DERIV = ZERO
         FAC   = DSQRT(PT5) / DOMEGA
         DO I=NDOCC+1,NOCCA
            FRAC    = FAC * (EFERMI - EA(I))
            OCCA(I) = PT5 + PT5 * DERF(FRAC)
            FUNC    = FUNC  + OCCA(I)
            DERIV   = DERIV + DEXP(-FRAC*FRAC)
         END DO
         DERIV = DERIV / (DOMEGA * DSQRT(TWO*PI))
         END SUBROUTINE SUMOCC


      END SUBROUTINE OCCFLO
