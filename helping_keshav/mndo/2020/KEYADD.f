      SUBROUTINE KEYADD (KEYWRD,KEYLEN,STRING,LENTST)
C     *
C     ADD NEW KEYWORD WITH INPUT VALUE (STRING) TO KEYWRD.
C     *
C     NOTATION. I=INPUT, O=OUTPUT.
C     KEYWRD    KEYWORD STRING (I,O).
C     KEYLEN    POSITION OF LAST NON-BLANK CHARACTER IN KEYWRD (I,O).
C     STRING    STRING TO BE ADDED, <KEYWORD>=<VALUE> (I).
C     LENTST    CHECK IF STRING(:LENTST) IS PART OF KEYWRD (I).
C     *
      IMPLICIT DOUBLE PRECISION (A-H,O-Z)
      PARAMETER (MAXCOL=80)
      CHARACTER*(*) KEYWRD,STRING
C *** RETURN IF STRING(:LENTST) IS ALREADY PART OF KEYWRD.
      IF(INDEX(KEYWRD(:KEYLEN),STRING(:LENTST)).GT.0) RETURN
C *** INITIALIZATION.
      LENKEY = LEN(KEYWRD)
      LENSTR = LEN(STRING)
      MXLINE = (LENKEY-1)/MAXCOL+1
C *** SET UP POINTERS.
      ILINE  = (KEYLEN-1)/MAXCOL
      ICOL1  = KEYLEN+2
      ICOL2  = ICOL1+LENSTR-1
      KCOL2  = ICOL2-ILINE*MAXCOL
      IF(KCOL2.GT.(MAXCOL-3)) THEN
         ILINE = ILINE+1
         IF(ILINE.GE.MXLINE) RETURN
         KEYWRD(ICOL1:ICOL1+1) = ' +'
         ICOL1 = ILINE*MAXCOL+2
         ICOL2 = ICOL1+LENSTR-1
      ENDIF
C *** ADD NEW KEYWORD.
      KEYWRD(ICOL1:ICOL2) = STRING
      KEYLEN = ICOL2
      RETURN
      END
