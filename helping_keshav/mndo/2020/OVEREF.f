      SUBROUTINE OVEREF (U,EIGVAL,LMH,DMAX,OMIN,OSMIN,NVAR,LORJK,
     1                   IPRINT,MODE)
C     *
C     DETERMINE THE MODE TO BE FOLLOWED IN THE TS SEARCH (MODE).
C     FIRST STEP : ACCORDING TO INPUT.
C     LATER STEPS: FROM OVERLAP WITH PREVIOUS TS MODE.
C     *
C     NOTATION IN THE ARGUMENT LIST.
C     U      : EIGENVECTORS OF HESSIAN MATRIX.
C     EIGVAL : EIGENVALUES  OF HESSIAN MATRIX.
C     LMH    : DIMENSION    OF HESSIAN MATRIX.
C     DMAX   : MAXIMUM STEP SIZE (TRUST RADIUS).
C     OMIN   : SMALLEST ACCEPTABLE OVERLAP WITH THE PREVIOUS TS MODE.
C              FOR AN OVERLAP LESS THAN OMIN, THE PREVIOUS STEP WILL
C              BE REJECTED (I.E. REPEATED WITH A SMALLER STEP SIZE).
C     OSMIN  : SMALLEST STEP SIZE FOR APPLYING THE OVERLAP CRITERION.
C              OSMIN=0.005 FOR UPDATED HESSIANS SINCE THERE IS LITTLE
C              HOPE TO IMPROVE OVERLAP WITH VERY SMALL STEPS.
C              OSMIN=0.001 FOR EXACTLY CALCULATED HESSIANS.
C     NVAR   : NUMBER OF VARIABLES.
C     LORJK  : TRUE ONLY IN THE CASE OF OMIN REJECTION (SEE ABOVE).
C     IPRINT : PRINTING FLAG.
C     MODE   : INPUT  - MODE THAT HAS BEEN FOLLOWED PREVIOUSLY.
C              OUTPUT - MODE THAT WILL BE  FOLLOWED IN THE NEXT STEP.
C
      USE LIMIT, ONLY: LMV
      IMPLICIT DOUBLE PRECISION (A-H,O-Z)
      LOGICAL LORJK,PRT
      COMMON
     ./CYCLES/ ICYC,NCOUNT
     ./NBFILE/ NBF(20)
     ./OPTEF / OLDG(LMV),VMODE(LMV),DD
      DIMENSION U(LMH,LMH),EIGVAL(NVAR)
C *** FILE NUMBERS.
      NB6    = NBF(6)
C *** INITIALIZATION.
      LORJK  = .FALSE.
      PRT    = IPRINT.GT.0
C *** FIRST STEP: SIMPLY DETERMINE WHICH MODE TO FOLLOW.
      IF(ICYC.EQ.1) THEN
         IF(MODE.LE.0) MODE=1
         IT  = MODE
C *** SUBSEQUENT STEPS: DETERMINE WHICH HESSIAN EIGENVECTOR HAS
C     THE GREATEST OVERLAP WITH THE MODE WE ARE FOLLOWING.
      ELSE
         IT     = 1
         TOVLP  = DDOT(NVAR,U(1,1),1,VMODE,1)
         TOVLP  = ABS(TOVLP)
         DO 10 I=2,NVAR
         OVLP   = DDOT(NVAR,U(1,I),1,VMODE,1)
         OVLP   = ABS(OVLP)
         IF(OVLP.GT.TOVLP) THEN
            TOVLP = OVLP
            IT    = I
         ENDIF
   10    CONTINUE
         IF(PRT) WRITE(NB6,500) IT,TOVLP
         IF(TOVLP.LT.OMIN) THEN
            IF(DMAX.GT.OSMIN) THEN
               LORJK = .TRUE.
               IF(PRT) WRITE(NB6,510) OMIN
               RETURN
            ELSE
               IF(PRT) WRITE(NB6,520) OMIN,DMAX,OSMIN
            ENDIF
         ENDIF
      ENDIF
C *** SAVE THE EIGENVECTOR IN VMODE.
      DO 20 I=1,NVAR
      VMODE(I) = U(I,IT)
   20 CONTINUE
C *** REDEFINE MODE (IF NECESSARY).
      IF(IT.NE.MODE) THEN
         IF(PRT) WRITE(NB6,530) MODE,IT
         MODE = IT
      ENDIF
C *** PRINT MODE AND ASSOCIATE EIGENVALUE.
      IF(PRT) THEN
         WRITE(NB6,540) MODE,EIGVAL(MODE)
      ENDIF
      RETURN
  500 FORMAT(1X,'OVERLAP OF CURRENT MODE',I4,' WITH PREVIOUS MODE IS ',
     1           F8.5)
  510 FORMAT(1X,'OVERLAP LESS THAN OMIN:',F8.5,' REJECT PREVIOUS STEP')
  520 FORMAT(1X,'OVERLAP LESS THAN OMIN:',F8.5,' BUT TRUST RADIUS',F8.5,
     1          ' IS LESS THAN',F6.3,
     2      /1X,'ACCEPT PREVIOUS STEP')
  530 FORMAT(/1X,'WARNING: SWITCHING FROM MODE',I3,' TO MODE',I3)
  540 FORMAT( 1X,'NUMBER OF TS MODE      = ',I2,
     1       /1X,'EIGENVALUE OF TS MODE  = ',E11.4)
      END
