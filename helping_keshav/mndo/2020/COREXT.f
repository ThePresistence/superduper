      SUBROUTINE COREXT (ISHELL,NI,NKO,R,CORT,FKO)
C     *
C     ANALYTICAL TWO-CENTER CHARGE-ELECTRON ATTRACTIONS (IN EV)
C     BETWEEN THE ELECTRONS AT ATOM I AND THE EXTERNAL UNIT CHARGE J.
C     CODE FOR GAUSSIAN BASIS FUNCTIONS (AS USED IN OM1 AND OM2).
C     *
C     NOTATION.  I=INPUT, O=OUTPUT.
C     ISHELL     SHELL NUMBER (I).
C     NI         ATOMIC NUMBER (I).
C     NKO        FLAG FOR KLOPMAN-OHNO SCALING (I).
C     R          DISTANCE I-J IN AU (I).
C     CORT       CHARGE-ELECTRON ATTRACTION INTEGRALS IN EV (O).
C     FKO        KLOPMAN-OHNO SCALING FACTOR (O).
C     *
C     SPECIAL CONVENTIONS: NO KLOPMAN-OHNO SCALING FOR NKO=0 (FKO=1.0).
C     OTHERWISE, THE SCALING FACTOR FKO IS DEFINED BY THE RATIO OF THE
C     SEMIEMPIRICAL AND ANALYTICAL ONE-ELECTRON INTEGRALS (SS,J).
C     NOTE THAT FKO IS NORMALLY DEFINED AS A RATIO OF TWO-ELECTRON
C     INTEGRALS (SS,SS) WHICH CANNOT BE USED HERE SINCE THE EXTERNAL
C     POINT CHARGE CARRIES NO ELECTRONS. THE DEFINITION CHOSEN FOR THE
C     SEMIEMPIRICAL INTEGRAL (SS,J) ADHERES TO THE SPIRIT OF THE
C     KLOPMAN-OHNO APPROXIMATION (SEE CODE BELOW).
C     *
      USE LIMIT, ONLY: LMZ
      IMPLICIT DOUBLE PRECISION (A-H,O-Z)
      COMMON
     ./CONSTF/ A0,AFACT,EV,EVCAL,PI,W1,W2,BIGEXP
     ./CONSTN/ ZERO,ONE,TWO,THREE,FOUR,PT5,PT25
     ./REP   / GSS(LMZ),REPDUM(LMZ*5)
      DIMENSION CORT(20)
C *** GAUSSIAN INTEGRALS IN LOCAL COORDINATES.
      CALL CORGTO (ISHELL,ITYP,R,CORT)
C *** KLOPMAN-OHNO FACTOR.
C     SEMI AND CORT(1) COMPUTED IN ATOMIC UNITS.
      IF(NKO.NE.0) THEN
         POI  = PT5*EV/GSS(NI)
         SEMI = ONE/SQRT(R*R+POI**2)
         FKO  = SEMI/CORT(1)
      ELSE
         FKO  = ONE
      ENDIF
C *** CONVERT AND STORE THE INTEGRALS.
      FJ      = - EV*FKO
      CORT(1) = CORT(1)*FJ
      IF(ITYP.GT.0) THEN
         CORT(2) = CORT(2)*FJ
         CORT(3) = CORT(3)*FJ
         CORT(4) = CORT(4)*FJ
      ENDIF
      RETURN
      END
