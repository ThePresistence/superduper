      SUBROUTINE MMOKIN
C     *
C     SETUP FOR MOLECULAR MECHANICS CORRECTION TO PEPTIDE BONDS.
C     INCLUDED FOR COMPATIBILITY WITH MOPAC(6.0).
C     CODE ADAPTED FROM MOPAC(6.0) WRITTEN BY J.J.P.STEWART.
C     *
      USE LIMIT, ONLY: LM1
      IMPLICIT DOUBLE PRECISION (A-H,O-Z)
      COMMON
     ./ATOMC / COORD(3,LM1)
     ./ATOMS / NUMAT,NAT(LM1),NFIRST(LM1),NLAST(LM1)
     ./INOPT1/ IN1(300)
     ./INOPT2/ IN2(300)
     ./MMCOM2/ NHCO(4,1+2*LM1/3),HTYPE
     ./NBFILE/ NBF(20)
C     STATEMENT FUNCTION.
      RXYZ(I,J) = SQRT( (COORD(1,I)-COORD(1,J))**2
     1                 +(COORD(2,I)-COORD(2,J))**2
     2                 +(COORD(3,I)-COORD(3,J))**2)
C     INPUT OPTIONS.
      IOP     = IN2(2)
      IMMOK   = IN1(20)
      JPRINT  = IN2(42)
C     INITIALIZATION.
      IN2(20) = 0
      HTYPE   = 0.0D0
      IF(IMMOK.LE.0) RETURN
C     SET UP MOLECULAR-MECHANICS CORRECTION TO -(C=O)-(NH)-LINKAGE.
C     THIS WILL BE USED IF MMOK HAS BEEN SPECIFIED.
      IF(IOP.EQ.-2 .OR. IOP.EQ.-12) THEN
         HTYPE = 3.3191D0
      ELSE IF(IOP.EQ.-7 .OR. IOP.EQ.-17) THEN
         HTYPE = 7.1853D0
      ELSE IF(IOP.EQ.1) THEN
         HTYPE = 1.7712D0
      ELSE
         HTYPE = 6.1737D0
      ENDIF
C     IDENTIFY O=C-N-H SYSTEMS VIA THE INTERATOMIC DISTANCES MATRIX
      NNHCO  = 0
      DO 90 I=1,NUMAT
      IF(NAT(I).NE.8) GO TO 90
      DO 80 J=1,NUMAT
      IF(NAT(J).NE.6) GO TO 80
      IF(RXYZ(I,J).GT.1.3D0) GO TO 80
      DO 70 K=1,NUMAT
      IF(NAT(K).NE.7) GO TO 70
      IF(RXYZ(J,K).GT.1.6D0) GO TO 70
      DO 60 L=1,NUMAT
      IF(NAT(L).NE.1) GO TO 60
      IF(RXYZ(K,L).GT.1.3D0) GO TO 60
C     WE HAVE A H-N-C=O SYSTEM. THE ATOM NUMBERS ARE L-K-J-I.
C     SEARCH ATOM ATTACHED TO NITROGEN TO SPECIFY X-N-C=O.
      DO 50 M=1,NUMAT
      IF(M.EQ.K .OR. M.EQ.L .OR. M.EQ.J) GO TO 50
      IF(RXYZ(M,K).GT.1.7D0) GO TO 50
      NNHCO  = NNHCO+1
      NHCO(1,NNHCO)=I
      NHCO(2,NNHCO)=J
      NHCO(3,NNHCO)=K
      NHCO(4,NNHCO)=M
      NNHCO  = NNHCO+1
      NHCO(1,NNHCO)=I
      NHCO(2,NNHCO)=J
      NHCO(3,NNHCO)=K
      NHCO(4,NNHCO)=L
      GO TO 90
   50 CONTINUE
   60 CONTINUE
   70 CONTINUE
   80 CONTINUE
   90 CONTINUE
      IF(NNHCO.GT.0) THEN
       NB6 = NBF(6)
       WRITE(NB6,'(//,A)')' MOLECULAR MECHANICS CORRECTION FOR PEPTIDES'
       WRITE(NB6,'(A,I6)')' NUMBER OF PEPTIDE TORSION ANGLES',NNHCO/2
       WRITE(NB6,'(A,F14.4)')' FORCE CONSTANT FOR CORRECTION',HTYPE
       IN2(20) = NNHCO
       IF(JPRINT.GE.2) THEN
          WRITE(NB6,500)
          INHCO = 0
          DO 100 I=1,NNHCO/2
          INHCO = INHCO+1
          WRITE(NB6,510) I,(NHCO(J,INHCO),J=1,4)
          INHCO = INHCO+1
          WRITE(NB6,510) I,(NHCO(J,INHCO),J=1,4)
  100     CONTINUE
       ENDIF
      ENDIF
      RETURN
  500 FORMAT(///1X,'ATOMS INVOLVED IN PEPTIDE LINKAGE',
     1       /  1X,'INDEX    I    J    K    L'/)
  510 FORMAT(   1X,5I5)
      END
