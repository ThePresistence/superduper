      SUBROUTINE VECTYP (AMS,COORD,C,EIG,LM2,N,NUMAT,ISYM,NDEG,LPRINT)
C     *
C     SYMMETRY OF NORMAL MODES FOR DEGENERATE POINT GROUPS.
C     *
C     THE NORMAL MODES ARE CLASSIFIED WITH RESPECT TO THE SYMMETRY
C     OPERATIONS AS BEING SYMMETRIC (+1), ANTISYMMETRIC (-1), OR
C     NON-ASSIGNABLE (0).
C     *
C     DEGENERACY NUMBERS ARE ASSIGNED BY COMPARING THE VALUES OF THE
C     HARMONIC FREQUENCIES. NDEG(I) MAY BE 1 OR 2 OR 3.
C     *
      IMPLICIT DOUBLE PRECISION (A-H,O-Z)
      CHARACTER*2 NEL(7)
      COMMON /NBFILE/ NBF(20)
      DIMENSION AMS(NUMAT),COORD(3,NUMAT),C(LM2,LM2),EIG(LM2)
      DIMENSION ISYM(3,NUMAT),NDEG(LM2)
      DIMENSION IEL(3),IS(3)
      DATA NEL/'YZ','XZ','XY','X ','Y ','Z ','I '/
      DATA TEN/10.0D0/
C     TWO HARMONIC FREQUENCIES ARE CONSIDERED TO BE EQUAL IF THEY
C     DIFFER BY LESS THAN SMALL (IN CM-1).
      DATA SMALL/0.5D0/
      DO 10 I=1,N
      NDEG(I)= 1
   10 CONTINUE
      DO 20 I=N,7,-1
      IF(NDEG(I).GT.1) GO TO 20
      IF((EIG(I)-EIG(I-1)).GT.SMALL) GO TO 20
      IF((EIG(I)-EIG(I-2)).GT.SMALL) THEN
         NDEG(I)   = 2
         NDEG(I-1) = 2
      ELSE
         NDEG(I)   = 3
         NDEG(I-1) = 3
         NDEG(I-2) = 3
      ENDIF
   20 CONTINUE
C *** FIND MOLECULAR SYMMETRY FROM CARTESIAN COORDINATES.
      IF(LPRINT.LT.-2) RETURN
      CALL ASSYM (NUMAT,AMS,COORD,ISUB,MSYM,IEL,ISYM,LPRINT)
      IF(ISUB.EQ.0 .OR. MSYM.EQ.0) RETURN
C *** FILE NUMBERS.
      NB6    = NBF(6)
C *** LOOP OVER MODES.
      WRITE(NB6,500) (NEL(IEL(M)),M=1,MSYM)
      DO 40 I=1,N
C     LOOP OVER SYMMETRY OPERATIONS.
      DO 30 M=1,MSYM
      MEL    = IEL(M)
      CALL BSYM(C(1,I),ISYM,LM2,NUMAT,M,MEL,ISS,IER,TEN)
      IS(M)  = ISS
   30 CONTINUE
      WRITE(NB6,510) I,EIG(I),(IS(M),M=1,MSYM)
   40 CONTINUE
      RETURN
  500 FORMAT (///5X,'BEHAVIOR WITH RESPECT TO SYMMETRY OPERATIONS',
     1        // 5X,'VECTOR    FREQUENCY',6X,A2,2(4X,A2))
  510 FORMAT (   4X,I5,F15.5,I8,2I6)
      END
