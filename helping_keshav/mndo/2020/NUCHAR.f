      SUBROUTINE NUCHAR (LINE,VALUE,NVALUE)
C     *
C     STRING HANDLING ROUTINE FOR MOPAC INPUT.
C     ADAPTED FROM MOPAC(6.0) WRITTEN BY J.J.P.STEWART.
C     *
C     NUCHAR  DETERMINES AND RETURNS THE VALUES OF ALL NUMBERS
C             FOUND IN 'LINE'. ALL CONNECTED SUBSTRINGS ARE ASSUMED
C             TO CONTAIN NUMBERS
C     INPUT   LINE  = CHARACTER STRING
C     OUTPUT  VALUE = ARRAY OF NVALUE DOUBLE PRECISION VALUES
C     *
      IMPLICIT DOUBLE PRECISION (A-H,O-Z)
      DIMENSION VALUE(40),ISTART(40)
      CHARACTER*(*) LINE
      CHARACTER*1 TAB,COMMA,SPACE
      LOGICAL LEADSP
      SAVE COMMA,SPACE
      DATA COMMA,SPACE/',',' '/

      L = len(line)

C     CLEAN OUT TABS AND COMMAS
      TAB    = CHAR(9)
      DO 10 I=1,L
      IF(LINE(I:I).EQ.TAB .OR. LINE(I:I).EQ.COMMA) LINE(I:I)=SPACE
   10 CONTINUE
C     FIND INITIAL DIGIT OF ALL NUMBERS, CHECK FOR LEADING SPACES
C     FOLLOWED BY A CHARACTER
      LEADSP = .TRUE.
      NVALUE = 0
      DO 20 I=1,L
         IF(LEADSP .AND. LINE(I:I).NE.SPACE) THEN
            NVALUE=NVALUE+1
            ISTART(NVALUE)=I
         ENDIF
         LEADSP=(LINE(I:I).EQ.SPACE)
   20 CONTINUE
C     FILL NUMBER ARRAY
      DO 30 I=1,NVALUE
      VALUE(I)=READA(LINE,ISTART(I),0)
   30 CONTINUE
      RETURN
      END
