      SUBROUTINE REPAM1 (NI,NJ,R,ENUCLR)
C     *
C     CORE REPULSION FUNCTION FOR AM1 AND PM3.
C     *
C     THE CONTRIBUTIONS FROM THE GAUSSIAN TERMS ARE ADDED.
C     THE CASE NI=0 OR NJ=0 CORRESPONDS TO THE INTERACTION
C     WITH AN EXTERNAL UNIT CHARGE (WITHOUT GAUSSIAN TERMS).
C     *
      USE LIMIT, ONLY: LMZ
      IMPLICIT DOUBLE PRECISION (A-H,O-Z)
      COMMON
     ./AMPGAU/ FN1(LMZ,4),FN2(LMZ,4),FN3(LMZ,4),GSCAL(LMZ),IMP(LMZ)
     ./CONSTN/ ZERO,ONE,TWO,THREE,FOUR,PT5,PT25
     ./INOPT2/ IN2(300)
     ./PARDER/ TORE(LMZ),EHEAT(LMZ),EISOL(LMZ)
      DIMENSION BORON1(3,4),BORON2(3,4),BORON3(3,4)
      DATA BORON1/  0.182613D0,  0.118587D0, -0.073280D0,
     1              0.412253D0, -0.149917D0,  0.000000D0,
     2              0.261751D0,  0.050275D0,  0.000000D0,
     3              0.359244D0,  0.074729D0,  0.000000D0/
      DATA BORON2/  6.0D0,  6.0D0,  5.0D0,
     1             10.0D0,  6.0D0,  0.0D0,
     2              8.0D0,  5.0D0,  0.0D0,
     3              9.0D0,  9.0D0,  0.0D0/
      DATA BORON3/  0.727592D0,  1.466639D0,  1.570975D0,
     1              0.832586D0,  1.186220D0,  0.000000D0,
     2              1.063995D0,  1.936492D0,  0.000000D0,
     3              0.819351D0,  1.574414D0,  0.000000D0/
      DATA CUTOFF/25.0D0/
C *** SPECIAL CHECK FOR AM1/d, ATOM PAIRS INVOLVING MOLYBDENUM.
      IOP = IN2(2)
      IF(IOP.EQ.-12 .AND. (NI.EQ.42 .OR. NJ.EQ.42)) RETURN
C *** INITIALIZATION.
      IF(NI.GT.0) THEN
         TORENI = TORE(NI)
      ELSE
         TORENI = ONE
      ENDIF
      IF(NJ.GT.0) THEN
         TORENJ = TORE(NJ)
      ELSE
         TORENJ = ONE
      ENDIF
      VIJ    = TORENI*TORENJ/R
      ADD    = ZERO
C *** SPECIAL SECTION FOR AM1, ATOM PAIRS INVOLVING BORON.
      IF((IOP.EQ.-2.OR.IOP.EQ.-12) .AND. (NI.EQ.5.OR.NJ.EQ.5)) THEN
         NK  = NI+NJ-5
         NL  = 1
         IF(NK.EQ.1) NL=2
         IF(NK.EQ.6) NL=3
         IF(NK.EQ.9.OR.NK.EQ.17.OR.NK.EQ.35.OR.NK.EQ.53) NL=4
         DO 10 I=1,3
         FN1(5,I)=BORON1(I,NL)
         FN2(5,I)=BORON2(I,NL)
         FN3(5,I)=BORON3(I,NL)
   10    CONTINUE
         IMP(5)=3
      ENDIF
C *** GENERAL SECTION.
      IF(NI.GT.0) THEN
         IMX = IMP(NI)
C$DIR SCALAR
*VDIR NOVECTOR
*VOCL LOOP,SCALAR
         DO 20 IG=1,IMX
         XX  = FN2(NI,IG)*(R-FN3(NI,IG))**2
         IF(XX.LT.CUTOFF) ADD = ADD+VIJ*FN1(NI,IG)*EXP(-XX)
   20    CONTINUE
      ENDIF
      IF(NI.EQ.NJ) THEN
         ADD = ADD+ADD
      ELSE IF(NJ.GT.0) THEN
         IMX = IMP(NJ)
C$DIR SCALAR
*VDIR NOVECTOR
*VOCL LOOP,SCALAR
         DO 30 IG=1,IMX
         XX  = FN2(NJ,IG)*(R-FN3(NJ,IG))**2
         IF(XX.LT.CUTOFF) ADD = ADD+VIJ*FN1(NJ,IG)*EXP(-XX)
   30    CONTINUE
      ENDIF
      GSC    = 1.D0
      IF(NI.GT.0)  GSC = GSC*GSCAL(NI)
      IF(NJ.GT.0)  GSC = GSC*GSCAL(NJ)
      ENUCLR = ENUCLR+GSC*ADD
      RETURN
      END
