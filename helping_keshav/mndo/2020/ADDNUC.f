      SUBROUTINE ADDNUC (ENUCLR,D2,D,LMD2,LMQ)
C     *
C     ADD COSMO CONTRIBUTIONS TO THE CORE-CORE REPULSION ENERGY.
C     *
C     NOTATION.  I=INPUT, O=OUTPUT.
C     ENUCLR     CORE-CORE REPULSION ENERGY (I,O).
C     D2(LMD2)   PACKED DIELECTRIC OPERATOR MATRIX (I).
C     D(LMQ,LMQ) SQUARE DIELECTRIC OPERATOR MATRIX (I).
C     *
      USE LIMIT, ONLY: LM1, LMI, LMZ
      IMPLICIT DOUBLE PRECISION (A-H,O-Z)
      COMMON
     ./ATOMS / NUMAT,NAT(LM1),NFIRST(LM1),NLAST(LM1)
     ./COSMO7/ JPC(LMI+LM1+1)
     ./INOPT2/ IN2(300)
     ./PARDER/ TORE(LMZ),EHEAT(LMZ),EISOL(LMZ)
      DIMENSION D2(LMD2),D(LMQ,LMQ)
C *** INITIALIZATION.
      MODCSM = IN2(235)
      LM6    = LMQ-NUMAT
      ENCLR  = 0.D0
C     CODE FOR PACKED DIELECTRIC OPERATOR MATRIX.
      IF(MODCSM.GT.0) THEN
         DO 20 I=1,NUMAT
         ID     = JPC(LM6+I)+LM6
         TORENI = TORE(NAT(I))
         DO 10 J=1,I-1
         ENCLR  = ENCLR - TORENI*D2(ID+J)*TORE(NAT(J))
   10    CONTINUE
         ENCLR  = ENCLR - 0.5D0*TORENI*D2(ID+I)*TORENI
   20    CONTINUE
C     CODE FOR SQUARE DIELECTRIC OPERATOR MATRIX.
      ELSE
         DO 40 I=1,NUMAT
         II     = LM6+I
         TORENI = TORE(NAT(I))
         DO 30 J=1,I-1
         ENCLR  = ENCLR - TORENI*D(LM6+J,II)*TORE(NAT(J))
   30    CONTINUE
         ENCLR  = ENCLR - 0.5D0*TORENI*D(II,II)*TORENI
   40    CONTINUE
      ENDIF
      ENUCLR = ENUCLR+ENCLR
      RETURN
      END
