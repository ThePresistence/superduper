      SUBROUTINE ECPDEF (JPRINT)
C     *
C     DEFINITION OF EFFECTIVE CORE POTENTIALS (ECP).
C     *
C     NOTATION. I=INPUT, O=OUTPUT.
C     JPRINT    PRINTING FLAG (I).
C     *
C     RESULTS ARE SAVED IN COMMON BLOCK ECPCM (O).
C     THE RELEVANT ARRAY ALWAYS REFER TO THE WHOLE MOLECULE.
C     NLP()      EXPONENTS OF R IN ECP EXPANSIONS.
C     ZLP()      EXPONENTS OF GAUSSIAN TERMMS IN ECP EXPANSIONS.
C     CLP()      COEFFICIENTS IN ECP EXPANSIONS.
C     KFIRST()   FIRST ENTRY FOR A GIVEN ECP OPERATOR.
C     KLAST()    LAST  ENTRY FOR A GIVEN ECP OPERATOR.
C     LFIRST(NI) FIRST ENTRY FOR A GIVEN ELEMENT WITH ATOMIC NUMBER NI.
C     LMAX(NI)   HIGHEST L QUANTUM NUMBER IN CORE PLUS ONE, FOR A GIVEN
C                ELEMENT WITH ATOMIC NUMBER NI, E.G. 1 FOR AN S CORE.
C     *
C     SIMPLIFICATIONS COMPARED WITH OUR AB INITIO ECP CODE:
C     - RADIAL INTEGRALS ARE ALWAYS EVALUATED BY THE KAHN FORMALISM.
C     - ONLY PREDEFINED ECP POTENTIALS ARE AVAILABLE (NO INPUT).
C     - SOME INITIALIZATIONS ARE INCLUDED HERE.
C     *
      USE LIMIT, ONLY: LM1, LMZ, LMK, LML
      IMPLICIT DOUBLE PRECISION (A-H,O-Z)
C     LML - MAXIMUM NUMBER OF ECP OPERATORS PER MOLECULE
C     LMK - MAXIMUM NUMBER OF ECP DATA SET PARAMETERS PER MOLECULE
      CHARACTER*4 LLABEL(5)
      COMMON
     ./ATOMS / NUMAT,NAT(LM1),NFIRST(LM1),NLAST(LM1)
     ./ECPCM / CLP(LMK),ZLP(LMK),NLP(LMK),KFIRST(LML),KLAST(LML),
     .         LFIRST(LMZ),LMAX(LMZ)
     ./NBFILE/ NBF(20)
      DIMENSION NBFP(5),NLPP(40),ZLPP(40),CLPP(40)
      DIMENSION NCNTR(LMZ)
      DATA LLABEL/'LMAX','  S ','  P ','  D ','  F '/
C *** FILE NUMBERS.
      NB6    = NBF(6)
C *** GENERAL INITIALIZATION.
      DO 10 I=1,LMZ
      LFIRST(I) = 0
      LMAX(I)   = 0
      NCNTR(I)  = 0
   10 CONTINUE
C *** INITIALIZATION FOR RADIAL INTEGRALS..
C     KAHN FORMALISM (ONLY OPTION PROVIDED).
      CALL DAWT
      CALL ERRT
      CALL DAWERT
C *** INTERNAL DEFINITION OF EFFECTIVE POTENTIALS.
      LECP   = 0
      KECP   = 0
      NIMAX  = 0
      DO 40 I=1,NUMAT
      NI     = NAT(I)
      IF(NI.LE.2 .OR. NCNTR(NI).EQ.1) GO TO 40
      CALL ECPPOT (NI,LSKIP,LMAXP,NECORE,NBFP,NLPP,ZLPP,CLPP)
      IF(LSKIP.EQ.1) GO TO 40
      LF     = LECP+1
      LL     = LF+LMAXP
      LECP   = LL
      IF(LECP.GT.LML) GO TO 410
      NCNTR(NI)  = 1
      LFIRST(NI) = LF
      LMAX(NI)   = LMAXP
C     ADD A NEW ECP DATA SET (NLP,ZLP,CLP).
      NIMAX  = MAX(NI,NIMAX)
      KPP    = 0
      DO 30 L=LF,LL
      KF     = KECP+1
      KL     = KECP+NBFP(L-LF+1)
      KECP   = KL
      IF(KECP.GT.LMK) GO TO 420
      KFIRST(L) = KF
      KLAST(L)  = KL
      DO 20 K=KF,KL
      KPP    = KPP+1
      NLP(K) = NLPP(KPP)
      ZLP(K) = ZLPP(KPP)
      CLP(K) = CLPP(KPP)
   20 CONTINUE
   30 CONTINUE
   40 CONTINUE
C *** PRINTING SECTION.
C     PRINT THE ECP PARAMETERS FOR EACH ELEMENT WITH AN ECP.
      IF(JPRINT.LT.2 .OR. NIMAX.EQ.0) RETURN
      WRITE(NB6,500)
      WRITE(NB6,510)
      DO 80 NI=3,NIMAX
      IF(NCNTR(NI).EQ.0) GO TO 80
      WRITE(NB6,520)
      LF     = LFIRST(NI)
      LL     = LF+LMAX(NI)
      DO 70 L=LF,LL
      KF     = KFIRST(L)
      KL     = KLAST(L)
      DO 60 K=KF,KL
      WRITE(NB6,530) K,NI,LLABEL(L-LF+1),K-KF+1,NLP(K),ZLP(K),CLP(K)
   60 CONTINUE
   70 CONTINUE
   80 CONTINUE
C     PRINT TECHNICAL AND CUTOFF INFORMATION.
      IF(JPRINT.GE.5) THEN
         WRITE(NB6,540)
      ENDIF
      RETURN
C *** ERROR EXITS
  410 WRITE(NB6,610) LML
      STOP 'ECPDEF'
  420 WRITE(NB6,620) LMK
      STOP 'ECPDEF'
  500 FORMAT(///1X,'DEFINITION OF EFFECTIVE CORE POTENTIALS')
  510 FORMAT(// 1X,'OVERALL  ATOMIC  TYPE(L)  LOCAL  EXPONENT      ',
     1             'EXPONENT          COEFFICIENT',
     2       /  1X,' INDEX   NUMBER  OF ECP   INDEX    NKL         ',
     3             '  ZETA                DKL')
  520 FORMAT(   1X)
  530 FORMAT(   1X,I5,I8,5X,A4,I7,I8,F19.8,F20.8)
  540 FORMAT(/  1X,'RADIAL ECP INTEGRALS BASED ON KAHN FORMULAS ' )
  610 FORMAT(// 1X,'TOO MANY ECP OPERATORS OVERALL.',
     1       /  1X,'THE MAXIMUM NUMBER IS',I10,//)
  620 FORMAT(// 1X,'TOO MANY ECP DATA SETS OVERALL.',
     1       /  1X,'THE MAXIMUM NUMBER IS',I10,//)
      END
