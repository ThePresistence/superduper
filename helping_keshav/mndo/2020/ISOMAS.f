      SUBROUTINE ISOMAS (KMASS,IMASS,NUMAT,NAT,LPRINT,IFORM,AMSUM)
C     *
C     DEFINITION OF ATOMIC MASSES.
C     *
      USE LIMIT, ONLY: LM1, LMZ
      IMPLICIT DOUBLE PRECISION (A-H,O-Z)
      COMMON
     ./AMASS / AMS(LM1)
     ./ISTOPE/ CMS(LMZ),BMS(LMZ)
     ./NBFILE/ NBF(20)
      DIMENSION IMASS(NUMAT),NAT(NUMAT)
C *** FILE NUMBERS.
      NB5    = NBF(5)
      NB6    = NBF(6)
C     *
C     ASSIGN DEFAULT VALUES FOR ATOMIC MASSES.
C     KMASS.LT.0. ATOMIC WEIGHTS (CMS), DEFINED IN BLOCK0.
C     KMASS.GT.0. MASSES OF PRINCIPAL ISOTOPE (BMS), DEFINED IN BLOCK0.
C     KMASS.EQ.0. DEFAULT VALUES (AMS) HAVE BEEN DEFINED PREVIOUSLY.
C     - STANDARD. MASSES OF PRINCIPAL ISOTOPE (BMS), DEFINED IN ATMASS.
C     - MOPAC   . ATOMIC WEIGHTS (CMS) OR DATA FROM INPUT.
      IF(KMASS.LT.0) THEN
         DO 10 I=1,NUMAT
         AMS(I) = CMS(NAT(I))
   10    CONTINUE
      ELSE IF(KMASS.GT.0) THEN
         DO 15 I=1,NUMAT
         AMS(I) = BMS(NAT(I))
   15    CONTINUE
      ENDIF
      AMSUM  = 0.0D0
      DO 20 I=1,NUMAT
      AMSUM  = AMSUM+AMS(I)
   20 CONTINUE
      IF(KMASS.LE.0) GO TO 320
C     *
C     READ ISOTOPE NUMBERS AND ASSIGN CORRESPONDING ATOMIC MASSES.
      IF(IFORM.LE.0) THEN
         READ(NB5,510) IMASS
      ELSE
         READ(NB5,*)   IMASS
      ENDIF
      DO 300 I=1,NUMAT
      MI     = IMASS(I)
      NI     = NAT(I)
      ISO    = NINT(BMS(NI))
      IF(MI.LE.0) MI=ISO
      IF(MI.EQ.ISO) GO TO 300
      IF(NI.GT.18)  GO TO 300
      GO TO (30,200,40,200,50,60,70,80,200,200,200,90,200,100,200,
     1       110,120,200), NI
C     HYDROGEN.
   30 IF(MI.EQ.2) AMS(I)=2.01400D0
      IF(MI.EQ.3) AMS(I)=3.01605D0
      IF(MI.LT.1 .OR. MI.GT.3) GO TO 200
      GO TO 300
C     LITHIUM.
   40 IF(MI.EQ.6) AMS(I)=6.01512D0
      IF(MI.LT.6 .OR. MI.GT.7) GO TO 200
      GO TO 300
C     BORON.
   50 IF(MI.EQ.10) AMS(I)=10.01290D0
      IF(MI.LT.10 .OR. MI.GT.11) GO TO 200
      GO TO 300
C     CARBON.
   60 IF(MI.EQ.13) AMS(I)=13.00335D0
      IF(MI.LT.12 .OR. MI.GT.13) GO TO 200
      GO TO 300
C     NITROGEN.
   70 IF(MI.EQ.15) AMS(I)=15.00011D0
      IF(MI.LT.14 .OR. MI.GT.15) GO TO 200
      GO TO 300
C     OXYGEN.
   80 IF(MI.EQ.17) AMS(I)=16.99913D0
      IF(MI.EQ.18) AMS(I)=17.99916D0
      IF(MI.LT.16 .OR. MI.GT.18) GO TO 200
      GO TO 300
C     MAGNESIUM.
   90 IF(MI.EQ.25) AMS(I)=24.98584D0
      IF(MI.EQ.26) AMS(I)=25.98259D0
      IF(MI.LT.24 .OR. MI.GT.26) GO TO 200
      GO TO 300
C     SILICON.
  100 IF(MI.EQ.29) AMS(I)=28.97649D0
      IF(MI.EQ.30) AMS(I)=29.97376D0
      IF(MI.LT.28 .OR. MI.GT.30) GO TO 200
      GO TO 300
C     SULFUR.
  110 IF(MI.EQ.33) AMS(I)=32.97146D0
      IF(MI.EQ.34) AMS(I)=33.96786D0
      IF(MI.LT.32 .OR. MI.GT.34) GO TO 200
      GO TO 300
C     CHLORINE.
  120 IF(MI.EQ.37) AMS(I)=36.96590D0
      IF(MI.NE.35 .AND. MI.NE.37) GO TO 200
      GO TO 300
C     ERROR SECTION.
  200 WRITE(NB6,520) I,NI,MI
  300 CONTINUE
      AMSUM  = 0.0D0
      DO 310 I=1,NUMAT
      AMSUM  = AMSUM+AMS(I)
  310 CONTINUE
C     *
C *** PRINTING SECTION.
  320 IF(LPRINT.LT.-4) RETURN
      IF(LPRINT.LE.0 .AND. KMASS.LE.0) RETURN
      IF(KMASS.EQ.0) WRITE(NB6,400)
      IF(KMASS.LT.0) WRITE(NB6,500)
      WRITE(NB6,530)
      DO 330 I=1,NUMAT
      MI     = IMASS(I)
      NI     = NAT(I)
      IF(MI.LE.0) MI=NINT(BMS(NI))
      WRITE(NB6,540) I,NI,MI,AMS(I)
  330 CONTINUE
      RETURN
  400 FORMAT(///5X,'DEFAULT MASSES ARE USED FOR ALL ATOMS.')
  500 FORMAT(///5X,'AVERAGE ATOMIC WEIGHTS ARE USED AS ATOMIC MASSES.')
  510 FORMAT(20I4)
  520 FORMAT(// 5X,'THE MASS OF ATOM',I3,' WITH ATOMIC NUMBER',I3,
     1             ' AND ISOTOPIC NUMBER',I3,' IS NOT PREDEFINED.',
     2       /  5X,'THE MASS OF THE PRINCIPAL ISOTOPE IS USED.')
  530 FORMAT(///5X,'ATOMIC MASSES FOR THE VIBRATIONAL ANALYSIS.',
     1       // 5X,'I',4X,'NI',4X,'MI',6X,'MASS(AMU)'/)
  540 FORMAT(   1X,I5,2I6,F15.5)
      END
