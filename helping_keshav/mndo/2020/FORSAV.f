      SUBROUTINE FORSAV (A,APT,CG,DMX,LMA,LMAPT,I3N,NCOL,MODE,MIDDLE)
C     *
C     RESTART INFORMATION FOR FORCE CONSTANTS ON FILE NB4.
C     *
      IMPLICIT DOUBLE PRECISION (A-H,O-Z)
      PARAMETER (ID4=2)
      COMMON
     ./ERGREF/ H298,H0,HF298,HF0,HAT298,HAT0,D00,EVIB,SCFENR
     ./FORCE3/ TMA,TMF
     ./NBFILE/ NBF(20)
      DIMENSION A(LMA),APT(LMAPT),CG(I3N),DMX(3)
C *** FILE NUMBERS.
      NB4    = NBF(4)
      NB6    = NBF(6)
C *** MODE=-1. READ RESTART INFORMATION.
      IF(MODE.GE.0) GO TO 10
      IF(MIDDLE.LT.0) RETURN
      REWIND NB4
      READ(NB4,ERR=100,END=200) IDREAD
      IF(IDREAD.NE.ID4) GO TO 300
      READ(NB4,ERR=100,END=200) A
      READ(NB4,ERR=100,END=200) APT
      READ(NB4,ERR=100,END=200) NCOL,TMA,TMF,SCFENR
      RETURN
C *** MODE=1-I3N. UPDATE FORCE CONSTANTS AND DIPOLE DERIVATIVES.
   10 IF(MODE.EQ.0) GO TO 40
      IF(MIDDLE.GE.0) THEN
         REWIND NB4
         READ(NB4) IDREAD
         READ(NB4) A
         READ(NB4) APT
      ENDIF
      ISTART = I3N*(MODE-1)
      DO 20 I=1,I3N
      A(ISTART+I) = CG(I)
   20 CONTINUE
      DO 30 I=1,3
      J      = I3N*(I-1)+MODE
      APT(J) = DMX(I)
   30 CONTINUE
C *** MODE=0-I3N. SAVE RESTART INFORMATION.
   40 IF(MIDDLE.LT.0) RETURN
      REWIND NB4
      WRITE(NB4) ID4
      WRITE(NB4) A
      WRITE(NB4) APT
      WRITE(NB4) NCOL,TMA,TMF,SCFENR
      RETURN
C *** ERRORS EXITS.
  100 WRITE(NB6,500)
      STOP 'FORSAV'
  200 WRITE(NB6,510)
      STOP 'FORSAV'
  300 WRITE(NB6,500)
      WRITE(NB6,520) IDREAD,ID4
      STOP 'FORSAV'
  500 FORMAT(///1X,'ERROR WHEN READING RESTART FILE',
     1       /  1X,'FOR FORCE CONSTANT CALCULATION',
     2       /  1X,'IN SUBROUTINE FORSAV.'/)
  510 FORMAT(///1X,'END-OF-FILE ENCOUNTERED WHEN READING RESTART FILE',
     1       /  1X,'FOR FORCE CONSTANT CALCULATION',
     2       /  1X,'IN SUBROUTINE FORSAV.'/)
  520 FORMAT(   1X,'WRONG TYPE OF RESTART FILE: IDREAD =',I3,
     1       /  1X,'EXPECTED VALUE OF LABEL   : IDREAD =',I3,/)
      END
