      SUBROUTINE QMMDR1 (PA,PB,LM4)
C     *
C     FAST GRADIENTS IN CARTESIAN COORDINATES BY FINITE DIFFERENCE.
C     CONTRIBUTIONS FROM ELECTROSTATIC QM/MM INTERACTIONS TO THE
C     GRADIENT ARE INCLUDED FOR QM AND MM ATOMS IN CG(K,I).
C     QM ATOMS: I = 1,...,NUMAT.
C     MM ATOMS: I = NUMAT+1,...,NUMAT+NUMATM.
C     *
C     SPECIAL VERSION FOR OPTION MMPOT=1.
C     *
      USE LIMIT, ONLY: LM1, LMX, LMZ, LM1M
      IMPLICIT DOUBLE PRECISION (A-H,O-Z)
      COMMON
     ./ATOMC / COORD(3,LM1)
     ./ATOMS / NUMAT,NAT(LM1),NFIRST(LM1),NLAST(LM1)
     ./CGRAD / CG(3,LM1+LM1M)
     ./CONSTF/ A0,AFACT,EV,EVCAL,PI,W1,W2,BIGEXP
     ./CONSTN/ ZERO,ONE,TWO,THREE,FOUR,PT5,PT25
     ./INDEX / INDX(LMX)
     ./INOPT2/ IN2(300)
     ./PARDER/ TORE(LMZ),EHEAT(LMZ),EISOL(LMZ)
     ./QMMM1 / COORDM(3,LM1M),CHARGM(LM1M)
     ./QMMM2 / LINK(LM1)
      DIMENSION PA(LM4),PB(LM4)
      DIMENSION DELX(2),ETOT(3)
      DIMENSION XCOORD(3,2)
C *** INPUT OPTIONS.
      IOP    = IN2(2)
      NUMATM = IN2(120)
      MMCOUP = IN2(121)
      MMPOT  = IN2(122)
      MMLINK = IN2(123)
C *** INITIALIZATION.
      CG(1:3,NUMAT+1:NUMAT+NUMATM) = ZERO
      IF(MMPOT.NE.1) RETURN
      IF(MMCOUP.LT.2) RETURN
      DEL    = 1.0D-06
      RDEL   = 5.0D+05
      DELX(1)= DEL
      DELX(2)=-DEL
C *** LOOP OVER QM ATOMS (I).
      DO 90 I=1,NUMAT
      IF(MMLINK.EQ.1 .AND. LINK(I).GT.0) GO TO 90
      NI     = NAT(I)
      IA     = NFIRST(I)
      IB     = NLAST(I)
      XCOORD(1,2) = COORD(1,I)
      XCOORD(2,2) = COORD(2,I)
      XCOORD(3,2) = COORD(3,I)
C     SUM OF DIAGONAL DENSITY MATRIX ELEMENTS (ATOM I).
      QSUM   = ZERO
      DO 20 K=IA,IB
      KK     = INDX(K)+K
      QSUM   = QSUM+PA(KK)+PB(KK)
   20 CONTINUE
C     WRITE(*,*) 'I,NI,IA,IB,QSUM',I,NI,IA,IB,QSUM
C *** LOOP OVER MM ATOMS (M).
      DO 80 M=1,NUMATM
      PTCHG       = CHARGM(M)
      XCOORD(1,1) = COORDM(1,M)
      XCOORD(2,1) = COORDM(2,M)
      XCOORD(3,1) = COORDM(3,M)
C *** LOOP OVER THREE CARTESIAN COORDINATES (K).
      DO 70 K=1,3
C *** LOOP OVER DISPLACEMENTS (L).
      DO 60 L=1,2
      XCOORD(K,2) = COORD(K,I)+DELX(L)
C     DISTANCE R (AU) AND MONOPOLE INTERACTION TERM.
      R      = SQRT( (XCOORD(1,1)-XCOORD(1,2))**2
     1              +(XCOORD(2,1)-XCOORD(2,2))**2
     2              +(XCOORD(3,1)-XCOORD(3,2))**2 ) / A0
      TEMP   =  EV/R
C     CONTRIBUTIONS FROM THE CORE HAMILTONIAN.
      EH     = -TEMP*PTCHG*QSUM
C     CONTRIBUTIONS FROM THE CORE-CORE REPULSIONS.
      ENUC   =  TEMP*PTCHG*TORE(NI)
C     TOTAL CONTRIBUTION.
      ETOT(L)  = EH+ENUC
      XCOORD(K,2) = COORD(K,I)
C     WRITE(*,*) 'I,M,K,L,EH,ENUC,QSUM',I,M,K,L,EH,ENUC,QSUM
   60 CONTINUE
      DERQ     = (ETOT(1)-ETOT(2))*EVCAL*RDEL
      CG(K,I)  = CG(K,I) + DERQ
      CG(K,NUMAT+M) = CG(K,NUMAT+M) - DERQ
   70 CONTINUE
   80 CONTINUE
   90 CONTINUE
      RETURN
      END
