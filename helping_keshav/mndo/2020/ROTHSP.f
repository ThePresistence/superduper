      SUBROUTINE ROTHSP (IA,IP,R,COORD,CORE,H,LMH)
C     *
C     CONTRIBUTIONS TO THE CORE HAMILTONIAN FROM ELECTROSTATIC
C     INTERACTIONS BETWEEN AN ATOM (I=1) WITH SP BASIS FUNCTIONS
C     AND AN EXTERNAL POINT CHARGE (J=2) FOR QM/MM TREATMENTS.
C     *
C     THIS ROUTINE COMPUTES THE ROTATION MATRIX FOR I-J FROM THE
C     GIVEN CARTESIAN COORDINATES, TRANSFORMS THE GIVEN CORE ELECTRON
C     ATTRACTION INTEGRALS FROM LOCAL TO MOLECULAR COORDINATES, AND
C     INCLUDES THEM IN THE CORE HAMILTONIAN.
C     *
C     NOTATION. I=INPUT, O=OUTPUT.
C     IA        INDEX OF FIRST BASIS ORBITAL AT ATOM I (I).
C     IP        INDEX FOR (S,S) OF ATOM I IN LINEAR ARRAY H (I).
C     R         INTERATOMIC DISTANCE, IN ATOMIC UNITS (I).
C     COORD()   CARTESIAN COORDINATES, IN ANGSTROM (I).
C     CORE()    LOCAL CORE ELECTRON ATTRACTION INTEGRALS (I).
C     H(LMH)    CORE HAMILTONIAN MATRIX (O).
C     *
C     DEPENDING ON THE INPUT DATA IN THE ARGUMENT LIST, THE INTEGRALS
C     ARE INCLUDED EITHER IN THE FULL CORE HAMILTONIAN H(LM4) OR IN
C     ITS ONE-CENTER PART H(LM6) OR IN A LOCAL ARRAY.
C                    FIRST CASE      SECOND CASE     THIRD CASE
C     CALL FROM      QMINT           QMINT           QMMDER
C     IA             NFIRST(I)       1               1
C     IP             INDX(IA)+IA     NW(I)           1
C     LMH            LM4             LM6             45
C     *
C     THIS SPECIAL ROUTINE IS DERIVED FROM ROTMAT AND ROTCOH.
C     *
      IMPLICIT DOUBLE PRECISION (A-H,O-Z)
      PARAMETER (SMALL=1.0D-07)
      PARAMETER (PT5SQ3=0.866025403784438647D0)
      COMMON
     ./CONSTF/ A0,AFACT,EV,EVCAL,PI,W1,W2,BIGEXP
     ./CONSTN/ ZERO,ONE,TWO,THREE,FOUR,PT5,PT25
      DIMENSION COORD(3,2),CORE(10,2),H(LMH)
C *** CONVERT DISTANCE FROM ATOMIC UNITS TO ANGSTROM.
      RIJ    = R*A0
C *** CALCULATE GEOMETRIC DATA FROM CARTESIAN COORDINATES.
C     CA  = COS(PHI)    , SA  = SIN(PHI)
C     CB  = COS(THETA)  , SB  = SIN(THETA)
C     C2A = COS(2*PHI)  , S2A = SIN(2*PHI)
C     C2B = COS(2*THETA), S2B = SIN(2*PHI)
      X11    = COORD(1,1)-COORD(1,2)
      X22    = COORD(2,1)-COORD(2,2)
      X33    = COORD(3,1)-COORD(3,2)
      B      = X11*X11+X22*X22
      SQB    = SQRT(B)
      SB     = SQB/RIJ
C     CHECK FOR SPECIAL CASE (BOTH ATOMS ON Z AXIS).
      IF(SB.GT.SMALL) THEN
         CA  = X11/SQB
         SA  = X22/SQB
         CB  = X33/RIJ
      ELSE
         SA  = ZERO
         SB  = ZERO
         IF(X33.LT.ZERO) THEN
            CA  =-ONE
            CB  =-ONE
         ELSE IF(X33.GT.ZERO) THEN
            CA  = ONE
            CB  = ONE
         ELSE
            CA  = ZERO
            CB  = ZERO
         ENDIF
      ENDIF
C *** CALCULATE ROTATION MATRIX ELEMENTS.
      P11 = CA*SB
      P21 = CA*CB
      P31 =-SA
      P12 = SA*SB
      P22 = SA*CB
      P32 = CA
      P13 = CB
      P23 =-SB
C *** TRANSFORM ONE-ELECTRON INTEGRALS AND INCLUDE THEM IN THE
C     CORE HAMILTONIAN.
C     S-S
      H(IP)  = H(IP)+CORE(1,1)
C     P-S
      IX     = IP+IA
      IY     = IX+IA+1
      IZ     = IY+IA+2
      H(IX)  = H(IX)+CORE(2,1)*P11
      H(IY)  = H(IY)+CORE(2,1)*P12
      H(IZ)  = H(IZ)+CORE(2,1)*P13
C     P-P
      H(IX+1)= H(IX+1)+CORE(3,1)*P11*P11+CORE(4,1)*(P21*P21+P31*P31)
      H(IY+1)= H(IY+1)+CORE(3,1)*P11*P12+CORE(4,1)*(P21*P22+P31*P32)
      H(IY+2)= H(IY+2)+CORE(3,1)*P12*P12+CORE(4,1)*(P22*P22+P32*P32)
      H(IZ+1)= H(IZ+1)+CORE(3,1)*P11*P13+CORE(4,1)* P21*P23
      H(IZ+2)= H(IZ+2)+CORE(3,1)*P12*P13+CORE(4,1)* P22*P23
      H(IZ+3)= H(IZ+3)+CORE(3,1)*P13*P13+CORE(4,1)* P23*P23
      RETURN
      END
