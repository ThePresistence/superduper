      SUBROUTINE PRTCSM (SUP,LMSUP)
C     *
C     PRINT DETAILED COSMO RESULTS.
C     *
C     NOTATION. I=INPUT, O=OUTPUT.
C     SUP       SUPERDELOCALIZABILITIES (I).
C     LMSUP     DIMENSION OF SUP (LARGER THAN NEEDED) (I).
C     *
      USE LIMIT, ONLY: LM1, LMZ, LMM, LMNPS
      IMPLICIT DOUBLE PRECISION (A-H,O-Z)
      PARAMETER (CONST1= 1.362611D0)
      PARAMETER (CONST2=-0.546934D0)
      PARAMETER (CONST3= 2.196084D0)
      PARAMETER (CONST4= 2.70D0)
      LOGICAL DISTOK,FLAG1,FLAG2,FLAG3
      COMMON
     ./ATOMC / COORD(3,LM1)
     ./ATOMS / NUMAT,NAT(LM1),NFIRST(LM1),NLAST(LM1)
     ./CONSTF/ A0,AFACT,EV,EVCAL,PI,W1,W2,BIGEXP
     ./CONSTN/ ZERO,ONE,TWO,THREE,FOUR,PT5,PT25
     ./COSMO1/ EGAS,ESOLUT,ESOLV,EDISP,EDIE
     ./COSMO4/ IATSP(LMNPS)
     ./COSMO5/ ATOMAR(LM1),AREA1,NPS1
     ./COSMO6/ CAVITA(LMZ),ATOMDI(LMZ),BONDDI(LMZ),BONDSP(LMZ),
     .         DELTA(LMZ),OMEGA(LMZ)
     ./COSMO8/ COSURF(3,LMNPS),AREASG(LMNPS),QSG(LMNPS)
     ./INOPT1/ IN1(300)
     ./INOPT2/ IN2(300)
     ./NBFILE/ NBF(20)
     ./PAR2  / MOL
     ./PAR26 / JOPREF(LMM),JCOSMO(LMM)
      DIMENSION SUP(LMSUP)
      DIMENSION IGROUP(10,4)
C *** FILE NUMBERS.
      NB6    = NBF(6)
C *** INPUT OPTIONS.
      INREFD = IN2(10)
      ICOSMO = IN2(28)
      NPRINT = IN2(72)
      NITRO  = IN2(234)
C *** SPECIFY TYPE OF COSMO CALCULATION.
      WRITE(NB6,500)
      IF(INREFD.NE.0) THEN
         IF(JCOSMO(MOL).LT.0) THEN
            WRITE(NB6,510)
         ELSE IF(JCOSMO(MOL).GT.0) THEN
            WRITE(NB6,520)
         ENDIF
      ELSE
         IF(IN1(28).LT.0) THEN
            WRITE(NB6,530)
         ELSE IF(IN1(28).GT.0) THEN
            WRITE(NB6,540)
         ENDIF
      ENDIF
      IF(ICOSMO.GT.1) THEN
         WRITE(NB6,550)
         IF(INREFD.NE.0 .OR. IN1(28).LT.0) THEN
            WRITE(NB6,551)
         ELSE
            WRITE(NB6,552)
         ENDIF
      ENDIF
C *** PRINT SUMMARY OF COSMO RESULTS.
      ESOLV  = ESOLUT+EDISP-EGAS
      WRITE(NB6,560) ESOLUT
      IF(EGAS.NE.ZERO) THEN
         WRITE(NB6,570) EGAS
         WRITE(NB6,580) ESOLV
         WRITE(NB6,590) ESOLUT-EGAS
      ENDIF
      IF(ICOSMO.GT.1) WRITE(NB6,600) EDISP
      WRITE(NB6,610) EDIE*EVCAL
      WRITE(NB6,620) AREA1
      WRITE(NB6,630) NPS1
C *** PRINT INFORMATION ON SURFACE SEGMENTS.
      IF(NPRINT.GT.0) THEN
         WRITE(NB6,631)
         SUMASG = ZERO
         SUMQSG = ZERO
         DO  5 I=1,NPS1
         SUMASG = SUMASG+AREASG(I)
         SUMQSG = SUMQSG+QSG(I)
         WRITE(NB6,632) I,IATSP(I),(COSURF(J,I),J=1,3),AREASG(I),QSG(I)
    5    CONTINUE
         WRITE(NB6,633) SUMASG,SUMQSG
      ENDIF
C *** PRINT MATRIX OF ATOM AND BOND SUPERDELOCALIZABILITIES.
      IF(ICOSMO.LE.1) GO TO 50
      WRITE(NB6,640)
      WRITE(NB6,650)
      CALL VECPRT (SUP,LMSUP,NUMAT)
C *** COMPUTE THE CONTRIBUTIONS TO THE SOLVATION ENERGY
C     DUE TO THE ATOM AND BOND SUPERDELOCALIZABILITIES.
      DO 10 I=1,NUMAT
      NI     = NAT(I)
      II     = I*(I+1)/2
      SUP(II)= SUP(II)*ATOMDI(NI)*ATOMAR(I)*1.0D-03
   10 CONTINUE
      DO 25 I=2,NUMAT
      ID     = I*(I-1)/2
      NI     = NAT(I)
      ATOMAI = ATOMAR(I)
      SQAI   = SQRT(ATOMAI)
      DO 20 J=1,I-1
      IJ     = ID+J
      NJ     = NAT(J)
      ATOMAJ = ATOMAR(J)
      BTERM  = SQAI*SQRT(ATOMAJ)*SUP(IJ)
      IF(((NI.EQ.7 .OR. NI.EQ.8) .AND. NJ.EQ.1) .OR.
     1   ((NJ.EQ.7 .OR. NJ.EQ.8) .AND. NI.EQ.1)) THEN
         DIST   = (COORD(1,I)-COORD(1,J))**2+
     1            (COORD(2,I)-COORD(2,J))**2+
     2            (COORD(3,I)-COORD(3,J))**2
         EXPON  = DIST+0.1D0*(ATOMAI+ATOMAJ)
         SUP(IJ)= BTERM*EXP(-EXPON)*(BONDSP(NI)+BONDSP(NJ))
      ELSE
         SUP(IJ)= BTERM*(BONDDI(NI)+BONDDI(NJ))*1.0D-03
      ENDIF
   20 CONTINUE
   25 CONTINUE
C *** PRINT DISPERSION CONTRIBUTIONS IN MATRIX FORM.
      WRITE(NB6,660)
      CALL VECPRT (SUP,LMSUP,NUMAT)
C *** PRINT CAVITATION AND DISPERSION CONTRIBUTIONS PER ATOM.
      WRITE(NB6,670)
      WRITE(NB6,680)
      WRITE(NB6,690)
      SUM1   = ZERO
      SUM2   = ZERO
      SUM3   = ZERO
      DO 40 I=1,NUMAT
      ID     = I*(I-1)/2
      NI     = NAT(I)
      ATOMAI = ATOMAR(I)
      TERM1  = ATOMAI*CAVITA(NI)*1.0D-03
      TERM2  = SUP(ID+I)
      TERM3  = ZERO
      DO 30 J=1,NUMAT
      IF(J.NE.I) THEN
         IF(J.LT.I) THEN
            IJ  = ID+J
         ELSE
            IJ  = J*(J-1)/2+I
         ENDIF
         NJ     = NAT(J)
         IF(((NI.EQ.7 .OR. NI.EQ.8) .AND. NJ.EQ.1) .OR.
     1      ((NJ.EQ.7 .OR. NJ.EQ.8) .AND. NI.EQ.1)) THEN
            IF((BONDSP(NI)+BONDSP(NJ)).NE.ZERO) THEN
               FIJ   = BONDSP(NI)/(BONDSP(NI)+BONDSP(NJ))
               TERM3 = TERM3+FIJ*SUP(IJ)
            ENDIF
         ELSE
            IF((BONDDI(NI)+BONDDI(NJ)).NE.ZERO) THEN
               FIJ   = BONDDI(NI)/(BONDDI(NI)+BONDDI(NJ))
               TERM3 = TERM3+FIJ*SUP(IJ)
            ENDIF
         ENDIF
      ENDIF
   30 CONTINUE
      WRITE(NB6,700) I,NI,ATOMAI,TERM1,TERM2,TERM3
      SUM1   = SUM1+TERM1
      SUM2   = SUM2+TERM2
      SUM3   = SUM3+TERM3
   40 CONTINUE
      WRITE(NB6,710)
      WRITE(NB6,720) AREA1,SUM1,SUM2,SUM3
   50 CONTINUE
C *** PRINT SOLVATION ENERGIES AND HENRY CONSTANTS.
C     POSSIBLE ONLY IF REFERENCE DATA ARE DEFINED.
      IF(INREFD.EQ.0) RETURN
      IF(ESOLV.EQ.ZERO) RETURN
      ZA1    = (ESOLV-CONST2)/CONST1
      ZA2    = (ESOLV-CONST3)/CONST1
      HENRY1 = 10.0D0**ZA1
      HENRY2 = 10.0D0**ZA2
      WRITE(NB6,730) HENRY1
      WRITE(NB6,740) HENRY2
C *** SPECIFIC EMPIRICAL CORRECTIONS FOR NITRO GROUPS.
      IF(NITRO.NE.1) RETURN
      INITRO = 0
      DO 70 J=1,4
      DO 60 I=1,10
      IGROUP(I,J) = 0
   60 CONTINUE
   70 CONTINUE
      DO 90 I=1,NUMAT
      IF(NAT(I).EQ.7) THEN
         FLAG1  = .FALSE.
         FLAG2  = .FALSE.
         FLAG3  = .FALSE.
         DO 80 J=1,NUMAT
         DIST   = (COORD(1,I)-COORD(1,J))**2+
     1            (COORD(2,I)-COORD(2,J))**2+
     2            (COORD(3,I)-COORD(3,J))**2
         DIST   = SQRT(DIST)
         DISTOK = DIST.GT.1.1D0 .AND. DIST.LT.1.6D0
         IF(DISTOK .AND. NAT(J).NE.8) THEN
            FLAG1 = .TRUE.
            ATOM1 = J
         ELSE IF(DISTOK .AND. NAT(J).EQ.8) THEN
            IF(.NOT.(FLAG2.AND.FLAG3)) THEN
               IF(.NOT.FLAG2) THEN
                  FLAG2 = .TRUE.
                  ATOM2 = J
               ELSE
                  FLAG3 = .TRUE.
                  ATOM3 = J
               ENDIF
            ELSE
               FLAG2 = .FALSE.
               FLAG3 = .FALSE.
            ENDIF
         ENDIF
   80    CONTINUE
         IF(FLAG1 .AND. FLAG2 .AND. FLAG3) THEN
            INITRO = INITRO+1
            IGROUP(INITRO,1) = I
            IGROUP(INITRO,2) = INT(ATOM1)
            IGROUP(INITRO,3) = INT(ATOM2)
            IGROUP(INITRO,4) = INT(ATOM3)
         ENDIF
      ENDIF
   90 CONTINUE
      IF(INITRO.GT.0) THEN
         WRITE(NB6,750)
         IF(INITRO.EQ.1) THEN
            WRITE(NB6,760) INITRO
         ELSE
            WRITE(NB6,770) INITRO
         ENDIF
         WRITE(NB6,780)
         DO 100 I=1,INITRO
         WRITE(NB6,790) IGROUP(I,1),IGROUP(I,3),IGROUP(I,4),IGROUP(I,2)
  100    CONTINUE
         ESOLV  = ESOLV+INITRO*CONST4
         ZA1    = (ESOLV-CONST2)/CONST1
         ZA2    = (ESOLV-CONST3)/CONST1
         HENRY1 = 10.0D0**ZA1
         HENRY2 = 10.0D0**ZA2
         WRITE(NB6,800)
         WRITE(NB6,580) ESOLV
         WRITE(NB6,730) HENRY1
         WRITE(NB6,740) HENRY2
      ENDIF
      RETURN
  500 FORMAT(///5X,'*** SUMMARY OF COSMO RESULTS ***')
  510 FORMAT(/  5X,'SINGLE-POINT COSMO CALCULATION WITH REFERENCE DATA')
  520 FORMAT(/  5X,'STANDARD COSMO CALCULATION WITH REFERENCE DATA')
  530 FORMAT(/  5X,'SINGLE-POINT COSMO CALCULATION')
  540 FORMAT(/  5X,'STANDARD COSMO CALCULATION')
  550 FORMAT(/  5X,'DISPERSION-CAVITATIONS TERMS ARE EVALUATED.')
  551 FORMAT(   5X,'THEY ARE INCLUDED IN THE ENTHALPY OF SOLVATION,',
     1       /  5X,'BUT NOT IN THE HEAT OF FORMATION IN SOLUTION.')
  552 FORMAT(   5X,'THEY ARE NOT INCLUDED IN THE HEAT OF FORMATION',
     1          1X,'IN SOLUTION.')
  560 FORMAT(/  5X,'HEAT OF FORMATION IN SOLUTION ',F15.5,' KCAL/MOL')
  570 FORMAT(   5X,'HEAT OF FORMATION IN GAS PHASE',F15.5,' KCAL/MOL')
  580 FORMAT(   5X,'ENTHALPY OF SOLVATION         ',F15.5,' KCAL/MOL')
  590 FORMAT(   5X,'ELECTROSTATIC COSMO TERM      ',F15.5,' KCAL/MOL')
  600 FORMAT(   5X,'DISPERSION-CAVITATION TERM    ',F15.5,' KCAL/MOL')
  610 FORMAT(   5X,'DIELECTRIC SCREENING ENERGY   ',F15.5,' KCAL/MOL')
  620 FORMAT(   5X,'SURFACE OF THE MOLECULE       ',F15.5,' A**2')
  630 FORMAT(   5X,'TOTAL NUMBER OF SEGMENTS      ',I15)
  631 FORMAT(///1X,'INFORMATION ON SURFACE SEGMENTS',
     1       // 5X,'I  ATOM         COORDINATES (IN ANGSTROM) ',
     2          5X,'  SEGMENT   SCREENING',
     3       /  5X,'                X           Y           Z ',
     4          5X,'    AREA      CHARGE'/)
  632 FORMAT(   1X,2I5,2X,5F12.5)
  633 FORMAT(/  3X,'SUM',43X,2F12.5)
  640 FORMAT(///2X,'*** DISPERSION AND CAVITATION TERMS ***')
  650 FORMAT(// 2X,'ATOM AND BOND SUPERDELOCALIZABILITIES')
  660 FORMAT(// 2X,'ATOM AND BOND DISPERSION ENERGIES (KCAL/MOL)')
  670 FORMAT(// 2X,'  ATOM      ATOMIC        ATOMIC             CAVITAT
     1ION            ATOMIC               BOND')
  680 FORMAT(2X,' NUMBER     NUMBER        SURFACE              ENERGY
     1            ENERGY              ENERGY')
  690 FORMAT(25X,'(ANGSTROM**2)',9X,'(KCAL/MOL)',10X,'(KCAL/MOL)',10X,
     1                              '(KCAL/MOL)',/)
  700 FORMAT(   2X,I5,6X,I5,4(5X,F12.5,3X))
  710 FORMAT(/  3X,92('-')/)
  720 FORMAT(   4X,'SUM',11X,4(5X,F12.5,3X),//)
  730 FORMAT(   5X,'HENRY''S LAW CONSTANT (298K)',E20.3,' KPA*M3/MOL')
  740 FORMAT(   5X,'HENRY''S LAW CONSTANT (298K)',E20.3,' ATM*M3/MOL')
  750 FORMAT(// 2X,'CORRECTIONS FOR NITRO-GROUPS'/)
  760 FORMAT(   2X,I1,' NITRO GROUP WAS DETECTED')
  770 FORMAT(   2X,I2,' NITRO GROUPS WERE DETECTED'/)
  780 FORMAT(2X,'NITROGEN-ATOM  OXYGEN-ATOM1  OXYGEN-ATOM2     CONNECTED
     1 TO'/)
  790 FORMAT(7X,I2,13X,I2,12X,I2,16X,I2)
  800 FORMAT(1X)
      END
