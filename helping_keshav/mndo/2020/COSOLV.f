      SUBROUTINE COSOLV
C     *
C     CALCULATION OF THE COSMO SOLVATION ENERGY AS THE
C     ENERGY DIFFERENCE BETWEEN SOLUTION AND GAS PHASE.
C     A REFERENCE SYSTEM MUST HAVE BEEN DEFINED.
C     *
C     NOTATION FOR THE ENERGIES IN COMMON BLOCK COSMO1:
C     EGAS     HEAT OF FORMATION IN THE GAS PHASE
C              OBTAINED PREVIOUSLY FOR THE REFERENCE SYSTEM.
C     ESOLUT   HEAT OF FORMATION IN SOLUTION OBTAINED PRESENTLY
C              WHICH INCLUDES THE ELECTROSTATIC COSMO TERM
C              AND DOES NOT INCLUDE ANY OTHER CONTRIBUTIONS.
C              ESOLUT-EGAS IS THE ELECTROSTATIC COSMO TERM.
C     ESOLV    ENTHALPY OF SOLVATION INCLUDING ELECTROSTATIC
C              AND CAVITATION-DISPERSION CONTRIBUTIONS.
C     EDISP    CAVITATION-DISPERSION TERM (FROM ADDDSP).
C     EDIE     DIELECTRIC SCREENING ENERGY (FROM DIELEN).
C     *
      USE LIMIT, ONLY: LMF, LMM
      IMPLICIT DOUBLE PRECISION (A-H,O-Z)
      COMMON
     ./CONSTF/ A0,AFACT,EV,EVCAL,PI,W1,W2,BIGEXP
     ./COSMO1/ EGAS,ESOLUT,ESOLV,EDISP,EDIE
     ./ENERGT/ EE,ENUCLR,EAT,ATHEAT
     ./INOPT2/ IN2(300)
     ./MMCOM1/ EMM
     ./MMDP  / EMMDP
     ./PAR2  / MOL
     ./PAR14 / EREF(LMM),EHF298(LMM),ED00(LMM),ITHERM(LMM)
     ./PAR21 / ID1(7,LMF),LITEXP(LMF)
     ./PAR24 / NREF1(LMM),NREF2(LMM)
C *** INPUT OPTIONS.
      INREFD = IN2(10)
      IATERG = IN2(119)
C *** INITIALIZATION.
      IF(IATERG.EQ.1) THEN
         ESOLUT = ATHEAT+EVCAL*(EE+ENUCLR-EAT)+EMM+EMMDP
      ELSEIF(IATERG.EQ.-1) THEN
         ESOLUT = EVCAL*(EE+ENUCLR)+EMM+EMMDP
      ENDIF
C *** CHECK FOR REFERENCE SYSTEM.
      IF(INREFD.EQ.0) RETURN
      NR1    = NREF1(MOL)
      NR2    = NREF2(MOL)
      DO 10 I=NR1,NR2
      ID     = ID1(1,I)
      IF(ID.EQ.1 .OR. ID.EQ.11 .OR. ID.EQ.28) THEN
         EGAS   = EREF(ID1(4,I))
         ESOLV  = ESOLUT+EDISP-EGAS
      ENDIF
   10 CONTINUE
      RETURN
      END
