      SUBROUTINE TRANS4 (B,C,PR,I3N,IA,IB)
C     *
C     TRANSFORMATION OF MODES FROM PRINCIPAL TO ORIGINAL AXES.
C     *
C     NOTATION. I=INPUT, O=OUTPUT.
C     B()       MODES IN MASS-WEIGHTED CARTESIAN COORDINATES,
C               WITH RESPECT TO PRINCIPAL AXES (I).
C     C()       MODES IN MASS-WEIGHTED CARTESIAN COORDINATES,
C               WITH RESPECT TO ORIGINAL AXES (O).
C     PR(3,3)   ROTATION MATRIX BETWEEN ORIGINAL AND PRINCIPAL AXES (I).
C     I3N       NUMBER OF CARTESIAN COORDINATES (I).
C     IA        FIRST MODE TO BE TRANSFORMED (I).
C     IB        LAST  MODE TO BE TRANSFORMED (I).
C     *
C     ARRAYS B AND C MAY OCCUPY THE SAME CORE SPACE.
C     *
      IMPLICIT DOUBLE PRECISION (A-H,O-Z)
      DIMENSION B(I3N,I3N),C(I3N,I3N),PR(3,3)
      N      = I3N/3
      DO 20 I=IA,IB
      DO 10 J=1,N
      J1     = 3*J-2
      J2     = J1+1
      J3     = J2+1
      B1     = B(J1,I)*PR(1,1)+B(J2,I)*PR(1,2)+B(J3,I)*PR(1,3)
      B2     = B(J1,I)*PR(2,1)+B(J2,I)*PR(2,2)+B(J3,I)*PR(2,3)
      B3     = B(J1,I)*PR(3,1)+B(J2,I)*PR(3,2)+B(J3,I)*PR(3,3)
      C(J1,I)= B1
      C(J2,I)= B2
      C(J3,I)= B3
   10 CONTINUE
   20 CONTINUE
      RETURN
      END
