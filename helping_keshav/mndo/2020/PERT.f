      SUBROUTINE PERT (C,E,C12,WW,G,CC,W,LM2,LM3,LM6,LM7,LM8,LM9,LY9,
     1                 NB2,NB3,NROOT,NOPRT)
C     *
C     CONTROL ROUTINE FOR SECOND-ORDER PERTURBATION TREATMENT.
C     *
      USE LIMIT, ONLY: LM1, LMX
      IMPLICIT DOUBLE PRECISION (A-H,O-Z)
      LOGICAL NOPRT
      COMMON
     ./CIMOS / IMOCI(LMX)
     ./CONSTF/ A0,AFACT,EV,EVCAL,PI,W1,W2,BIGEXP
     ./ENERGT/ EE,ENUCLR(3)
     ./IJWORK/ NSYM(LMX),ISYM(3,LM1)
     ./INOPT2/ IN2(300)
     ./MULTAB/ IPROD(8,8)
     ./NBFILE/ NBF(20)
     ./ORBITS/ NUMB,NORBS,NMOS,NALPHA,NBETA
      DIMENSION C(LM2,LM3),E(LM3),C12(LM6),WW(LM6),CC(LM7),G(LM8),W(LY9)
C *** FILE NUMBERS.
      NB6    = NBF(6)
C *** INPUT OPTIONS.
      NPRINT = IN2(72)
      KCI    = IN2(77)
      ICI1   = IN2(131)
      ICI2   = IN2(132)
      IOUTCI = IN2(133)
      MOVO   = IN2(134)
      MPERT  = IN2(135)
      IMODE  = IN2(211)
C *** INITIALIZATION.
      LM10   = ICI1+ICI2
      IA     = ICI1+1
      IB     = LM10
      IABSCI = ABS(KCI)
      IOUT2  = IOUTCI
      IF(NOPRT) IOUT2=-11
      IEN2   = 1
      IF(MOD(MPERT,10).EQ.0 .AND. MOD(MPERT/100,10).EQ.0) IEN2=0
C *** ASSIGNMENT OF MO SYMMETRY.
      IOUTMO = IOUT2
      IF(NPRINT.GE.IOUTCI) IOUTMO=-11
      CALL MOSYM (C,E,LM2,LM3,NROOT,NSYM,ISYM,ISUB,IOUTMO)
C *** CONVERT EIGENVALUES TO ATOMIC UNITS.
      DO 10 I=1,LM3
      E(I) = E(I)/EV
   10 CONTINUE
C *** DEFINITION OF PI MOS TO BE INCLUDED IN CORRELATION TREATMENT.
      IF(MOVO.LT.0) CALL PIMOCI (C,E,LM2,LM3,IOUT2,.FALSE.)
C *** ORDERING OF AO REPULSION INTEGRALS.
      CALL CPUSEC (T1)
      IF(IMODE.GT.0) THEN
         CALL WORDER (CC,W,LM7,LM9,NB2,NB3,IOUT2)
      ELSE
         CALL WSTORP (CC,W,LM6,LM7,NB3)
      ENDIF
C *** PERTURBATION TREATMENT (ONE MAIN CONFIGURATION).
      IF(IOUT2.GT.-5) WRITE(NB6,110)
      IF(IABSCI.LE.2) THEN
        CALL MOINT2 (C,CC,C12,G,G,W,WW,IMOCI,IPROD,NSYM,LM2,LM3,LM6,LM7,
     1               LM8,LY9,LM10,NB2,NB3,IA,IB,ICI1,IEN2,IOUT2,NNTOT)
        CALL CPUSEC (T2)
        CALL MPEN   (E2,E,G,W,IMOCI,IPROD,NSYM,LM3,LY9,LM10,NB2,IA,IB,
     1               ICI1,IOUT2,NNTOT,NUMB)
C *** PERTURBATION TREATMENT (TWO MAIN CONFIGURATIONS).
      ELSE
        KK = NUMB
        LL = NUMB+1
        CALL MINICI (C,CC,C12,E,WW,LM2,LM3,LM6,LM7,NB3,IOUT2,CA,CB,
     1               DELT,KK,LL)
        CALL MOINT3 (C,CC,C12,G,G,W,WW,IMOCI,IPROD,NSYM,LM2,LM3,LM6,LM7,
     1               LM8,LY9,LM10,NB2,NB3,IA,IB,ICI1,IEN2,IOUT2,NNTOT,
     2               CA,CB,KK,LL,IABSCI)
        CALL CPUSEC (T2)
        CALL DGPERT (E2,E,G,W,IMOCI,IPROD,NSYM,LM3,LY9,LM10,NB2,IA,IB,
     1               ICI1,IOUT2,NNTOT,CA,CB,DELT,KK,LL,IABSCI)
      ENDIF
C *** ELECTRONIC ENERGY.
      EE     = EE+E2
      DO 20 I=1,LM3
      E(I)   = E(I)*EV
   20 CONTINUE
C *** TIMING.
      IF(IOUT2.GE.-2) THEN
         CALL CPUSEC (T3)
         TX1 = T2-T1
         TX2 = T3-T2
         WRITE(NB6,100) TX1,TX2
      ENDIF
      RETURN
  100 FORMAT (///1X,'COMPUTATION TIME FOR'/
     1           1X,'INTEGRAL TRANSFORMATION',F19.3,' SECONDS'/
     2           1X,'PERTURBATION TREATMENT ',F19.3,' SECONDS')
  110 FORMAT (///1X,'SECOND-ORDER PERTURBATION TREATMENT.'/)
      END
