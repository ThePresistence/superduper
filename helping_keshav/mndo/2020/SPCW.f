      FUNCTION SPCW(C1,C2,C3,C4,CKL,W,LM2,LM6)
C     *
C     SPCW CALCULATES THE REPULSION BETWEEN ELECTRON 1 IN MOLECULAR
C     ORBITALS C1,C2 AND ELECTRON 2 IN C3,C4 FOR THE VALENCE SHELL.
C     SPECIAL MNDO VERSION WITH INTEGRALS W(IJ,KL) IN SQUARE ARRAY.
C     *
      USE LIMIT, ONLY: LMI
      IMPLICIT DOUBLE PRECISION (A-H,O-Z)
      COMMON
     ./FINDX1/ IP(LMI),IP1(LMI),IP2(LMI)
      DIMENSION C1(LM2),C2(LM2),C3(LM2),C4(LM2),CKL(LM6),W(LM6,LM6)
      SPCW   = 0.0D0
      DO 10 KL=1,LM6
      K      = IP1(KL)
      L      = IP2(KL)
      CKL(KL)= C3(K)*C4(L)
      IF(K.NE.L) CKL(KL)=CKL(KL)+C3(L)*C4(K)
   10 CONTINUE
      DO 30 IJ=1,LM6
      WIJ    = 0.0D0
      DO 20 KL=1,LM6
      WIJ    = WIJ+CKL(KL)*W(KL,IJ)
   20 CONTINUE
      I      = IP1(IJ)
      J      = IP2(IJ)
      CIJ    = C1(I)*C2(J)
      IF(I.NE.J) CIJ=CIJ+C1(J)*C2(I)
      SPCW   = SPCW+CIJ*WIJ
   30 CONTINUE
      RETURN
      END
