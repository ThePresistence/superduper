      SUBROUTINE QMINT (H,LMH,ENUCQM,P,PTCHG,MODE,ICNTR)
C     *
C     ELECTROSTATIC INTERACTIONS OF ELECTRONS AND CORES (QM PART)
C     WITH ONE EXTERNAL POINT CHARGE (MM PART): INTEGRAL EVALUATION.
C     REF: DIRK BAKOWIES AND W. THIEL, J.COMPUT.CHEM. 17, 87 (1996).
C     *
C     SEMIEMPIRICAL TREATMENT OF THE ELECTROSTATIC INTERACTIONS.
C     SEVERAL OPTIONS (MMPOT=3-6) CAN BE COVERED BY THE SAME CODE,
C     SINCE THEY ONLY DIFFER IN THE PARAMETER VALUES FOR DELTAM AND
C     OMEGAM WHICH ARE DEFINED IN SUBROUTINE INIPOT. THESE OPTIONS
C     TREAT THE EXTERNAL TEST CHARGE AS A PURELY CLASSICAL MONOPOLE.
C     OPTION MMPOT=7 IMPLEMENTS THE FIELD-BASH-KARPLUS APPROACH, AND
C     OPTION MMPOT=8 TREATS THE EXTERNAL CHARGE LIKE A HYDROGEN ATOM.
C     *
C     NOTATION. I=INPUT, O=OUTPUT.
C     H(LMH)    CORE HAMILTONIAN MATRIX (I,O).
C     ENUCQM    CHARGE-CORE REPULSION TERMS SUMMED OVER ALL CORES (O).
C     P(3)      CARTESIAN COORDINATES OF POINT CHARGE IN ANGSTROM (I).
C     PTCHG     VALUE OF THE POINT CHARGE IN ATOMIC UNITS (I).
C     MODE      CONTENTS OF CORE HAMILTONIAN MATRIX (I).
C               = 1  ONLY ONE-CENTER ELEMENTS, DIMENSION LM6, DEFAULT.
C               = 2  ALL ELEMENTS, DIMENSION LM4.
C     ICNTR     SPECIAL OPTION TO TREAT A SINGLE QM ATOM (I).
C               = 0  INCLUDE INTERACTIONS WITH ALL QM ATOMS, DEFAULT.
C               = I  INCLUDE INTERACTIONS ONLY WITH QM ATOM I.
C     *
C     CASE PTCHG=1.0: UNIT EXTERNAL CHARGE.
C     - IN THE NOTATION OF THE REFERENCE, THE ONE-ELECTRON INTEGRALS
C       -VI(MU,NU) ARE EVALUATED AND ADDED TO THE CORE HAMILTONIAN.
C       IF THE INPUT CORE HAMILTONIAN HAS BEEN INITIALIZED TO ZERO,
C       THE OUTPUT ARRAY H(LMH) WILL CONTAIN THE INTEGRALS NEEDED TO
C       COMPUTE THE ONE-ELECTRON PART OF THE ELECTROSTATIC POTENTIAL
C       AT THE POSITION OF THE EXTERNAL UNIT CHARGE, SEE EQ.(2) IN REF,
C       BY EVALUATING THE CORRESPONDING SUM OVER P(MU,NU)*H(MU,NU).
C     - IN ADDITION, THE CHARGE-CORE INTERACTIONS V(A,I) ARE COMPUTED
C       AND ADDED UP IN THE LOOP OVER THE CORES. THE RESULT (ENUCQM)
C       CORRESPONDS TO THE SUM OVER ZA*V(A,I), AND REPRESENTS THE
C       NUCLEAR PART OF THE ELECTROSTATIC POTENTIAL AT THE POSITION
C       OF THE EXTERNAL UNIT CHARGE, SEE EQ.(2) IN REF.
C     *
C     CASE PTCHG.NE.=1.0: SPECIFIC EXTERNAL CHARGE.
C     - THE ONE-ELECTRON INTEGRALS ARE EVALUATED AS BEFORE, MULTIPLIED
C       BY PTCHG, AND THEN ADDED TO THE CORE HAMILTONIAN, WHICH THEN
C       INCLUDES THE INTERACTION WITH A SPECIFIC EXTERNAL CHARGE (MM).
C       THIS PROCEDURE IS USED (WITH THE STANDARD CORE HAMILTONIAN AS
C       INPUT) TO COMPUTE THE QM WAVEFUNCTION IN THE PRESENCE OF MM
C       CHARGES. THE CORRESPONDING SUM OVER P(MU,NU)*H(MU,NU) WILL
C       INCORPORATE THE ATTRACTIVE ELECTROSTATIC QM/MM INTERACTIONS.
C     - THE PREVIOUS RESULT (ENUCQM) IS MULTIPLIED BY PTCHG TO OBTAIN
C       THE REPULSIVE ELECTROSTATIC QM/MM INTERACTION.
C     *
      USE LIMIT, ONLY: LM1, LMX, LMZ
      IMPLICIT DOUBLE PRECISION (A-H,O-Z)
      LOGICAL AM1PM3,NOTOMI
      COMMON
     ./ATOMC / COORD(3,LM1)
     ./ATOMS / NUMAT,NAT(LM1),NFIRST(LM1),NLAST(LM1)
     ./CONSTF/ A0,AFACT,EV,EVCAL,PI,W1,W2,BIGEXP
     ./CONSTN/ ZERO,ONE,TWO,THREE,FOUR,PT5,PT25
     ./EXTRA1/ RI(22),CORE(10,2),REPT(10,2),WW(2025)
     ./EXTRA2/ SIJ(14),T(14),YY(675)
     ./INDEX / INDX(LMX)
     ./INDEXW/ NW(LM1)
     ./INOPT2/ IN2(300)
     ./MULTIP/ DD(6,0:LMZ),PO(9,0:LMZ)
     ./PARDER/ TORE(LMZ),EHEAT(LMZ),EISOL(LMZ)
     ./QMMM2 / LINK(LM1)
     ./QMMM3 / DELTAM(LMZ),OMEGAM(LMZ)
      DIMENSION H(LMH),P(3)
      DIMENSION XCOORD(3,2)
C$OMP THREADPRIVATE (/EXTRA1/,/EXTRA2/)
C *** INPUT OPTIONS.
      IOP    = IN2(2)
      MMPOT  = IN2(122)
      MMLINK = IN2(123)
C *** INITIALIZATION.
      AM1PM3 = IOP.EQ.-5 .OR. IOP.EQ.-7 .OR. IOP.EQ.-12 .OR. IOP.EQ.-17
      NOTOMI = IOP.NE.-5 .AND. IOP.NE.-6 .AND. IOP.NE.-8 .AND. IOP.NE.-9
      NKO    = 1
      ENUCQM = ZERO
C *** SPECIFY EXTERNAL POINT.
      XCOORD(1,1) = P(1)
      XCOORD(2,1) = P(2)
      XCOORD(3,1) = P(3)
C *** LOOP OVER QM ATOMS.
      DO 10 I=1,NUMAT
      IF(MMLINK.EQ.1 .AND. LINK(I).GT.0) GO TO 10
      IF(ICNTR.GT.0 .AND. ICNTR.LE.NUMAT .AND. ICNTR.NE.I) GO TO 10
C     DEFINE LOCAL VARIABLES.
      NI     = NAT(I)
      IORBS  = NLAST(I)-NFIRST(I)+1
      ISHELL = I
      IF(MODE.EQ.2) THEN
         IA  = NFIRST(I)
         IS  = INDX(IA)+IA
      ELSE
         IA  = 1
         IS  = NW(I)
      ENDIF
      XCOORD(1,2) = COORD(1,I)
      XCOORD(2,2) = COORD(2,I)
      XCOORD(3,2) = COORD(3,I)
C     DISTANCE R (AU) AND ROTATION MATRIX.
      IF(IORBS.LE.4) THEN
         R   = SQRT( (XCOORD(1,1)-XCOORD(1,2))**2
     1              +(XCOORD(2,1)-XCOORD(2,2))**2
     2              +(XCOORD(3,1)-XCOORD(3,2))**2 ) / A0
      ELSE
         CALL ROTMAT (1,2,0,IORBS,2,XCOORD,R,YY)
      ENDIF
C     LOCAL CHARGE-ELECTRON ATTRACTION INTEGRALS.
      IF(NOTOMI) THEN
         IF(IORBS.GT.1) THEN
            CALL REPP0 (NI,R,RI,CORE)
            IF(IORBS.GE.9) THEN
               CALL REPPD (NI,0,R,RI,CORE,WW,45,1,1)
            ENDIF
         ELSE
            AEE       = (PO(1,NI)+PO(1,0))**2
            RI(1)     = EV/SQRT(R*R+AEE)
            CORE(1,1) = -RI(1)
         ENDIF
      ELSE
         CALL COREXT (ISHELL,NI,NKO,R,CORE,FKO)
      ENDIF
C     MULTIPLICATION BY POINT CHARGE.
      IF(PTCHG.NE.ONE) THEN
         CORE(1,1) = CORE(1,1)*PTCHG
         IF(IORBS.GE.4) THEN
            CORE(2:4,1) = CORE(2:4,1)*PTCHG
            IF(IORBS.GE.9) THEN
               CORE(5:10,1) = CORE(5:10,1)*PTCHG
            ENDIF
         ENDIF
      ENDIF
C     CONTRIBUTIONS TO THE CORE HAMILTONIAN.
      IF(IORBS.LE.1) THEN
         H(IS) = H(IS)+CORE(1,1)
      ELSE IF(IORBS.LE.4) THEN
         CALL ROTHSP (IA,IS,R,XCOORD,CORE,H,LMH)
      ELSE
         CALL ROTCOH (IA,0,IORBS,0,IS,0,CORE,YY,H,LMH)
      ENDIF
C     CONTRIBUTIONS TO THE CORE-CORE REPULSIONS.
      IF(NOTOMI) THEN
         XX     = OMEGAM(NI)*(R*A0-DELTAM(NI))
         IF(XX.LT.BIGEXP) THEN
            SCALE  = ONE+EXP(-XX)
         ELSE
            SCALE = ONE
         ENDIF
         IF(MMPOT.EQ.7) THEN
            SCALE  = SCALE + EXP(-5.0D0*R*A0)
            ENUC   = TORE(NI)*RI(1)*SCALE
            IF(AM1PM3) THEN
               CALL REPAM1 (NI,0,R,ENUC)
            ENDIF
         ELSE IF(MMPOT.EQ.8) THEN
            SCALE  = SCALE + EXP(-OMEGAM(1)*R*A0)
            ENUC   = TORE(NI)*RI(1)*SCALE
            IF(AM1PM3) THEN
               CALL REPAM1 (NI,1,R,ENUC)
            ENDIF
         ELSE
            ENUC   = TORE(NI)*RI(1)*SCALE
         ENDIF
      ELSE
         ENUC   = FKO*TORE(NI)*EV/R
      ENDIF
      ENUCQM = ENUCQM+ENUC
   10 CONTINUE
C *** MULTIPLICATION OF NUCLEAR TERM BY POINT CHARGE.
      IF(PTCHG.NE.ONE) THEN
         ENUCQM = ENUCQM*PTCHG
      ENDIF
      RETURN
      END
