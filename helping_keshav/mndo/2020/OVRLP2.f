      SUBROUTINE OVRLP2 (COORD,S,B,LM4,MODE)
C     *
C     COMPUTE OVERLAP AND RESONANCE INTEGRALS.
C     THIS ROUTINE IS USED ONLY FOR THE GRADIENT IN OM2.
C     *
C     NOTATION. I=INPUT, O=OUTPUT.
C     S(LM4)    OVERLAP INTEGRALS (I,O).
C     B(LM4)    RESONANCE INTEGRALS (I,O).
C     COORD()   CURRENT ATOMIC COORDINATES (I).
C     MODE      TYPE OF INTEGRAL CALCULATION.
C               = 0  COMPUTE ALL INTEGRALS.
C               = I  RECOMPUTE INTEGRALS INVOLVING ATOM I.
C     *
      USE LIMIT, ONLY: LM1
      IMPLICIT DOUBLE PRECISION (A-H,O-Z)
      PARAMETER (IZERO=0)
      COMMON
     ./ATOMS / NUMAT,NAT(LM1),NFIRST(LM1),NLAST(LM1)
     ./CONSTN/ ZERO,ONE,TWO,THREE,FOUR,PT5,PT25
     ./INOPT2/ IN2(300)
     ./NBFILE/ NBF(20)
      DIMENSION S(LM4),B(LM4),COORD(3,LM1)
      DIMENSION SIJ(14),BIJ(14),YY(675)
C *** INPUT VARIABLES.
      NPRINT = IN2(72)
C *** INITIALIZATION.
      IF(MODE.LE.0) THEN
         DO 10 I=1,LM4
         B(I)   = ZERO
         S(I)   = ZERO
   10    CONTINUE
      ENDIF
C *** COMPUTE OVERLAP AND RESONANCE INTEGRALS.
      DO 40 I=2,NUMAT
      NI     = NAT(I)
      IA     = NFIRST(I)
      IORBS  = NLAST(I)-IA+1
      DO 30 J=1,I-1
      IF(MODE.LE.0 .OR. MODE.EQ.I .OR. MODE.EQ.J) THEN
         NJ     = NAT(J)
         JA     = NFIRST(J)
         JORBS  = NLAST(J)-JA+1
         CALL ROTMAT (J,I,JORBS,IORBS,NUMAT,COORD,R,YY)
         CALL BETOM  (IZERO,I,J,NI,NJ,R,SIJ,BIJ)
         CALL ROTBET (IA,JA,IORBS,JORBS,SIJ,YY,S,LM4)
         CALL ROTBET (IA,JA,IORBS,JORBS,BIJ,YY,B,LM4)
      ENDIF
   30 CONTINUE
   40 CONTINUE
C *** DEBUG PRINT.
      IF(NPRINT.GT.5) THEN
         NB6 = NBF(6)
         NORBS = NLAST(NUMAT)
         WRITE(NB6,500)
         CALL VECPRT (S,LM4,NORBS)
         WRITE(NB6,510)
         CALL VECPRT (B,LM4,NORBS)
      ENDIF
      RETURN
  500 FORMAT(///1X,'OVERLAP MATRIX (DEBUG PRINT FROM OVRLP2)'/)
  510 FORMAT(///1X,'RESONANCE MATRIX (DEBUG PRINT FROM OVRLP2)'/)
      END
