      SUBROUTINE DDPOHY (NI)
C     *
C     CALCULATION OF CHARGE SEPARATIONS AND ADDITIVE TERMS USED
C     TO COMPUTE THE TWO-CENTER TWO-ELECTRON INTEGRALS IN MNDO/D.
C     NI IS THE ATOMIC NUMBER OF THE CURRENT ELEMENT.
C     *
C     IN THE CASE OF AN SP BASIS, THE CHARGE SEPARATIONS AND ADDITIVE
C     TERMS ARE EQUIVALENT IN MNDO/d AND MNDO.
C     *
C     CHARGE SEPARATIONS DD(6,LMZ) FROM FORMULA FUNCTION AIJL.
C     ADDITIVE TERMS     PO(9,LMZ) FROM FUNCTION POIJ.
C     SEE EQUATIONS (12)-(16) OF TCA PAPER FOR DD.
C     SEE EQUATIONS (19)-(26) OF TCA PAPER FOR PO.
C     *
C     INDEX OF DD AND PO : SS 1, SP 2, PP 3, SD 4, PD 5, DD 6.
C     MULTIPOLE          :  L=0,  L=1,  L=2,  L=2,  L=1,  L=2.
C     SPECIAL INDEX OF PO: PP 7, DD 8.
C     MULTIPOLE          :  L=0,  L=0.
C     FOR ATOMIC CORE    : ADDITIVE TERM PO(9,NI)
C     *
C     THE HYBRIDIZATION CONTRIBUTIONS TO THE DIPOLE MOMENT CONTAIN
C     FACTORS WHICH DEPEND ON THE CHARGE SEPARATIONS AS FOLLOWS.
C     HYFSP(NI) = DD(2,NI)*5.0832D0
C     HYFPD(NI) = DD(5,NI)*5.0832D0
C     THESE FACTORS ARE COMPUTED AND SAVED IN COMMON BLOCK DIPOL1.
C     *
      USE LIMIT, ONLY: LMZ
      IMPLICIT DOUBLE PRECISION (A-H,O-Z)
      PARAMETER (DIPFAC=5.0832D0)
      COMMON
     ./CONSTF/ A0,AFACT,EV,EVCAL,PI,W1,W2,BIGEXP
     ./CONSTN/ ZERO,ONE,TWO,THREE,FOUR,PT5,PT25
     ./DNBND / III(LMZ),IIID(LMZ)
     ./DPARM / LORBS(LMZ)
     ./DPARM1/ UDD(LMZ),ZD(LMZ),BETAD(LMZ)
     ./DPARM4/ REPD(52,LMZ)
     ./DIPOL1/ HYF(LMZ),HYFPD(LMZ)
     ./MULTIP/ DD(6,0:LMZ),PO(9,0:LMZ)
     ./PAROPT/ USS(LMZ),UPP(LMZ),ZS(LMZ),ZP(LMZ),BETAS(LMZ),BETAP(LMZ),
     .         ALP(LMZ)
     ./REP   / GSS(LMZ),GPP(LMZ),GSP(LMZ),GP2(LMZ),HSP(LMZ),HPP(LMZ)
C     THE ARRAY FC(I) CONTAINS THE FACTORIALS OF (I-1).
      DIMENSION FC(17)
      DATA FC /1.0D0,1.0D0,2.0D0,6.0D0,24.0D0,120.0D0,720.0D0,5040.0D0,
     1         40320.0D0,362880.0D0,3628800.0D0,39916800.0D0,
     2         4.790016D+08,6.2270208D+09,8.71782912D+10,
     3         1.307674368D+12,2.092278989D+13/
C
C *** GENERAL FORMULA FOR AIJ-VALUES (SEE BELOW).
C     SEE EQUATION (7) OF TCA PAPER.
      AIJL(Z1,Z2,N1,N2,L)= FC(N1+N2+L+1) / SQRT( FC(2*N1+1)*FC(2*N2+1) )
     1   *  ( 2*Z1/(Z1+Z2) ) ** N1 * SQRT( 2*Z1/(Z1+Z2) )
     2   *  ( 2*Z2/(Z1+Z2) ) ** N2 * SQRT( 2*Z2/(Z1+Z2) )
     3   /  (Z1+Z2) ** L
C     AIJ-VALUES ARE COMPUTED AS DEFINED IN EQUATION (7) OF TCA PAPER
C     AND THEN STORED AS VARIABLES AIJ.. WITH TWO DIGITS AT THE END.
C     FIRST  DIGIT: 1 SS, 2 SP, 3 PP, 4 SD, 5 PD, 6 DD.
C     SECOND DIGIT: L+1 FROM DEFINITION OF MULTIPOLE.
C
C *** SECTION FOR AN S BASIS.
C     THERE IS ONLY ONE ADDITIVE TERM.
      PO(1,NI) = PT5*EV/GSS(NI)
      IF(LORBS(NI).EQ.1) RETURN
C
C *** SECTION FOR AN SP BASIS SET.
C     CHARGE SEPARATIONS AND ADDITIVE TERMS MUST BE COMPUTED.
      N        = III(NI)
      NP       = MAX(N,2)
      Z1       = ZS(NI)
      Z2       = ZP(NI)
      AIJ22    = AIJL(Z1,Z2,N,NP,1)
      DD(2,NI) = AIJ22/SQRT(THREE)
      D        = DD(2,NI)
      FG       = HSP(NI)
      PO(2,NI) = POIJ(1,D,FG)
C     AIJ33    = AIJL(Z1,Z2,NP,NP,2)
C     DD(3,NI) = SQRT(AIJ33/5.0D0)
      DD(3,NI) = SQRT((2*NP+1)*(2*NP+2)/20.0D0) / ZP(NI)
      D        = DD(3,NI)*SQRT(TWO)
      FG       = HPP(NI)
      PO(3,NI) = POIJ(2,D,FG)
      PO(7,NI) = PO(1,NI)
      HYF(NI)  = DD(2,NI)*DIPFAC
      HYFPD(NI)= ZERO
      IF(LORBS(NI).EQ.4) RETURN
C
C *** SECTION FOR AN SPD BASIS SET.
C     ADDITIONAL TERMS INVOLVING D ORBITALS.
C     NOTE THE EXTRA FACTOR OF SQRT2 FOR THE CHARGE SEPARATIONS
C     DD(I,NI) WITH I=4,6, WHICH REFER TO SQUARE QUADRUPOLES,
C     FOR SIMPLIFICATION OF THE CODE IN REPPD.
      Z3       = ZD(NI)
      ND       = IIID(NI)
      AIJ52    = AIJL(Z2,Z3,NP,ND,1)
      AIJ43    = AIJL(Z1,Z3,N ,ND,2)
      AIJ63    = AIJL(Z3,Z3,ND,ND,2)
C     SD
      DA       = SQRT(ONE/15.0D0)
      D        = SQRT(AIJ43*DA)
      D        = D*SQRT(TWO)
      FG       = REPD(19,NI)
      DD(4,NI) = D
      PO(4,NI) = POIJ(2,D,FG)
C     PD
      D        = AIJ52/SQRT(5.0D0)
      FG       = REPD(23,NI)-1.8D0*REPD(35,NI)
C     PREVIOUS STATEMENT AS IN THE TCA PAPER,
C     NEXT STATEMENT AS A POSSIBLE ALTERNATIVE.
C     FG       = REPD(33,NI)-1.6D0*REPD(35,NI)
      DD(5,NI) = D
      PO(5,NI) = POIJ(1,D,FG)
      HYFPD(NI)= D*DIPFAC
C     DD
      FG       = 0.2D0*(REPD(29,NI)+TWO*REPD(30,NI)+TWO*REPD(31,NI))
      PO(8,NI) = POIJ(0,ONE,FG)
CDEC  NEXT TWO STATEMENTS RUN INTO COMPILER BUG ON DEC ALPHA (OSF1).
C     THEY ARE REPLACED BY TWO MATHEMATICALLY EQUIVALENT STATEMENTS.
C     D        = SQRT(AIJ63/7.0D0)
C     D        = D*SQRT(TWO)
      D        = AIJ63/7.0D0
      D        = SQRT(TWO*D)
      FG       = REPD(44,NI)-(20.0D0/35.0D0)*REPD(52,NI)
      DD(6,NI) = D
      PO(6,NI) = POIJ(2,D,FG)
      RETURN
      END
