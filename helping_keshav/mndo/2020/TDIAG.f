      SUBROUTINE TDIAG (A,Z,W,FV1,NV,NM,N,M)
C     *
C     THIS SUBROUTINE CALLS THE RECOMMENDED SEQUENCE OF
C     SUBROUTINES FROM THE EIGENSYSTEM SUBROUTINE PACKAGE (EISPACK)
C     TO FIND THE EIGENVALUES AND EIGENVECTORS (IF DESIRED)
C     OF A SYMMETRIC PACKED MATRIX.
C     *
C
C     ON INPUT-
C
C        A  CONTAINS THE LOWER TRIANGLE OF THE SYMMETRIC
C        PACKED MATRIX STORED ROW-WISE.
C
C        NV IS THE DIMENSION OF THE ARRAY  A
C        AS SPECIFIED IN THE CALLING PROGRAM.
C
C        NM IS THE ROW DIMENSION OF THE EIGENVECTORS
C        AS SPECIFIED IN THE CALLING PROGRAM.
C
C        N  IS THE ORDER OF THE MATRIX  A.
C
C        M  IS THE NUMBER OF EIGENVECTORS TO BE CALCULATED.
C        FOR M.LE.0 ONLY THE EIGENVALUES ARE CALCULATED.
C
C     ON OUTPUT-
C
C        W  CONTAINS THE EIGENVALUES IN ASCENDING ORDER.
C
C        Z  CONTAINS THE EIGENVECTORS IF M IS NOT ZERO.
C
C        FV1  IS A TEMPORARY STORAGE ARRAY.
C
C     QUESTIONS AND COMMENTS SHOULD BE DIRECTED TO B. S. GARBOW,
C     APPLIED MATHEMATICS DIVISION, ARGONNE NATIONAL LABORATORY
C
      IMPLICIT DOUBLE PRECISION (A-H,O-Z)
      PARAMETER (ZERO=0.0D0)
      PARAMETER (ONE =1.0D0)
      COMMON /NBFILE/ NBF(20)
      DIMENSION A(NV),Z(NM,N),W(N),FV1(N*2)
C *** FILE NUMBERS.
      NB6    = NBF(6)
C *** CHECK FOR SIMPLE ERRORS.
      IF(N.GT.NM .AND. M.GT.0) THEN
         WRITE(NB6,500)
         STOP 'TDIAG'
      ENDIF
C *** PUT MATRIX IN TRIDIAGONAL FORM.
      CALL TRED3(N,NV,A,W,FV1,FV1(N+1))
C *** FIND EIGENVALUES AND EIGENVECTORS OF TRIDIAGONAL MATRIX.
      DO 20 I=1,N
      DO 10 J=1,N
      Z(J,I) = ZERO
   10 CONTINUE
   20 CONTINUE
      DO 30 I=1,N
      Z(I,I) = ONE
   30 CONTINUE
      CALL TQL2(NM,N,W,FV1,Z,IERR)
      IF(IERR.NE.0) THEN
         WRITE(NB6,510) IERR
         STOP 'TDIAG'
      ENDIF
C *** FIND EIGENVECTORS OF ORIGINAL MATRIX.
      IF(M.LT.1) RETURN
      MEIG   = MIN(M,N)
      CALL TRBAK3(NM,N,NV,A,MEIG,Z)
      RETURN
  500 FORMAT(///1X,'ERROR IN CALL TO DIAGONALIZATION ROUTINE.',
     1       /  1X,'ORDER OF MATRIX IS GREATER THAN ROW DIMENSION ',
     2       /  1X,'OF EIGENVECTORS.'//)
  510 FORMAT(///1X,'ERROR IN DIAGONALIZATION ROUTINE.',
     1       /  1X,'NO CONVERGENCE FOR EIGENVALUE',I3//)
      END
