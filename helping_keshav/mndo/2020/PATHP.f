      SUBROUTINE PATHP (A,LM5,SCFCAL)
C     *
C     REACTION PATH CALCULATION WITH ONE PATH VARIABLE.
C     *
C     PARALLEL VERSION WITH THE FOLLOWING RESTRICTION.
C     - RESTARTS ARE NOT SUPPORTED SINCE THEY SHOULD NOT BE REQUIRED
C       ON A MASSIVELY PARALLEL MACHINE.
C     *
      USE LIMIT, ONLY: LM1, LMV, LMR
      IMPLICIT DOUBLE PRECISION (A-H,O-Z)
      EXTERNAL SCFCAL
      CHARACTER*80  KTITLE,KOMENT
      COMMON
     ./CONSTF/ A0,AFACT,EV,EVCAL,PI,W1,W2,BIGEXP
     ./DIPOL / DIP(7)
     ./ERG   / ENERGY,GRAD(LMV),GNORM,CNORM
     ./FLAG1 / KTITLE,KOMENT
     ./INOPT2/ IN2(300)
     ./NBFILE/ NBF(20)
     ./PARM1 / AA(LMV),NA(LM1,4),NATOMS
     ./PARM4 / RC(LMR),LREACT,LTOTAL
      DIMENSION A(LM5)
      DIMENSION HEATS(LMR),DIPS(LMR)
      DIMENSION CNORMS(LMR),GNORMS(LMR)
C *** FILE NUMBERS.
      NB6    = NBF(6)
C *** INPUT OPTIONS.
      JOP    = IN2(3)
      IGEOM  = IN2(4)
      MAXEND = IN2(32)
C *** TURN OFF RESTART OPTION.
C     MIDDLE = -1
      IN2(37)= -1
C *** LOOP OVER ALL POINTS ON THE REACTION PATH.
      L1     = (LREACT+2)/3
      L2     = LREACT-3*(L1-1)
      DO 20 L=1,LTOTAL
      RCV    = RC(L)
      AA(LREACT) = RCV
      IF(IGEOM.EQ.0 .AND. L2.GT.1) RCV=RCV*AFACT
      WRITE(NB6,500) KOMENT,KTITLE
      WRITE(NB6,510) L2,L1,RCV
      CALL SYMTRY(+1)
      CALL GMETRY(-1)
C *** DO THE CALCULATION FOR A GIVEN POINT.
C     SINGLE SCF CALCULATION.
      IF(JOP.EQ.-1 .OR. MAXEND.EQ.1) THEN
         ICALL = 0
         CALL SCF (A,LM5,ICALL,SCFCAL)
C     SINGLE SCF AND GRADIENT CALCULATION.
      ELSE IF(JOP.EQ.-2) THEN
         ICALL = 1
         CALL SCF (A,LM5,ICALL,SCFCAL)
C     OPTIMIZE THE MOLECULAR GEOMETRY.
      ELSE
         CALL GEOOPT (A,LM5,ICALL,SCFCAL)
         IF(IN2(45).EQ.0) IN2(45)=2
      ENDIF
C *** STORE RESULTS.
      HEATS (L) = ENERGY
      DIPS  (L) = DIP(1)
      CNORMS(L) = CNORM
      GNORMS(L) = GNORM
   20 CONTINUE
C *** PRINTING SECTION.
      WRITE(NB6,520)
      DO 40 L=1,LTOTAL
      RCV    = RC(L)
      IF(IGEOM.EQ.0 .AND. L2.GT.1) RCV=RCV*AFACT
      WRITE(NB6,530) L,RCV,HEATS(L),DIPS(L),GNORMS(L),CNORMS(L)
   40 CONTINUE
      RETURN
  500 FORMAT(///1X,A,/1X,A)
  510 FORMAT(/  1X,'REACTION COORDINATE: VARIABLE',I2,' OF ATOM',I3,
     1       /  1X,'ITS CURRENT VALUE IS ',F10.5//)
  520 FORMAT(///1X,'SUMMARY OF RESULTS FROM REACTION PATH CALCULATION.',
     1       // 3X,'I    REACTION     HEAT OF    DIPOLE   OPTIMIZED',
     2             '   CARTESIAN',
     3       /  3X,'    COORDINATE   FORMATION   MOMENT    GRADIENT',
     4             '    GRADIENT',
     5       /  3X,'                (KCAL/MOL)  (DEBYE)      NORM  ',
     6             '      NORM'/)
  530 FORMAT(   1X,I3,F11.3,F13.5,F9.3,F11.3,F12.3)
      END
