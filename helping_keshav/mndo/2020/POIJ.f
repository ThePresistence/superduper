      FUNCTION POIJ (L,D,FG)
C     *
C     DETERMINE ADDITIVE TERMS RHO=POIJ FOR TWO-CENTER TWO-ELECTRON
C     INTEGRALS FROM THE REQUIREMENT THAT THE APPROPRIATE ONE-CENTER
C     TWO-ELECTRON INTEGRALS ARE REPRODUCED.
C     *
C     INPUT DATA (SEE EQUATIONS (19)-(26) IN TCA PAPER).
C     L     L QUANTUM NUMBER OF ADDITIVE TERM.
C     D     CHARGE SEPARATION.
C     FG    REFERENCE ONE-CENTER INTEGRAL (OR SLATER-CONDON PARAMETER).
C     *
C     SPECIAL CONVENTION IN THE CASE L=2.
C     THE INPUT VALUE OF D (AS DEFINED IN THE CALLING ROUTINE) EQUALS
C     D2*SQRT(TWO), WITH D2 DEFINED IN EQUATIONS (14)-(16) OF THE TCA
C     PAPER. THIS SPECIAL CONVENTION FOR L=2 SHOULD BE KEPT IN MIND
C     WHEN COMPARING THE CODE BELOW WITH EQUATIONS (24)-(26) OF THE
C     TCA PAPER. THE CONVENTION IS MOTIVATED BY CODE SIMPLIFICATIONS
C     IN SUBROUTINE REPPD WHICH ARISE WHEN VALUES OF D2*SQRT(TWO) ARE
C     STORED IN DD(4,NI) AND DD(6,NI).
C     *
      IMPLICIT DOUBLE PRECISION (A-H,O-Z)
      PARAMETER (EPSIL=1.0D-08)
      PARAMETER (G1=0.382D0)
      PARAMETER (G2=0.618D0)
      PARAMETER (NITER=100)
      COMMON
     ./CONSTF/ A0,AFACT,EV,EVCAL,PI,W1,W2,BIGEXP
     ./CONSTN/ ZERO,ONE,TWO,THREE,FOUR,PT5,PT25
C *** TERM FOR SS.
      IF(L.EQ.0) THEN
         POIJ = PT5*EV/FG
         RETURN
      ENDIF
C *** HIGHER TERMS.
      DSQ    = D*D
      EV4    = EV*PT25
      EV8    = EV/8.0D0
      A1     = 0.1D0
      A2     = 5.0D0
      DO 10 I=1,NITER
      DELTA  = A2-A1
      IF(DELTA.LT.EPSIL) GO TO 20
      Y1     = A1 + DELTA*G1
      Y2     = A1 + DELTA*G2
      IF(L.EQ.1) THEN
         F1  = (EV4*(ONE/Y1-ONE/SQRT(Y1**2+DSQ)) - FG)**2
         F2  = (EV4*(ONE/Y2-ONE/SQRT(Y2**2+DSQ)) - FG)**2
      ELSE IF(L.EQ.2) THEN
         F1  = (EV8*(ONE/Y1-TWO/SQRT(Y1**2+DSQ*PT5)
     1                     +ONE/SQRT(Y1**2+DSQ)) - FG)**2
         F2  = (EV8*(ONE/Y2-TWO/SQRT(Y2**2+DSQ*PT5)
     1                     +ONE/SQRT(Y2**2+DSQ)) - FG)**2
      ENDIF
      IF(F1.LT.F2) THEN
         A2  = Y2
      ELSE
         A1  = Y1
      ENDIF
  10  CONTINUE
C     DEFINE ADDITIVE TERM AFTER CONVERGENCE OF ITERATIONS.
  20  IF(F1.GE.F2) THEN
         POIJ = A2
      ELSE
         POIJ = A1
      ENDIF
      RETURN
      END
