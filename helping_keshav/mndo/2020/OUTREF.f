      SUBROUTINE OUTREF (JPRINT,KSTOP,MOL,NTAB)
C     *
C     OUTPUT OF REFERENCE DATA.
C     *
C     NOTATION. I=INPUT, O=OUTPUT.
C     JPRINT    PRINTING FLAG (I).
C     KSTOP     NUMBER OF REMOVED INPUT LINES (I). USED WITH NTAB=0,1.
C     MOL       NUMBER OF THE CURRENT MOLECULE (I).
C     NTAB      FORMAT OF OUTPUT TABLE (I).
C               = 0  STANDARD FORMAT FOR ECHOING INPUT.
C               = 1  EXTENDED FORMAT FOR ECHOING INPUT.
C               = 2  STANDARD FORMAT FOR PRINTING RESULTS.
C     *
      USE LIMIT, ONLY: LMF, LMM, LMPR
      IMPLICIT DOUBLE PRECISION (A-H,O-Z)
      CHARACTER*30 KOMEXP
      CHARACTER*23 KOMOUT
      CHARACTER*80 KTITLE,KOMENT
      CHARACTER*30 PROP1
      CHARACTER*31 PROP2
      CHARACTER*37 PROP3
      CHARACTER*10 UNITS
      COMMON
     ./FLAG1 / KTITLE,KOMENT
     ./NBFILE/ NBF(20)
     ./PAR21 / ID1(7,LMF),LITEXP(LMF)
     ./PAR22 / XEXP(LMF),ERREXP(LMF),WEXP(LMF)
     ./PAR23 / KOMEXP(LMF)
     ./PAR24 / NREF1(LMM),NREF2(LMM)
     ./PAR25 / XCALC(LMF)
     ./TXPROP/ PROP1(LMPR),PROP2(LMPR),PROP3(LMPR),UNITS(LMPR)
      DIMENSION NEED(LMPR)
C *** INITIALIZATION.
      IF(JPRINT.LT.0) RETURN
      NR1    = NREF1(MOL)
      NR2    = NREF2(MOL)
      IF(NR2.LT.NR1) RETURN
      DO 10 L=1,LMPR
      NEED(L)= 0
   10 CONTINUE
C *** FILE NUMBERS.
      NB6    = NBF(6)
C *** STANDARD OUTPUT FOR ECHOING INPUT DATA.
      IF(NTAB.LE.0) THEN
         WRITE(NB6,500)
         IF(JPRINT.LE.0 .AND. KSTOP.GT.0) WRITE(NB6,510) KSTOP
         WRITE(NB6,520)
         DO 20 NR=NR1,NR2
         ID1ABS = ABS(ID1(1,NR))
         NEED(ID1ABS) = 1
         WRITE(NB6,530) ID1(1,NR),(ID1(J,NR),J=4,7),XEXP(NR),ERREXP(NR),
     1                LITEXP(NR),KOMEXP(NR)(1:23)
   20    CONTINUE
C *** EXTENDED OUTPUT FOR ECHOING INPUT DATA.
      ELSE IF(NTAB.EQ.1) THEN
         WRITE(NB6,500)
         IF(JPRINT.LE.0 .AND. KSTOP.GT.0) WRITE(NB6,510) KSTOP
         WRITE(NB6,540)
         DO 30 NR=NR1,NR2
         ID1ABS = ABS(ID1(1,NR))
         NEED(ID1ABS) = 1
         WRITE(NB6,550) (ID1(J,NR),J=1,7),XEXP(NR),ERREXP(NR),WEXP(NR),
     1                LITEXP(NR),KOMEXP(NR)
   30    CONTINUE
C *** STANDARD OUTPUT FOR PRINTING RESULTS.
      ELSE
         WRITE(NB6,560) KTITLE
         WRITE(NB6,570)
         DO 40 NR=NR1,NR2
         ID1ABS = ABS(ID1(1,NR))
         NEED(ID1ABS) = 1
         ICALL  = ID1(2,NR)+90
         KOMOUT = ' '
         IF(ICALL.EQ.-1 .OR. ICALL.EQ.-2) THEN
            KOMOUT = 'NO SCF'
         ELSE IF(ICALL.EQ.-3) THEN
            KOMOUT = 'UNSUCCESSFUL'
         ELSE IF(ICALL.EQ.-4) THEN
            KOMOUT = 'NO PARAMETERS'
         ELSE IF(ICALL.EQ.-5) THEN
            KOMOUT = 'NO RELATIVE ENERGY'
         ELSE
            KOMOUT = KOMEXP(NR)(1:23)
         ENDIF
         WRITE(NB6,580) ID1(1,NR),(ID1(J,NR),J=4,7),XCALC(NR),XEXP(NR),
     1                XCALC(NR)-XEXP(NR),KOMOUT
   40    CONTINUE
      ENDIF
C *** WRITE TEXT FOR PROPERTIES (IF REQUESTED).
      IF(JPRINT.GE.5) THEN
         WRITE(NB6,590)
         DO 50 L=1,LMPR
         IF(NEED(L).EQ.1) WRITE(NB6,600) L,PROP3(L)
   50    CONTINUE
      ENDIF
      RETURN
  500 FORMAT(///1X,'EXPERIMENTAL REFERENCE DATA.')
  510 FORMAT(/  1X,'NUMBER OF LINES IGNORED BY SUBROUTINE INREF.',I5)
  520 FORMAT(/  1X,'PROPERTY DEFINITION  EXPERIMENT',
     1          1X,'EXPERIMENT LITERATURE',
     2       /  1X,'ID1 ID4 ID5 ID6 ID7     VALUE  ',
     3          1X,'   ERROR    REFERENCE'/)
  530 FORMAT(   1X,I3,4I4,1X,2F11.5,I8,5X,A)
  540 FORMAT(/  1X,'PROPERTY/SUBGROUP DEFINITION  EXPERIMENT',
     1          1X,'EXPERIMENT INDIVIDUAL LITERATURE',
     2       /  1X,'ID1  ID2 ID3 ID4 ID5 ID6 ID7     VALUE  ',
     3          1X,'   ERROR     WEIGHT    REFERENCE'/)
  550 FORMAT(   1X,I3,1X,6I4,1X,3F11.5,I8,5X,A)
  560 FORMAT(///1X,A,
     1       // 1X,'COMPARISON OF THEORETICAL AND EXPERIMENTAL',
     2          1X,'RESULTS.')
  570 FORMAT(/  1X,'PROPERTY DEFINITION  CALCULATED',
     1          1X,'  OBSERVED  DEVIATION',
     2       /  1X,'ID1 ID4 ID5 ID6 ID7     VALUE  ',
     3          1X,'    VALUE    CALC-OBS'/)
  580 FORMAT(   1X,I3,4I4,1X,3F11.5,3X,A)
  590 FORMAT(// 1X,'DESCRIPTION OF THE ID1-LABELS USED ABOVE.',
     1       /  1X,'ID1   ASSOCIATED PROPERTIES (WITH UNITS).'/)
  600 FORMAT(   1X,I3,3X,A)
      END
