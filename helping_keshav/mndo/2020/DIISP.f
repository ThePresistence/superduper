      SUBROUTINE DIISP (FA,FB,PA,PB,FDA,FDB,ED,BD,AD,X,LM2,LM4,N,
     1                  MXDIIS,MX1P,NDIIS,JDIIS,NPRINT,EDMAX)
C     *
C     DIIS CONVERGENCE ACCELERATION.
C     *
C     REFERENCES.
C     1) P. PULAY, CHEM.PHYS.LETT. 73, 393-398 (1980).
C     2) P. PULAY, J.COMPUT.CHEM. 3, 556-560 (1982).
C     3) T.P. HAMILTON AND P. PULAY, J.CHEM.PHYS. 84, 5728-5734 (1986).
C     *
C     NOTATION : I=INPUT, O=OUTPUT, S=SCRATCH.
C     FA(LM2,*)  CURRENT FOCK MATRIX (I). RHF OR ALPHA.
C                UPDATED FOCK MATRIX (O). RHF OR ALPHA.
C     FB(LM2,*)  CURRENT FOCK MATRIX (I). BETA (UHF).
C                UPDATED FOCK MATRIX (O). BETA (UHF).
C     PA(LM2,*)  CURRENT DENSITY MATRIX (I). RHF OR ALPHA.
C     PB(LM2,*)  CURRENT DENSITY MATRIX (I). BETA (UHF).
C     FDA(LM4,*) FOCK MATRICES FROM DIIS ITERATIONS (I,O). RHF OR ALPHA.
C     FDB(LM4,*) FOCK MATRICES FROM DIIS ITERATIONS (I,O). BETA (UHF).
C     ED(LM4,*)  ERROR MATRICES FROM DIFFERENT DIIS ITERATIONS (I,O).
C     BD(MX1P)   COEFFICIENT MATRIX FOR LINEAR EQUATIONS (I,O).
C     AD(MX1P)   COEFFICIENT MATRIX FOR LINEAR EQUATIONS (S).
C     X(MX+1)    RHS VECTOR FOR LINEAR EQUATIONS (S) OVERWRITTEN BY
C                SOLUTIONS  OF  LINEAR EQUATIONS (S).
C     LM2        LEADING DIMENSION OF CORRESPONDING SQUARE ARRAYS (I).
C     LM4        LEADING DIMENSION OF ED,FD: LM4=N*(N+1)/2 (I).
C     N          NUMBER OF BASIS FUNCTIONS (I).
C     MXDIIS     MAXIMUM NUMBER OF DIIS ITERATIONS ALLOWED (I).
C     MX1P       DIMENSION OF BD,AD: MX1P=(MXDIIS+1)*(MXDIIS+2)/2 (I).
C     NDIIS      OVERALL COUNTER FOR DIIS ITERATIONS (I,O).
C     JDIIS      INPUT OPTION FOR DIIS (I): JDIIS=IDIIS/10.
C                =0,1  SCALE DIAGONAL ELEMENTS OF ERROR MATRIX (REF.3).
C                =1,3  EVALUATE ERROR MATRIX ONLY VIA DSPMM (NOT DGEMM).
C     NPRINT     PRINTING FLAG (I).
C     EDMAX      MAXIMUM ELEMENT OF ERROR MATRIX (O), ABSOLUTE VALUE.
C     *
C     DIIS-RELATED MATRICES ARE STORED IN PACKED LINEAR FORM.
C     AD(MX1P) IS DESTROYED DURING THE SOLUTION OF THE LINEAR EQUATIONS.
C     BD(MX1P) IS KEPT AND UPDATED IN EACH DIIS ITERATION.
C     *
C     SIMPLE OVERFLOW MECHANISM IN THE CASE OF NDIIS.GT.MXDIIS:
C     THERE IS A SEPARATE COUNTER KDIIS=MOD(NDIIS-1,MXDIIS)+1
C     WHICH REMAINS BETWEEN 1 AND MXDIIS. IN THE CASE OF OVERFLOW,
C     THE DIIS PROCEDURE IS RESTARTED, AND THE DIIS EXTRAPOLATION
C     IS DONE USING THE LATEST KDIIS ITERATIONS.
C     *
C     USE LIMIT, ONLY: LM1, LMX
      USE LIMIT, ONLY: LM1
      IMPLICIT DOUBLE PRECISION (A-H,O-Z)
      PARAMETER (SCALE=1.02D0)
      LOGICAL UHF
      COMMON
     ./CONSTN/ ZERO,ONE,TWO,THREE,FOUR,PT5,PT25
     ./IJWORK/ IWORK(12*LM1)
C    ./INDEX / INDX(LMX)
     ./NBFILE/ NBF(20)
     ./UHF   / UHF
      DIMENSION FA(LM2*N),PA(LM2*N),FDA(LM4,MXDIIS+1)
      DIMENSION FB(LM2*N),PB(LM2*N),FDB(LM4,MXDIIS+1)
      DIMENSION ED(LM4,MXDIIS+1),BD(MX1P),AD(MX1P),X(MXDIIS+1)
C *** INITIALIZATION.
      NDIIS  = NDIIS+1
      KDIIS  = MOD(NDIIS-1,MXDIIS)+1
C *** COMPUTE THE ERROR MATRIX ED = F*P - P*F
C     NOTE THAT F AND P ARE SYMMETRIC, HENCE P*F = (F*P)transposed.
C     THE CALL TO DGEMM PROVIDES F*P IN A SCRATCH ARRAY (FDA).
C     THE CALL TO TRPDIF COMPUTES F*P - (F*P)transposed (ED).
      CALL ZZERO (N,N,FDA(1,KDIIS),LM2)
      CALL DGEMM ('N','N',N,N,N,ONE,FA,LM2,PA,LM2,ZERO,FDA(1,KDIIS),LM2)
      IF(UHF) THEN
        CALL DGEMM('N','N',N,N,N,ONE,FB,LM2,PB,LM2,ONE,FDA(1,KDIIS),LM2)
      ENDIF
      CALL TRPDIF (FDA(1,KDIIS),ED(1,KDIIS),N,LM2,LM4)
C *** FIND THE LARGEST ELEMENT OF THE ERROR MATRIX (IN ABSOLUTE VALUE).
      IEDMAX = IDAMAX(LM4,ED(1,KDIIS),1)
      EDMAX  = ABS(ED(IEDMAX,KDIIS))
C *** STORE THE CURRENT FOCK MATRIX.
      CALL LINEAR (FA,FDA(1,KDIIS),N,LM2,LM4)
      IF(UHF) THEN
         CALL LINEAR (FB,FDB(1,KDIIS),N,LM2,LM4)
      ENDIF
C *** UPDATE THE COEFFICIENT MATRIX (BD) OF THE LINEAR EQUATIONS.
C     FOR THE DEFINITION OF BD, SEE EQUATION (1) IN REFERENCE 2.
C     IT IS OBVIOUS FROM THIS DEFINITION THAT ONLY THE NEW ELEMENTS
C     OF BD (REFERRING TO KDIIS) NEED TO BE EVALUATED.
C     THE NONTRIVIAL ELEMENTS BD(KDIIS+1,LDIIS+1) ARE OBTAINED FROM
C     THE SCALAR PRODUCT OF THE FULL SYMMETRIC ERROR MATRICES ED FOR
C     ITERATIONS KDIIS AND LDIIS, RESPECTIVELY. ED IS ANTISYMMETRIC AND
C     ZERO ON THE DIAGONAL, HENCE THIS SCALAR PRODUCT CAN BE COMPUTED
C     AS TWICE THE DOT PRODUCT OF LINEARLY PACKED MATRICES (ED).
      MX1    = KDIIS+1
      KD     = (KDIIS*(KDIIS+1))/2+1
      BD(1)  = ZERO
      BD(KD) = -ONE
      DO 30 I=1,KDIIS
      BD(KD+I) = -TWO * DDOT(LM4,ED(1,I),1,ED(1,KDIIS),1)
   30 CONTINUE
      IF(JDIIS.LT.2) THEN
         BD(KD+KDIIS) = BD(KD+KDIIS)*SCALE
      ENDIF
      IF(KDIIS.EQ.1) RETURN
C *** COPY COEFFICIENTS FROM BD(MX1P) TO AD(MX1P).
C     SCRATCH ARRAY WILL BE OVERWRITTEN BY DSPSV.
      KX1P   = (MX1*(MX1+1))/2
      DO 40 I=1,KX1P
      AD(I)  = BD(I)
   40 CONTINUE
C *** DEFINE RHS VECTOR OF LINEAR EQUATIONS.
      X(1)   = -ONE
      DO 50 I=2,MX1
      X(I)   = ZERO
   50 CONTINUE
C *** SOLVE THE SYSTEM OF LINEAR EQUATIONS.
      CALL DSPSV ('U',MX1,1,AD,IWORK,X,MX1,INFO)
      IF(INFO.NE.0) THEN
         NB6 = NBF(6)
         IF(NPRINT.GE.2) WRITE(NB6,500) INFO
         NDIIS  = 0
         RETURN
      ENDIF
C *** DIIS EXTRAPOLATION FOR THE FOCK MATRIX.
      DO 60 I=1,LM4
      FA(I)  = ZERO
   60 CONTINUE
      DO 70 M=2,MX1
      CX     = X(M)
      CALL DAXPY (LM4,CX,FDA(1,M-1),1,FA,1)
   70 CONTINUE
      IF(UHF) THEN
         DO 80 I=1,LM4
         FB(I)  = ZERO
   80    CONTINUE
         DO 90 M=2,MX1
         CX     = X(M)
         CALL DAXPY (LM4,CX,FDB(1,M-1),1,FB,1)
   90    CONTINUE
      ENDIF
C *** DEBUG PRINT.
      IF(NPRINT.GE.5) THEN
         NB6    = NBF(6)
         IFDMAX = 0
         FDMAX  = ZERO
         DO 100 I=1,LM4
         FDIFF  = ABS(FA(I)-FDA(I,KDIIS))
         IF(FDIFF.GT.FDMAX) THEN
            IFDMAX = I
            FDMAX  = FDIFF
         ENDIF
  100    CONTINUE
         WRITE(NB6,600) NDIIS,KDIIS,MXDIIS
         WRITE(NB6,610) IEDMAX,EDMAX
         WRITE(NB6,620) IFDMAX,FDMAX
         WRITE(NB6,630)
         WRITE(NB6,640) (X(I),I=1,MX1)
         IF(NPRINT.GT.5) THEN
            WRITE(NB6,650)
            CALL VECPRT (ED(1,KDIIS),LM4,N)
            WRITE(NB6,660)
            CALL VECPRT (BD,MX1P,MX1)
            WRITE(NB6,670)
            CALL VECPRT (FDA(1,KDIIS),LM4,N)
            WRITE(NB6,680)
            CALL VECPRT (FA,LM4,N)
         ELSE
            WRITE(NB6,690)
         ENDIF
      ENDIF
C *** RETURN SQUARE FOCK MATRIX.
      CALL SQUARE (FA,FA,N,LM2,LM4)
      IF(UHF) THEN
         CALL SQUARE (FB,FB,N,LM2,LM4)
      ENDIF
      RETURN
  500 FORMAT( 1X,'ERROR CODE INFO =',I4,' FROM DSPSV IN DIIS SECTION.',
     1       /1X,'DIIS PROCEDURE IS RESTARTED.')
  600 FORMAT(///1X,'DEBUG PRINT FROM DIIS: NDIIS, KDIIS, MXDIIS  ',3I5)
  610 FORMAT(/  1X,'LARGEST ABSOLUTE DIIS ERROR   : ELEMENT',I11,F23.12)
  620 FORMAT(/  1X,'LARGEST CHANGE IN FOCK MATRIX : ELEMENT',I11,F23.12)
  630 FORMAT(/  1X,'DIIS SOLUTION VECTOR.'/)
  640 FORMAT(   5X,10F12.5)
  650 FORMAT(// 1X,'DIIS ERROR MATRIX.'/)
  660 FORMAT(// 1X,'DIIS B MATRIX.'/)
  670 FORMAT(// 1X,'INITIAL FOCK MATRIX.')
  680 FORMAT(// 1X,'UPDATED FOCK MATRIX.')
  690 FORMAT(1X)
      END
