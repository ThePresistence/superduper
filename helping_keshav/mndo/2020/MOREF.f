      SUBROUTINE MOREF (C,EIG,LM2,LM3,N,NPRINT)
C     *
C     STORE EIGENVALUES AND MO LABELS AS REFERENCE DATA.
C     *
C     THE LABELS NSYMO(I) ARE DEFINED AS FOLLOWS.
C     FOR NONDEGENERATE POINT GROUPS: MO SYMMETRY LABELS.
C     FOR DEGENERATE    POINT GROUPS: MO DEGENERACY NUMBERS.
C     *
      USE LIMIT, ONLY: LM1, LMX, LMV
      IMPLICIT DOUBLE PRECISION (A-H,O-Z)
      COMMON
     ./AMASS / AMS(LM1)
     ./ATOMC / COORD(3,LM1)
     ./ATOMS / NUMAT,NAT(LM1),NFIRST(LM1),NLAST(LM1)
     ./IJWORK/ ISYM(LMX),IJKDUM(LMV)
     ./NBFILE/ NBF(20)
     ./ORBENG/ EIGMO(LMX),NSYMO(LMX)
      DIMENSION C(LM2,LM3),EIG(LM3)
      DIMENSION IEL(3)
C     EIGENVALUES ARE CONSIDERED TO BE DEGENERATE IF THEY
C     DIFFER BY LESS THAN SMALL (EV).
      DATA SMALL/1.0D-03/
C *** INITIALIZATION.
      DO 10 I=1,N
      EIGMO(I) = EIG(I)
      NSYMO(I) = 1
   10 CONTINUE
      IF(N.EQ.1) RETURN
      IF(NPRINT.GT.5) THEN
         NPRNT = NPRINT
      ELSE
         NPRNT = -11
      ENDIF
C *** FIND MOLECULAR SYMMETRY FROM CARTESIAN COORDINATES.
      CALL ASSYM(NUMAT,AMS,COORD,ISUB,MSYM,IEL,ISYM,NPRNT)
      IF(ISUB.EQ.0 .OR. MSYM.EQ.0) RETURN
C *** ASSIGN TYPE OF MOLECULE AND HIGHEST-ORDER SYMMETRY AXIS.
      CALL AXIS (AMS,COORD,IAX,ITYPE,NUMAT)
C *** ASSIGNMENT OF MO SYMMETRY.
      IF(ITYPE.EQ.4) THEN
         CALL MOSYM (C,EIG,LM2,LM3,N,NSYMO,ISYM,ISUB,NPRNT)
C *** ASSIGNMENT OF MO DEGENERACY NUMBERS.
      ELSE
         DO 20 I=1,N-2
         IF(NSYMO(I).GT.1) GO TO 20
         IF((EIG(I+1)-EIG(I)).GT.SMALL) GO TO 20
         IF((EIG(I+2)-EIG(I)).GT.SMALL) THEN
            NSYMO(I)   = 2
            NSYMO(I+1) = 2
         ELSE
            NSYMO(I)   = 3
            NSYMO(I+1) = 3
            NSYMO(I+2) = 3
         ENDIF
   20    CONTINUE
         IF(NSYMO(N-1).EQ.1) THEN
            IF((EIG(N)-EIG(N-1)).LE.SMALL) THEN
               NSYMO(N)   = 2
               NSYMO(N-1) = 2
            ENDIF
         ENDIF
      ENDIF
C *** PRINTING SECTION.
      IF(NPRNT.GE.5) THEN
         NB6 = NBF(6)
         WRITE(NB6,500)
         DO 100 I=1,N
         WRITE(NB6,510) I,EIG(I),NSYMO(I)
  100    CONTINUE
      ENDIF
      RETURN
  500 FORMAT (///4X,'MO     EIGENVALUE     NSYMO'/)
  510 FORMAT (   1X,I5,F15.5,5X,I2)
      END
