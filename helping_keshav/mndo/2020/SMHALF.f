      SUBROUTINE SMHALF (C,E,F,H,Q,S,LM2,LM3,LM4,MODE,NPRINT)
C     *
C     COMPUTE OVERLAP MATRIX AND S**(-1/2) MATRIX.
C     *
C     NOTATION.
C     C       SCRATCH ARRAY FOR EIGENVECTORS OF OVERLAP MATRIX.
C     E       SCRATCH ARRAY FOR EIGENVALUES  OF OVERLAP MATRIX.
C     F       S**(-1/2) MATRIX.
C     H       OVERLAP MATRIX (DESTROYED).
C     Q       SCRATCH ARRAY FOR DIAGONALIZATION BUFFER.
C     S       OVERLAP MATRIX (SAVED).
C     MODE=0  RETURN F AS LOWER TRIANGLE.
C     MODE=1  RETURN F AS SQUARE MATRIX.
C     NPRINT  PRINTING FLAG.
C     *
C     OUTPUT.
C     F       S**(-1/2) MATRIX (STORED ACCORDING TO MODE).
C     S       OVERLAP MATRIX   (STORED AS LOWER TRIANGLE).
C     *
      USE LIMIT, ONLY: LM1
      IMPLICIT DOUBLE PRECISION (A-H,O-Z)
      COMMON
     ./ATOMC / COORD(3,LM1)
     ./ATOMS / NUMAT,NAT(LM1),NFIRST(LM1),NLAST(LM1)
     ./CONSTN/ ZERO,ONE,TWO,THREE,FOUR,PT5,PT25
     ./EXTRA2/ SIJ(14),T(14),YY(675)
     ./INOPT2/ IN2(300)
     ./NBFILE/ NBF(20)
     ./ORBITS/ NUMB,NORBS,NMOS,NALPHA,NBETA
      DIMENSION C(LM2,LM3),E(LM2),F(LM4),H(LM4),Q(LM2,5),S(LM4)
C$OMP THREADPRIVATE (/EXTRA2/)
C *** FILE NUMBERS.
      NB6    = NBF(6)
C *** COMPUTE OVERLAP INTEGRALS.
      IOP    = IN2(2)
      DO 10 I=1,LM4
      H(I)   = ZERO
   10 CONTINUE
      DO 20 I=1,NORBS
      II     = (I*(I+1))/2
      H(II)  = ONE
   20 CONTINUE
      DO 40 I=2,NUMAT
      NI     = NAT(I)
      IA     = NFIRST(I)
      IORBS  = NLAST(I)-IA+1
      DO 30 J=1,I-1
      NJ     = NAT(J)
      JA     = NFIRST(J)
      JORBS  = NLAST(J)-JA+1
      CALL ROTMAT (J,I,JORBS,IORBS,NUMAT,COORD,R,YY)
      IF(IOP.EQ.-5.OR.IOP.EQ.-6.OR.IOP.EQ.-8.OR.IOP.EQ.-9
     1            .OR.IOP.EQ.-22 .OR.IOP.EQ.-23) THEN
         CALL SPOVER (I,J,R,T)
      ELSE
         CALL OVERLP (NI,NJ,R,T,0,0)
      ENDIF
      CALL ROTBET (IA,JA,IORBS,JORBS,T,YY,H,LM4)
   30 CONTINUE
   40 CONTINUE
      DO 50 I=1,LM4
      S(I)   = H(I)
   50 CONTINUE
      IF(NPRINT.GE.5) THEN
         WRITE(NB6,500)
         CALL VECPRT (H,LM4,NORBS)
      ENDIF
C *** DIAGONALIZE OVERLAP MATRIX.
      CALL TDIAG (H,C,E,Q,LM4,LM2,NORBS,NORBS)
      IF(NPRINT.GE.5) THEN
         WRITE(NB6,510)
         CALL MATOUT (C,E,NORBS,NORBS,LM2,LM3)
      ENDIF
C *** COMPUTE INVERSE SQUARE ROOTS OF EIGENVALUES.
      DO 60 I=1,NORBS
      E(I)   = ONE/SQRT(ABS(E(I)))
   60 CONTINUE
C *** COMPUTE AND SAVE S**(-1/2) MATRIX.
      IJ     = 0
      DO 90 I=1,NORBS
      DO 80 J=1,I
      IJ     = IJ+1
      F(IJ)  = ZERO
      DO 70 K=1,NORBS
      F(IJ)  = F(IJ)+C(I,K)*E(K)*C(J,K)
   70 CONTINUE
   80 CONTINUE
   90 CONTINUE
      IF(NPRINT.GE.5) THEN
         WRITE(NB6,520)
         CALL VECPRT (F,LM4,NORBS)
      ENDIF
C *** GENERATE SQUARE MATRIX FROM LOWER TRIANGLE.
      IF(MODE.EQ.1) CALL SQUARE(F,F,NORBS,LM2,LM4)
      RETURN
  500 FORMAT(///1X,'OVERLAP MATRIX (DEBUG PRINT FROM SMHALF)'/)
  510 FORMAT(///1X,'EIGENVALUES AND EIGENVECTORS OF OVERLAP MATRIX'/)
  520 FORMAT(///1X,'INVERSE SQUARE ROOT OF OVERLAP MATRIX'/)
      END
