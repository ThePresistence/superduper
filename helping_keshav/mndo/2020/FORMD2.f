      SUBROUTINE FORMD2 (EIGVAL,FX,U,LMH,NVAR,NVARLM,DMAX,LRSCAL,IPRINT)
C     *
C     THIS ROUTINE DEFINES THE SEARCH DIRECTION VECTOR (D) FOR UPDATING
C     THE GEOMETRY USING A PURE NEWTON-RAPHSON STEP.
C     *
      USE LIMIT, ONLY: LMV,LMYL
      IMPLICIT DOUBLE PRECISION (A-H,O-Z)
      LOGICAL PRT
      COMMON
     ./CONSTN/ ZERO,ONE,TWO,THREE,FOUR,PT5,PT25
     ./FLPOCM/ GRAD(LMV),D(LMV),ALPHA,FIP1
     ./INOPT2/ IN2(300)
     ./NBFILE/ NBF(20)
     ./OPTEF / OLDG(LMV),VMODE(LMV),DD
     ./YARLAG/ YLVAL(LMYL),YLGRD(LMYL),YLD(LMYL),NYL
      DIMENSION EIGVAL(NVARLM),FX(NVARLM),U(LMH,LMH)
C *** FILE NUMBERS.
      NB6    = NBF(6)
C     INPUT OPTIONS
      ICROSS = IN2(160)
C *** INITIALIZATION.
      PRT    = IPRINT.GE.1
      ALPHA  = ONE
      SKAL   = ONE
C     XLAMD  = ZERO
C     XLAMD0 = ZERO
C *** DETERMINE NEWTON-RAPHSON STEP.
      DO 10 I=1,NVAR
      D(I)   = ZERO
   10 CONTINUE
      IF(ICROSS.EQ.5) THEN
         DO 15 I=1,NYL
            YLD(I) = ZERO
   15    CONTINUE
      ENDIF
      DO 30 I=1,NVARLM
         IF(EIGVAL(I).NE.ZERO) THEN
            TEMP   = -FX(I)/EIGVAL(I)
         ELSE
            TEMP   = ZERO
         ENDIF
         DO 20 J=1,NVAR
            D(J)   = D(J)+TEMP*U(J,I)
   20    CONTINUE
         IF(ICROSS.EQ.5) THEN
            DO 25 J=1,NYL
               YLD(J) = YLD(J)+TEMP*U(NVAR+J,I)
   25       CONTINUE
         ENDIF
   30 CONTINUE
C *** CHECK STEP SIZE.
C     NB: CASE ICROSS=5: LAGRANGE MULTIPLIER STEPS
C     ARE NOT INCLUDED IN THE (PURELY GEOMETRICAL)
C     TRUST RADIUS
      DDNR   = SQRT(DDOT(NVAR,D,1,D,1))
      DD     = DDNR
      IF(DDNR.GT.DMAX .AND. LRSCAL.EQ.1) THEN
         SKAL   = DMAX/DDNR
         DO 40 J=1,NVAR
         D(J)   = D(J)*SKAL
   40    CONTINUE
         IF(ICROSS.EQ.5) THEN
            DO 45 J=1,NYL
               YLD(J) = YLD(J)*SKAL
   45       CONTINUE
         ENDIF
         DD     = DMAX
      ENDIF
C *** PRINTING SECTION.
      IF(PRT) THEN
         WRITE(NB6,500) DDNR
         IF(DDNR.GT.DMAX) THEN
            WRITE(NB6,510) DMAX
            IF(LRSCAL.EQ.1) THEN
               WRITE(NB6,520) SKAL
            ELSE
               WRITE(NB6,530) DMAX/DDNR
            ENDIF
         ENDIF
      ENDIF
      RETURN
  500 FORMAT( 1X,'PURE NR STEP SIZE      = ',E11.4)
  510 FORMAT( 1X,'EXCEEDS TRUST RADIUS   = ',E11.4)
  520 FORMAT( 1X,'SCALE FACTOR APPLIED   = ',E11.4)
  530 FORMAT( 1X,'SCALE FACTOR WOULD BE  = ',E11.4)
      END
