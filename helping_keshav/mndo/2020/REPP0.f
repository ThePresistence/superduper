      SUBROUTINE REPP0 (NI,R,A,CORE)
C     *
C     CALCULATION OF THE INTEGRALS BETWEEN AN ATOM (I) WITH AN SP BASIS
C     AND AN EXTERNAL POINT CHARGE (J) WITHOUT BASIS ORBITALS IN LOCAL
C     COORDINATES. THE EXTERNAL POINT CHARGE IS 1 ATOMIC UNIT.
C     *
C     NOTATION. I=INPUT, O=OUTPUT.
C     NI        ATOMIC NUMBER OF THE ATOM (I).
C     R         DISTANCE BETWEEN ATOM AND CHARGE, IN ATOMIC UNITS (I).
C     A(K)      LOCAL TWO-CENTRE TWO-ELECTRON INTEGRALS, IN EV (O).
C     CORE(K,1) LOCAL TWO-CENTRE CORE-ELECTRON INTEGRALS, IN EV (O).
C     *
      USE LIMIT, ONLY: LMZ
      IMPLICIT DOUBLE PRECISION (A-H,O-Z)
      COMMON
     ./CONSTF/ A0,AFACT,EV,EVCAL,PI,W1,W2,BIGEXP
     ./CONSTN/ ZERO,ONE,TWO,THREE,FOUR,PT5,PT25
     ./MULTIP/ DD(6,0:LMZ),PO(9,0:LMZ)
      DIMENSION A(22),CORE(10,2)
C *** INTEGRALS INVOLVING ONLY S ORBITALS.
      R2     = R*R
      AEE    = (PO(1,NI)+PO(1,0))**2
      A(1)   = EV/SQRT(R2+AEE)
      CORE(1,1) = -A(1)
C *** INTEGRALS INVOLVING ALSO P ORBITALS.
      DA     = DD(2,NI)
      TWOQA  = DD(3,NI)*TWO
      ADE    = (PO(2,NI)+PO(1,0))**2
      AQE    = (PO(3,NI)+PO(1,0))**2
      X2     = -PT5/SQRT(R2+AQE)
      X3     = -PT5/SQRT((R+DA)**2+ADE)
      X4     =  PT5/SQRT((R-DA)**2+ADE)
      X5     = PT25/SQRT((R-TWOQA)**2+AQE)
      X6     = PT25/SQRT((R+TWOQA)**2+AQE)
      X7     =  PT5/SQRT(R2+TWOQA*TWOQA+AQE)
      A(2)   = (X3+X4)*EV
      A(3)   = A(1)+(X2+X5+X6)*EV
      A(4)   = A(1)+(X2+X7)*EV
      CORE(2,1) = -A(2)
      CORE(3,1) = -A(3)
      CORE(4,1) = -A(4)
      RETURN
      END
