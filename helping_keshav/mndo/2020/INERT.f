      SUBROUTINE INERT (AMS,C,PC,PM,PR,ITYPE,N,NAT,LPRINT)
*VOCL TOTAL,SCALAR
C     *
C     MOMENTS OF INERTIA AND RELATED QUANTITIES.
C     *
C     NOTATION. I=INPUT, O=OUTPUT.
C     AMS     ATOMIC MASSES, IN AMU (I).
C     C       CARTESIAN COORDINATES FOR ORIGINAL  AXES, IN ANGSTROM (I).
C     PC      CARTESIAN COORDINATES FOR PRINCIPAL AXES, IN ANGSTROM (O).
C     PM      PRINCIPAL MOMENTS OF INERTIA, IN AMU*ANGSTROM**2 (O).
C     PR      ROTATION MATRIX RELATING ORIGINAL AND PRINCIPAL AXES (O).
C     ITYPE   TYPE OF MOLECULE ACCORDING TO MOMENTS OF INERTIA (O).
C             = 1  LINEAR.
C             = 2  SPHERICAL TOP.
C             = 3  SYMMETRIC TOP.
C             = 4  ASYMMETRIC TOP.
C     N       NUMBER OF ATOMS (I).
C     NAT     ATOMIC NUMBERS (I).
C     LPRINT  PRINTING FLAG (I).
C     *
      IMPLICIT DOUBLE PRECISION (A-H,O-Z)
      COMMON /NBFILE/ NBF(20)
      DIMENSION AMS(N),C(3,N),PC(3,N),PM(3),PR(3,3),NAT(N)
      DIMENSION DUM(15),T(6)
C     *
C     CONVERSION FACTORS.
C     C1     FROM AMU*ANGSTROM**2 TO 10**(-40)*GRAM*CM**2
C     C2     FROM AMU*ANGSTROM**2 TO CM**(-1)
C     C3     FROM CM**(-1) TO MHZ
      DATA C1/1.6605655D0/
      DATA C2/16.85803902D0/
      DATA C3/2.99792458D+04/
      DATA ZERO,ONE/0.0D0,1.0D0/
C     MOMENTS OF INERTIA ARE CONSIDERED TO BE ZERO IF THEY ARE
C     LESS THAN SMALL, IN AMU*ANGSTROM**2.
      DATA SMALL/1.0D-07/
C     MOMENTS OF INERTIA ARE CONSIDERED TO BE EQUAL IF THEIR RATIO
C     DIFFERS FROM 1 BY LESS THAN SMALLM.
      DATA SMALLM/1.0D-06/
C     ATOMS ARE CONSIDERED TO LIE ON A SYMMETRY ELEMENT IF THE
C     RESPECTIVE CARTESIAN COORDINATES DIFFER BY LESS THAN SMALLC.
      DATA SMALLC/1.0D-07/
C *** FILE NUMBERS.
      NB6    = NBF(6)
C     *
C     MOLECULAR WEIGHT AND CENTER-OF-GRAVITY COORDINATES.
      AMSUM  = ZERO
      CGX    = ZERO
      CGY    = ZERO
      CGZ    = ZERO
      DO 10 I=1,N
      AMSI   = AMS(I)
      AMSUM  = AMSUM+AMSI
      CGX    = CGX+AMSI*C(1,I)
      CGY    = CGY+AMSI*C(2,I)
      CGZ    = CGZ+AMSI*C(3,I)
   10 CONTINUE
      RAMSUM = ONE/AMSUM
      CGX    = CGX*RAMSUM
      CGY    = CGY*RAMSUM
      CGZ    = CGZ*RAMSUM
C     *
C     TENSOR OF INERTIA.
      DO 30 I=1,6
      T(I)   = ZERO
   30 CONTINUE
      DO 40 I=1,N
      AMSI   = AMS(I)
      XI     = C(1,I)-CGX
      YI     = C(2,I)-CGY
      ZI     = C(3,I)-CGZ
      T(1)   = T(1)+AMSI*(YI*YI+ZI*ZI)
      T(2)   = T(2)-AMSI* XI*YI
      T(3)   = T(3)+AMSI*(XI*XI+ZI*ZI)
      T(4)   = T(4)-AMSI* XI*ZI
      T(5)   = T(5)-AMSI* YI*ZI
      T(6)   = T(6)+AMSI*(XI*XI+YI*YI)
   40 CONTINUE
C     *
C     PRINCIPAL MOMENTS OF INERTIA, IN AMU*ANGSTROM**2.
C     IF THE MOLECULE IS ALIGNED ALONG THE PRINCIPAL AXES,
C     THE EIGENVALUES AND EIGENVECTORS ARE DEFINED DIRECTLY
C     SUCH THAT THE EIGENVALUES ARE IN ASCENDING ORDER.
C     OTHERWISE THEY ARE DETERMINED BY DIAGONALIZATION.
      IF(ABS(T(2)).LT.SMALL .AND. ABS(T(4)).LT.SMALL .AND.
     1   ABS(T(5)).LT.SMALL) THEN
         DO 42 I=1,3
         DO 41 J=1,3
         PR(I,J)= ZERO
   41    CONTINUE
   42    CONTINUE
         DO 44 I=1,3
         II     = I*(I+1)/2
         K      = 1
         DO 43 J=1,3
         IF(I.EQ.J) GO TO 43
         JJ     = J*(J+1)/2
         IF(T(II).GT.T(JJ)) K=K+1
         IF(T(II).EQ.T(JJ) .AND. J.GT.I) K=K+1
   43    CONTINUE
         PM(K)  = T(II)
         PR(I,K)= ONE
   44    CONTINUE
      ELSE
         CALL TDIAG (T,PR,PM,DUM,6,3,3,3)
      ENDIF
C     *
C     ASSIGN TYPE OF MOLECULE FROM PRINCIPAL MOMENTS OF INERTIA.
      ITYPE  = 4
      JTYPE  = 0
      F12    = ONE-PM(1)/PM(2)
      F23    = ONE-PM(2)/PM(3)
      IF(F12.LT.SMALLM .OR.  F23.LT.SMALLM) ITYPE=3
      IF(F12.LT.SMALLM .AND. F23.LT.SMALLM) ITYPE=2
      IF(PM(1).LT.SMALL) ITYPE=1
      IF(F23.LT.SMALLM .AND. ITYPE.EQ.3) JTYPE=1
C     *
C     SWITCH AXES FOR LINEAR MOLECULES AND PROLATE SYMMETRIC TOPS
C     TO CONFORM TO THE SPECTROSCOPIC CONVENTION THAT THE Z-AXIS
C     IS THE SYMMETRY AXIS OF HIGHEST ORDER.
      IF(ITYPE.EQ.1 .OR. JTYPE.EQ.1) THEN
         TEMP   = PM(1)
         PM(1)  = PM(3)
         PM(3)  = TEMP
         DO 50 I=1,3
         TEMP   = PR(I,1)
         PR(I,1)= PR(I,3)
         PR(I,3)= TEMP
   50    CONTINUE
      ENDIF
C     *
C     AT THIS POINT, SYMMETRIC TOPS HAVE THE Z-AXIS AS SYMMETRY AXIS.
C     WE NOW FIX THE XZ-PLANE AS A PLANE OF SYMMETRY.
      IF(ITYPE.EQ.3) THEN
         DO 70 I=1,N
         XI = C(1,I)-CGX
         YI = C(2,I)-CGY
         ZI = C(3,I)-CGZ
         T1 = XI*PR(1,1)+YI*PR(2,1)+ZI*PR(3,1)
         T2 = XI*PR(1,2)+YI*PR(2,2)+ZI*PR(3,2)
         IF(ABS(T1).LT.SMALLC .AND. ABS(T2).LT.SMALLC) GO TO 70
         IF(ABS(T1).GT.SMALLC .AND. ABS(T2).LT.SMALLC) GO TO 80
         T3 = SQRT(T1*T1+T2*T2)
         T1 = T1/T3
         T2 = T2/T3
         DO 60 J=1,3
         A1 = T1*PR(J,1)+T2*PR(J,2)
         A2 = T1*PR(J,2)-T2*PR(J,1)
         PR(J,1) = A1
         PR(J,2) = A2
   60    CONTINUE
         GO TO 80
   70    CONTINUE
   80    CONTINUE
      ENDIF
C     *
C     CHECK ROTATION MATRIX FOR CONSERVATION OF CHIRALITY.
C     TEST   = PR(1,1)*(PR(2,2)*PR(3,3)-PR(3,2)*PR(2,3))
C    1        +PR(1,2)*(PR(2,3)*PR(3,1)-PR(2,1)*PR(3,3))
C    2        +PR(1,3)*(PR(2,1)*PR(3,2)-PR(2,2)*PR(3,1))
C     IF(TEST.LT.ZERO) THEN
C        PR(1,2) = -PR(1,2)
C        PR(2,2) = -PR(2,2)
C        PR(3,2) = -PR(3,2)
C     ENDIF
C     *
C     CARTESIAN COORDINATES WITH RESPECT TO PRINCIPAL AXES.
      DO 90 I=1,N
      XI     = C(1,I)-CGX
      YI     = C(2,I)-CGY
      ZI     = C(3,I)-CGZ
      PC(1,I) = XI*PR(1,1)+YI*PR(2,1)+ZI*PR(3,1)
      PC(2,I) = XI*PR(1,2)+YI*PR(2,2)+ZI*PR(3,2)
      PC(3,I) = XI*PR(1,3)+YI*PR(2,3)+ZI*PR(3,3)
   90 CONTINUE
C     *
C     PRINTING SECTION.
      IF(LPRINT.LE.-5) RETURN
C     ROTATION MATRIX AND COORDINATES.
      IF(LPRINT.GT.1) THEN
         WRITE(NB6,600)
         DO 110 I=1,N
         WRITE(NB6,610) NAT(I),(C(J,I),J=1,3)
  110    CONTINUE
         WRITE(NB6,620)
         DO 120 I=1,3
         WRITE(NB6,610) I,(PR(I,J),J=1,3)
  120    CONTINUE
         WRITE(NB6,630)
         DO 130 I=1,N
         WRITE(NB6,610) NAT(I),(PC(J,I),J=1,3)
  130    CONTINUE
         WRITE(NB6,640) CGX,CGY,CGZ
      ENDIF
C     MOMENTS OF INERTIA.
      WRITE(NB6,500) AMSUM
      WRITE(NB6,510) (PM(I),I=1,3)
      DUM(1) = C1*PM(1)
      DUM(2) = C1*PM(2)
      DUM(3) = C1*PM(3)
      WRITE(NB6,520) (DUM(I),I=1,3)
C     ROTATIONAL CONSTANTS.
      DO 140 I=1,3
      IF(PM(I).LT.SMALL) THEN
         DUM(I) = ZERO
      ELSE
         DUM(I) = C2/PM(I)
      ENDIF
  140 CONTINUE
      WRITE(NB6,530) (DUM(I),I=1,3)
      DUM(1) = C3*DUM(1)
      DUM(2) = C3*DUM(2)
      DUM(3) = C3*DUM(3)
      WRITE(NB6,540) (DUM(I),I=1,3)
C     TYPE OF MOLECULE.
      IF(ITYPE.EQ.1) WRITE(NB6,550)
      IF(ITYPE.EQ.2) WRITE(NB6,560)
      IF(ITYPE.EQ.3 .AND. JTYPE.EQ.0) WRITE(NB6,570)
      IF(ITYPE.EQ.3 .AND. JTYPE.EQ.1) WRITE(NB6,580)
      IF(ITYPE.EQ.4) WRITE(NB6,590)
      RETURN
  500 FORMAT(///5X,'MOLECULAR WEIGHT IN AMU                ', F15.5)
  510 FORMAT(/  5X,'MOMENTS OF INERTIA IN AMU*ANGSTROM**2  ',3F15.5)
  520 FORMAT(/  5X,'MOMENTS OF INERTIA IN 10**(-40)*G*CM**2',3F15.5)
  530 FORMAT(/  5X,'ROTATIONAL CONSTANTS IN CM-1           ',3F15.5)
  540 FORMAT(/  5X,'ROTATIONAL CONSTANTS IN MHZ            ',3F15.2)
  550 FORMAT(/  5X,'THE MOLECULE IS LINEAR')
  560 FORMAT(/  5X,'THE MOLECULE IS A SPHERICAL TOP')
  570 FORMAT(/  5X,'THE MOLECULE IS AN OBLATE SYMMETRIC TOP')
  580 FORMAT(/  5X,'THE MOLECULE IS A PROLATE SYMMETRIC TOP')
  590 FORMAT(/  5X,'THE MOLECULE IS AN ASYMMETRIC TOP')
  600 FORMAT(///5X,'CARTESIAN COORDINATES FOR ORIGINAL AXES.',
     1       // 8X,'NI',14X,'X',13X,'Y',13X,'Z'/)
  610 FORMAT(   5X,I5,4X,3F15.10)
  620 FORMAT(///5X,'ROTATION MATRIX RELATING ORIGINAL AND ',
     1             'PRINCIPAL AXES.',
     2       // 9X,'I',14X,'1',13X,'2',13X,'3'/)
  630 FORMAT(///5X,'CARTESIAN COORDINATES FOR PRINCIPAL AXES.',
     1       // 8X,'NI',14X,'X',13X,'Y',13X,'Z'/)
  640 FORMAT(///5X,'CARTESIAN COORDINATES OF CENTER OF GRAVITY.',
     1       // 24X,'X',13X,'Y',13X,'Z',
     2       // 14X,3F15.10)
      END
