      SUBROUTINE VECSYM (AMS,COORD,C,EIG,LM2,N,NUMAT,ISYM,NSYM,LPRINT)
C     *
C     ASSIGNMENT OF SYMMETRY OF NORMAL MODES.
C     *
C     THE PRESENT VERSION DOES NOT HANDLE DEGENERATE POINT GROUPS.
C     IT IS RESTRICTED TO THE ABELIAN GROUPS CS,C2,C2V,D2H,C2H,D2,CI.
C     *
      IMPLICIT DOUBLE PRECISION (A-H,O-Z)
      CHARACTER*3 NGROUP
      CHARACTER*4 IRREP
      COMMON
     ./CONSTN/ ZERO,ONE,TWO,THREE,FOUR,PT5,PT25
     ./NBFILE/ NBF(20)
     ./SYMLAB/ NGROUP(7),IRREP(26)
     ./SYMLIM/ IRREPA(7),IRREPN(7)
      DIMENSION AMS(NUMAT),COORD(3,NUMAT),C(LM2,LM2),EIG(LM2)
      DIMENSION ISYM(3,NUMAT),NSYM(LM2)
      DIMENSION IEL(3),IS(3)
C *** FILE NUMBERS.
      NB6    = NBF(6)
C *** INITIALIZATION.
      DO 10 I=1,N
      NSYM(I) = 1
   10 CONTINUE
C *** FIND MOLECULAR SYMMETRY FROM CARTESIAN COORDINATES.
      CALL ASSYM (NUMAT,AMS,COORD,ISUB,MSYM,IEL,ISYM,LPRINT)
      IF(ISUB.EQ.0 .OR. MSYM.EQ.0) RETURN
C *** LOOP OVER MODES FOR SYMMETRY ASSIGNMENT.
      DO 60 I=1,N
C     LOOP OVER SYMMETRY OPERATIONS.
      DO 20 M=1,MSYM
      MEL    = IEL(M)
      CALL BSYM(C(1,I),ISYM,LM2,NUMAT,M,MEL,ISS,IER,ONE)
      IF(IER.EQ.1) GO TO 200
      IS(M)  = ISS
   20 CONTINUE
C     ASSIGN MODE SYMMETRY FOR A GIVEN POINT GROUP.
      IS1    = IS(1)
      GO TO (30,30,40,50,40,40,30),ISUB
C     CS,C2,CI.
   30 IF(IS1.EQ. 1) NSYM(I)=1
      IF(IS1.EQ.-1) NSYM(I)=2
      GO TO 60
C     C2V,C2H,D2.
   40 IS2    = IS(2)
      IF(ISUB.GT.3) THEN
         IS1 = IS(2)
         IS2 = IS(1)
      ENDIF
      IF(IS1.EQ. 1 .AND. IS2.EQ. 1) NSYM(I)=1
      IF(IS1.EQ.-1 .AND. IS2.EQ.-1) NSYM(I)=2
      IF(IS1.EQ. 1 .AND. IS2.EQ.-1) NSYM(I)=3
      IF(IS1.EQ.-1 .AND. IS2.EQ. 1) NSYM(I)=4
      GO TO 60
C     D2H.
   50 IS2    = IS(2)
      IS3    = IS(3)
      IF(IS1.EQ. 1 .AND. IS2.EQ. 1 .AND. IS3.EQ. 1) NSYM(I)=1
      IF(IS1.EQ.-1 .AND. IS2.EQ.-1 .AND. IS3.EQ.-1) NSYM(I)=2
      IF(IS1.EQ.-1 .AND. IS2.EQ.-1 .AND. IS3.EQ. 1) NSYM(I)=3
      IF(IS1.EQ. 1 .AND. IS2.EQ. 1 .AND. IS3.EQ.-1) NSYM(I)=4
      IF(IS1.EQ.-1 .AND. IS2.EQ. 1 .AND. IS3.EQ.-1) NSYM(I)=5
      IF(IS1.EQ. 1 .AND. IS2.EQ.-1 .AND. IS3.EQ. 1) NSYM(I)=6
      IF(IS1.EQ. 1 .AND. IS2.EQ.-1 .AND. IS3.EQ.-1) NSYM(I)=7
      IF(IS1.EQ.-1 .AND. IS2.EQ. 1 .AND. IS3.EQ. 1) NSYM(I)=8
   60 CONTINUE
C *** PRINTING SECTION.
      IF(LPRINT.LT.-2) RETURN
      WRITE(NB6,500)
      NLAB   = IRREPA(ISUB)
      DO 100 I=1,N
      LABEL  = NLAB+NSYM(I)
      WRITE(NB6,510) I,EIG(I),IRREP(LABEL),NSYM(I)
  100 CONTINUE
      RETURN
C *** ERROR SECTION.
  200 IMO    = I
      DO 210 I=1,N
      NSYM(I) = 1
  210 CONTINUE
      IF(LPRINT.LT.-4) RETURN
      WRITE(NB6,520) IMO
      RETURN
  500 FORMAT (///5X,'VECTOR    FREQUENCY     LABEL     NSYM'/)
  510 FORMAT (   4X,I5,F15.5,5X,A4,6X,I2)
  520 FORMAT (///5X,'SYMMETRY OF VECTOR',I3,' CANNOT BE ASSIGNED.')
      END
