      SUBROUTINE RESET2 (S,B,COR,SG,BG,CORG,LM2,LM4,MODE)
C     *
C     REPLACE ELEMENTS OF MATRICES (SG,BG,CORG) REFERRING TO A DISPLACED
C     ATOM (I) BY ELEMENTS COMPUTED AT THE REFERENCE GEOMETRY (S,B,COR).
C     THIS ROUTINE IS USED ONLY FOR THE GRADIENT IN OM2.
C     *
C     NOTATION: I=INPUT, O=OUTPUT.
C     S(LM4)    OVERLAP MATRIX AT REFERENCE GEOMETRY (I).
C     B(LM4)    RESONANCE INTEGRALS AT REFERENCE GEOMETRY (I).
C     COR()     LOCAL CORE MATRIX AT REFERENCE GEOMETRY (I).
C     SG(LM4)   OVERLAP MATRIX AT DISTORTED GEOMETRY (I,O).
C     BG(LM4)   RESONANCE INTEGRALS AT DISTORTED GEOMETRY (I,O).
C     CORG()    LOCAL CORE MATRIX AT DISTORTED GEOMETRY (I,O).
C     MODE      NUMBER OF DISPLACED ATOM.
C     *
      USE LIMIT, ONLY: LM1, LMX
      IMPLICIT DOUBLE PRECISION (A-H,O-Z)
      COMMON
     ./ATOMS / NUMAT,NAT(LM1),NFIRST(LM1),NLAST(LM1)
     ./INDEX / INDX(LMX)
      DIMENSION S(LM4),B(LM4),SG(LM4),BG(LM4)
      DIMENSION COR(LM2,LM2),CORG(LM2,LM2)
C *** INITIALIZATION.
      IA     = NFIRST(MODE)
      IB     = NLAST(MODE)
      NORBS  = NLAST(NUMAT)
C *** REPLACE OVERLAP AND RESONANCE INTEGRALS.
      IF(MODE.GT.1) THEN
         JLAST  = NLAST(MODE-1)
         DO 20 I=IA,IB
         IND = INDX(I)
         DO 10 J=1,JLAST
         SG(IND+J) = S(IND+J)
         BG(IND+J) = B(IND+J)
   10    CONTINUE
   20    CONTINUE
      ENDIF
      IF(MODE.LT.NUMAT) THEN
         JFIRST = NFIRST(MODE+1)
         DO 40 J=JFIRST,NORBS
         IND = INDX(J)
         DO 30 I=IA,IB
         SG(IND+I) = S(IND+I)
         BG(IND+I) = B(IND+I)
   30    CONTINUE
   40    CONTINUE
      ENDIF
C *** REPLACE LOCAL CORE INTEGRALS.
      DO 60 J=1,NUMAT
      DO 50 I=IA,IB
      CORG(I,J) = COR(I,J)
   50 CONTINUE
   60 CONTINUE
      DO 70 I=1,NORBS
      CORG(I,MODE) = COR(I,MODE)
   70 CONTINUE
      RETURN
      END
