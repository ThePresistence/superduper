      SUBROUTINE OCCDEF (CA,CB,EA,EB,LM2,LM3)
C     *
C     DEFINE OCCUPATION NUMBERS.
C     *
C     NOTATION IN COMMON BLOCK OCCFL.
C     IMOCC    EQUAL TO ABS(IUHF) (INPUT VARIABLE IUHF).
C              SYMMETRY OCCUPATION NUMBERS ARE DEFINED FOR IMOCC.EQ.3.
C              AVERAGE  OCCUPATION NUMBERS ARE DEFINED FOR IMOCC.EQ.4.
C              THE USER MAY SUPPLY ADDITIONAL CODE IN THIS SUBROUTINE
C              TO DEFINE NON-STANDARD OCCUPATIONS FOR IMOCC.GT.4.
C     NOCCA    NUMBER OF HIGHEST OCCUPIED RHF OR UHF-ALPHA ORBITAL.
C     NOCCB    NUMBER OF HIGHEST OCCUPIED UHF-BETA ORBITAL.
C     *
      USE LIMIT, ONLY: LMX
      IMPLICIT DOUBLE PRECISION (A-H,O-Z)
      COMMON
     ./CONSTN/ ZERO,ONE,TWO,THREE,FOUR,PT5,PT25
     ./OCCFL / IMOCC,NOCCA,NOCCB,MSUB,MOSUMA,MOSUMB,MOCCA(8),MOCCB(8)
     ./OCCNM / OCCA(LMX),OCCB(LMX)
     ./ORBITS/ NUMB,NORBS,NMOS,NALPHA,NBETA
      DIMENSION CA(LM2,LM3),EA(LM2)
      DIMENSION CB(LM2,LM3),EB(LM2)
C *** DEFINE OCCUPATION NUMBERS FOR IRREDUCIBLE REPRESENTATIONS
C     AS REQUESTED IN THE INPUT DATA.
      IF(IMOCC.EQ.3) THEN
         CALL OCCSYM (CA,CB,EA,EB,LM2,LM3)
C *** DEFINE OCCUPATION NUMBERS FOR QUASI-DEGENERATE LEVELS.
C     DISTRIBUTE ELECTRONS EVENLY OVER SUCH LEVELS.
C     DE IS THE ENERGY RANGE (IN EV) WITHIN WHICH LEVELS ARE
C     CONSIDERED TO BE QUASI-DEGENERATE.
C     THE CODE REFERS TO HALF-ELECTRON RHF CALCULATIONS WITH
C     NBETA DOUBLY OCCUPIED LEVELS AND (NALPHA-NBETA) ELECTRONS
C     IN OPEN SHELLS.
      ELSE IF(IMOCC.EQ.4) THEN
         NOCCA  = NUMB
         DO 70 I=1,NALPHA
         OCCA(I) = ONE
   70    CONTINUE
         DO 80 I=NBETA+1,NALPHA
         OCCA(I) = PT5
   80    CONTINUE
         DO 90 I=NALPHA+1,NORBS
         OCCA(I) = ZERO
   90    CONTINUE
         DE  = 0.1D0
         I   = 1
  100    IF(I.GE.NORBS) GO TO 140
         IC  = 1
         A   = OCCA(I)
         DO 110 I1=I,NORBS-1
         I2  = I1+1
         EE  = EA(I2)-EA(I1)
         IF(EE.GT.DE) GO TO 120
         IC  = IC+1
         A   = A+OCCA(I2)
  110    CONTINUE
  120    A   = A / IC
         IF(A.LT.0.01D0) THEN
            GO TO 140
         ELSE
            IF(NOCCA.LT.I1) NOCCA=I1
         ENDIF
         DO 130 K=I,I1
         OCCA(K) = A
  130    CONTINUE
         I   = I2
         GO TO 100
  140    CONTINUE
      ELSE IF(IMOCC.EQ.5) THEN
         CALL SMEAR (EA,EB,LM2)
      ELSE IF(IMOCC.EQ.6) THEN
         CALL OCCFLO(EA,EB,LM2)
      ENDIF
      RETURN
      END
