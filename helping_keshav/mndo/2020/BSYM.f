      SUBROUTINE BSYM (C,ISYM,LM2,NUMAT,M,IEL,ISS,IER,FACT)
C     *
C     SYMMETRY ASSIGNMENT OF A NORMAL MODE EIGENVECTOR C(LM2)
C     WITH RESPECT TO A SYMMETRY ELEMENT.
C     *
C     NOTATION. I=INPUT, O=OUTPUT.
C     C(LM2)    NORMAL MODE COEFFICIENTS (I).
C     ISYM(3,*) PERMUTED TABLE OF ATOM NUMBERS AFTER APPLYING
C               THE CORRESPONDING SYMMETRY OPERATIONS (I).
C     NUMAT     NUMBER OF ATOMS (I).
C     M         INDEX OF CURRENT SYMMETRY ELEMENT (I) IN ISYM(M,*).
C     IEL       LABEL OF CURRENT SYMMETRY ELEMENT (I) FROM ASSYM.
C     ISS       CLASSIFICATION OF EIGENVECTOR (O).
C               = 0 IMPOSSIBLE.
C               =-1 ANTISYMMETRIC.
C               = 1 SYMMETRIC.
C     IER       ERROR FLAG (O).
C               = 0 NO ERROR.
C               = 1 ASSIGNMENT IMPOSSIBLE (ISS=0).
C     FACT      FACTOR TO ADJUST TOLERANCE (I), DEFAULT 0.001.
C     *
      IMPLICIT DOUBLE PRECISION (A-H,O-Z)
      DIMENSION C(LM2),ISYM(3,NUMAT)
      DATA SMALL/1.0D-03/
      IER    = 0
      SMALLF = SMALL*FACT
C *** LOOP OVER ATOMS.
      DO 10 I=1,NUMAT
      KA     = 3*I-2
      KB     = 3*I
      J      = ISYM(M,I)
C     CASE 1. ATOM I LIES IN THE SYMMETRY ELEMENT.
      IF(I.NE.J) GO TO 30
      KX     = 0
      KY     = 0
      KZ     = 0
      IF(ABS(C(KA  )).GE.SMALLF) KX=1
      IF(ABS(C(KA+1)).GE.SMALLF) KY=1
      IF(ABS(C(KA+2)).GE.SMALLF) KZ=1
      IF((KX+KY+KZ).EQ.0) GO TO 10
      GO TO (21,22,23,24,25,26,110),IEL
   21 IF(KX.EQ.1 .AND. (KY+KZ).EQ.0) GO TO 110
      IF(KX.EQ.0 .AND. (KY+KZ).GT.0) GO TO 100
      GO TO 120
   22 IF(KY.EQ.1 .AND. (KX+KZ).EQ.0) GO TO 110
      IF(KY.EQ.0 .AND. (KX+KZ).GT.0) GO TO 100
      GO TO 120
   23 IF(KZ.EQ.1 .AND. (KX+KY).EQ.0) GO TO 110
      IF(KZ.EQ.0 .AND. (KX+KY).GT.0) GO TO 100
      GO TO 120
   24 IF(KX.EQ.1 .AND. (KY+KZ).EQ.0) GO TO 100
      IF(KX.EQ.0 .AND. (KY+KZ).GT.0) GO TO 110
      GO TO 120
   25 IF(KY.EQ.1 .AND. (KX+KZ).EQ.0) GO TO 100
      IF(KY.EQ.0 .AND. (KX+KZ).GT.0) GO TO 110
      GO TO 120
   26 IF(KZ.EQ.1 .AND. (KX+KY).EQ.0) GO TO 100
      IF(KZ.EQ.0 .AND. (KX+KY).GT.0) GO TO 110
      GO TO 120
C     CASE 2. ATOM I IS RELATED TO ATOM J BY SYMMETRY.
   30 LA     = 3*J-2
      DO 40 K=KA,KB
      IF(ABS(C(K)).LT.SMALLF) GO TO 40
      KK     = K-KA+1
      L      = K-KA+LA
      IF(ABS(C(K)-C(L)).GT.SMALLF) GO TO 50
      GO TO (41,42,43,44,45,46,47,47,47),IEL
   41 GO TO (110,100,100),KK
   42 GO TO (100,110,100),KK
   43 GO TO (100,100,110),KK
   44 GO TO (100,110,110),KK
   45 GO TO (110,100,110),KK
   46 GO TO (110,110,100),KK
   47 GO TO (110,110,110),KK
   50 IF(ABS(C(K)+C(L)).GT.SMALLF) GO TO 120
      GO TO (51,52,53,54,55,56,57,57,57),IEL
   51 GO TO (100,110,110),KK
   52 GO TO (110,100,110),KK
   53 GO TO (110,110,100),KK
   54 GO TO (110,100,100),KK
   55 GO TO (100,110,100),KK
   56 GO TO (100,100,110),KK
   57 GO TO (100,100,100),KK
   40 CONTINUE
   10 CONTINUE
      GO TO 120
C *** ASSIGNMENT OF SYMMETRY LABEL.
  100 ISS    = 1
      RETURN
  110 ISS    =-1
      RETURN
C *** ERROR EXIT.
  120 IER    = 1
      ISS    = 0
      RETURN
      END
