      SUBROUTINE MOSYM (C,EIG,LM2,LM3,N,NSYM,ISYM,ISUB,NPRINT)
C     *
C     ASSIGNMENT OF MO SYMMETRY.
C     *
C     THE PRESENT VERSION ASSIGNS THE MO SYMMETRY FOR NONDEGENERATE
C     ABELIAN POINT GROUPS (Cs,C2,C2v,D2h,C2h,D2,Ci). IN THE CASE
C     OF DEGENERATE POINT GROUPS, THE MO SYMMETRY IS ASSIGNED WITH
C     RESPECT TO AN ABELIAN SUBGROUP, AFTER SYMMETRIZATION OF THE
C     DEGENERATE MOS (SHOULD WORK FOR Cnv,Dnh,Dnd,Dn).
C     *
      USE LIMIT, ONLY: LM1
      IMPLICIT DOUBLE PRECISION (A-H,O-Z)
      CHARACTER*3 NGROUP
      CHARACTER*4 IRREP
      COMMON
     ./AMASS / AMS(LM1)
     ./ATOMC / COORD(3,LM1)
     ./ATOMS / NUMAT,NAT(LM1),NFIRST(LM1),NLAST(LM1)
     ./NBFILE/ NBF(20)
     ./SYMLAB/ NGROUP(7),IRREP(26)
     ./SYMLIM/ IRREPA(7),IRREPN(7)
      DIMENSION C(LM2,LM3),EIG(LM3),NSYM(LM3),ISYM(LM3)
      DIMENSION IEL(3),IS(3)
C *** FILE NUMBERS.
      NB6    = NBF(6)
C *** INITIALIZATION.
      DO 10 I=1,N
      NSYM(I) = 1
   10 CONTINUE
C *** FIND MOLECULAR SYMMETRY FROM CARTESIAN COORDINATES.
      CALL ASSYM(NUMAT,AMS,COORD,ISUB,MSYM,IEL,ISYM,NPRINT)
      IF(ISUB.EQ.0 .OR. MSYM.EQ.0) RETURN
C *** CHECK FOR SYMMETRIZATION OF DEGENERATE MOS (IF PRESENT).
      CALL DEGMO(C,EIG,LM2,LM3,MSYM,IEL,ISYM,NUMAT,N,NPRINT)
C *** LOOP OVER MOS FOR SYMMETRY ASSIGNMENT.
      DO 60 I=1,N
C     LOOP OVER SYMMETRY OPERATIONS.
      DO 20 M=1,MSYM
      MEL    = IEL(M)
      CALL CSYM(C(1,I),NFIRST,NLAST,ISYM,LM2,NUMAT,M,MEL,ISS,IER)
      IF(IER.EQ.1) GO TO 200
      IS(M)  = ISS
   20 CONTINUE
C     ASSIGN MO SYMMETRY FOR A GIVEN POINT GROUP.
      IS1    = IS(1)
      GO TO (30,30,40,50,40,40,30),ISUB
C     CS,C2,CI.
   30 IF(IS1.EQ. 1) NSYM(I)=1
      IF(IS1.EQ.-1) NSYM(I)=2
      GO TO 60
C     C2V,C2H,D2.
   40 IS2    = IS(2)
      IF(ISUB.GT.3) THEN
         IS1 = IS(2)
         IS2 = IS(1)
      ENDIF
      IF(IS1.EQ. 1 .AND. IS2.EQ. 1) NSYM(I)=1
      IF(IS1.EQ.-1 .AND. IS2.EQ.-1) NSYM(I)=2
      IF(IS1.EQ. 1 .AND. IS2.EQ.-1) NSYM(I)=3
      IF(IS1.EQ.-1 .AND. IS2.EQ. 1) NSYM(I)=4
      GO TO 60
C     D2H.
   50 IS2    = IS(2)
      IS3    = IS(3)
      IF(IS1.EQ. 1 .AND. IS2.EQ. 1 .AND. IS3.EQ. 1) NSYM(I)=1
      IF(IS1.EQ.-1 .AND. IS2.EQ.-1 .AND. IS3.EQ.-1) NSYM(I)=2
      IF(IS1.EQ.-1 .AND. IS2.EQ.-1 .AND. IS3.EQ. 1) NSYM(I)=3
      IF(IS1.EQ. 1 .AND. IS2.EQ. 1 .AND. IS3.EQ.-1) NSYM(I)=4
      IF(IS1.EQ.-1 .AND. IS2.EQ. 1 .AND. IS3.EQ.-1) NSYM(I)=5
      IF(IS1.EQ. 1 .AND. IS2.EQ.-1 .AND. IS3.EQ. 1) NSYM(I)=6
      IF(IS1.EQ. 1 .AND. IS2.EQ.-1 .AND. IS3.EQ.-1) NSYM(I)=7
      IF(IS1.EQ.-1 .AND. IS2.EQ. 1 .AND. IS3.EQ. 1) NSYM(I)=8
   60 CONTINUE
C *** PRINTING SECTION.
      IF(NPRINT.GE.0) THEN
         WRITE(NB6,500)
         NLAB   = IRREPA(ISUB)
         DO 100 I=1,N
         LABEL  = NLAB+NSYM(I)
         WRITE(NB6,510) I,EIG(I),IRREP(LABEL),NSYM(I)
  100    CONTINUE
         IF(NPRINT.GE.5) THEN
            WRITE(NB6,520)
            CALL MOOUT (C,EIG,ISUB,NSYM,N,LM3,LM2,LM3,NPRINT,1)
         ENDIF
      ENDIF
      RETURN
C *** ERROR SECTION.
  200 CONTINUE
      IMO    = I
      DO 210 I=1,N
      NSYM(I) = 1
  210 CONTINUE
      IF(NPRINT.GE.-5) THEN
         WRITE(NB6,530) IMO
      ENDIF
      RETURN
  500 FORMAT (///4X,'MO     EIGENVALUE     LABEL     NSYM'/)
  510 FORMAT (   1X,I5,F15.5,6X,A4,5X,I2)
  520 FORMAT (///1X,'DEBUG PRINT OF EIGENVALUES AND EIGENVECTORS',
     1           1X,'FROM SUBROUTINE MOSYM.')
  530 FORMAT (///1X,'THE SYMMETRY OF MO',I3,' CANNOT BE ASSIGNED.',
     1        /  1X,'THE CALCULATION IS DONE WITHOUT SYMMETRY.'/)
      END
