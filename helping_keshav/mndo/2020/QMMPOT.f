      SUBROUTINE QMMPOT (H,PA,PB,Q,LM4,LM6)
C     *
C     CALCULATE THE ELECTROSTATIC POTENTIAL (ELPOT)
C     GENERATED BY THE QM ATOMS AT THE EXTERNAL POINTS.
C     *
C     NOTATION. I=INPUT, O=OUTPUT, S=SCRATCH.
C     H(LM6)    ONE-CENTER ELEMENTS OF CORE HAMILTONIAN MATRIX (S).
C     PA(LM4)   DENSITY MATRIX, ALPHA ELECTRONS (I).
C     PB(LM4)   DENSITY MATRIX, BETA  ELECTRONS (I).
C     Q(LM6)    PRECOMBINED ONE-CENTER DENSITY MATRIX ELEMENTS (S).
C     *
C     UNITS.
C     COORDM()  CARTESIAN COORDINATES OF EXTERNAL POINTS, IN ANGSTROM.
C     ELPOT()   ELECTROSTATIC POTENTIAL, IN EV/E = V.
C     *
C     FOR ADDITIONAL DETAILS SEE COMMENTS IN SUBROUTINE QMINT.
C     REF: D. BAKOWIES AND W. THIEL, J.COMPUT.CHEM. 17, 87 (1996).
C     *
      USE LIMIT, ONLY: LMI, LM1M
      IMPLICIT DOUBLE PRECISION (A-H,O-Z)
      COMMON
     ./CONSTN/ ZERO,ONE,TWO,THREE,FOUR,PT5,PT25
     ./FINDX1/ IP(LMI),IP1(LMI),IP2(LMI)
     ./INOPT2/ IN2(300)
     ./QMMM1 / COORDM(3,LM1M),CHARGM(LM1M)
     ./QMMM4 / ELPOT(LM1M),ESF(3,LM1M)
C    ./NBFILE/ NBF(20)
      DIMENSION H(LM6),PA(LM4),PB(LM4),Q(LM6)
C *** FILE NUMBERS.
C     NB6    = NBF(6)
C *** INPUT OPTIONS.
C     NPRINT = IN2(72)
      NUMATM = IN2(120)
      MMPOT  = IN2(122)
C     IF(NPRINT.GT.5) WRITE(NB6,500) MMPOT
C *** INITIALIZATION.
      PTCHG  = ONE
C *** COMPUTE ONE-CENTER DENSITY MATRIX ELEMENTS (Q).
      DO 10 I=1,LM6
      Q(I)  = (PA(IP(I))+PB(IP(I)))
      IF(IP1(I).NE.IP2(I)) Q(I)=Q(I)*TWO
   10 CONTINUE
C *** LOOP OVER ALL EXTERNAL POINTS (M).
      DO 30 M=1,NUMATM
      DO 20 I=1,LM6
      H(I)   = ZERO
   20 CONTINUE
C     INTEGRALS FOR ELECTROSTATIC POTENTIAL, SEE EQ.(2) IN REF.
      IF(MMPOT.LE.2) THEN
         CALL QMINTC (H,LM6,ENUCQM,COORDM(1,M),PTCHG,1,0)
      ELSE
         CALL QMINT  (H,LM6,ENUCQM,COORDM(1,M),PTCHG,1,0)
      ENDIF
C     ONE-ELECTRON CONTRIBUTION TO ELECTROSTATIC POTENTIAL.
      ELECQM = DDOT(LM6,H,1,Q,1)
C     ADD ELECTRONIC AND NUCLEAR CONTRIBUTIONS.
      ELPOT(M) = ELECQM + ENUCQM
C     IF(NPRINT.GT.5) WRITE(NB6,510) M,ELECQM,ENUCQM,ELPOT(M)
   30 CONTINUE
      RETURN
C 500 FORMAT(///1X,'DEBUG PRINT FROM QMMPOT: MMPOT =',I2,
C    1       // 6X,'M       ELECQM         ENUCQM         ELPOT'/)
C 510 FORMAT(1X,I6,3F15.8)
      END
