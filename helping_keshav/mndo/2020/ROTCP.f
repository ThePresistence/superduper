C     ******************************************************************
      SUBROUTINE ROTCP (IAR,IAC,IAOS,CORE,KK,YY,H,LMR,LMF)
C     *
C     THIS ROUTINE TRANSFORMS THE CORE ELECTRON ATTRACTION INTEGRALS
C     FROM LOCAL TO MOLECULAR COORDINATES, AND INCLUDES THEM DIRECTLY
C     INTO THE CORE HAMILTONIAN MATRIX.
C     *
C     NOTATION.
C     IAR       ROW    INDEX OF S ORBITAL AT ATOM I IN H(LMR,LMF).
C     IAC       COLUMN INDEX OF S ORBITAL AT ATOM I IN H(LMR,LMF).
C     IAOS      NUMBER OF ATOMIC ORBITALS AT ATOM I.
C     CORE      LOCAL CORE ELECTRON ATTRACTION INTEGRALS.
C     KK        COLUMN INDEX OF CURRENTLY USED INTEGRALS CORE(K,KK).
C     YY        PRECALCULATED COMBINATION OF ROTATION MATRIX ELEMENTS.
C     H         CORE HAMILTONIAN MATRIX.
C     LMR       ROW    DIMENSION OF CORE HAMILTONIAN MATRIX.
C     LMF       COLUMN DIMENSION OF CORE HAMILTONIAN MATRIX.
C     *
C     NOTE THAT THE INPUT FOR IAR,IAC,IAOS,KK MUST BE CONSISTENT.
C     THE CALLING SUBROUTINE CONTINUES A LOOP OVER ATOM PAIRS I-J.
C     THE OUTER LOOP OVER I CORRESPONDS TO KK=1.
C     THE INNER LOOP OVER J CORRESPONDS TO KK=2.
C     *
      IMPLICIT DOUBLE PRECISION (A-H,O-Z)
      DIMENSION H(LMR,LMF)
      DIMENSION CORE(10,2),YY(15,45)
      DIMENSION HPP(6),HDP(15),HDD(15)
C     S-S
      H(IAR,IAC) = H(IAR,IAC)+CORE(1,KK)
      IF(IAOS.LE.1) RETURN
C     INTERMEDIATE RESULTS FOR P-P
      DO 10 I=1,6
      HPP(I) = CORE(3,KK)*YY(I,3)+CORE(4,KK)*(YY(I,6)+YY(I,10))
   10 CONTINUE
C     P-S
      H(IAR+1,IAC)   = H(IAR+1,IAC)+CORE(2,KK)*YY(1,2)
      H(IAR+2,IAC)   = H(IAR+2,IAC)+CORE(2,KK)*YY(2,2)
      H(IAR+3,IAC)   = H(IAR+3,IAC)+CORE(2,KK)*YY(3,2)
C     P-P
      H(IAR+1,IAC+1) = H(IAR+1,IAC+1)+HPP(1)
      H(IAR+2,IAC+1) = H(IAR+2,IAC+1)+HPP(2)
      H(IAR+2,IAC+2) = H(IAR+2,IAC+2)+HPP(3)
      H(IAR+3,IAC+1) = H(IAR+3,IAC+1)+HPP(4)
      H(IAR+3,IAC+2) = H(IAR+3,IAC+2)+HPP(5)
      H(IAR+3,IAC+3) = H(IAR+3,IAC+3)+HPP(6)
C     INTERMEDIATE RESULTS FOR D-P AND D-D
      IF(IAOS.LE.4) RETURN
      DO 20 I=1,15
      HDP(I) = CORE(6,KK)*YY(I,12)+CORE( 8,KK)*(YY(I,18)+YY(I,25))
      HDD(I) = CORE(7,KK)*YY(I,15)+CORE( 9,KK)*(YY(I,21)+YY(I,28))
     1                            +CORE(10,KK)*(YY(I,36)+YY(I,45))
   20 CONTINUE
C     D-S
      IDP    = 0
      IDD    = 0
      IDR    = IAR-1
      IDC    = IAC-1
      DO 50 I=5,9
      H(IDR+I,IAC)   = H(IDR+I,IAC)+CORE(5,KK)*YY(I-4,11)
C     D-P
      DO 30 J=2,4
      IDP    = IDP+1
      H(IDR+I,IDC+J) = H(IDR+I,IDC+J)+HDP(IDP)
   30 CONTINUE
C     D-D
      DO 40 J=5,I
      IDD    = IDD+1
      H(IDR+I,IDC+J) = H(IDR+I,IDC+J)+HDD(IDD)
   40 CONTINUE
   50 CONTINUE
      RETURN
      END
