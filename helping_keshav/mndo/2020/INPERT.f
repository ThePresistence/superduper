      SUBROUTINE INPERT (IFORM,JPRINT)
C     *
C     INPUT OF OPTIONS FOR PERTURBATION TREATMENT.
C     *
      USE LIMIT, ONLY: LMX
      IMPLICIT DOUBLE PRECISION (A-H,O-Z)
      CHARACTER*4 NAME(4)
      COMMON
     ./CIMOS / IMOCI(LMX)
     ./INOPT1/ IN1(300)
     ./INOPT2/ IN2(300)
     ./MOPAC / IMOPAC
     ./NBFILE/ NBF(20)
     ./ORBITS/ NUMB,NORBS,NMOS,NALPHA,NBETA
      DIMENSION IPERT(4)
      DATA NAME/'RSMP','RSEN','BWMP','BWEN'/
C *** FILE NUMBERS.
      NB5    = NBF(5)
      NB6    = NBF(6)
C *** CHECK FOR OPEN-SHELL SYSTEMS.
      IMULT  = IN2(66)
      IF(IMULT.GT.0) THEN
         WRITE(NB6,600)
         STOP 'INPERT'
      ENDIF
C *** READ OPTIONS FOR PERTURBATION TREATMENT.
      IF(IMOPAC.EQ.0) THEN
         IF(IFORM.LE.0) THEN
            READ(NB5,500) (IN1(I),I=131,135),(IN1(I),I=151,153)
         ELSE
            READ(NB5,*)   (IN1(I),I=131,135),(IN1(I),I=151,153)
         ENDIF
      ENDIF
      DO 10 I=131,135
      IN2(I) = IN1(I)
   10 CONTINUE
      DO 20 I=151,153
      IN2(I) = IN1(I)
   20 CONTINUE
C *** ASSIGN DEFAULT VALUES.
C     TOTAL NUMBER OF ACTIVE OCCUPIED ORBITALS (ICI1).
      IF(IN2(131).LE.0) THEN
         IN2(131) = 20
      ENDIF
      IN2(131) = MIN(IN2(131),NUMB)
C     TOTAL NUMBER OF ACTIVE UNOCCUPIED ORBITALS (ICI2).
      IF(IN2(132).LE.0) THEN
         IN2(132) = 20
      ENDIF
      IN2(132) = MIN(IN2(132),NORBS-NUMB)
C     TYPE OF PERTURBATION TREATMENT (MPERT).
      IF(IN2(135).LE.0) THEN
         IN2(135) = 1
      ENDIF
C     NUMBER OF ACTIVE OCCUPIED PI OR D MOS (JCI1).
      IF(IN2(151).LE.0) THEN
         IN2(151) = IN2(131)
      ENDIF
C     NUMBER OF ACTIVE VIRTUAL PI OR D MOS (JCI2).
      IF(IN2(152).LE.0) THEN
         IN2(152) = IN2(132)
      ENDIF
C     PI POPULATION THRESHOLD (PIPOP).
      IF(IN2(153).LE.0) THEN
         IN2(153) = 0
      ENDIF
C *** DEFINE ORBITALS INVOLVED IN THE PERTURBATION TREATMENT.
      ICI1   = IN2(131)
      ICI2   = IN2(132)
      MOVO   = IN2(134)
      IA     = ICI1+1
      IB     = ICI1+ICI2
      DO 30 I=1,ICI1
      IMOCI(I)=NUMB+1-I
   30 CONTINUE
      DO 40 I=IA,IB
      IMOCI(I)=NUMB+1+I-IA
   40 CONTINUE
      NMOS   = MAX(NMOS,IMOCI(IB))
      IF(MOVO.GT.0) THEN
         IF(IFORM.LE.0) THEN
            READ(NB5,500) (IMOCI(I),I=1,ICI1)
            READ(NB5,500) (IMOCI(I),I=IA,IB)
         ELSE
            READ(NB5,*)   (IMOCI(I),I=1,ICI1)
            READ(NB5,*)   (IMOCI(I),I=IA,IB)
         ENDIF
         DO 50 I=IA,IB
         NMOS = MAX(NMOS,IMOCI(I))
   50    CONTINUE
      ENDIF
C *** PRINTING SECTION.
      IF(JPRINT.LT.0) RETURN
      WRITE(NB6,510)
      WRITE(NB6,520) (IN2(I),I=131,135),(IN2(I),I=151,153)
      IF(MOVO.GT.0) THEN
         WRITE(NB6,530) (IMOCI(I),I=1,ICI1)
         WRITE(NB6,540) (IMOCI(I),I=IA,IB)
      ENDIF
C     PERTURBATION TREATMENT.
      MPERT    = IN2(135)
      IPERT(1) = MOD(MPERT/1000,10)
      IPERT(2) = MOD(MPERT/100,10)
      IPERT(3) = MOD(MPERT/10,10)
      IPERT(4) = MOD(MPERT,10)
      NPERT    = 0
      DO 60 I=1,4
      IF(IPERT(I).GT.0) THEN
         NPERT = NPERT+1
         ISLCT = I
      ENDIF
   60 CONTINUE
      IF(NPERT.EQ.1) THEN
         WRITE(NB6,550) NAME(ISLCT)
      ELSE IF(NPERT.GT.1) THEN
         WRITE(NB6,560)
         DO 70 I=1,4
         IF(IPERT(I).EQ.1) WRITE(NB6,570) NAME(I)
         IF(IPERT(I).GT.1) WRITE(NB6,580) NAME(I)
   70    CONTINUE
      ENDIF
      RETURN
  500 FORMAT(20I4)
  510 FORMAT(///1X,'*** OPTIONS FOR PERTURBATION TREATMENT      ***',
     1       /  1X,'*** DEFINED EITHER EXPLICITLY OR BY DEFAULT ***')
  520 FORMAT(/  1X,'ICI1   =',I5,5X,'ICI2   =',I5,5X,'IOUTCI =',I5,
     1          5X,'MOVO   =',I5,5X,'MPERT  =',I5,
     2       /  1X,'JCI1   =',I5,5X,'JCI2   =',I5,5X,'PIPOP  =',I5)
  530 FORMAT(/  1X,'MOS',12X,20I4/(16X,20I4))
  540 FORMAT(/  1X,'VOS',12X,20I4/(16X,20I4))
  550 FORMAT(/  1X,'SELECTED PERTURBATION TREATMENT: ',A4)
  560 FORMAT(/  1X,'SELECTED PERTURBATION TREATMENTS'/)
  570 FORMAT(   1X,A4,' PERTURBATION ENERGY ADDED TO TOTAL ENERGY')
  580 FORMAT(   1X,A4,' PERTURBATION ENERGY NOT ADDED TO TOTAL ENERGY')
  600 FORMAT(// 1X,'PERTURBATION TREATMENTS OF CORRELATION ARE NOT ',
     1             'AVAILABLE FOR OPEN-SHELL SYSTEMS.'//)
      END
