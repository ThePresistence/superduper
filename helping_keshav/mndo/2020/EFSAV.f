      SUBROUTINE EFSAV (HESS,LMH,IPOW,MODE,MIDDLE)
C     *
C     SAVE AND READ RESTART INFORMATION FOR EIGENVECTOR FOLLOWING.
C     *
C     NOTATION. I=INPUT, O=OUTPUT.
C     HESS()    HESSIAN MATRIX (I,O).
C     LMH       DIMENSION OF HESSIAN MATRIX (I).
C     IPOW      CHOICE OF WRITE/READ ACTIVITIES (I).
C               = 1 SAVE COMPLETE RESTART INFORMATION ON FILE.
C               = 2 READ COMPLETE RESTART INFORMATION FROM FILE.
C               = 3 READ ONLY HESSIAN MATRIX FROM FILE.
C     MODE      NUMBER OF THE EIGENVECTOR BEING FOLLOWED (I).
C     MIDDLE    NO ACTION FOR MIDDLE.LT.0 (I).
C     *
      USE LIMIT, ONLY: LMV
      IMPLICIT DOUBLE PRECISION (A-H,O-Z)
      PARAMETER (ID4=3)
      COMMON
     ./CYCLES/ ICYC,NCOUNT
     ./DFP   / XPARAM(LMV),NVAR
     ./FLAG2 / SECADD,TIME1
     ./FLPOCM/ GRAD(LMV),D(LMV),ALPHA,FIP1
     ./NBFILE/ NBF(20)
     ./OPTEF / OLDG(LMV),VMODE(LMV),DD
      DIMENSION HESS(LMH,LMH)
C *** FILE NUMBERS.
      NB4    = NBF(4)
      NB6    = NBF(6)
C *** INITIALIZATION.
      IF(MIDDLE.LT.0) RETURN
      REWIND NB4
C *** SAVE RESTART INFORMATION.
      IF(IPOW.EQ.1) THEN
         CALL CPUSEC(TIME2)
         SECTOT = SECADD+TIME2-TIME1
         WRITE(NB4) ID4
         WRITE(NB4) ((HESS(J,I),J=1,NVAR),I=1,NVAR)
         WRITE(NB4) (XPARAM(I),I=1,NVAR)
         WRITE(NB4) (GRAD(I),I=1,NVAR)
         WRITE(NB4) (OLDG(I),I=1,NVAR)
         WRITE(NB4) (D(I),I=1,NVAR)
         WRITE(NB4) (VMODE(I),I=1,NVAR)
         WRITE(NB4) FIP1,SECTOT,DD,MODE,ICYC
         CLOSE(NB4)
         RETURN
C *** READ RESTART INFORMATION.
      ELSE IF(IPOW.EQ.2) THEN
         READ(NB4,ERR=10,END=20) IDREAD
         IF(IDREAD.NE.ID4) GO TO 30
         READ(NB4,ERR=10,END=20) ((HESS(J,I),J=1,NVAR),I=1,NVAR)
         READ(NB4,ERR=10,END=20) (XPARAM(I),I=1,NVAR)
         READ(NB4,ERR=10,END=20) (GRAD(I),I=1,NVAR)
         READ(NB4,ERR=10,END=20) (OLDG(I),I=1,NVAR)
         READ(NB4,ERR=10,END=20) (D(I),I=1,NVAR)
         READ(NB4,ERR=10,END=20) (VMODE(I),I=1,NVAR)
         READ(NB4,ERR=10,END=20) FIP1,SECADD,DD,MODE,ICYC
         CLOSE(NB4)
         RETURN
C *** READ HESSIAN MATRIX (POSSIBLY SAVED IN FORSAV).
      ELSE IF(IPOW.EQ.3) THEN
         READ(NB4,ERR=10,END=20) IDREAD
         READ(NB4,ERR=10,END=20) ((HESS(J,I),J=1,NVAR),I=1,NVAR)
         CLOSE(NB4)
         RETURN
      ENDIF
C *** ERROR EXITS.
   10 WRITE(NB6,500)
      STOP 'EFSAV'
   20 WRITE(NB6,510)
      STOP 'EFSAV'
   30 WRITE(NB6,500)
      WRITE(NB6,520) IDREAD,ID4
      STOP 'EFSAV'
  500 FORMAT(///1X,'ERROR WHEN READING RESTART FILE',
     1       /  1X,'FOR EIGENVECTOR FOLLOWING',
     2       /  1X,'IN SUBROUTINE EFSAV.'/)
  510 FORMAT(///1X,'END-OF-FILE ENCOUNTERED WHEN READING RESTART FILE',
     1       /  1X,'FOR EIGENVECTOR FOLLOWING',
     2       /  1X,'IN SUBROUTINE EFSAV.'/)
  520 FORMAT(   1X,'WRONG TYPE OF RESTART FILE: IDREAD =',I3,
     1       /  1X,'EXPECTED VALUE OF LABEL   : IDREAD =',I3,/)
      END
