      SUBROUTINE PRTHDL
C     *
C     PRINT RESULTS FROM HDLC GEOMETRY OPTIMIZATION.
C     *
      USE LIMIT, ONLY: LM1, LMV, LM1M
      IMPLICIT DOUBLE PRECISION (A-H,O-Z)
      CHARACTER*80 KTITLE,KOMENT
      COMMON
     ./ATOMC / COORD(3,LM1)
     ./ATOMS / NUMAT,NAT(LM1),NFIRST(LM1),NLAST(LM1)
     ./CGRAD / CG(3,LM1+LM1M)
C    ./CONSTF/ A0,AFACT,EV,EVCAL,PI,W1,W2,BIGEXP
     ./CONSTN/ ZERO,ONE,TWO,THREE,FOUR,PT5,PT25
     ./CYCLES/ ICYC,NCOUNT
     ./DFP   / X(3,LM1),N
     ./ERG   / ENERGY,GRAD(LMV),GNORM,CNORM
     ./FLAG1 / KTITLE,KOMENT
     ./INOPT2/ IN2(300)
     ./NBFILE/ NBF(20)
     ./PARM1 / A(3,LM1),NA(LM1),NB(LM1),NC(LM1),NN(LM1),NATOMS
      RIJ(I,J) = SQRT( (COORD(1,I)-COORD(1,J))**2
     1                +(COORD(2,I)-COORD(2,J))**2
     2                +(COORD(3,I)-COORD(3,J))**2 )
C *** FILE NUMBERS.
      NB6    = NBF(6)
C *** INPUT OPTIONS.
C     JOP    = IN2(3)
      IGEOM  = IN2(4)
      IPRINT = IN2(38)
      IATERG = IN2(119)
      IF(IPRINT.LE.-9) RETURN
C *** PRINT HEADLINE AND MAIN RESULTS.
      WRITE(NB6,500) KOMENT,KTITLE
      WRITE(NB6,510) NCOUNT
      IF(IATERG.EQ.1) THEN
         WRITE(NB6,520) ENERGY
      ELSEIF(IATERG.EQ.-1) THEN
         WRITE(NB6,521) ENERGY
      ENDIF
      WRITE(NB6,530) CNORM
C *** PRINT OPTIMIZED CARTESIAN COORDINATES AND GRADIENTS.
      WRITE(NB6,540)
      DO 10 I=1,NUMAT
      WRITE(NB6,550) I,NAT(I),(COORD(J,I),J=1,3),(CG(J,I),J=1,3)
   10 CONTINUE
      WRITE(NB6,560) CNORM
C *** GET FULL SET OF CARTESIAN COORDINATES IN CASE OF DUMMY ATOMS.
C     STORE OPTIMIZED CARTESIAN COORDINATES OF REAL ATOMS.
      IF(NATOMS.GT.NUMAT) THEN
         DO 20 I=1,NUMAT
         X(1,I) = COORD(1,I)
         X(2,I) = COORD(2,I)
         X(3,I) = COORD(3,I)
   20    CONTINUE
C        COMPUTE ORIGINAL CARTESIAN COORDINATES OF DUMMY ATOMS.
         IF(IGEOM.EQ.0) THEN
            CALL SYMTRY(-1)
            CALL GMETRY(+1)
         ENDIF
C        COMBINE OPTIMIZED COORDINATES AND INITIAL DUMMY COORDINATES.
         IC = 0
         DO 40 I=1,NATOMS
         IF(NN(I).NE.99) THEN
            IC = IC+1
            DO 30 J=1,3
            COORD(J,I) = X(J,IC)
   30       CONTINUE
         ENDIF
   40    CONTINUE
C        PRINT FULL SET OF COORDINATES.
         WRITE(NB6,570)
         DO 50 I=1,NATOMS
         WRITE(NB6,580) I,NN(I),(COORD(J,I),J=1,3)
   50    CONTINUE
      ENDIF
C *** COMPUTE AND PRINT OPTIMIZED INTERNAL COORDINATES.
      IF(IGEOM.EQ.0) THEN
         DO 70 I=1,3
         DO 60 J=1,3
         A(J,I) = ZERO
   60    CONTINUE
   70    CONTINUE
         WRITE(NB6,590)
         WRITE(NB6,600)
         WRITE(NB6,610) NN(1)
         IF(NATOMS.GT.1) THEN
            A(1,2) = RIJ(1,2)
            WRITE(NB6,620) NN(2),A(1,2),NC(2)
         ENDIF
         IF(NATOMS.GT.2) THEN
            I = 3
            J = NC(I)
            K = NB(I)
            CALL BANGLE (I,J,K,ANGLE)
            A(1,3) = RIJ(I,J)
            A(2,3) = ANGLE
            WRITE(NB6,630) NN(3),A(1,3),A(2,3),NC(3),NB(3)
         ENDIF
         DO 80 I=4,NATOMS
         J = NC(I)
         K = NB(I)
         L = NA(I)
         CALL BANGLE (I,J,K,ANGLE)
         CALL DIHED (I,J,K,L,TWIST,SINTH)
         A(1,I) = RIJ(I,J)
         A(2,I) = ANGLE
         A(3,I) = TWIST
         WRITE(NB6,640) I,NN(I),A(1,I),A(2,I),A(3,I),NC(I),NB(I),NA(I)
   80    CONTINUE
      ENDIF
C *** PRINT INTERATOMIC DISTANCES.
      IF(IPRINT.GT.0 .OR. (IPRINT.GE.-1 .AND. NATOMS.LE.100)) THEN
         WRITE(NB6,650)
         CALL RIJPRT (COORD,NN,NATOMS,KSTOP)
      ENDIF
      CALL GMETRY(-2)
      RETURN
  500 FORMAT(///1X,A,/1X,A,/)
  510 FORMAT(// 1X,'HDLC OPTIMIZATION FINISHED AFTER',I8,
     1             ' FUNCTION EVALUATIONS')
  520 FORMAT(/  1X,'FINAL HEAT OF FORMATION         ',F20.5,' KCAL/MOL')
  521 FORMAT(/  1X,'FINAL TOTAL ENERGY              ',F20.5,' KCAL/MOL')
  530 FORMAT(/  1X,'FINAL CARTESIAN GRADIENT NORM   ',F20.5,
     1             ' KCAL/(MOL*ANGSTROM)')
  540 FORMAT (///26X,'COORDINATES (ANGSTROM)',
     1          15X,'GRADIENTS (KCAL/(MOL*ANGSTROM))',
     2        // 8X,'I   NI',10X,'X',11X,'Y',11X,'Z',16X,'X',11X,'Y',
     3          11X,'Z'/)
  550 FORMAT (   4X,2I5,3X,3F12.5,5X,3F12.5)
  560 FORMAT (// 5X,'CARTESIAN GRADIENT NORM',5X,F20.5)
  570 FORMAT(///1X,'CARTESIAN COORDINATES (ANGSTROMS) WITH DUMMIES',
     1       // 8X,'I',9X,'NI',16X,'X',11X,'Y',11X,'Z'/)
  580 FORMAT(   4X,I5,I11,9X,3F12.5)
  590 FORMAT(/)
  600 FORMAT(/6X,'ATOM',6X,'ATOMIC',15X,'BOND LENGTH',10X,'BOND ANGLE',
     1 11X,'TWIST ANGLE' /5X,'NUMBER',5X,'NUMBER',15X,'(ANGSTROMS)',
     2 11X,'(DEGREES)',12X,'(DEGREES)' /8X, 'I' ,32X,'NA I',16X,
     3 'NB NA I',12X,'NC NB NA I',16X,'NA',5X,'NB',5X,'NC' /)
  610 FORMAT(8X,'1',9X,I2)
  620 FORMAT(8X,'2',9X,I2,F26.5,2X,58X,I2)
  630 FORMAT(8X,'3',9X,I2,7X,2(F19.5,2X),37X,2(I2,5X) )
  640 FORMAT(6X,I3,9X,I2,7X,3(F19.5,2X),11X,3(4X,I3) )
  650 FORMAT(///1X,'INTERATOMIC DISTANCES (ANGSTROMS)'/)
      END
