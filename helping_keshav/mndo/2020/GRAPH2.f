      SUBROUTINE GRAPH2
C     *
C     WRITE AN OUTPUT FILE (NB8) FOR FURTHER EVALUATIONS.
C     MOPAC CONVENTIONS INCLUDING OUTPUT OF ATOMIC CHARGES.
C     THE OUTPUT FILE (NB8) MAY BE USED AS INPUT FOR SUBSEQUENT RUNS.
C     *
C     A SUBROUTINE NAMED GRAPH3 WRITTEN BY DARREN YORK GENERATING
C     MOLDEN-READABLE FILES HAS BEEN DROPPED.
C     *
      USE LIMIT, ONLY: LM1
      IMPLICIT DOUBLE PRECISION (A-H,O-Z)
      CHARACTER*800 KEYWRD
      CHARACTER*80  KTITLE,KOMENT
      CHARACTER*2   ELEMNT,ELE
      CHARACTER*7   METHOD
      COMMON
     ./CHARGE/ QI(LM1)
     ./CONSTF/ A0,AFACT,EV,EVCAL,PI,W1,W2,BIGEXP
     ./CONSTN/ ZERO,ONE,TWO,THREE,FOUR,PT5,PT25
     ./ELEMTS/ ELEMNT(107)
     ./FLAG1 / KTITLE,KOMENT
     ./INOPT2/ IN2(300)
     ./KEYWRD/ KEYWRD
     ./MOPAC / IMOPAC
     ./NBFILE/ NBF(20)
     ./PARM1 / A(3,LM1),NC(LM1),NB(LM1),NA(LM1),NN(LM1),NATOMS
     ./QMNAME/ METHOD(-23:6)
C *** DETERMINE DEFAULT KEYWORD FOR MOPAC FILE.
      IF(IMOPAC.EQ.0) THEN
         IOP = IN2(2)
         IF(IOP.GE.-23 .AND. IOP.LE.6) THEN
            KEYWRD = METHOD(IOP)
         ENDIF
      ENDIF
C *** WRITE FIRST THREE CARDS.
      NB8 = NBF(8)
      WRITE(NB8,500) KEYWRD(1:80)
      IF(INDEX(KEYWRD(1:80),' +').NE.0) THEN
         WRITE(NB8,500) KEYWRD(81:160)
         IF(INDEX(KEYWRD(81:160),' +').NE.0) THEN
            WRITE(NB8,500) KEYWRD(161:240)
         ENDIF
         WRITE(NB8,500) KOMENT
         WRITE(NB8,500) KTITLE
      ELSE IF(INDEX(KEYWRD(1:80),' &').NE.0) THEN
         WRITE(NB8,500) KEYWRD(81:160)
         IF(INDEX(KEYWRD(81:160),' &').NE.0) THEN
            WRITE(NB8,500) KEYWRD(161:240)
         ELSE
            WRITE(NB8,500) KTITLE
         ENDIF
      ELSE
         WRITE(NB8,500) KOMENT
         WRITE(NB8,500) KTITLE
      ENDIF
C *** WRITE THE GEOMETRY AND ATOMIC CHARGES IN MOPAC FORMAT.
      I1     = 1
      L      = 0
      DO 10 I=1,NATOMS
      NI     = NN(I)
      ELE    = ELEMNT(NI)
      W      = A(2,I)*AFACT
      X      = A(3,I)*AFACT
      IF(NI.NE.99) THEN
         L   = L+1
         QII = QI(L)
      ELSE
         QII = ZERO
      ENDIF
      WRITE(NB8,510) ELE,A(1,I),I1,W,I1,X,I1,NA(I),NB(I),NC(I),QII
   10 CONTINUE
      WRITE(NB8,530)
      RETURN
  500 FORMAT(A)
  510 FORMAT(1X,A2,3(F12.6,I3),3I5,F13.4)
  530 FORMAT(' ')
      END
