      SUBROUTINE MINICI (C,CC,C12,EIG,WW,LM2,LM3,KMAX,LM7,NB3,IOUT2,
     1                   CA,CB,DELT,KK,LL)
C     *
C     MINIMAL CONFIGURATION INTERACTION.
C     *
      IMPLICIT DOUBLE PRECISION (A-H,O-Z)
      COMMON
     ./CONSTF/ A0,AFACT,EV,EVCAL,PI,W1,W2,BIGEXP
     ./CONSTN/ ZERO,ONE,TWO,THREE,FOUR,PT5,PT25
     ./NBFILE/ NBF(20)
      DIMENSION C(LM2,LM3),CC(LM7),EIG(LM3)
      DIMENSION C12(KMAX),WW(KMAX)
      CALL WINT (KK,KK,KK,KK,GKK,C,CC,C12,WW,LM2,LM3,LM7,NB3,KMAX)
      CALL WINT (LL,LL,LL,LL,GLL,C,CC,C12,WW,LM2,LM3,LM7,NB3,KMAX)
      CALL WINT (KK,KK,LL,LL,GLK,C,CC,C12,WW,LM2,LM3,LM7,NB3,KMAX)
      CALL WINT (KK,LL,KK,LL,GKL,C,CC,C12,WW,LM2,LM3,LM7,NB3,KMAX)
      REV  = ONE/EV
      GKK  = GKK*REV
      GLL  = GLL*REV
      GLK  = GLK*REV
      GKL  = GKL*REV
      DIFF = TWO*EIG(LL)-TWO*EIG(KK)+GKK+GLL-FOUR*GLK+TWO*GKL
      BET2 = GKL*GKL
      DELT =-PT5*(DIFF-SQRT(DIFF**2+FOUR*BET2))
      DELL =-DELT
      DEL2 = DELT*DELT
      DENO = DEL2+BET2
      CA   = SQRT(BET2/DENO)
      CB   =-SQRT(DEL2/DENO)
      IF(IOUT2.GE.5) THEN
         NB6 = NBF(6)
         WRITE(NB6,500) KK,LL,DIFF,GKL,DELL,CA,CB
      ENDIF
      RETURN
  500 FORMAT (///1X,'MINIMAL CI TREATMENT INVOLVING MOS',2I5//
     1  1X,'ENERGY OF SECOND CONFIGURATION',F12.7,'  A.U.'/
     2  1X,'MATRIX ELEMENT                ',F12.7,'  A.U.'/
     3  1X,'ENERGY OF LOWEST CI ROOT      ',F12.7,'  A.U.'/
     4  1X,'CI COEFFICIENTS               ',2F12.7)
      END
