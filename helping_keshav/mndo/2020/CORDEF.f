      SUBROUTINE CORDEF (RI,CORE,NI,NJ)
C     *
C     DEFINITION OF CORE-ELECTRON ATTRACTION INTEGRALS IN TERMS
C     OF TWO-CENTER TWO-ELECTRON INTEGRALS.
C     *
C     NOTATION. I=INPUT, O=OUTPUT.
C     RI(22)    TWO-ELECTRON INTEGRALS IN STANDARD ORDER (I).
C     CORE()    CORE-ELECTRON INTEGRALS IN STANDARD ORDER (O).
C     NI,NJ     ATOMIC NUMBERS (I).
C     *
      USE LIMIT, ONLY: LMZ
      IMPLICIT DOUBLE PRECISION (A-H,O-Z)
      COMMON
     ./CONSTN/ ZERO,ONE,TWO,THREE,FOUR,PT5,PT25
     ./DPARM / LORBS(LMZ)
     ./PARDER/ TORE(LMZ),EHEAT(LMZ),EISOL(LMZ)
      DIMENSION RI(22),CORE(10,2)
C *** INITIALIZATION.
      IORBS  = LORBS(NI)
      JORBS  = LORBS(NJ)
      TORENI = TORE(NI)
      TORENJ = TORE(NJ)
C *** DEFINE CORE ELECTRON ATTRACTIONS.
C     NEGLECT OF PENETRATION INTEGRALS.
      CORE(1,1) = -RI(1)*TORENJ
      CORE(1,2) = -RI(1)*TORENI
      IF(IORBS.GE.4) THEN
         CORE(2,1) = -RI(2)*TORENJ
         CORE(3,1) = -RI(3)*TORENJ
         CORE(4,1) = -RI(4)*TORENJ
      ELSE
         CORE(2,1) = ZERO
         CORE(3,1) = ZERO
         CORE(4,1) = ZERO
      ENDIF
      IF(JORBS.GE.4) THEN
         CORE(2,2) = -RI(5) *TORENI
         CORE(3,2) = -RI(11)*TORENI
         CORE(4,2) = -RI(12)*TORENI
      ELSE
         CORE(2,2) = ZERO
         CORE(3,2) = ZERO
         CORE(4,2) = ZERO
      ENDIF
      RETURN
      END
