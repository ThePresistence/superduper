      SUBROUTINE SMEAR (EA,EB,LM2)
C     *
C     FERMI SMEARING.
C     *
C     NOTATION. I=INPUT, O=OUTPUT, S=SCRATCH.
C     EA(LM2)   RHF OR UHF-ALPHA MO EIGENVALUES (I).
C     EB(LM2)   UHF-BETA MO EIGENVALUES (I).
C     *
C     DATA TRANSFER VIA COMMON BLOCKS.
C     TFERMI    FERMI TEMPERATURE (I) FROM IN2(215).
C     NOCCA     INDEX OF HIGHEST OCCUPIED ALPHA ORBITAL (O).
C     NOCCB     INDEX OF HIGHEST OCCUPIED BETA  ORBITAL (O).
C     OCCA()    OCCUPATION NUMBERS OF ALPHA ORBITALS (O).
C     OCCB()    OCCUPATION NUMBERS OF BETA  ORBITALS (O).
C     ETS       ENTROPIC FREE ENERGY CONTRIBUTION (O).
C     *
      USE LIMIT, ONLY: LMX
      IMPLICIT DOUBLE PRECISION (A-H,O-Z)
      COMMON
     ./CONSTN/ ZERO,ONE,TWO,THREE,FOUR,PT5,PT25
     ./INOPT2/ IN2(300)
     ./NBFILE/ NBF(20)
     ./OCCETS/ ETS
     ./OCCFL / IMOCC,NOCCA,NOCCB,MSUB,MOSUMA,MOSUMB,MOCCA(8),MOCCB(8)
     ./OCCNM / OCCA(LMX),OCCB(LMX)
     ./ORBITS/ NUMB,NORBS,NMOS,NALPHA,NBETA
C     ARRAYS IN THE ARGUMENT LIST.
      DIMENSION EA(LM2),EB(LM2)
C     BOLTZMANN PARAMETER DEFINED AS k*1 Kelvin in eV
      PARAMETER (BOLTZ=8.6173324D-05)
      PARAMETER (SMALL=1.0D-10)
C     *
C *** INITIALIZATION.
      NB6    = NBF(6)
      NPRINT = IN2(72)
      NOCCA  = NORBS
      NOCCB  = NORBS
      TFERMI = DBLE(IN2(215))
      BKT    = BOLTZ*TFERMI
C     *
C *** ITERATIVE PROCEDURE TO DETERMINE OCCUPATIONS OF ALPHA ORBITALS.
C     EFERMI = CURRENT FERMI LEVEL (UPDATED DURING LOOP)
C     FERMI  = CURRENT OCCUPATION NUMBER OF MO I (UPDATED DURING LOOP)
C     DFERMI = CURRENT DERIVATIVE OF FERMI WITH REGARD TO EFERMI
C     TOTALN = CURRENT SUM OVER OCCUPATION NUMBERS
C     TOTALD = CURRENT SUM OVER DERIVATIVES
C     CHANGE = CURRENT CHANGE IN EFERMI
C     NALPHA = NUMBER OF ALPHA ELECTRONS (FIXED)
C     OCCA() = OCCUPATION NUMBERS (FINAL RESULT AFTER CONVERGENCE)
      EFERMI = PT5*(EA(NALPHA)+EA(NALPHA+1))
      TOTALN = ZERO
      NCYCLE = 0
      DO WHILE (ABS(NALPHA-TOTALN).GT.SMALL .AND. NCYCLE.LT.200)
        TOTALN = ZERO
        TOTALD = ZERO
        NCYCLE = NCYCLE+1
        DO 10 I=1,NORBS
          EXPON = EXP((EA(I)-EFERMI)/BKT)
          FERMI = ONE/(EXPON+ONE)
          IF(FERMI.GT.SMALL) THEN
             DFERMI = EXPON / (BKT*(EXPON+ONE)**2)
          ELSE
             DFERMI = ZERO
          END IF
          OCCA(I) = FERMI
          TOTALN = TOTALN + FERMI
          TOTALD = TOTALD + DFERMI
   10   CONTINUE
        CHANGE = (NALPHA-TOTALN)/TOTALD
        EFERMI = EFERMI+CHANGE
        IF(NPRINT.GE.7) THEN
           WRITE (NB6,100) NCYCLE,EFERMI,NALPHA-TOTALN
        ENDIF
      END DO
C     PRINTING SECTION.
      IF(NPRINT.GE.2) THEN
         WRITE(NB6,110) TFERMI,EFERMI
         IF(NPRINT.GE.5) THEN
            WRITE(NB6,120)
            OCCSUM = ZERO
            DO 20 I=1,NORBS
            WRITE(NB6,130) I,EA(I),OCCA(I)
            OCCSUM = OCCSUM+OCCA(I)
   20       CONTINUE
            WRITE(NB6,140) OCCSUM
         ENDIF
      ENDIF
C     *
C *** ITERATIVE PROCEDURE TO DETERMINE OCCUPATIONS OF BETA ORBITALS.
C     NOTATION ANALOGOUS TO ALPHA ORBITALS (SEE ABOVE).
      EFERMI = PT5*(EB(NBETA)+EB(NBETA+1))
      TOTALN = ZERO
      NCYCLE = 0
      DO WHILE (ABS(NBETA-TOTALN).GT.SMALL .AND. NCYCLE.LT.200)
        TOTALN = ZERO
        TOTALD = ZERO
        NCYCLE = NCYCLE+1
        DO 30 I=1,NORBS
          EXPON = EXP((EB(I)-EFERMI)/BKT)
          FERMI = ONE/(EXPON+ONE)
          IF(FERMI.GT.SMALL) THEN
             DFERMI = EXPON / (BKT*(EXPON+ONE)**2)
          ELSE
             DFERMI = ZERO
          END IF
          OCCB(I) = FERMI
          TOTALN = TOTALN + FERMI
          TOTALD = TOTALD + DFERMI
   30   CONTINUE
        CHANGE = (NBETA-TOTALN)/TOTALD
        EFERMI = EFERMI+CHANGE
        IF(NPRINT.GE.7) THEN
           WRITE (NB6,100) NCYCLE,EFERMI,NBETA-TOTALN
        ENDIF
      END DO
C     PRINTING SECTION.
      IF(NPRINT.GE.2) THEN
         WRITE(NB6,150) TFERMI,EFERMI
         IF(NPRINT.GE.5) THEN
            WRITE(NB6,120)
            OCCSUM = ZERO
            DO 40 I=1,NORBS
            WRITE(NB6,130) I,EB(I),OCCB(I)
            OCCSUM = OCCSUM+OCCB(I)
   40       CONTINUE
            WRITE(NB6,140) OCCSUM
         ENDIF
      ENDIF
C     *
C *** COMPUTE ENTROPIC ENERGY CONTRIBUTION.
C     ALPHA ORBITALS.
      SUM    = ZERO
      DO 50 I=1,NOCCA
      IF(OCCA(I).GT.SMALL) THEN
         SUM = SUM + OCCA(I)*LOG(OCCA(I))
      ENDIF
      TEMP   = ONE-OCCA(I)
      IF(TEMP.GT.SMALL) THEN
         SUM = SUM + TEMP*LOG(TEMP)
      ENDIF
   50 CONTINUE
      ETSA   = BKT*SUM
C     BETA ORBITALS.
      SUM    = ZERO
      DO 60 I=1,NOCCB
      IF(OCCB(I).GT.SMALL) THEN
         SUM = SUM + OCCB(I)*LOG(OCCB(I))
      ENDIF
      TEMP   = ONE-OCCB(I)
      IF(TEMP.GT.SMALL) THEN
         SUM = SUM + TEMP*LOG(TEMP)
      ENDIF
   60 CONTINUE
      ETSB   = BKT*SUM
      ETS    = ETSA+ETSB
C     PRINTING SECTION.
      IF(NPRINT.GE.2) THEN
         WRITE(NB6,160) ETS,ETSA,ETSB
      ENDIF
  100 FORMAT( 1X,'FERMI SMEARING: CYCLE, EFERMI, MISSING',I5,2F20.10)
  110 FORMAT( 1X,'ELECTRONIC TEMPERATURE:',F10.2,' KELVIN',
     1        5X,'FERMI ALPHA LEVEL:',F14.3,' EV')
  120 FORMAT(/1X,'   MO     ENERGY(EV)     OCCUPATION'/)
  130 FORMAT( 1X,I5,2F15.5)
  140 FORMAT( 1X,'  SUM',15X,F15.5/)
  150 FORMAT( 1X,'ELECTRONIC TEMPERATURE:',F10.2,' KELVIN',
     1        5X,'FERMI BETA LEVEL: ',F14.3,' EV')
  160 FORMAT( 1X,'TOTAL ENTROPIC ENERGY TERM: ',F10.5,' EV',
     1       /1X,'CONTRIBUTION FROM ALPHA MOS:',F10.5,' EV',
     2       /1X,'CONTRIBUTION FROM BETA  MOS:',F10.5,' EV')
      END
