      SUBROUTINE QMINTC (H,LMH,ENUCQM,P,PTCHG,MODE,ICNTR)
C     *
C     ELECTROSTATIC INTERACTIONS OF ELECTRONS AND CORES (QM PART)
C     WITH ONE EXTERNAL POINT CHARGE (MM PART): INTEGRAL EVALUATION.
C     SEE SUBROUTINE QMINT FOR FURTHER GENERAL COMMENTS.
C     *
C     CLASSICAL REPRESENTATION OF THE ELECTRONS AND THE CORES.
C     MMPOT=1: ONLY POINT MONOPOLES ARE TAKEN INTO ACCOUNT.
C     MMPOT=2: POINT MONOPOLES, DIPOLES, AND QUADRUPOLES ARE INCLUDED.
C     MMPOT=2: ANALOGOUS TO THE STANDARD COSMO SOLVATION TREATMENT.
C     *
C     NOTATION. I=INPUT, O=OUTPUT.
C     H(LMH)    CORE HAMILTONIAN MATRIX (I,O).
C     ENUCQM    CHARGE-CORE REPULSION TERMS SUMMED OVER ALL CORES (O).
C     P(3)      CARTESIAN COORDINATES OF POINT CHARGE IN ANGSTROM (I).
C     PTCHG     VALUE OF THE POINT CHARGE IN ATOMIC UNITS (I).
C     MODE      CONTENTS OF CORE HAMILTONIAN MATRIX (I).
C               = 1  ONLY ONE-CENTER ELEMENTS, DIMENSION LM6, DEFAULT.
C               = 2  ALL ELEMENTS, DIMENSION LM4.
C     ICNTR     SPECIAL OPTION TO TREAT A SINGLE QM ATOM (I).
C               = 0  INCLUDE INTERACTIONS WITH ALL QM ATOMS, DEFAULT.
C               = I  INCLUDE INTERACTIONS ONLY WITH QM ATOM I.
C     *
      USE LIMIT, ONLY: LM1, LMI, LMX, LMZ
      IMPLICIT DOUBLE PRECISION (A-H,O-Z)
      PARAMETER (SQ3=1.7320508075689D0)
      COMMON
     ./ATOMC / COORD(3,LM1)
     ./ATOMS / NUMAT,NAT(LM1),NFIRST(LM1),NLAST(LM1)
     ./CONSTF/ A0,AFACT,EV,EVCAL,PI,W1,W2,BIGEXP
     ./CONSTN/ ZERO,ONE,TWO,THREE,FOUR,PT5,PT25
     ./FINDX1/ IP(LMI),IP1(LMI),IP2(LMI)
     ./INDEX / INDX(LMX)
     ./INDEXW/ NW(LM1)
     ./INOPT2/ IN2(300)
     ./MULTIP/ DD(6,0:LMZ),PO(9,0:LMZ)
     ./PARDER/ TORE(LMZ),EHEAT(LMZ),EISOL(LMZ)
      DIMENSION H(LMH),P(3)
      DIMENSION B(45)
C *** INITIALIZATION.
C     NPRINT = IN2(72)
      MMPOT  = IN2(122)
      ENUCQM = ZERO
C *** LOOP OVER QM ATOMS.
      DO 50 I=1,NUMAT
      IF(ICNTR.GT.0 .AND. ICNTR.LE.NUMAT .AND. ICNTR.NE.I) GO TO 50
      NI     = NAT(I)
      IORBS  = NLAST(I)-NFIRST(I)+1
      JMAX   = INDX(IORBS)+IORBS
C     DIRECTION COSINES AND DISTANCE R (AU).
      X11    = (P(1)-COORD(1,I))/A0
      X22    = (P(2)-COORD(2,I))/A0
      X33    = (P(3)-COORD(3,I))/A0
      R      = SQRT(X11*X11+X22*X22+X33*X33)
      RM1    = ONE/R
C     INITIALIZATION FOR OPTION MMPOT=1.
      IF(IORBS.GT.1 .AND. MMPOT.EQ.1) THEN
         DO 10 J=2,JMAX
         B(J) = ZERO
   10    CONTINUE
      ENDIF
C     ATOMS WITH S-BASIS
      IF(IORBS.EQ.1) THEN
         B(1) = RM1
C     ATOMS WITH SP-BASIS
      ELSE IF(IORBS.EQ.4 .AND. MMPOT.EQ.1) THEN
         B(1) = RM1
         B(3) = RM1
         B(6) = RM1
         B(10)= RM1
      ELSE IF(IORBS.EQ.4 .AND. MMPOT.EQ.2) THEN
         DA   = DD(2,NI)
         QA   = DD(3,NI)*DD(3,NI)
         RM3  = RM1**3
         RM5  = RM1**5
         B(1) = RM1
         B(2) = X11*DA*RM3
         B(4) = X22*DA*RM3
         B(7) = X33*DA*RM3
         B(3) = RM1+THREE*X11*X11*QA*RM5-QA*RM3
         B(6) = RM1+THREE*X22*X22*QA*RM5-QA*RM3
         B(10)= RM1+THREE*X33*X33*QA*RM5-QA*RM3
         B(5) = THREE*X11*X22*QA*RM5
         B(8) = THREE*X11*X33*QA*RM5
         B(9) = THREE*X22*X33*QA*RM5
C     ATOMS WITH SPD-BASIS
      ELSE IF(IORBS.EQ.9 .AND. MMPOT.EQ.1) THEN
         B(1) = RM1
         B(3) = RM1
         B(6) = RM1
         B(10)= RM1
         B(15)= RM1
         B(21)= RM1
         B(28)= RM1
         B(36)= RM1
         B(45)= RM1
      ELSE
C     NOTE: THE QUADRUPOLE LENGTHS DD(4,NI) AND DD(6,NI) ALREADY
C     INCLUDE A FACTOR OF SQRT(2). SEE SUBROUTINES DDPOHY AND REPPD.
         DA   = DD(2,NI)
         QA   = DD(3,NI)*DD(3,NI)
         QB   = DD(4,NI)*DD(4,NI)*PT5
         DB   = DD(5,NI)
         QC   = DD(6,NI)*DD(6,NI)*PT5
         RM3  = RM1**3
         RM5  = RM1**5
         B(1) = RM1
         B(2) = X11*DA*RM3
         B(4) = X22*DA*RM3
         B(7) = X33*DA*RM3
         B(3) = RM1+THREE*X11*X11*QA*RM5-QA*RM3
         B(6) = RM1+THREE*X22*X22*QA*RM5-QA*RM3
         B(10)= RM1+THREE*X33*X33*QA*RM5-QA*RM3
         B(5) = THREE*X11*X22*QA*RM5
         B(8) = THREE*X11*X33*QA*RM5
         B(9) = THREE*X22*X33*QA*RM5
         DIPOLX = X11*DB*RM3
         DIPOLY = X22*DB*RM3
         DIPOLZ = X33*DB*RM3
         QUADRU = QC*RM5
         B(11) = 1.5D0*(X11-X22)*(X11+X22)*QB*RM5
         B(16) = THREE*X11*X33*QB*RM5
         B(22) = PT5*SQ3*(TWO*X33*X33-X11*X11-X22*X22)*QB*RM5
         B(29) = THREE*X22*X33*QB*RM5
         B(37) = THREE*X11*X22*QB*RM5
         B(12) = DIPOLX
         B(13) =-DIPOLY
         B(14) = ZERO
         B(17) = DIPOLZ
         B(18) = ZERO
         B(19) = DIPOLX
         B(23) =-DIPOLX/SQ3
         B(24) =-DIPOLY/SQ3
         B(25) = TWO*DIPOLZ/SQ3
         B(30) = ZERO
         B(31) = DIPOLZ
         B(32) = DIPOLY
         B(38) = DIPOLY
         B(39) = DIPOLX
         B(40) = ZERO
         B(15) = RM1-THREE*X33*X33*QUADRU+QC*RM3
         B(21) = RM1-THREE*X22*X22*QUADRU+QC*RM3
         B(28) = RM1+THREE*X33*X33*QUADRU-QC*RM3
         B(36) = RM1-THREE*X11*X11*QUADRU+QC*RM3
         B(45) = RM1-THREE*X33*X33*QUADRU+QC*RM3
         B(20) = THREE*X11*X33*QUADRU
         B(26) =-SQ3*(X11-X22)*(X11+X22)*QUADRU
         B(27) = SQ3*X11*X33*QUADRU
         B(33) =-THREE*X22*X33*QUADRU
         B(34) = THREE*X11*X22*QUADRU
         B(35) = SQ3*X22*X33*QUADRU
         B(41) = ZERO
         B(42) = THREE*X22*X33*QUADRU
         B(43) =-TWO*SQ3*X11*X22*QUADRU
         B(44) = THREE*X11*X33*QUADRU
      ENDIF
C     CONVERSION FROM ATOMIC UNITS TO EV.
C     MULTIPLICATION BY POINT CHARGE.
      FACTOR = EV*PTCHG
      DO 20 J=1,JMAX
      B(J)   = B(J)*FACTOR
   20 CONTINUE
C     CONTRIBUTIONS TO THE CORE HAMILTONIAN.
      IF(MODE.EQ.2) THEN
         DO 30 J=1,JMAX
         IJ     = IP(NW(I)-1+J)
         H(IJ)  = H(IJ)-B(J)
   30    CONTINUE
      ELSE
         DO 40 J=1,JMAX
         IJ     = NW(I)-1+J
         H(IJ)  = H(IJ)-B(J)
   40    CONTINUE
      ENDIF
C     CONTRIBUTIONS TO THE CORE-CORE REPULSIONS.
      ENUCQM = ENUCQM + PTCHG*TORE(NI)*RM1*EV
   50 CONTINUE
      RETURN
      END
