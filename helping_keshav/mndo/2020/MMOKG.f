      SUBROUTINE MMOKG (CG,DEL,NCG,NNHCO)
C     *
C     MOLECULAR MECHANICS CORRECTION TO PEPTIDE BONDS: GRADIENT.
C     INCLUDED FOR COMPATIBILITY WITH MOPAC(6.0).
C     CODE ADAPTED FROM MOPAC(6.0) WRITTEN BY J.J.P.STEWART.
C     *
      USE LIMIT, ONLY: LM1
      IMPLICIT DOUBLE PRECISION (A-H,O-Z)
      COMMON
     ./ATOMC / COORD(3,LM1)
     ./MMCOM2/ NHCO(4,1+2*LM1/3),HTYPE
      DIMENSION CG(3,NCG)
      IF(NNHCO.LE.0) RETURN
C *** SECTION FOR CONTRIBUTION TO CARTESIAN GRADIENT.
      DO 30 I=1,NNHCO
      NH1    = NHCO(1,I)
      NH2    = NHCO(2,I)
      NH3    = NHCO(3,I)
      NH4    = NHCO(4,I)
      DO 20 J=1,4
      NHJ    = NHCO(J,I)
      DO 10 K=1,3
      COORD(K,NHJ) = COORD(K,NHJ)-DEL
      CALL DIHED(NH1,NH2,NH3,NH4,ANG1,SIN1)
      COORD(K,NHJ) = COORD(K,NHJ)+DEL*2.0D0
      CALL DIHED(NH1,NH2,NH3,NH4,ANG2,SIN2)
      COORD(K,NHJ) = COORD(K,NHJ)-DEL
      CG(K,NHJ)    = CG(K,NHJ)-HTYPE*(SIN1**2-SIN2**2)/(DEL*2.0D0)
   10 CONTINUE
   20 CONTINUE
   30 CONTINUE
      RETURN
      END
