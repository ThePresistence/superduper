      SUBROUTINE CORREP (I,J,NI,NJ,R,WIJ,ENUCLR)
C     *
C     CORE-CORE REPULSION FUNCTION IN MNDO-TYPE METHODS.
C     CONTRIBUTION ENUCLR FROM A GIVEN ATOM PAIR I-J.
C     *
C     NOTATION. I=INPUT, O=OUTPUT.
C     I,J       ATOM PAIR I-J (I).
C     NI,NJ     CORRESPONDING ATOMIC NUMBERS (I).
C     R         DISTANCE IN ATOMIC UNITS (I).
C     WIJ       TWO-CENTER INTEGRAL (SS,SS) FOR IOP.GT.-10 (O).
C     *
      USE LIMIT, ONLY: LMZ
      IMPLICIT DOUBLE PRECISION (A-H,O-Z)
      COMMON
     ./ABOND / ALPB(LMZ,LMZ),MALPB(LMZ)
     ./CONSTF/ A0,AFACT,EV,EVCAL,PI,W1,W2,BIGEXP
     ./CONSTN/ ZERO,ONE,TWO,THREE,FOUR,PT5,PT25
     ./INOPT2/ IN2(300)
     ./MULTIP/ DD(6,0:LMZ),PO(9,0:LMZ)
     ./PARDER/ TORE(LMZ),EHEAT(LMZ),EISOL(LMZ)
     ./PAROPT/ USS(LMZ),UPP(LMZ),ZS(LMZ),ZP(LMZ),BETAS(LMZ),BETAP(LMZ),
     .         ALP(LMZ)
C     INITIALIZATION.
      IOP    = IN2(2)
      IPAROK = IN2(11)
      ALPNI  = ALP(NI)
      ALPNJ  = ALP(NJ)
C     CONVERT TO ANGSTROM.
      RIJ    = R*A0
C     USE BOND PARAMETERS IN MNDO/d (IF DEFINED).
C     ALPB(NJ,NI) IS USED AS ALPHA PARAMETER FOR THE ELEMENT WITH THE
C     ATOMIC NUMBER NI IN THE CASE OF THE PAIR NJ-NI,
C     - IF THERE ARE BOND PARAMETERS FOR NI (MALPB(NI).GT.0),
C     - IF THERE MAY BE A BOND PARAMETER FOR NJ-NI (NJ.LE.MALPB(NI)),
C     - IF ALPB(NJ,NI) IS NONZERO AND POSITIVE.
C     THE STANDARD ALPHA PARAMETER ALP(NI) IS USED IF ANY ONE OF THESE
C     CONDITIONS IS NOT SATISFIED.
      IF(IOP.EQ.-10 .OR. IOP.EQ.-13) THEN
         MALPNI = MALPB(NI)
         MALPNJ = MALPB(NJ)
         IF(MALPNI.GT.0 .AND. NJ.LE.MALPNI) THEN
            IF(ALPB(NJ,NI).GT.ZERO) ALPNI = ALPB(NJ,NI)
         ENDIF
         IF(MALPNJ.GT.0 .AND. NI.LE.MALPNJ) THEN
            IF(ALPB(NI,NJ).GT.ZERO) ALPNJ = ALPB(NI,NJ)
         ENDIF
      ENDIF
C     CALCULATE SCALE FACTOR INCLUDING EXPONENTIAL TERMS.
      ENI    = EXP(-ALPNI*RIJ)
      IF(NI.EQ.NJ) THEN
         ENJ = ENI
      ELSE
         ENJ = EXP(-ALPNJ*RIJ)
      ENDIF
      SCALE  = ONE+ENI+ENJ
      NT     = NI+NJ
      IF(NT.EQ.8 .OR. NT.EQ.9) THEN
         IF(NI.EQ.7 .OR. NI.EQ.8) SCALE=SCALE+(RIJ-ONE)*ENI
         IF(NJ.EQ.7 .OR. NJ.EQ.8) SCALE=SCALE+(RIJ-ONE)*ENJ
      ENDIF
C     SPECIAL CORE-CORE REPULSION TERMS FOR AM1/d INVOLVING MOLYBDENUM.
C     A.A. VOITYUK AND N. ROESCH, J.PHYS.CHEM. A 104, 4089 (2000).
C     NOTE THAT A PREFACTOR OF 2 IS MISSING IN THE FORMULA FOR ECORE
C     ACCORDING TO A.A. VOITYUK, PRIVATE COMMUNICATION (2001).
C     THIS PREFACTOR IS INCLUDED BELOW (SEE ENI AND ENJ).
      IF(IOP.EQ.-12 .AND. NI.EQ.42) THEN
         ENI   = TWO*EXP(-ALPB(NJ,NI)*RIJ)
         SCALE = ONE+ENI*1.5D0
         IF(NJ.EQ.1) SCALE = ONE+ENI*RIJ
         IF(NJ.EQ.6 .OR. NJ.EQ.17) SCALE = ONE+ENI*2.5D0
         IF(NJ.EQ.42) SCALE = ONE+ENI*6.0D0
      ELSE IF(IOP.EQ.-12 .AND. NJ.EQ.42) THEN
         ENJ   = TWO*EXP(-ALPB(NI,NJ)*RIJ)
         SCALE = ONE+ENJ*1.5D0
         IF(NI.EQ.1) SCALE = ONE+ENJ*RIJ
         IF(NI.EQ.6 .OR. NI.EQ.17) SCALE = ONE+ENJ*2.5D0
      ENDIF
C     CALCULATE BASIC REPULSIVE TERM.
      IF(IOP.LE.-10) THEN
         WIJ = EV/SQRT(RIJ*RIJ/(A0*A0)+(PO(9,NI)+PO(9,NJ))**2)
      ENDIF
      ENUC   = TORE(NI)*TORE(NJ)*WIJ
C     SPECIAL CORE-CORE REPULSION TERMS FOR AM1, PM3 AND PDDG/PM3.
      ENUC1  = ZERO
      IF(IOP.EQ.- 2 .OR. IOP.EQ.- 7 .OR.
     1   IOP.EQ.-12 .OR. IOP.EQ.-15 .OR. IOP.EQ.-17) THEN
         CALL REPAM1 (NI,NJ,RIJ,ENUC1)
      ENDIF
C     SPECIAL CORE-CORE REPULSION TERMS FOR MNDOH.
      ENUC2  = ZERO
      IF((IOP.EQ.-3 .OR.  IOP.EQ.-13)
     1              .AND. (NT.GE.8 .AND. NT.LE.10)
     2              .AND. (NI.EQ.1 .OR.  NJ.EQ.1 )) THEN
         CALL MNDOH (I,J,RIJ,ENUC,ENUC2,SCALE)
      ENDIF
C     SPECIAL CORE-CORE REPULSION TERMS FOR PDDG/MNDO AND PDDG/PM3.
      ENUC3  = ZERO
      IF(IPAROK.EQ.8) THEN
         CALL PDDG (NI,NJ,RIJ,ENUC3)
      ENDIF
C     COLLECT TERMS.
      ENUCLR = ENUC*SCALE+ENUC1+ENUC2+ENUC3
      RETURN
      END
