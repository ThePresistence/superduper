      SUBROUTINE INPOCC (IFORM,JPRINT)
C     *
C     INPUT OF THE NUMBER OF OCCUPIED MOLECULAR ORBITALS
C     FOR EACH IRREDUCIBLE REPRESENTATION.
C     *
      IMPLICIT DOUBLE PRECISION (A-H,O-Z)
      CHARACTER*3 NGROUP
      CHARACTER*4 IRREP
      LOGICAL UHF
      COMMON
     ./HALFE / IODD,JODD
     ./NBFILE/ NBF(20)
     ./OCCFL / IMOCC,NOCCA,NOCCB,MSUB,MOSUMA,MOSUMB,MOCCA(8),MOCCB(8)
     ./ORBITS/ NUMB,NORBS,NMOS,NALPHA,NBETA
     ./SYMLAB/ NGROUP(7),IRREP(26)
     ./SYMLIM/ IRREPA(7),IRREPN(7)
     ./UHF   / UHF
C *** FILE NUMBERS.
      NB5    = NBF(5)
      NB6    = NBF(6)
C *** READ POINT GROUP LABEL.
      IF(IFORM.LE.0) THEN
         READ(NB5,500) MSUB
      ELSE
         READ(NB5,*)   MSUB
      ENDIF
C *** NUMBER OF IRREDUCIBLE REPRESENTATIONS.
      IF(MSUB.GT.0 .AND. MSUB.LE.7) THEN
         NIRREP = IRREPN(MSUB)
      ELSE
         WRITE(NB6,510) MSUB
         STOP 'INPOCC'
      ENDIF
C *** NUMBER OF OCCUPIED MOLECULAR ORBITALS PER REPRESENTATION.
      IF(IFORM.LE.0) THEN
         READ(NB5,500) (MOCCA(I),I=1,NIRREP)
         IF(UHF .OR. IODD.GT.0) READ(NB5,500) (MOCCB(I),I=1,NIRREP)
      ELSE
         READ(NB5,*)   (MOCCA(I),I=1,NIRREP)
         IF(UHF .OR. IODD.GT.0) READ(NB5,*)   (MOCCB(I),I=1,NIRREP)
      ENDIF
C *** CHECK FOR SIMPLE ERRORS.
      KSTOP  = 0
      MOSUMA = 0
      MOSUMB = 0
      DO 10 I=1,NIRREP
      MOSUMA = MOSUMA+MOCCA(I)
   10 CONTINUE
      IF(UHF .OR. IODD.GT.0) THEN
         DO 20 I=1,NIRREP
         MOSUMB = MOSUMB+MOCCB(I)
   20    CONTINUE
      ENDIF
      IF(UHF) THEN
         IF(NALPHA.NE.MOSUMA) THEN
            KSTOP = KSTOP+1
            WRITE(NB6,520)
            WRITE(NB6,530) NALPHA,MOSUMA
         ENDIF
         IF(NBETA.NE.MOSUMB) THEN
            KSTOP = KSTOP+1
            IF(KSTOP.EQ.1) WRITE(NB6,520)
            WRITE(NB6,540) NBETA,MOSUMB
         ENDIF
      ELSE
         NEL    = NALPHA+NBETA
         MELSUM = 2*MOSUMA+MOSUMB
         IF(MELSUM.NE.NEL) THEN
            KSTOP = KSTOP+1
            WRITE(NB6,520)
            WRITE(NB6,550) NEL,MELSUM
         ENDIF
         IF(IODD.GT.0) THEN
            NODD = 1
            IF(JODD.GT.0) NODD=2
            IF(NODD.NE.MOSUMB) THEN
               KSTOP = KSTOP+1
               IF(KSTOP.EQ.1) WRITE(NB6,520)
               WRITE(NB6,560) NODD,MOSUMB
            ENDIF
         ENDIF
      ENDIF
C *** PRINTING SECTION.
      IF(JPRINT.GE.0 .OR. KSTOP.GT.0) THEN
         WRITE(NB6,570) NGROUP(MSUB),MSUB
         IF(UHF) THEN
            WRITE(NB6,580)
         ELSE
            WRITE(NB6,590)
         ENDIF
         DO 30 I=1,NIRREP
         WRITE(NB6,600) IRREP(IRREPA(MSUB)+I),MOCCA(I)
   30    CONTINUE
         IF(UHF .OR. IODD.GT.0) THEN
            IF(UHF) THEN
               WRITE(NB6,610)
            ELSE
               WRITE(NB6,620)
            ENDIF
            DO 40 I=1,NIRREP
            WRITE(NB6,600) IRREP(IRREPA(MSUB)+I),MOCCB(I)
   40       CONTINUE
         ENDIF
      ENDIF
      IF(KSTOP.GT.0) STOP 'INPOCC'
      RETURN
  500 FORMAT(8I5)
  510 FORMAT(///1X,'ILLEGAL INPUT FOR POINT GROUP LABEL',
     1       /  1X,'MSUB =',I5,/)
  520 FORMAT(///1X,'THE INPUT FOR THE OCCUPATION OF MOLECULAR ORBITALS',
     1       /  1X,'IS INCOMPATIBLE WITH THE GENERAL INPUT DATA.',
     2       /  1X,'PLEASE CHECK ALL IRREDUCIBLE REPRESENTATIONS.')
  530 FORMAT(///1X,'NUMBER OF ALPHA ELECTRONS FROM GENERAL INPUT',I8,
     1       /  1X,'NUMBER OF ALPHA ELECTRONS FROM OCCUPATIONS  ',I8)
  540 FORMAT(///1X,'NUMBER OF BETA ELECTRONS FROM GENERAL INPUT',I8,
     1       /  1X,'NUMBER OF BETA ELECTRONS FROM OCCUPATIONS  ',I8)
  550 FORMAT(///1X,'NUMBER OF ELECTRONS FROM GENERAL INPUT',I8,
     1       /  1X,'NUMBER OF ELECTRONS FROM OCCUPATIONS  ',I8)
  560 FORMAT(///1X,'NUMBER OF UNPAIRED ELECTRONS FROM GENERAL INPUT',I8,
     1       /  1X,'NUMBER OF UNPAIRED ELECTRONS FROM OCCUPATIONS  ',I8)
  570 FORMAT(///1X,'INPUT FOR OCCUPATION OF MOLECULAR ORBITALS',
     1       /  1X,'POINT GROUP ',A3,' (MSUB=',I1,')')
  580 FORMAT(// 1X,'ALPHA-SPIN ORBITALS'/)
  590 FORMAT(// 1X,'DOUBLY OCCUPIED ORBITALS'/)
  600 FORMAT(   1X, A4,I10)
  610 FORMAT(// 1X,'BETA-SPIN ORBITALS'/)
  620 FORMAT(// 1X,'SINGLY OCCUPIED ORBITALS'/)
      END
