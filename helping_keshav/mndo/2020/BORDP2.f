      SUBROUTINE BORDP2 (D,P,PN,PL,LM2,LM3,N,NITER,KEXT)
C     *
C     UPDATE OF DENSITY MATRIX.
C     OPTIONALLY COMBINED WITH EXTRAPOLATION OR DAMPING.
C     FULL SQUARE ARRAYS, NO FILE HANDLING.
C     *
C     NOTATION. I=INPUT, O=OUTPUT, S=SCRATCH.
C     D(LM2,*)  DIFFERENCE DENSITY MATRIX (I,O).
C     P(LM2,*)  DENSITY MATRIX: OLD (I), NEW (O).
C     PN(LM2,*) NEW DENSITY MATRIX (I).
C     PL        MAXIMUM CHANGE IN DIAGONAL ELEMENTS (O).
C     N         NUMBER OF BASIS FUNCTIONS (I).
C     NITER     NUMBER OF SCF ITERATION (I).
C     KEXT      TYPE OF SCF ITERATION (I,O).
C               =-1 CONVERGED DENSITY, NO MODIFICATION ALLOWED (I).
C               = 0 STANDARD CASE, NO EXTRAPOLATION OR DAMPING (O).
C               = 1 EXTRAPOLATION FOR DENSITY DONE (O).
C               = 2 DAMPING FOR DENSITY DONE (O).
C     *
C     COMMENTS ON OTHER AVAILABLE INPUT OPTIONS.
C     NSTEP .GT.0  -  ATTEMPT EXTRAPOLATION, IF CURRENTLY POSSIBLE.
C     NSTEP .LT.0  -  ATTEMPT DAMPING, IF CURRENTLY POSSIBLE.
C     NSTART.LT.0  -  NO EXTRAPOLATION OR DAMPING, PL STILL EVALUATED.
C     *
      IMPLICIT DOUBLE PRECISION (A-H,O-Z)
      PARAMETER (YCRIT=0.06D0)
      COMMON
     ./CONSTN/ ZERO,ONE,TWO,THREE,FOUR,PT5,PT25
     ./INOPT2/ IN2(300)
     ./NBFILE/ NBF(20)
      DIMENSION D(LM2,LM3),P(LM2,LM3),PN(LM2,LM3)
      SAVE YL
C *** INPUT OPTIONS.
      NPRINT = IN2(72)
      NSTART = IN2(78)
      NSTEP  = IN2(79)
C *** CALCULATE MAXIMUM CHANGE IN DIAGONAL MATRIX ELEMENTS.
      PL     = ZERO
      DO 80 I=1,N
      PL     = MAX(ABS(PN(I,I)-P(I,I)),PL)
   80 CONTINUE
C     *
C *** SIMPLEST CASE: NO EXTRAPOLATION OR DAMPING.
C     *
C     COPY DENSITY MATRIX AND RETURN.
      IF(NSTART.LT.0 .OR. KEXT.LT.0) THEN
         CALL MATCOP (PN,P,LM2,N,1)
         RETURN
      ENDIF
C     *
C *** SECTION FOR EXTRAPOLATION OR DAMPING.
C     *
C *** INITIALIZATION.
      IF(NITER.EQ.1 .AND. NSTEP.GT.0) THEN
         YL  = ZERO
         DO 120 I=1,N
         DO 110 J=1,N
         D(J,I) = ZERO
  110    CONTINUE
  120    CONTINUE
         YLAMB  = ZERO
         DEN1   = ZERO
         DEN2   = ZERO
      ENDIF
C     COMPUTE DOT PRODUCTS INVOLVING OLD DIFFERENCE DENSITY MATRIX.
      IF(NITER.GT.1 .AND. NSTEP.GT.0) THEN
         DEN1   = DOTMAT(D,D,LM2,N,1)
         YLAMB  = DOTMAT(D,PN,LM2,N,1)-DOTMAT(D,P,LM2,N,1)
      ENDIF
C *** COMPUTE NEW DIFFERENCE DENSITY MATRIX D(LM2,LM3).
      CALL MATDIF (PN,P,D,LM2,N)
C *** EXTRAPOLATION.
      KEXT   = 0
      IF(NSTEP.GT.0) THEN
         FAC    = ONE
         DEN2   = DOTMAT(D,D,LM2,N,1)
         IF(NITER.GE.NSTART) THEN
            II  = NITER-NSTART
            II  = II-(II/NSTEP)*NSTEP
            IF(II.EQ.0) KEXT=1
         ENDIF
         IF(NITER.LT.2 .OR. DEN1.EQ.ZERO .OR. DEN2.EQ.ZERO) THEN
            KEXT = 0
         ELSE
            YLAMB = YLAMB/DEN1
            IF(ABS(YLAMB).GE.ONE) YLAMB=YLAMB*DEN1/DEN2
            IF(ABS(YLAMB-YL).GT.YCRIT) KEXT=0
            IF(YLAMB.EQ.ONE) KEXT=0
            YL = YLAMB
         ENDIF
         IF(KEXT.EQ.1) FAC = ONE/(ONE-YLAMB)-ONE
C *** DAMPING.
      ELSE IF(NSTEP.LT.0) THEN
         IF(NITER.GE.NSTART .AND. NITER.GT.1) THEN
            KEXT = 2
            FAC  = DBLE(MIN(-NSTEP,9))/10.0D0
         ENDIF
      ENDIF
C *** UPDATE OF DENSITY MATRIX.
      IF(KEXT.GT.0) THEN
         CALL MATUPD (PN,D,P,FAC,LM2,N)
      ELSE
         CALL MATCOP (PN,P,LM2,N,1)
      ENDIF
C *** DEBUG PRINT.
      IF(NPRINT.GE.5) THEN
         NB6 = NBF(6)
         WRITE(NB6,530)
         WRITE(NB6,540) NITER,KEXT,YL,YLAMB,DEN1,DEN2,FAC
         IF(NPRINT.GE.9 .AND. KEXT.GT.0) THEN
            WRITE(NB6,550) NITER,FAC
            CALL MATPRT (P,N,N,LM2,LM2)
         ENDIF
      ENDIF
      RETURN
  530 FORMAT(// 1X,'BORDP2: NITER,KEXT,YL,YLAMB,DEN1,DEN2,FAC')
  540 FORMAT(   1X,'BORDP2: ',I5,I3,5G15.5)
  550 FORMAT(// 1X,'BORDP2: MODIFIED DENSITY MATRIX, NITER =',I3,
     1          5X,'FAC =',F10.5/)
      END
