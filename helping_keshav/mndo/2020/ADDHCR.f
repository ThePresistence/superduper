      SUBROUTINE ADDHCR (H,D2,D,LM4,LMD2,LMQ)
C     *
C     ADD COSMO CONTRIBUTIONS TO THE ONE-ELECTRON MATRIX.
C     *
C     NOTATION.  I=INPUT, O=OUTPUT.
C     H(LM4)     CORE HAMILTONIAN MATRIX (I,O).
C     D2(LMD2)   PACKED DIELECTRIC OPERATOR MATRIX (I).
C     D(LMQ,LMQ) SQUARE DIELECTRIC OPERATOR MATRIX (I).
C     *
      USE LIMIT, ONLY: LM1, LMI, LMZ
      IMPLICIT DOUBLE PRECISION (A-H,O-Z)
      COMMON
     ./ATOMS / NUMAT,NAT(LM1),NFIRST(LM1),NLAST(LM1)
     ./COSMO7/ JPC(LMI+LM1+1)
     ./FINDX1/ IP(LMI),IP1(LMI),IP2(LMI)
     ./INOPT2/ IN2(300)
     ./PARDER/ TORE(LMZ),EHEAT(LMZ),EISOL(LMZ)
      DIMENSION H(LM4),D2(LMD2),D(LMQ,LMQ)
C *** INITIALIZATION.
      MODCSM = IN2(235)
      LM6    = LMQ-NUMAT
C *** OUTER LOOP OVER ALL ONE CENTER-PAIRS.
      DO 30 I=1,LM6
      IM     = IP(I)
      HIM    = 0.D0
C *** INNER LOOP OVER ATOMS.
C     CODE FOR PACKED DIELECTRIC OPERATOR MATRIX.
      IF(MODCSM.GT.0) THEN
         DO 10 J=1,NUMAT
         IJ     = JPC(LM6+J)+I
         HIM    = HIM + D2(IJ) * TORE(NAT(J))
   10    CONTINUE
      ELSE
C     CODE FOR SQUARE DIELECTRIC OPERATOR MATRIX.
         DO 20 J=1,NUMAT
         HIM    = HIM + D(LM6+J,I) * TORE(NAT(J))
   20    CONTINUE
      ENDIF
      H(IM)  = H(IM) + HIM
   30 CONTINUE
      RETURN
      END
